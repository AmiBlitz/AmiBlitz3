; XTRA
; Embedded .xtra Header
; 
; General Info
; -------------------------------------------------------
; ExePath         = "///Tools"
; ExeFile         = "AB3MenuEditor"
; CreateIcon      = 0
; Residents       = "all.res"
; 
; Compiler
; -------------------------------------------------------
; StringBuffer    = 100000
; MakeSmallest    = 1
; FuncOptimize    = 1
; Version         = 1.1.0
; NumberOfBuilds  = 94
; 
; Debugger
; -------------------------------------------------------
; CliArgs         = ""
; StackSize       = 8191
; RuntimeDebug    = 0
; DebugInfo       = 0
; CreateDbgFile   = 0
; OverflowCheck   = 0
; AssemblerCheck  = 0
; InterruptCheck  = 1
; AutoRun         = 1
; 
; Editor
; -------------------------------------------------------
; CursorLine      = 63
; CursorColumn    = 54
; LabelSearch     = "my_configfile"
; LabelRemark     = 0
; LabelAll        = 1
; LabelCase       = 0
; LabelPosition   = 0
; 
; Blitz Objects
; -------------------------------------------------------
; Max File        = 100
; Max GadgetList  = 100
; Max Shape       = 100
; Max Bank        = 1
; Max MenuList    = 100
; Max GTList      = 100
; Max Palette     = 4
; Max BitMap      = 10
; Max Screen      = 5
; Max IntuiFont   = 10
; Max Window      = 100
; Max BlitzFont   = 1
; Max IconInfo    = 1
; Max MUIObject   = 50
; Max AsyncReq    = 4
; Max Req-Lib     = 5
; Max GTMenuList  = 100
; Max Console     = 5
; Max TCPSock     = 5
; Max Tape        = 5
; Max TagList     = 5
; Max Database    = 16
; Max Sound       = 10
; Max MedModule   = 8
; Max Buffer      = 10
; Max Queue       = 10
; Max Sprite      = 20
; Max Module      = 5
; Max Slice       = 10
; Max Page        = 4
; Max CopList     = 10
; Max PTModule    = 5
; Max Anim        = 10
; Max NChunky     = 50
; Max Chunky      = 20
; Max Stencil     = 5
; Max XBSound     = 10
; /XTRA
NEWTYPE.custommenu
  itemname.s
  path.s
  arguments.s
  shortcut.s
  runwbmode.w
  execute.w
  id.l
  stacksize.l
  prio.w
End NEWTYPE
Dim List custommenu.custommenu(0)
DEFTYPE.List menuedlist

NoCli
OPTIMIZE 5
SYNTAX 6

JMP skipvers:
Dc.b "$VER: "
.version
Dc.b "AB3MenuEditor \\__VER_MAJOR.\\__VER_MINOR (\\__DATE_GER__)" ,0
Even
skipvers:


#USE_WIZARD = 1
#WZ_USE_IMAGES = 0
#log_Width  = 120

XINCLUDE "dos.include.ab3"
XINCLUDE "file.include.ab3"
XINCLUDE "asl.include.ab3"
XINCLUDE "screen.include.ab3"
XINCLUDE "logging.include.ab3"


; ############################
.MAIN
log_SetDestination{#log_tofile,"t:AB3_MenuEditor.log"}
log_Reset{}
log_SetFormat{#log_short}

diskobject.l       = dos_OpenToolTypes{dos_GetProgFile{""}}
If diskobject
  my_debugmode.b     = dos_GetToolState{"DEBUG"}
  my_pubScreenName.s = dos_GetToolString{"PUBSCREEN", "AmiBlitz3"}
  my_configfile.s    = dos_GetToolString{"CONFIGFILE" ,"Blitz3:System/PED.menus"}
  dos_CloseToolTypes{}
Else
  error{"Could not open tooltypes."}
EndIf

If my_debugmode = True
  log_SetLevel{#log_debug}
  log_SetDestination{#log_toboth}
Else
  If my_debugmode = 1 Then log_SetLevel{#log_nothing}
EndIf

RunErrsOff
WZLoadGUI ?wzgui
RunErrsOn

; --
; -- check of PubScreen with the given PubScreenname exists
If my_pubScreenName <> ""
  If NOT screen_Open{my_pubScreenName,#scr_mode_find}
    error{"configured pubscreen <" + my_pubScreenName +" > not found!"}
  EndIf
EndIf

WZWindow WZID("WIN_MENUED"),?wzgui,128,-2,-2,-2,-2,WZID("WIN_MENUED")
Use Window WZID("WIN_MENUED")
;
Gosub load_custommenus
Gosub start_menueditor
;
FlushEvents
WZListRemove menuedlist
WZCloseWindow WZID("WIN_MENUED")
screen_Close{}
End


wzgui:
  IncBin "AB3MenuEditor.wizard"
wzguiEnd:


.update_menueditorgadgets:
    WZPrint "MENUED_ITEMNAME" ,custommenu()\itemname
    WZPrint "MENUED_ARGUMENTS",custommenu()\arguments
    WZPrint "MENUED_STACK"    ,custommenu()\stacksize
    WZPrint "MENUED_PRIO"     ,custommenu()\prio
    WZPrint "MENUED_EXECUTE"  ,custommenu()\execute

    If custommenu()\runwbmode = 1
      WZPrint "MENUED_RUNMODE",On
      WZDisable "MENUED_STACK" 
      WZDisable "MENUED_PRIO"
      WZDisable "MENUED_ARGUMENTS"
    Else
      WZPrint "MENUED_RUNMODE",Off
      WZEnable "MENUED_STACK" 
      WZEnable "MENUED_PRIO"
      WZEnable "MENUED_ARGUMENTS"
    EndIf
 
    ;If custommenu()\execute = 1
    ;    WZPrint "MENU_EXECUTE",On
    ;Else
    ;    WZPrint "MENU_EXECUTE",Off
    ;EndIf

    WZEnable "MENUED_DEL"     : WZEnable "MENUED_SORT"
    WZEnable "MENUED_RUNMODE"
    WZEnable "MENUED_PATH"
    WZEnable "MENUED_EXECUTE"

Return

.disable_menueditorgadgets:
    WZPrint "MENUED_ITEMNAME",""
    WZPrint "MENUED_STACK"   ,8192
    WZPrint "MENUED_PRIO"    ,-1
    WZPrint "MENUED_RUNMODE" ,Off
    WZPrint "MENUED_EXECUTE" ,Off
    WZDisable "MENUED_DEL"   : WZDisable "MENUED_SORT"
    WZDisable "MENUED_STACK" : WZDisable "MENUED_PRIO"
    WZDisable "MENUED_RUNMODE"
    WZDisable "MENUED_PATH"
    WZDisable "MENUED_ARGUMENTS"
    WZDisable "MENUED_EXECUTE"
Return

.start_menueditor:
  ; -- get all the defined menuentries
  If FirstItem(custommenu())
    item_counter.b = 0
    Repeat
      WZListAdd menuedlist,custommenu()\itemname,-2
      item_counter+1
    Until NOT NextItem(custommenu())
    WZPrint "MENUED_LIST",0,menuedlist
    item_counter = 0

    dummy.l = FirstItem(custommenu()) 
    Gosub update_menueditorgadgets
  Else
    Gosub disable_menueditorgadgets
  EndIf
  
  menued_exit.b = False
  Repeat
    menued_ev.l = WaitEvent

    Select menued_ev
      Case #IDCMP_CLOSEWINDOW
        menued_exit = True

      Case #IDCMP_VANILLAKEY:
        If EventCode = $1b Then menued_exit = True

      Case #IDCMP_IDCMPUPDATE
        Select WZGadName
          Case "MENUED_ADD"
            ResetList custommenu()
            If AddLast (custommenu())
                WZListAdd menuedlist,"",-2
                custommenu()\itemname = ""
                custommenu()\path     = ""
                custommenu()\arguments= ""
                custommenu()\execute  = False
                custommenu()\runwbmode = False
                custommenu()\stacksize = 8192
                custommenu()\prio = -1
            EndIf
            item_counter = WZListItems (menuedlist)-1
            WZPrint "MENUED_LIST",item_counter,menuedlist,0 
            dummy.l = LastItem(custommenu())
            Gosub update_menueditorgadgets
            WZDisable "MENUED_SAVE"
            WZDisable "MENUED_ADD"
            WZDisable "MENUED_SORT"
            WZDisable "MENUED_LIST"

          Case "MENUED_DEL"
            ResetList custommenu()
            item_counter = 0
            While (item_counter < WZListNum ("MENUED_LIST")) AND NextItem(custommenu())
              item_counter + 1
            Wend
            If item_counter = WZListNum ("MENUED_LIST")
              KillItem custommenu()
              WZListRemove menuedlist,WZListNum ("MENUED_LIST")
              WZPrint "MENUED_LIST",0,menuedlist,0
            EndIf
            If FirstItem(custommenu())
              Gosub update_menueditorgadgets
            Else
              Gosub disable_menueditorgadgets
            EndIf
            WZEnable "MENUED_SAVE"
            WZEnable "MENUED_ADD"
            WZEnable "MENUED_LIST"

          Case "MENUED_SORT"
            If FirstItem(custommenu())

              StringSort custommenu(),SizeOf.custommenu

              WZListRemove menuedlist
              dummy = FirstItem(custommenu())
              Repeat
                WZListAdd menuedlist,custommenu()\itemname,-2
              Until NOT NextItem (custommenu())

              WZPrint "MENUED_LIST",0,menuedlist,0
              dummy = FirstItem(custommenu())
              Gosub update_menueditorgadgets
              WZEnable "MENUED_SAVE"
            Else
              Gosub disable_menueditorgadgets
            EndIf

          Case "MENUED_LIST"
            item_counter = 0
            ResetList custommenu()
            While (item_counter < WZListNum ("MENUED_LIST")) AND NextItem(custommenu())
              item_counter + 1
            Wend
            If item_counter = WZListNum ("MENUED_LIST")
              Gosub update_menueditorgadgets
            Else
              Gosub disable_menueditorgadgets
            EndIf

          Case "MENUED_PATH"
            aslfr_SetRequesterTitle {0,"Select a Programm","Ok","Cancel"}

            If custommenu()\path <> ""
              aslfr_SetPath {0,dos_PathPart{custommenu()\path},dos_FilePart{custommenu()\path} }
            EndIf
            If aslfr_Request{0,False,False,False}
              custommenu()\path = aslfr_GetNextFile{}
              If custommenu()\path <>""
                custommenu()\itemname = dos_FilePart{custommenu()\path}
                WZPrint "MENUED_ITEMNAME",custommenu()\itemname

                WZListRemove menuedlist
                ResetList custommenu()
                While NextItem (custommenu())
                  WZListAdd menuedlist,custommenu()\itemname,-2
                Wend

                WZPrint "MENUED_LIST",item_counter,menuedlist,0
                WZEnable "MENUED_SAVE"
                WZEnable "MENUED_ADD"   
                WZEnable "MENUED_SORT"
                WZEnable "MENUED_LIST"
              EndIf
            EndIf

          Case "MENUED_STACK"
            custommenu()\stacksize = WZInput
            If custommenu()\itemname<>"" Then WZEnable "MENUED_SAVE"

          Case "MENUED_PRIO"
            custommenu()\prio = WZInput
            If custommenu()\itemname<>"" Then WZEnable "MENUED_SAVE"

          Case "MENUED_ARGUMENTS"
            custommenu()\arguments = WZInputstr
            If custommenu()\itemname<>"" Then WZEnable "MENUED_SAVE"
            
          Case "MENUED_RUNMODE"
            checked.l = WZInput
            If checked = 1
              custommenu()\runwbmode = 1
              WZDisable "MENUED_STACK" : WZDisable "MENUED_PRIO" : WZDisable "MENUED_ARGUMENTS"
            Else
              custommenu()\runwbmode = False
              WZEnable "MENUED_STACK" : WZEnable "MENUED_PRIO" : : WZEnable "MENUED_ARGUMENTS"
            EndIf
            If custommenu()\itemname<>"" Then WZEnable "MENUED_SAVE"

          Case "MENUED_EXECUTE"
            custommenu()\execute = WZInput
            If custommenu()\itemname<>"" Then WZEnable "MENUED_SAVE" 

          Case "MENUED_SAVE"
            Gosub save_custommenus
            WZDisable "MENUED_SAVE"

          Case "MENUED_EXIT" 
            menued_exit = True
        End Select
    End Select
  Until menued_exit = True
Return


.load_custommenus:
  If dos_Exist{my_configfile}
    fid.l = file_Open{my_configfile,#file_read}
    If fid >= 0
      ClearList custommenu()
      While file_EOF{fid} = False
        textline.s = file_ReadLine{fid}
        If textline <> "" AND AddLast(custommenu())
          InitArgParse textline
          custommenu()\itemname  = NextArgChar$(@",")
          custommenu()\shortcut  = NextArgChar$(@",")
          custommenu()\id        = Vallong(NextArgChar$(@","))
          custommenu()\path      = NextArgChar$(@",")
          custommenu()\runwbmode = Vallong(NextArgChar$(@","))
          custommenu()\stacksize = Vallong(NextArgChar$(@","))
          custommenu()\prio      = Vallong(NextArgChar$(@","))
          custommenu()\arguments = NextArgChar$(@",")
          custommenu()\execute   = Vallong(NextArgChar$(@","))          
        EndIf
      Wend
      file_Close{fid}
    End If
  EndIf
Return

.save_custommenus:
  fid.l = file_Open{my_configfile,#file_forcewrite}
  If fid >= 0
    If FirstItem(custommenu())
      entry.s = ""
      Repeat
        entry = custommenu()\itemname + ","
        entry + "~,0,"
        entry + custommenu()\path + ","
        entry + Str$(custommenu()\runwbmode) + ","
        entry + Str$(custommenu()\stacksize) + ","
        entry + Str$(custommenu()\prio) + ","
        entry + custommenu()\arguments + ","
        entry + Str$(custommenu()\execute)
        file_WriteLine{fid, entry}
      Until NOT NextItem(custommenu())
    EndIf
  EndIf
  file_Close{fid}
Return
