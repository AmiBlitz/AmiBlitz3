; XTRA
; Embedded .xtra Header
; 
; General Info
; -------------------------------------------------------
; ExePath         = "C:"
; ExeFile         = "AB2GuideInject"
; CreateIcon      = 0
; Residents       = "all.res"
; 
; Compiler
; -------------------------------------------------------
; StringBuffer    = 502400
; MakeSmallest    = 1
; FuncOptimize    = 1
; Version         = 0.0.0
; NumberOfBuilds  = 3
; 
; Debugger
; -------------------------------------------------------
; CliArgs         = "-i Sourcecodes:Includes/dbl_display.include.bb2 -o RAM: -g Blitz2:Docs/Includes"
; StackSize       = 8191
; RuntimeDebug    = 0
; DebugInfo       = 0
; CreateDbgFile   = 0
; OverflowCheck   = 0
; AssemblerCheck  = 0
; InterruptCheck  = 1
; AutoRun         = 1
; 
; Editor
; -------------------------------------------------------
; CursorLine      = 65
; CursorColumn    = 1
; LabelSearch     = ""
; LabelRemark     = 0
; LabelAll        = 0
; LabelPosition   = 0
; 
; Blitz Objects
; -------------------------------------------------------
; Max IconInfo    = 1
; Max NChunky     = 50
; Max MUIObject   = 50
; Max PTModule    = 5
; Max AsyncReq    = 4
; Max Req-Lib     = 5
; Max GTMenuList  = 5
; Max Console     = 5
; Max TCPSock     = 5
; Max XBSound     = 10
; Max Chunky      = 20
; Max File        = 5
; Max GadgetList  = 5
; Max Queue       = 10
; Max Screen      = 5
; Max Shape       = 100
; Max CopList     = 10
; Max Sprite      = 20
; Max Stencil     = 5
; Max Module      = 5
; Max Window      = 20
; Max Anim        = 10
; Max Sound       = 10
; Max Bank        = 5
; Max Buffer      = 10
; Max BitMap      = 10
; Max Slice       = 10
; Max Page        = 4
; Max Tape        = 5
; Max IntuiFont   = 5
; Max MedModule   = 8
; Max Palette     = 4
; Max MenuList    = 5
; Max BlitzFont   = 4
; Max GTList      = 20
; /XTRA
optimize 7
Syntax 2
WBStartup
XINCLUDE "file.include.ab3"
XINCLUDE "multilist.include.ab3"

ltop.s    = ";==============================================================================="
lbegin.s  = ";= "
lend.s    = " ="
lbottom.s = ";==============================================================================="

ltop.s    = ";///////////////////////////////////////////////////////////////////////////////"
lbegin.s  = ";/ "
lend.s    = " /"
lbottom.s = ";///////////////////////////////////////////////////////////////////////////////"
 
;ltop.s    = ";*******************************************************************************"
;lbegin.s  = ";* "
;lend.s    = " *"
;lbottom.s = ";*******************************************************************************"
 
;ltop.s    = ";+------------------------------------------------------------------------------+"
;lbegin.s  = ";\ "
;lend.s    = " \"
;lbottom.s = ";+------------------------------------------------------------------------------+"
 


Function.l file_writeAB2cm{dos.l,a.s}
SHARED lbegin.s,lend.s,llen.l
succ.l = True
Repeat
  If FLen(a.s)>llen
    b.s = Left$(a.s,llen-2)+"::"
    a.s = Right$(a.s,FLen(a.s)-llen+2)
  Else
    b.s = a.s
    a.s = ""
  End If
  b.s + LSet$(" ",llen-FLen(b.s))
  succ&file_WriteBB2Line{dos,lbegin.s+b.s+lend.s}
Until a.s=""
Function Return succ
End Function


Function.l file_writeAB2docu{dos.l,name.s,desc.s}
                 While desc.s><""
                   npos.l = Instr(desc,Chr$(10),1);
                   If npos>0
                     wdesc.s = Left$(desc,npos-1)
                     desc = Right$(desc,FLen(desc)-npos)
                   Else
                     wdesc = desc
                     desc=""
                   End If
                   file_writeAB2cm{dos,wdesc}
                 Wend
Function Return -1
End Function

llen.l  = FLen(ltop.s) - FLen(lend.s) - FLen(lbegin.s)

Function.s lindex2{liststring.s, lindex.l}
    i.l          = 0;
    cpos.l       = 0;
    ipos.l       = 0;
    quoted.l     = 0;
    tclbraced.l  = 0;
    doublespace.l= 1;
    item.s       = ""

    ;// jump over elements ...
    While (i<lindex+1)
        c.b = Peek.b(&liststring+cpos)
        Select c
            Case @"{"
                If (quoted=0) Then tclbraced=1;
                If doublespace=0
                  i+1
                End If
                doublespace=0;
            Case @"}":
                If (quoted=0)
                    tclbraced=0;
                    doublespace=1;
                    i+1;
                End If
            Case $22: quoted=1-quoted;
                doublespace=0;
            Case 0:
               lindex=-1;
            Default:
                If (c=10 OR c=13 OR c=9 OR c=@" ")
                  If (quoted=0 AND tclbraced=0 AND doublespace=0)
                    i+1;
                    doublespace=1;
                  Else
                    If (tclbraced><0 OR quoted><0)
                        If (i=lindex)
                            item.s + Chr$(c) : ipos+1;
                        End If
                    End If
                  End If
                Else
                  doublespace=0;
                  If (i=lindex)
                    item.s + Chr$(c) : ipos+1;
                  EndIf
                End If
        End Select
        cpos+1;
    Wend
Function Return item;
End Function



Macro add_sort ; Listname, Sortentry, Value
breakme.l = False
succ.l = False
ResetList `1
Repeat
  If NextItemFast(`1)
    If `1\`2>=`3
      If LCase$(`1\`2)=LCase$(`3)
        ; already Exists !
        NPrint "WARNING: Function already declared: ",`3
        breakme = True
      Else
      If PrevItem (`1) ; whoops, we seeked 1 to far
        If AddItem (`1)
          `1\`2 = `3
          succ=True
        End If
        breakme = True
      Else
        If AddFirst (`1)
          `1\`2 = `3
          succ=True
        End If
        breakme=True
      End If
      End If
    End If
  Else
    If AddItem (`1) ; seek till there is no more
      `1\`2 = `3
      succ = True
    End If
    breakme = True
  End If
Until breakme = True
End Macro


NEWTYPE.ab2func
typ.s
name.s
lname.s
xname.s
synt.s
desc.s
End NEWTYPE


Dim List ab2func.ab2func(0)
Dim List srcfunc.ab2func(0)

; check if a token is present in line
Function.l notokens{a.s}
succ.l = False

For n.l=0 To Len(a.s)-1
  c.b =  Peek.b(&a.s+n)
  If (c&$80) Then succ=False : n = Len(a.s)
  If c=@";" Then n = Len(a.s) : succ=True
Next

Function Return succ
End Function


Function.s trim{a.s}
While Peek.b(&a.s)<=32 AND Peek.b(&a.s)>0:a.s = Right$(a.s,Len(a.s)-1) : Wend
While Peek.b(&a.s+Len(a.s)-1)<=32 AND Peek.b(&a.s+Len(a.s)-1)>0:a.s = Left$(a.s,Len(a.s)-1):Wend
Function Return a.s
End Function


Function.s makenice{a.s}
succ.l = True

count.l = 0
strptr.l = &a.s
For n.l=0 To Peek.l(strptr-4)-1
  c.b =  Peek.b(strptr+n)
  If c<32 Then c=32

  If c=@"*"
    If count=0 Then lptr.l = strptr+n
    count+1
  Else
    If count>1
      For xptr.l = lptr To strptr+n-1
        Poke.b xptr,32
      Next
    End If
    count=0
  End If
  Poke.b (strptr+n),c
Next

    If count>=1
      For xptr.l = lptr To strptr+Len(a.s)-1
        Poke.b xptr,32
      Next
    End If

a.s = Replace$(a.s,";","")
a.s = Replace$(a.s,":","")
a.s = trim{a.s}
Function Return a.s
End Function


Function.s funcname{a.s}
SHARED totalfuncs.l
succ.l = True
func.s = ""
;NPrint a.s
a.s = trim{a.s}
For n.l=0 To Len(a.s)-1
  c.b = Peek.b(&a.s+n)
  If c<0
    n+1
    c2.b = Peek.b(&a.s+n)
    libnum.l  = c&$7f*2 + ((c2&$80) LSR 7)
    tokenid.l = c2&$7f ;-128;: str_ptr+1:blength-1
    If libnum = 0
      If tokenid = 8 OR tokenid = 9 OR tokenid=13
        endpos.l = Instr(a.s,"}",n)
        If endpos=0 Then endpos = Instr(a.s,":",n+1)
        If endpos=0 Then endpos = Len(a.s)
        If endpos
          func.s = Mid$(a.s,n+1,endpos-n)
          For m.l=0 To Len(func.s)-1
            d.b = Peek.b(&func.s+m)
            If d.b<0 Then func.s=""
          Next
         ; NPrint "."+func.s
          func.s = trim{func.s}
          If func.s><""
            If tokenid =  9 Then func.s = "Function result"+Left$(func.s,2)+" = "+trim{Right$(func.s,Len(func.s)-2)}
            If tokenid = 13 Then func.s = "Macro !"+func.s;: error {"Found macro:"+func.s}
            If tokenid = 8 Then func.s = "Statement "+func.s
           ; NPrint func.s
           totalfuncs+1
          End If
        End If
      End If
    End If
    n=Len(a.s)
  End If
Next
func.s = makenice{func.s}
func.s = trim{func.s}
Function Return func.s
End Function


Function.l findFuncCall{a.s,ipos.l}
epos.l = Instr(a.s,"{",ipos)
spos.l = epos-2
apos.l = -1
gotsomething.l=0
If epos>0
  apos=1
  ;Print "getting..."
  While spos>0
    c.b=Peek.b(&a.s+spos)
    If c.b=@" " OR c.b = @"(" OR c.b = @":" OR c.b=@"{" OR c.b = @"+" OR c.b=@"*" OR c.b=@"/" OR c.b=@"-" OR c.b=@"&" OR c.b=@"|" OR c.b=@"=" OR c.b=96 OR c.b=@";" OR c.b=@","
      If gotsomething
        apos.l = spos+2
        spos=0
      End If
    Else
      If c>32 AND c<=127
      gotsomething=-1
      End If
    End If
    ;Print Chr$(c)
    spos-1
  Wend
  ;Print "\\n"
  ;name.s = Mid$(a.s,apos,epos-apos)
 ; NPrint "INTERN Func: ",name.s
End If
Function Return apos
End Function



error_SetFlags {#error_stdout}

infos.l = True
succ.l = False
showhelp.l = False

savepath.s = "RAM:"
; get the CLI args ...
Repeat
  ioption.s = dos_GetNextArg{}
  option.s = LCase$(ioption.s)
  If Peek.b(&option.s) = @"-" Then option.s = Right$(option.s,FLen(option.s)-1);: Else Goto skip
  Select option.s
    Case "o"  ;
      savepath.s = dos_GetNextArg{}
    Case "?"  ; help
      showhelp = True
    Case "h"
      showhelp.l = True
    Case "q"  ; quiet
      infos = False
    Case "i"
      includepattern.s = dos_GetNextArg{}
    Case "g"
      guidedir.s = dos_GetNextArg{}
    Default
      If option.s><""
        NPrint "ERROR: unknown option: \\22",ioption.s,"\\22"
        showhelp = True
        err.l = True
        option.s = ""
      End If
  End Select
  skip:
Until option.s = ""

If includepattern = "" OR guidedir = "" Then showhelp = True

If showhelp
  NPrint "AB2Inject -i <includes patt> -g <guides dir> [-o <output dir>] [-h]"
  NPrint " "
  NPrint "This little tool extracts the function descriptions of a amiga guide files and copies them into the source code."
  NPrint "Example:"
  NPrint "AB2Inject -i DH0:Sources/Includes/#?.bb2 -g Blitz2:Docs/Includes -o RAM:"
  NPrint ""
  End
End If

  filename.s = includepattern
  path.s     = dos_PathPart{filename}
  filen.s    = dos_FilePart{filename}
  *parent.ml_node =  ml_CreatePathTree {path.s,filen.s}
  *parent    = ml_GetFirstNode{*parent}
  *freenode.ml_node = *parent
  breakme.l = False
  count.l   = 0
  totalfuncs.l = 0
  While *parent
    filename.s = ml_GetUserString{*parent}
    dosD.l=-1
    dosG.l=-1
    dosI.l=-1
    If dos_IsDir{filename.s} = False
      ClearList ab2func()
      If infos Then NPrint "Analysing ",filename.s," ..."
      name.s  = dos_FilePart{filename}
      path.s  = dos_PathPart{filename}
      guide.s = dos_SetExt{name,"guide"}
      guidefile.s = dos_AddPart{guidedir.s,guide.s}
      remprefix.s = Replace$(dos_RemExt{name},".","_")+"_"
      If dos_Exist{guidefile.s}=False
        guidefile.s = Replace$(guidefile.s,".include.",".")

      End If
      If savepath=path
        newname.s = dos_RemExt{name.s}
        newname.s = dos_SetExt{newname.s + "_","bb2"}
      Else
        newname.s = name.s
      End If
      savefile.s = dos_AddPart{savepath.s,newname.s}
      dosI.l = file_Open {filename.s,#file_read}
      dosG.l = file_Open {guidefile.s,#file_read}
      dosD.l = file_Open {savefile.s,#file_write}
      If dosI>=0 AND dosG>=0 AND dosD>=0
        somethingdone.l = False
        ; Guide scannen ....
        space.l = True
        isdesc.l = False
        While file_EOF{dosG}=False AND breakme=False
           a.s = file_ReadLine{dosG}
           posA.l = Instr(a.s,"@NODE",1)
           If posA>0
             b.s = Right$(a.s,FLen(a.s)-posA)
             name.s = lindex{b.s,1}
             If Left$(name.s,FLen(remprefix)) = remprefix Then name.s = Right$(name,FLen(name)-FLen(remprefix))
             If AddItem(ab2func())
               ab2func()\name = name.s
               ab2func()\lname = LCase$(name)
               xname.s = Replace$(LCase$(name),"_","")
               ab2func()\xname = xname
               ab2func()\desc = ""
               isdesc.l = True
               space.l = False
             End If
           End If

           posE.l = Instr(a.s,"@ENDNODE",1)
           If posE>0
             isdesc = False
             space=False
           End If

           If posE=0 AND posA=0 AND isdesc><0
             a.s = Replace$(a.s,"@{B}","")
             a.s = Replace$(a.s,"@{UB}","")
             a.s = Replace$(a.s,"\{","{")
             a.s = Replace$(a.s,"\}","}")
             a.s = Replace$(a.s,"¯","-")
             a.s = Replace$(a.s,"@{fg shine}","")
             a.s = Replace$(a.s,"@{fg highlight}","")
             a.s = Replace$(a.s,"@{fg text}","")
             a.s = Replace$(a.s,"@{fg shadow}","")
             If Instr(a.s,"Inputs:")>0 AND Instr(a.s,"none")>0 Then a.s = ""
             If Instr(a.s,"Bugs")>0 AND Instr(a.s,"no bugs")>0 Then a.s = ""
             If Instr(a.s,"Result:")>0 AND Instr(a.s,"none")>0 Then a.s = ""
             If Instr(a.s,"Example:")>0 AND Instr(a.s,"no example")>0 Then a.s = ""
             If Instr(a.s,"See also:")>0 AND Instr(a.s,"-")>0 Then a.s = ""
             Repeat  ; remove links
               pos.l = Instr(a.s,"@{")
               If pos>0
                 posE.l = Instr(a.s,"link",pos)
                 posX.l = Instr(a.s,"}",pos)
                 name.s = trim{Mid$(a.s,pos+3,posE-pos-5)}
                 repl.s = Mid$(a.s,pos,posX-pos+1)
                 a.s = Replace$(a.s,repl.s,name.s)
               End If
             Until pos =0
             If Instr(a.s,"Syntax:")>0
               ab2func()\desc="\\n"
             End If

             If a.s><"" OR space=False
               ab2func()\desc + a.s + Chr$(10)
               If a.s="" Then space=True :Else space=False
             End If
           End If
        Wend

        ; write intro
        file_WriteBB2Line{dosD,ltop}
        ;file_writeAB2cm{dosD,""}
        ResetList ab2func()
        While NextItemFast(ab2func())
          If ab2func()\name = "Introduction" OR ab2func()\name = "Abstract"
            file_writeAB2docu{dosD,"",ab2func()\desc}
            KillItem ab2func()
          Else
            If ab2func()\name = "main"
              KillItem ab2func()
            End If
          End If
        Wend
        file_WriteBB2Line{dosD,lbottom}

        While file_EOF{dosI} = False AND breakme=False
          a.s = file_ReadBB2Line{dosI}
          skipit.l = False
          If Peek.b(&a.s) = @";"
            If Left$(a.s,2) = Left$(lbegin.s,2) OR a.s = ltop.s OR a.s = lbottom.s OR Left$(a.s,2) =";*"
              ; this is a docu comment and will be skipped!
              skipit = True
            End If
          End If
          If skipit = False
            func.s = funcname{a.s}
            If func.s><""
              ;NPrint "Found: ",func.s
              func.s = Replace$(func.s," FAST "," ")
              typ.s = lindex{func.s,0}
              Select typ.s
                Case "Function"
                  name.s = lindex{func.s,3}
                  arg.s  = lindex{func.s,4}
                  synt.s = lindex{func.s,1} + " = "+ name + " {" + arg +"}"
                Case "Statement"
                  name.s = lindex{func.s,1}
                  arg.s  = lindex{func.s,2}
                  synt.s = name + " {"+arg+"}"
                Case "Macro"
                  name.s = lindex{func.s,1}
                  aeg.s = ""
                  synt.s = name.s
              End Select
              file_WriteBB2Line{dosD,ltop.s}

              ;NPrint "Search Docu for ...",name.s
              lname.s=LCase$(name)
              xname.s = Replace$(lname,"_","")
              ResetList ab2func()
              done.l = False

              While NextItemFast(ab2func()) AND done=False
                If ab2func()\lname = lname OR ab2func()\xname = xname
                  ;NPrint "Found Docu for ",name.s
                  If ab2func()\name><name
                    NPrint "WARNING: upper/lower case has changed: ",ab2func()\name," >< ",name.s
                    ab2func()\desc = Replace$(ab2func()\desc,ab2func()\name,name.s)
                  End If
                  file_writeAB2docu{dosD,"", ab2func()\desc}
                  KillItem ab2func()
                  done.l = True
                End If
              Wend

              If done=False ; try harder ...
                xname.s = Replace$(name,"_get","_")
                xname.s = Replace$(xname,"_set","_")
                xname.s = LCase$(xname)
                xname.s = Replace$(xname,"_","")
                ;NPrint "Nothing found, searching for ",xname
                ResetList ab2func()
                While NextItemFast(ab2func()) AND done=False
                 If ab2func()\xname = xname
                  If ab2func()\name><name
                    NPrint "WARNING: get/set has changed: ",ab2func()\name," >< ",name.s
                    ab2func()\desc = Replace$(ab2func()\desc,ab2func()\name,name.s)
                  End If
                  file_writeAB2docu{dosD,"", ab2func()\desc}
                  KillItem ab2func()
                  done.l = True
                 End If
                Wend
              End If

              If done=False
                file_writeAB2cm{dosD,""}
                ;file_writeAB2cm{dosD,name.s}
                ;file_writeAB2cm{dosD,""}
                file_writeAB2cm{dosD,"Syntax: "+synt.s}
                file_writeAB2cm{dosD,""}
                file_writeAB2cm{dosD,"Description:"}
                file_writeAB2cm{dosD,"* no description available *"}
                file_writeAB2cm{dosD,""}
                NPrint "WARNING: didnt find any docu for "+name.s
              End If

              file_WriteBB2Line{dosD,lbottom.s}
            End If

            file_WriteBB2Line{dosD,a.s} : somethingdone = -1
          End If

          signals.l = SetSignal_(0,#SIGBREAKF_CTRL_C)
          If signals&#SIGBREAKF_CTRL_C
            *parent = 0
            breakme=True
            NPrint "*** USER BREAK ***"
          End If

        Wend


        tokenstat.w = $8008
        tokenend.w  = $8002
        ResetList ab2func()
        While NextItemFast(ab2func())
          NPrint "WARNING: didnt find the Function "+ab2func()\name+" in source, will ADD it as obsolete..."
          arg.s = "unknown.l"
          spos.l = Instr(ab2func()\desc,"Syntax:",1)
          If spos>0
            apos.l = Instr(ab2func()\desc,"{",spos)
            epos.l = Instr(ab2func()\desc,"}",spos)
            If apos<epos
              arg.s = Mid$(ab2func()\desc,apos+1,epos-apos-1)
            End If
          End If
          file_WriteBB2Line{dosD,""}
          file_WriteBB2Line{dosD,ltop}
          file_writeAB2docu{dosD,"", ab2func()\desc}
          file_WriteBB2Line{dosD,lbottom}
          file_WriteBB2Line{dosD,Mki$(tokenstat)+" "+ab2func()\name+" {"+arg.s+"}; this is only a dummy!"}
          file_WriteBB2Line{dosD,Mki$(tokenend)+" "+Mki$(tokenstat)}
          file_WriteBB2Line{dosD,""}
          KillItem ab2func()
        Wend

      Else
        If infos Then NPrint "Unable to open files!"
      End If
    End If
    If dosD>=0
      If somethingdone
        file_Close{dosD}
        somethingdone=False
      Else
        NPrint "nothing written, will erase .bb2"
        file_Erase {dosD}
      End If
    End If
    If dosI>=0 Then file_Close {dosI}
    If dosG>=0 Then file_Close {dosG}
    If *parent Then *parent = ml_GetNextNode{*parent}
    count+1
  Wend
  ml_RemoveAllNodes{*freenode}
  NPrint "Found ",totalfuncs," functions/statements/macros."
End

