; XTRA
; Embedded .xtra Header
; 
; General Info
; -------------------------------------------------------
; ExePath         = "Blitz3:BlitzLibs/UserLibs"
; ExeFile         = "mouselib_new.obj"
; CreateIcon      = 1
; Residents       = "libmacs.res,libjsrs.res,LVO.res"
; 
; Compiler
; -------------------------------------------------------
; StringBuffer    = 10240
; MakeSmallest    = 1
; FuncOptimize    = 1
; Version         = 0.0.0
; NumberOfBuilds  = 32
; 
; Debugger
; -------------------------------------------------------
; CliArgs         = ""
; StackSize       = 8191
; RuntimeDebug    = 0
; DebugInfo       = 0
; CreateDbgFile   = 0
; OverflowCheck   = 1
; AssemblerCheck  = 0
; InterruptCheck  = 1
; AutoRun         = 1
; 
; Editor
; -------------------------------------------------------
; CursorLine      = 168
; CursorColumn    = 35
; LabelSearch     = "_execbase"
; LabelRemark     = 0
; LabelAll        = 1
; LabelCase       = 0
; LabelPosition   = 0
; 
; Blitz Objects
; -------------------------------------------------------
; Max File        = 5
; Max GadgetList  = 5
; Max Shape       = 100
; Max Bank        = 5
; Max MenuList    = 5
; Max GTList      = 5
; Max BitMap      = 10
; Max Screen      = 5
; Max IntuiFont   = 5
; Max Window      = 5
; Max Palette     = 4
; Max BlitzFont   = 4
; Max IconInfo    = 1
; Max MUIObject   = 50
; Max AsyncReq    = 4
; Max Req-Lib     = 5
; Max GTMenuList  = 5
; Max Console     = 5
; Max TCPSock     = 5
; Max Tape        = 5
; Max TagList     = 5
; Max Database    = 16
; Max Sound       = 10
; Max MedModule   = 8
; Max Buffer      = 10
; Max Queue       = 10
; Max Sprite      = 20
; Max Module      = 5
; Max Slice       = 10
; Max Page        = 4
; Max CopList     = 10
; Max PTModule    = 5
; Max Anim        = 10
; Max NChunky     = 50
; Max Chunky      = 20
; Max Stencil     = 5
; Max XBSound     = 10
; /XTRA

JOY0DAT  EQU $DFF00A  ; Port 0
JOY0DATB EQU $DFF00B
JOY1DAT  EQU $DFF00C  ; Port 1
JOY1DATB EQU $DFF00D
INTENA   EQU $DFF09A

!libheader{#mouselib,init,0,0,errorhandling}


!astatement
!args{#word}
!libs
!subs{do_Mouse,1,0}
!name{"Mouse","On|Off"}


!astatement
!args{#word,#word}
!libs{#spriteslib,#ia3|#pd0,#blitzcoplib,#ua2}
!subs{do_Pointer,chksprite+1,0}
!args{#word,#word,#word}
!libs{#spriteslib,#ia3|#pd0,#blitzcoplib,#ua2}
!subs{do_Pointer,chksprite+1,0}
!args
!libs
!subs{do_RemovePointer,1,0}
!name{"Pointer","Sprite#,Sprite Channel,[Port]"}

!astatement
!args{#word,#word,#word,#word}
!libs
!subs{do_MouseArea,0,0}
!args{#word,#word,#word,#word,#word}
!libs
!subs{do_MouseArea,0,0}
!name{"MouseArea","Minx,Miny,Maxx,Maxy,[(Port)]"}


!afunction{#quick}
!args
!libs
!subs{do_MouseX,1,0}
!args{#word}
!libs
!subs{do_MouseX,chkport+1,0}
!name{"MouseX","[(Port)]"}


!afunction{#quick}
!args
!libs
!subs{do_MouseY,1,0}
!args{#word}
!libs
!subs{do_MouseY,chkport+1,0}
!name{"MouseY","[(Port)]"}


!afunction{#quick}
!args
!libs
!subs{do_MouseXSpeed,1,0}
!args{#word}
!libs
!subs{do_MouseXSpeed,chkport+1,0}
!name{"MouseXSpeed","[(Port)]"}


!afunction{#quick}
!args
!libs
!subs{do_MouseYSpeed,1,0}
!args{#word}
!libs
!subs{do_MouseYSpeed,chkport+1,0}
!name{"MouseYSpeed","[(Port)]"}


!astatement
!args{#word,#word}
!libs
!subs{do_PositionMouse,0,0}
!args{#word,#word,#word}
!libs
!subs{do_PositionMouse,0,0}
!name{"PositionMouse","x,y[,Port]"}


init: !nullsub{initcode,0,0,#blitzcoplib,#ld0|#pd4}

!libfin
 ; ==============================================================================

mouseposx: Ds.w 1
mouseposy: Ds.w 1
oldHorizCnt: Ds.b 1
oldVertCnt: Ds.b 1
mousespeedx: Ds.w 1
mousespeedy: Ds.w 1
spriteNum: Ds.w 1
spriteChannel: Ds.w 1
area_minx: Ds.w 1
area_miny: Ds.w 1
area_maxx: Ds.w 1
area_maxy: Ds.w 1

mouseposx1: Ds.w 1
mouseposy1: Ds.w 1
oldHorizCnt1: Ds.b 1
oldVertCnt1: Ds.b 1
mousespeedx1: Ds.w 1
mousespeedy1: Ds.w 1
spriteNum1: Ds.w 1
spriteChannel1: Ds.w 1
area_minx1: Ds.w 1
area_miny1: Ds.w 1
area_maxx1: Ds.w 1
area_maxy1: Ds.w 1

storeA5: Ds.l 1
usedCopList: Ds.l 1
ptr_blitzcoplib: Ds.w 1



.initcode:
  MOVE.l A5,storeA5
  MOVE.w D0,ptr_blitzcoplib
  ;-- Port 0
  CLR.w area_minx
  CLR.w area_miny
  MOVE.w #$0140,area_maxx
  MOVE.w #$00c8,area_maxy
  MOVE.w #$ffff,spriteNum

  ; -- Port 1
  CLR.w area_minx1
  CLR.w area_miny1
  MOVE.w #$0140,area_maxx1
  MOVE.w #$00c8,area_maxy1
  MOVE.w #$ffff,spriteNum1
RTS


.do_PositionMouse:
  TST.w D2 : BEQ pm
    MOVEM.w D0-D1,mouseposx1
    RTS
  pm:
  MOVEM.w D0-D1,mouseposx
RTS

.do_MouseX:
   TST.w D0 : BEQ mx
     MOVE.w mouseposx1(PC),D0
     BRA.s swapx
   mx:
   MOVE.w mouseposx(PC),D0   
   swapx:
   SWAP D0
   CLR.w D0
RTS

.do_MouseY:
  TST.w D0 : BEQ my
    MOVE.w mouseposy1(PC),D0
    BRA.s swapy
  my:
  MOVE.w mouseposy(PC),D0
  swapy:
  SWAP D0
  CLR.w D0
RTS

.do_MouseXSpeed:
  TST.w D0 : BEQ mxs
    MOVE.w mousespeedx1(PC),D0
    BRA.s swapxs
  mxs:
  MOVE.w mousespeedx(PC),D0
  swapxs:
  SWAP D0
  CLR.w D0
RTS

.do_MouseYSpeed:
  TST.w D0 : BEQ mys
    MOVE.w mousespeedy1(PC),D0
    BRA.s swapys
  mys:
  MOVE.w mousespeedy(PC),D0
  swapys:
  SWAP D0
  CLR.w D0
RTS

.do_Pointer: ; a2: current used coplist
  MOVE.l A2,usedCopList
  LSR.w #4,D0

  TST.w D2 : BEQ _pointer
    MOVE.w D0,spriteNum1
    MOVE.w D1,spriteChannel1
    RTS
  _pointer:
    MOVE.w D0,spriteNum
    MOVE.w D1,spriteChannel
RTS

.do_RemovePointer:
  MOVE.w #$ffff,spriteNum
  MOVE.w #$ffff,spriteNum1
RTS

.do_MouseArea:
  TST.w D4 : BEQ marea
    MOVE.w D0,area_minx1
    MOVE.w D1,area_miny1
    MOVE.w D2,area_maxx1
    MOVE.w D3,area_maxy1
    RTS 
  marea:
  MOVE.w D0,area_minx
  MOVE.w D1,area_miny
  MOVE.w D2,area_maxx
  MOVE.w D3,area_maxy
RTS

.do_Mouse:
  TST.w D0 : BEQ.w turn_off
    MOVE.b JOY0DAT,oldHorizCnt
    MOVE.b JOY0DATB,oldVertCnt
    MOVE.b JOY1DAT,oldHorizCnt1
    MOVE.b JOY1DATB,oldVertCnt1

    MOVE.w #$8015,D0                ; VBlank interrupt (5), other bits will be ANDed
    MOVE.l #interruptCode,D1
    BLibJsr #addanint               ; $C100 (d1: code, d0: level)
    RTS
  turn_off:
    MOVE.w #$8015,D0                ; VBlank interrupt (5)
    BLibJsr #clranint               ; $C101
RTS

.interruptCode:
  MOVEM.l D2-D7/A2-A4,-(A7)

  ;-- get data for Port 0
  MOVE.b JOY0DATB,D1
  MOVE.b oldHorizCnt(PC),D0
  MOVE.b D1,oldHorizCnt
  SUB.b D0,D1 : EXT.w D1 : MOVE.w D1,mousespeedx

  MOVE.w mouseposx(PC),D0 : ADD.w D1,D0
  CMP.w area_minx(PC),D0 : BGE.w LAB_001D
    MOVE.w area_minx(PC),D0
    BRA.w LAB_001E
  LAB_001D:
    CMP.w area_maxx(PC),D0 : BLT.w LAB_001E
      MOVE.w area_maxx(PC),D0
      SUBQ.w #1,D0
  LAB_001E:
  MOVE.w D0,mouseposx

  MOVE.b JOY0DAT,D1
  MOVE.b oldVertCnt(PC),D0
  MOVE.b D1,oldVertCnt
  SUB.b D0,D1 : EXT.w D1 : MOVE.w D1,mousespeedy

  MOVE.w mouseposy(PC),D0 : ADD.w D1,D0
  CMP.w area_miny(PC),D0 : BGE.w LAB_001F
    MOVE.w area_miny(PC),D0
    BRA.w LAB_0020
  LAB_001F:
    CMP.w area_maxy(PC),D0 : BLT.w LAB_0020
      MOVE.w area_maxy(PC),D0
      SUBQ.w #1,D0
  LAB_0020:
  MOVE.w D0,mouseposy

  ;--  get data for Port 1
  MOVE.b JOY1DATB,D1
  MOVE.b oldHorizCnt1(PC),D0
  MOVE.b D1,oldHorizCnt1
  SUB.b D0,D1 : EXT.w D1 : MOVE.w D1,mousespeedx1

  MOVE.w mouseposx1(PC),D0 : ADD.w D1,D0
  CMP.w area_minx1(PC),D0 : BGE.w LAB_001Db
    MOVE.w area_minx1(PC),D0
    BRA.w LAB_001Eb
  LAB_001Db:
    CMP.w area_maxx1(PC),D0 : BLT.w LAB_001Eb
      MOVE.w area_maxx1(PC),D0
      SUBQ.w #1,D0
  LAB_001Eb:
  MOVE.w D0,mouseposx1

  MOVE.b JOY1DAT,D1
  MOVE.b oldVertCnt1(PC),D0
  MOVE.b D1,oldVertCnt1
  SUB.b D0,D1 : EXT.w D1 : MOVE.w D1,mousespeedy1

  MOVE.w mouseposy1(PC),D0 : ADD.w D1,D0
  CMP.w area_miny1(PC),D0 : BGE.w LAB_001Fb
    MOVE.w area_miny1(PC),D0
    BRA.w LAB_0020b
  LAB_001Fb:
    CMP.w area_maxy1(PC),D0 : BLT.w LAB_0020b
      MOVE.w area_maxy1(PC),D0
      SUBQ.w #1,D0
  LAB_0020b:
  MOVE.w D0,mouseposy1

  ;-- update mouse sprites if activated via Pointer-command
  MOVE.w spriteNum(PC),D0 : BMI.w LAB_0021
    MOVE.w mouseposx(PC),D1
    MOVE.w mouseposy(PC),D2
    MOVE.w spriteChannel(PC),D3      ; spritechannel

    MOVEA.l storeA5(PC),A5
    MOVE.w ptr_blitzcoplib(PC),D4
    MOVE.w #$4000,INTENA             ; clr bit 14 of INTENA: disable MASTER Interrupt
    MOVE.l 0(A5,D4.w),-(A7)
    MOVE.l usedCopList(PC),0(A5,D4.w)

    BLibJsr #spriteslib LSL 7 + 2 ;  $3802

    MOVE.w ptr_blitzcoplib(PC),D4
    MOVE.l (A7)+,0(A5,D4.w)
    MOVE.w #$c000,INTENA            ; set bit 14 of INTENA: enable MASTER Interrupt
  LAB_0021:

  MOVE.w spriteNum1(PC),D0 : BMI.w LAB_0021b
    MOVE.w mouseposx1(PC),D1
    MOVE.w mouseposy1(PC),D2
    MOVE.w spriteChannel1(PC),D3      ; spritechannel

    MOVEA.l storeA5(PC),A5
    MOVE.w ptr_blitzcoplib(PC),D4
    MOVE.w #$4000,INTENA             ; clr bit 14 of INTENA: disable MASTER Interrupt
    MOVE.l 0(A5,D4.w),-(A7)
    MOVE.l usedCopList(PC),0(A5,D4.w)

    BLibJsr #spriteslib LSL 7 + 2 ;  $3802

    MOVE.w ptr_blitzcoplib(PC),D4
    MOVE.l (A7)+,0(A5,D4.w)
    MOVE.w #$c000,INTENA            ; set bit 14 of INTENA: enable MASTER Interrupt
  LAB_0021b:


  MOVEM.l (A7)+,D2-D7/A2-A4
  MOVEQ #0,D0
RTS

; ===================================================================================================

errorhandling: 

errSprite: MOVE.l #errmsprite,D0 : TRAP #0
errSlice: MOVE.l #errmslice,D0 : TRAP #0
errChannel: MOVE.l #errmchannel,D0 : TRAP #0
errPort: MOVE.l #errmport,D0 : TRAP #0

errmsprite:  Dc.b "Uninitialized Sprite",0,0
errmslice:   Dc.b "Uninitialized Slice",0
errmchannel: Dc.b "Sprite Channel Unavailable",0,0
errmport:    Dc.b "Invalid Port",0,0

chkport:
 TST.w D0 : BEQ.w skip_chk
 CMPI.w #$0001,D0 : BEQ.w skip_chk
  MOVE.l #errPort,D0 : TRAP #0
 skip_chk:
RTS

.chksprite:
  TST.l (A3) : BEQ.w errSprite
  TST.w (A2) : BEQ.w errSlice

  MOVE.w D1,D7 
  ADD.w 6(A3),D7
  CMP.w 6(A2),D7 : BHI.w errChannel
RTS
