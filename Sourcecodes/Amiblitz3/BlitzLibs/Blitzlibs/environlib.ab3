; XTRA
; Embedded .xtra Header
; 
; General Info
; -------------------------------------------------------
; ExePath         = "Ram Disk:"
; ExeFile         = "environlib.obj"
; CreateIcon      = 0
; Residents       = "libmacs.res,blitzoffs.res,all.res"
; 
; Compiler
; -------------------------------------------------------
; StringBuffer    = 10240
; MakeSmallest    = 1
; FuncOptimize    = 1
; Version         = 0.0.0
; NumberOfBuilds  = 22
; 
; Debugger
; -------------------------------------------------------
; CliArgs         = ""
; StackSize       = 8191
; RuntimeDebug    = 0
; DebugInfo       = 0
; CreateDbgFile   = 0
; OverflowCheck   = 1
; AssemblerCheck  = 0
; InterruptCheck  = 1
; AutoRun         = 1
; 
; Editor
; -------------------------------------------------------
; CursorLine      = 126
; CursorColumn    = 94
; LabelSearch     = "a6"
; LabelRemark     = 25
; LabelAll        = 20
; LabelPosition   = 0
; 
; Blitz Objects
; -------------------------------------------------------
; Max File        = 5
; Max GadgetList  = 5
; Max Shape       = 100
; Max Bank        = 5
; Max MenuList    = 5
; Max BlitzFont   = 4
; Max GTList      = 5
; Max Palette     = 4
; Max BitMap      = 10
; Max Screen      = 5
; Max IntuiFont   = 5
; Max Window      = 5
; Max IconInfo    = 1
; Max MUIObject   = 50
; Max AsyncReq    = 4
; Max Req-Lib     = 5
; Max GTMenuList  = 5
; Max Console     = 5
; Max TCPSock     = 5
; Max Tape        = 5
; Max Sound       = 10
; Max MedModule   = 8
; Max Buffer      = 10
; Max Queue       = 10
; Max Sprite      = 20
; Max Module      = 5
; Max Slice       = 10
; Max Page        = 4
; Max CopList     = 10
; Max PTModule    = 5
; Max Anim        = 100
; Max NChunky     = 50
; Max Chunky      = 20
; Max Stencil     = 5
; Max XBSound     = 10
; /XTRA
;
; ********* ALRIGHTY - A VAL( FUNCITON
;
;Nov14th ,2022
; moved GetD0,PutD0,GetRegD0,PutRegD0 from wzlib and editlib in this lib

;Nov25th, 2019
; added ProcessorFPU command to get available FPU
;Aug28th
; fixed WBDepth cos getscreen data was only fetching 84 bytes
; add 060 /FPU values

!libheader{#environlib,init,0,finit,0}

!afunction{3}
!args
!libs
!subs{dowbwidth,0,0}
!name{"WBWidth","returns the width of the WB-screen"}

!afunction{3}
!args
!libs
!subs{dowbhite,0,0}
!name{"WBHeight","returns the height of the WB-screen"}

!afunction{2}
!args
!libs
!subs{dowbdepth,0,0}
!name{"WBDepth","returns the colour-depth of the WB-screen"}

!afunction{2}
!args
!libs
!subs{dowbviewmode,0,0}
!name{"WBViewMode",""}

!afunction{2}
!args
!libs
!subs{doprocessor,0,0}
!name{"Processor","0=68000,1=68010,2=68020,3=68030,4=68040,5=68060"}

!afunction{3}
!args
!libs
!subs{do_checkvbr,0,0}
!name{"VBRcheck","checks if the vector base register (VBR) has been moved to fastram (returns the current vbr address)"}

!afunction{2}
!args
!libs
!subs{doexecversion,0,0}
!name{"ExecVersion","returns the version of exec.library"}

!afunction{2}
!args
!libs
!subs{doextwbviewmode,0,0}
!name{"WBExtViewMode",""}

!afunction{2}
!args
!libs
!subs{doprocessorfpu,0,0}
!name{"ProcessorFPU","0=none, 1=68881, 2=68882, 3=68040, 4=68060"}

!afunction{3}
!args
!libs
!subs{do_movevbr,0,0}
!name{"VBRmove","moves the vector base register (VBR) to fastram, if not already done"}

!afunction{2}
!args
!libs
!subs{do_clearvbr,0,0}
!name{"VBRclear","moves the VBR back to address 0"}

!astatement
!args {#long}
!libs
!subs {0,0,0}
!args {#long,#long}
!libs
!subs {0,0,0}
!args {#long,#long,#long}
!libs
!subs {0,0,0}
!name {"GetD0","variable,.. ;put variables to d0-d2 (a2 = newtypeaddr)"}

!afunction {#long}
!args
!libs
!subs {0,0,0}
!name {"PutD0","Put d0 to variable, e.g.  var = PutD0"}

!astatement
!args {#long}
!libs
!subs {0,0,0}
!args {#long,#long}
!libs
!subs {0,0,0}
!args {#long,#long,#long}
!libs
!subs {0,0,0}
!name {"GetRegD0","variable,.. ;put variables to d0-d2 (a2 = newtypeaddr)"}

!afunction {#long}
!args
!libs
!subs {0,0,0}
!name {"PutRegD0","Put d0 to Variable, e.g. var = PutD0"}

!astatement
!args {#long}
!libs 
!subs {do_pushD0,0,0}
!name {"PushD0","Push register d0 to stack"}


;*********************************************************

init: !nullsub{codeinit,0,0,#intuitionlib,#la6} ; intuition library is NOT needed ? why this dependency?
finit:!nullsub{codefinit,0,0}
!libfin

;********************* CODE BELOW ************************

scdata   :Ds.b  346  ;new screen size

wbwidth_ :Dc.l 0
wbhite_  :Dc.l 0
wbview_  :Dc.l 0
wbdepth_ :Dc.w 0

cpu_     :Dc.w 0  ;0=68000,1= 68010, 2=68020, 3=68030, 4=68040, 5=68060
fpu_     :Dc.w 0  ;0=none, 1= 68881, 2=68882, 3=68040, 4=68060
vbr_     :Dc.l 0
execver_ :Dc 0

.codeinit
  MOVE.l 4,a0
  MOVE 20(a0),execver_
  LEA scdata(pc),a0
  MOVE.l #346,d0
  MOVEQ #1,d1
  SUB.l a1,a1
  JSR -426(a6)                                        ; _SetMode
  LEA scdata(pc),a0
  MOVE 12(a0),wbwidth_+2
  MOVE 14(a0),wbhite_+2
  MOVE.l 88(a0),a1
  MOVE.b 5(a1),wbdepth_+1
  MOVE.l 44+32(a0),wbview_

  ;-- cpu / fpu checks
  MOVE.l 4,a0
  MOVE 296(a0),d0                                     ; AttnFlags of execbase

  BTST #7,d0
  BNE `cpu_68060

  MOVE d0,-(a7)
  AND #15,d0
  BSR `get_highest_bit
  MOVE d0,cpu_

  MOVE.w (a7)+,d0
  LSR #4,d0
  AND #7,d0
  BSR `get_highest_bit
  MOVE d0,fpu_
  BRA `cpu_fpu_done

 `cpu_68060:
  MOVE #5, cpu_
  MOVE #4, fpu_
  BRA `cpu_fpu_done

 `get_highest_bit:
  MOVEQ #0,d1
 `loop_highest_bit
  TST d0
  BEQ `done_highest_bit
    ADDQ #1,d1
    LSR #1,d0
    BRA `loop_highest_bit
 `done_highest_bit
  MOVE.l d1, d0
  RTS
 `cpu_fpu_done:
                                                                                                                                                                                                                                                        

  ;-- check vbr
  JSR do_checkvbr
RTS

.codefinit:
RTS


.getvbr:
  MOVE.l a5,a0
  Dc $4e7a,$2801  ;movec vbr,d7
RTE

.setvbr:
  MOVE.l a5,a0
  Dc $4e7b,$7801 ;movec d7,vbr
RTE


.dowbwidth
  MOVE.l wbwidth_(pc),d0
RTS

.dowbhite
  MOVE.l wbhite_(pc),d0
RTS

.dowbdepth
  MOVE wbdepth_(pc),d0
RTS

.dowbviewmode
  MOVE.w wbview_(pc),d0
RTS

.doextwbviewmode
  MOVE.w wbview_+2(pc),d0
RTS

.doprocessor
  MOVE cpu_(pc),d0
RTS

.doprocessorfpu
  MOVE fpu_(pc),d0
RTS

.do_checkvbr:
  MOVEQ #0,d0
  MOVE.w cpu_(pc),d1
  CMP #1,d1 : BCS 'skipcheck                          ; check at least 68010
    MOVEQ #0,d7
    MOVEA.l 4,A6
    JSR -150(A6)      ;_LVOSuperState
    Dc $4E7A,$7801    ;movec vbr,D7
    JSR -156(A6)      ;_LVOUserState
    MOVE.l d7,vbr_
    MOVE.l d7,d0
 'skipcheck:
RTS

.do_movevbr:
  MOVE.w cpu_(pc),d1
  CMP #1,d1 : BCS 'skipmove
    MOVE.l vbr_(pc),d0
    TST.l d0
    BNE.s 'skipmove
      MOVE.l 4,A6
      MOVE.l #$400,d5
      MOVE.l d5,d0
      MOVEQ  #0,d1      ; system will use fastmem by default if available
      JSR -198(a6)      ; AllocMem_
      MOVE.l d0,d7
      BEQ.s 'skipmove
        SUB.l a0,a0     ; source: 0
        MOVE.l d0,a1    ; dest: allocated memory
        MOVE.l d5,d0    ; memsize
        JSR -624(A6)    ;_LVOCopyMem (A0:source, A1:dest, D0:size)
        JSR -150(A6)    ;_LVOSuperState
        Dc $4E7B,$7801  ;movec d7,vbr
        JSR -156(A6)    ;_LVOUserState
        MOVE.l d7,vbr_
        MOVE.l d7,d0
   'skipmove
RTS

.do_clearvbr:
  MOVE.w cpu_(pc),d1
  CMP #1,d1 : BCS 'notmoved
    MOVE.l vbr_(pc),d7
    TST.l d7
    BEQ.s 'notmoved
      MOVEA.l 4,A6
      MOVEQ #0,d7
      JSR -150(A6)      ;_LVOSuperState
      Dc $4E7B,$7801    ;movec d7,vbr
      JSR -156(A6)      ;_LVOUserState
      MOVE.l vbr_(pc),A1
      MOVE.l #$400,d0
      JSR -210(a6)      ;freemem(block, bytesize)
      MOVE.l #0,vbr_
      MOVEQ #1,d0
 'notmoved
  MOVEQ #0,d0
RTS

.doexecversion
  MOVE execver_(pc),d0
RTS



.do_pushD0:
  Dc.w $a000,'e-'s
 's:
  MOVE.l d0,-(a7)
 'e:
;rts   ?? missing ??

;******************** ERROR CHECKING *********************

errors

