
!libheader {#speaklib,init,0,finit,0}

!astatement
!args {#string}
!libs
!subs {_speak,0,0}
!name {"Speak","string"}

!astatement
!args {#word, #word, #word, #word, #word, #word}
!libs
!subs {_setvoice,0,0}
!name {"SetVoice","rate(150),pitch(110),expression(1),sex,volume(64),freq(22200)"}

!afunction {#string}
!args {#string}
!libs
!subs {_translate,0,0}
!name {"Translate$","returns phoneme translation of a string"}

!astatement
!args {#string}
!libs
!subs {_phospeak,0,0}
!name {"PhoneticSpeak","phonetic string"}

!afunction {#long}
!args
!libs
!subs {_voiceloc,0,0}
!name {"VoiceLoc","returns location of newvoice struct"}

init:       !nullsub{libinit,0,0}
finit:      !nullsub{libfinit,0,0}

!libfin

_voiceloc:  MOVE.l  #v37,d0
            RTS

_setvoice:  CMP     #40,d0      ;clip all parameters
            BPL     n1
            MOVE    #40,d0
n1:         CMP     #400,d0
            BMI     n2
            MOVE    #400,d0
n2:         CMP     #65,d1
            BPL     n3
            MOVE    #65,d1
n3:         CMP     #320,d1
            BMI     n4
            MOVE    #320,d1
n4:         AND     #1,d2
            AND     #1,d3
            CMP     #64,d4
            BMI     n5
            MOVE    #64,d4
n5:         CMP     #5000,d5
            BPL     n6
            MOVE    #5000,d5
n6:         CMP     #28000,d5
            BMI     n7
            MOVE    #28000,d5
n7:         MOVEM   d0-d3,rate
            MOVEM   d4-d5,vol
            RTS

_speak:     MOVE.l  d0,a0         ;d0=string
            MOVE.l  -(a2),d0      ;-(a2)=string length
test:       MOVE.l  a6,-(a7)     ;save a6
            LEA     spkbuff(pc),a1
            MOVE.l  #1024,d1
            MOVE.l  TransBase(pc),a6
            JSR     -30(a6)       ;translate text to buffer

            TST     initial
            BEQ     nowait
            MOVE.l  4,a6          ;don't hassle me
            LEA     IOReq(pc),a1
            JSR     _WaitIO(a6)
nowait:
            MOVEQ   #0,d0
            LEA     spkbuff(pc),a0
srchnull:   ADDQ    #1,d0
            TST.b   (a0)+
            BNE     srchnull
            MOVE.w  #3,_SCommand  ;send message to narrator
            MOVE.l  #0,_Offset
            MOVE.l  #spkbuff,_SData
            MOVE.l  d0,_SLen
            MOVE.l  4,a6
            LEA     IOReq(pc),a1
            JSR     _SendIO(a6)
            MOVE    #-1,initial
            MOVE.l  (a7)+,a6      ;restore a6
            RTS

initial:    Dc.w      0

_translate: MOVE.l  d0,a3         ;take string
            MOVE.l  d0,a0
            MOVE.l  -(a2),d0
            MOVE.l  a6,-(a7)
            LEA     spkbuff(pc),a1
            MOVE.l  #1024,d1
            MOVE.l  TransBase(pc),a6
            JSR     -30(a6)
            MOVE.l  (a7)+,a6
            MOVEQ   #0,d0
            LEA     spkbuff(pc),a0
scrhnull2:  ADDQ    #1,d0
            MOVE.b  (a0)+,(a3)+
            BNE     scrhnull2
            RTS

_phospeak:  MOVE.l  a6,-(a7)      ;send string straight
            MOVE.l  d0,a3         ;to narrator

            TST     initial
            BEQ     nowait2
            MOVE.l  4,a6          ;don't hassle me
            LEA     IOReq(pc),a1
            JSR     _WaitIO(a6)
nowait2:
            MOVE.l  a3,d0
            MOVE.w  #3,_SCommand
            MOVE.l  #0,_Offset
            MOVE.l  d0,_SData
            MOVE.l  -(a2),_SLen
            MOVE.l  4,a6
            LEA     IOReq(pc),a1
            JSR     _SendIO(a6)
            MOVE    #-1,initial
            MOVE.l  (a7)+,a6
            RTS

libinit:    LEA     Translator(pc),a1 ;Open Translator Library
            MOVE.l  4,a6
            MOVEQ   #33,d0
            JSR     _OpenLibrary(a6)
            MOVE.l  d0,TransBase
            BEQ     failed

            LEA     Narrator(pc),a0   ;OpenNarrator device
            MOVEQ   #0,d0
            LEA     IOReq(pc),a1
            MOVEQ   #0,d1
            JSR     _OpenDevice(a6)
            TST.l   d0
            BNE     failed

            MOVEQ   #-1,d0             ;allocate a signal
            JSR     _AllocSignal(a6)
            MOVE.b  d0,_SigBit
            TST.l   d0
            BMI     failed

            SUB.l   a1,a1             ;a1=0
            JSR     _FindTask(a6)
            MOVE.l  d0,_Task
            MOVE.l  #audchan,ch_masks
            MOVE.w  #4,nm_masks
            MOVE.b  #0,mouths
            RTS

failed:     MOVE.l  #report,d0
            TRAP    #0

report:     Dc.b    "Failed To Open Speech Library",0

libfinit:   MOVE.l  a6,-(a7)
            MOVE.l  4,a6
            TST     initial
            BEQ     nowait3
            LEA     IOReq(pc),a1
            JSR     _WaitIO(a6)
nowait3:    LEA     IOReq(pc),a1
            JSR     _CloseDevice(a6)
            MOVEQ   #0,d0             ;free signal bit
            MOVE.b  _SigBit,d0
            JSR     _FreeSignal(a6)
            MOVE.l  TransBase,a1      ;close translator library
            JSR     _CloseLibrary(a6)
            MOVE.l  (a7)+,a6
            RTS

Translator: Dc.b    "translator.library",0
            Even
Narrator:   Dc.b    "narrator.device",0
            Even
TransBase:  Dc.l    0

IOReq:      Dc.l    0,0                 ;message
            Dc.b    5,0
            Dc.l    0
            Dc.l    StdMsgPort
MsgLen:     Dc.w    IOEnd-IOReq

_SDev:      Dc.l    0,0                 ;device unit
_SCommand:  Dc.w    0
            Dc.b    0,0                 ;flags, error
            Dc.l    0                   ;actual
_SLen:      Dc.l    0
_SData:     Dc.l    0
_Offset:    Dc.l    0

rate:       Dc.w    0
pitch:      Dc.w    0
mode:       Dc.w    0
sex:        Dc.w    0
ch_masks:   Dc.l    0
nm_masks:   Dc.w    0
vol:        Dc.w    0
sampfreq:   Dc.w    0
mouths:     Dc.b    0
chanmask:   Dc.b    0
numchan:    Dc.b    0
v37:        Dcb.b   19,0

IOEnd:

audchan:    Dc.b    3,5,10,12

StdMsgPort: Dc.l    0,0                 ;node
            Dc.b    4,0                 ;type,pri
            Dc.l    0
_Flags:     Dc.b    0
_SigBit:    Dc.b    0
_Task:      Dc.l    0
_List:      Dc.l    _List+4,0,_List
            Dc.b    0,0                 ;Type,pad

            Even

spkbuff:    Dcb.b   4096,0

