; XTRA
; Embedded .xtra Header
; 
; General Info
; -------------------------------------------------------
; ExePath         = "Development:AmiBlitz3/Sourcecodes/Examples/OS library examples/BOOPSI"
; ExeFile         = "Empty"
; CreateIcon      = 0
; Residents       = "all.res"
; 
; Compiler
; -------------------------------------------------------
; StringBuffer    = 100000
; MakeSmallest    = 1
; FuncOptimize    = 1
; Version         = 0.0.0
; NumberOfBuilds  = 88
; 
; Debugger
; -------------------------------------------------------
; CliArgs         = ""
; StackSize       = 8192
; RuntimeDebug    = 1
; DebugInfo       = 0
; CreateDbgFile   = 0
; OverflowCheck   = 0
; AssemblerCheck  = 0
; InterruptCheck  = 1
; AutoRun         = 1
; 
; Editor
; -------------------------------------------------------
; CursorLine      = 146
; CursorColumn    = 38
; LabelSearch     = "*lib"
; LabelRemark     = 0
; LabelAll        = 1
; LabelCase       = 0
; LabelPosition   = 0
; 
; Blitz Objects
; -------------------------------------------------------
; Max File        = 100
; Max GadgetList  = 100
; Max Shape       = 200
; Max Bank        = 1
; Max MenuList    = 100
; Max GTList      = 100
; Max Palette     = 4
; Max BitMap      = 10
; Max Screen      = 5
; Max IntuiFont   = 10
; Max Window      = 100
; Max BlitzFont   = 1
; Max IconInfo    = 1
; Max MUIObject   = 50
; Max AsyncReq    = 4
; Max Req-Lib     = 5
; Max GTMenuList  = 100
; Max Console     = 5
; Max TCPSock     = 5
; Max Tape        = 5
; Max TagList     = 5
; Max Database    = 16
; Max Sound       = 10
; Max MedModule   = 8
; Max Buffer      = 10
; Max Queue       = 10
; Max Sprite      = 20
; Max Module      = 5
; Max Slice       = 10
; Max Page        = 4
; Max CopList     = 10
; Max PTModule    = 5
; Max Anim        = 10
; Max NChunky     = 50
; Max Chunky      = 20
; Max Stencil     = 5
; Max XBSound     = 10
; /XTRA
FindScreen 0:WBStartup : NoCli

;INCDIR "blitz2:includes/"
XINCLUDE "includes/textfield.gadget.bb2"
XINCLUDE "error.include.ab3"

*lib.Library = OpenLibrary_("sys:classes/gadgets/textfield.gadget",0)
If *lib=0
   Request "Error","Textfield.Gadget not found !","Ok"
   End
EndIf

*sc.Screen = LockPubScreen_(0)
If *sc
  *dra.DrawInfo = GetScreenDrawInfo_ (*sc)
  If *dra
;----------- get size of sizegadget ------------------------

wd.l=18
wh.l=11

Dim siztags.TagItem(3)
 siztags(0)\ti_Tag=#SYSIA_Which,#SIZEIMAGE
 siztags(1)\ti_Tag=#SYSIA_DrawInfo,*dra
 siztags(2)\ti_Tag=#TAG_END,0

 *sgad.Gadget=NewObjectA_(0,"sysiclass",&siztags(0))

 GetAttr_ #IA_Width,*sgad,&wd
 GetAttr_ #IA_Height,*sgad,&wh
 DisposeObject_ *sgad

arrowwidth.l=10
arrowheight.l=11

Dim arrowtags.TagItem(3)
 arrowtags(0)\ti_Tag=#SYSIA_Which,#UPIMAGE
 arrowtags(1)\ti_Tag=#SYSIA_DrawInfo,*dra
 arrowtags(2)\ti_Tag=#TAG_END,0

 *argad.Gadget=NewObjectA_(0,"sysiclass",&arrowtags(0))

 GetAttr_ #IA_Width,*argad,&arrowwidth
 GetAttr_ #IA_Height,*argad,&arrowheight
 DisposeObject_ *argad

;------------- maps ---------------------------------------
Dim propmap1.TagItem(2)
 propmap1(0)\ti_Tag=#PGA_Top,#TEXTFIELD_Top
 propmap1(1)\ti_Tag=#TAG_END,0

Dim propmap2.TagItem(4)
 propmap2(0)\ti_Tag=#TEXTFIELD_Top,#PGA_Top
 propmap2(1)\ti_Tag=#TEXTFIELD_Visible,#PGA_Visible
 propmap2(2)\ti_Tag=#TEXTFIELD_Lines,#PGA_Total
 propmap2(3)\ti_Tag=#TAG_END,0

Dim uptoedit.TagItem(2)
 uptoedit(0)\ti_Tag=#GA_ID,#TEXTFIELD_Up
 uptoedit(1)\ti_Tag=#TAG_END,0

Dim downtoedit.TagItem(2)
 downtoedit(0)\ti_Tag=#GA_ID,#TEXTFIELD_Down
 downtoedit(1)\ti_Tag=#TAG_END,0

;------------------- arrow up ------------------------------------
Dim uptags.TagItem(3)
 uptags(0)\ti_Tag=#SYSIA_Which,#UPIMAGE
 uptags(1)\ti_Tag=#SYSIA_DrawInfo,*dra
 uptags(2)\ti_Tag=#TAG_END,0

 *gad.Gadget=NewObjectA_(0,"sysiclass",&uptags(0))

Dim knop.TagItem(8)
 knop(0)\ti_Tag=#GA_Top,*sc\RastPort\TxHeight + *sc\WBorTop + 1
 knop(1)\ti_Tag=#GA_RelRight,-wd+1
 knop(2)\ti_Tag=#GA_Image,*gad
 knop(3)\ti_Tag=#GA_RightBorder,1
 knop(4)\ti_Tag=#GA_RelVerify,1
 knop(5)\ti_Tag=#ICA_MAP,&uptoedit(0)
 knop(6)\ti_Tag=#GA_ID,3
 knop(7)\ti_Tag=#TAG_END,0

 *button.Gadget=NewObjectA_(0,"buttongclass",&knop(0))

;------------------- arrow down ------------------------------------
Dim downtags.TagItem(3)
 downtags(0)\ti_Tag=#SYSIA_Which,#DOWNIMAGE
 downtags(1)\ti_Tag=#SYSIA_DrawInfo,*dra
 downtags(2)\ti_Tag=#TAG_END,0

 *gad1.Gadget=NewObjectA_(0,"sysiclass",&downtags(0))

Dim knop1.TagItem(9)
 knop1(0)\ti_Tag=#GA_Top,*sc\RastPort\TxHeight + *sc\WBorTop + 1 + *button\Height
 knop1(1)\ti_Tag=#GA_RelRight,-wd + 1
 knop1(2)\ti_Tag=#GA_Image,*gad1
 knop1(3)\ti_Tag=#GA_RightBorder,1
 knop1(4)\ti_Tag=#GA_RelVerify,1
 knop1(5)\ti_Tag=#ICA_MAP,&downtoedit(0)
 knop1(6)\ti_Tag=#GA_Previous,*button
 knop1(7)\ti_Tag=#GA_ID,4
 knop1(8)\ti_Tag=#TAG_END,0

 *button1.Gadget=NewObjectA_(0,"buttongclass",&knop1(0))

;------------------ propgad -----------------------
Dim prop.TagItem(15)
 prop(0)\ti_Tag =#GA_ID,1
 prop(1)\ti_Tag =#GA_Top,*sc\RastPort\TxHeight + *sc\WBorTop + 4 + *button\Height + *button1\Height
 prop(2)\ti_Tag =#GA_RelRight,-wd+5
 prop(3)\ti_Tag =#GA_Width,wd-8
 prop(4)\ti_Tag =#GA_RelHeight,-(*sc\WBorTop+*sc\Font\ta_YSize + wh + *button\Height + *button1\Height+8)+1
 prop(5)\ti_Tag =#GA_DrawInfo,*dra
 prop(6)\ti_Tag =#ICA_MAP,&propmap1(0)
 prop(7)\ti_Tag =#PGA_Total,50
 prop(8)\ti_Tag =#PGA_NewLook,1
 prop(9)\ti_Tag =#PGA_Visible,1
 prop(10)\ti_Tag =#PGA_Freedom,#FREEVERT
 prop(11)\ti_Tag=#PGA_Borderless,1
 prop(12)\ti_Tag=#GA_RightBorder,1
 prop(13)\ti_Tag=#GA_Previous,*button1
 prop(14)\ti_Tag=#TAG_END,0

*pgad.Gadget=NewObjectA_(0,"propgclass",&prop(0))
If *pgad

;------------------- textfieldgadget ------------------------------
Dim txtf.TagItem(16)
 txtf(0)\ti_Tag =#GA_ID,2
 txtf(1)\ti_Tag =#GA_Top,*sc\WBorTop+*sc\Font\ta_YSize + 10
 txtf(2)\ti_Tag =#GA_Left,10
 txtf(3)\ti_Tag =#GA_RelWidth,-(*sc\WBorRight+10+wd+1)
 txtf(4)\ti_Tag =#GA_RelHeight,-(*sc\RastPort\TxHeight + *sc\WBorTop + (2*wh) + 5)
 txtf(5)\ti_Tag =#ICA_MAP,&propmap2(0)
 txtf(6)\ti_Tag =#ICA_TARGET,*pgad
 txtf(7)\ti_Tag =#GA_Previous,*pgad
 txtf(8)\ti_Tag =#GA_RelVerify,1
 txtf(9)\ti_Tag =#GA_DrawInfo,*dra
 txtf(10)\ti_Tag =#TEXTFIELD_Text,GetGlobalStrAddr("Please enter a long Text !")
 txtf(11)\ti_Tag=#TEXTFIELD_TextAttr,*sc\Font
 txtf(12)\ti_Tag=#TEXTFIELD_Border,#TEXTFIELD_BORDER_BEVEL
 txtf(13)\ti_Tag=#TEXTFIELD_ReadOnly,0
 txtf(14)\ti_Tag=#TEXTFIELD_NoGhost,1
 txtf(15)\ti_Tag=#TAG_END,0

*txtfgad.Gadget = NewObjectA_(*lib,"",&txtf(0))
If *txtfgad

;-------------------- window ---------------------------------------
flags.l=#WFLG_CLOSEGADGET|#WFLG_SIZEBRIGHT|#WFLG_SIZEGADGET|#WFLG_DEPTHGADGET|#WFLG_DRAGBAR|#WFLG_SIZEBBOTTOM
Dim wintags.TagItem(14)
 wintags(0)\ti_Tag = #WA_Left,(*sc\Width/2)-100
 wintags(1)\ti_Tag = #WA_Top,(*sc\Height/2)-80
 wintags(2)\ti_Tag = #WA_Width,220
 wintags(3)\ti_Tag = #WA_Height,200
 wintags(4)\ti_Tag = #WA_Width,200
 wintags(5)\ti_Tag = #WA_Activate,1
 wintags(6)\ti_Tag = #WA_AutoAdjust,1
 wintags(7)\ti_Tag = #WA_CloseGadget,1
 wintags(8)\ti_Tag = #WA_Title,GetGlobalStrAddr("Textfield.gadget")
 wintags(9)\ti_Tag = #WA_IDCMP,#IDCMP_CLOSEWINDOW|#IDCMP_INTUITICKS|#IDCMP_GADGETUP|#IDCMP_REFRESHWINDOW|#IDCMP_GADGETDOWN
 wintags(10)\ti_Tag= #WA_Flags,flags
 wintags(11)\ti_Tag= #WA_MinWidth,100
 wintags(12)\ti_Tag= #WA_Gadgets,*button
 wintags(13)\ti_Tag= #TAG_END,0

*win._Window = OpenWindowTagList_(0,&wintags(0))
If *win

Dim temptag.TagItem(2)
 temptag(0)\ti_Tag=#ICA_TARGET,*txtfgad
 temptag(1)\ti_Tag=#TAG_END,0
 SetGadgetAttrsA_*pgad,*win,0,&temptag(0)
 SetGadgetAttrsA_*button,*win,0,&temptag(0)
 SetGadgetAttrsA_*button1,*win,0,&temptag(0)

loop=1
 While loop
  WaitPort_ *win\UserPort
   *imsg.IntuiMessage = GT_GetIMsg_(*win\UserPort)
    *class = *imsg\Class
     *code = *imsg\Code
    GT_ReplyIMsg_ *imsg
  Select *class
   Case #IDCMP_CLOSEWINDOW
   loop=0
   Case #IDCMP_GADGETUP|#IDCMP_GADGETDOWN
   *gads.Gadget=*imsg\IAddress
    Select *gads\GadgetID
     Case 5
     NPrint "e"
    End Select
  End Select
Wend

RemoveGList_ *win,*button,-1
DisposeObject_ *txtfgad
DisposeObject_ *pgad
DisposeObject_ *button1
DisposeObject_ *gad1
DisposeObject_ *button
DisposeObject_ *gad
CloseWindow_ *win
FreeScreenDrawInfo_ *sc,*dra
UnlockPubScreen_ 0,*sc
CloseLibrary_ *lib
EndIf:EndIf:EndIf:EndIf:EndIf
End

