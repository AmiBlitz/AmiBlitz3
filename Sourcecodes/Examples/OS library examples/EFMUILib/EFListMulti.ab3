; XTRA
; Embedded .xtra Header
; 
; General Info
; -------------------------------------------------------
; ExePath         = ""
; ExeFile         = ""
; CreateIcon      = 1
; Residents       = "all.res"
; 
; Compiler
; -------------------------------------------------------
; StringBuffer    = 10240
; MakeSmallest    = 0
; FuncOptimize    = 1
; Version         = 0.0.0
; NumberOfBuilds  = 3
; 
; Debugger
; -------------------------------------------------------
; CliArgs         = ""
; StackSize       = 8191
; RuntimeDebug    = 1
; DebugInfo       = 0
; CreateDbgFile   = 0
; OverflowCheck   = 0
; AssemblerCheck  = 0
; InterruptCheck  = 1
; AutoRun         = 1
; 
; Editor
; -------------------------------------------------------
; CursorLine      = 124
; CursorColumn    = 53
; LabelSearch     = ""
; LabelRemark     = 0
; LabelAll        = 0
; LabelCase       = 0
; LabelPosition   = 0
; 
; Blitz Objects
; -------------------------------------------------------
; Max File        = 5
; Max GadgetList  = 5
; Max Shape       = 100
; Max Bank        = 5
; Max MenuList    = 5
; Max GTList      = 20
; Max BitMap      = 10
; Max Screen      = 5
; Max IntuiFont   = 5
; Max Window      = 20
; Max Palette     = 32
; Max BlitzFont   = 4
; Max IconInfo    = 1
; Max MUIObject   = 50
; Max AsyncReq    = 4
; Max Req-Lib     = 5
; Max GTMenuList  = 5
; Max Console     = 5
; Max TCPSock     = 5
; Max Tape        = 5
; Max TagList     = 5
; Max Database    = 16
; Max Sound       = 10
; Max MedModule   = 8
; Max Buffer      = 10
; Max Queue       = 10
; Max Sprite      = 20
; Max Module      = 5
; Max Slice       = 10
; Max Page        = 4
; Max CopList     = 10
; Max PTModule    = 5
; Max Anim        = 10
; Max NChunky     = 50
; Max Chunky      = 20
; Max Stencil     = 5
; Max XBSound     = 10
; /XTRA
;******************************************
;* EFListMulti.asc                        *
;* ~~~~~~~~~~~~~~~                        *
;* Shows how to use a multi-columns list. *
;******************************************
;
;(C)opyRight 1998 by Vivid Imagination


NEWTYPE.en              ;This NEWTYPE is used
  *entrie.b[3]          ;to get the entries
End NEWTYPE             ;

DEFTYPE.en *buf
DEFTYPE.l

Dim *en.b(40)        ;*en() contains the pointers to ALL entries
Dim *entr.b(15)      ;*entr() contains the pointers for each line

Dim *en2.b(40)       ;This the second
Dim *entr2.b(15)     ;arrays of entries


n=0
For i=0 To 30 Step 3                                          ;We create pointers
  For j=0 To 2                                                ;to our entries
    *en(i+j)=GetGlobalStrAddr("Coords : ("+Str$(j+1)+","+Str$(n+1)+")")   ;There is no other
    *en2(i+j)=GetGlobalStrAddr("A second list! ("+Str$(j+1)+","+Str$(n+1)+")")
  Next                                                        ;proper way of doing
  *entr(n)=&*en(i)                                            ;multi-columns
  *entr2(n)=&*en2(i)
  n+1
Next

MUIApplicationTitle       "EFListMulti"
MUIApplicationVersion     "$VER: EFListMulti 1.0 (02.03.98)"
MUIApplicationCopyright   "(c)1998, Vivid Imagination"
MUIApplicationAuthor      "Erwan Fouret"
MUIApplicationDescription "An example of Multi-columns list."
MUIApplicationBase        "EFLISTMULTI"

MUIListHook On
MUIAddTags 1,#MUIA_Frame,#MUIV_Frame_InputList
MUIAddTags 1,#MUIA_List_ShowDropMarks,1                                ;This lines must be
MUIAddTags 1,#MUIA_List_DragSortable,1                                 ;removed if you don't
MUIAddTags 2,#MUIA_Listview_DragType,#MUIV_Listview_DragType_Immediate ;want Drag'n'Drop
MUIList 1,"BAR,BAR,",*entr()

MUIListView 2,1

MUIAddTags 3,#MUIA_String_AttachedList,MUIObjLoc(2)   ;<- This tag allows you
MUIString 3,"",100                                    ;to use cursor keys to
                                                      ;browse the list when a
MUIAddTags 4,#MUIA_String_AttachedList,MUIObjLoc(2)   ;String gadget is selected
MUIString 4,"",100

MUIAddTags 5,#MUIA_String_AttachedList,MUIObjLoc(2)
MUIString 5,"",100

MUISimpleButton 6,"_Clear"
MUISimpleButton 7,"_Change"
MUISimpleButton 8,"_Quit"

MUIAddObjsHGroup 0,3,4,5
MUICreateHGroup 0

MUIAddObjsHGroup 9,6,7,8
MUICreateHGroup 9

MUIAddObjsVGroup 10,2,0,9
MUICreateVGroup 10

MUICreateWindow 11,"EFListMulti","EFLM",10
MUIAddSubWindow 11
If MUICreateApplication<>True Then End

MUINotifyApp 1,#MUIA_List_Active,#MUIV_EveryTime,1
MUINotifyApp 3,#MUIA_String_Contents,#MUIV_EveryTime,3
MUINotifyApp 4,#MUIA_String_Contents,#MUIV_EveryTime,4
MUINotifyApp 5,#MUIA_String_Contents,#MUIV_EveryTime,5
MUINotifyApp 6,#MUIA_Pressed,0,6
MUINotifyApp 7,#MUIA_Pressed,0,7
MUINotifyApp 8,#MUIA_Pressed,0,-1
MUINotifyApp 11,#MUIA_Window_CloseRequest,1,-1

dummy.l = MUIOpenWindow(11)

ls=1  ;the acual list set is *en (=>1)

Repeat
  ev.l=MUIWaitEvent
  Select ev
  Case 1
    MUIDoMethod 1,#MUIM_List_GetEntry,#MUIV_List_GetEntry_Active,&*buf
    MUISet 3,#MUIA_String_Contents,*buf\entrie[0]
    MUISet 4,#MUIA_String_Contents,*buf\entrie[1]
    MUISet 5,#MUIA_String_Contents,*buf\entrie[2]
  Case 3
    If ls>0
      act=MUIGet(1,#MUIA_List_Active)
      tstr$=MUIGetString$(3)
      If ls=1
        *en((act)*3)=GetGlobalStrAddr(tstr$)
      Else
        *en2((act)*3)=GetGlobalStrAddr(tstr$)
      EndIf
      MUIRedrawSingle 2,act
    EndIf
  Case 4
    If ls>0
      act=MUIGet(1,#MUIA_List_Active)
      tstr$=MUIGetString$(4)
      If ls=1
        *en((act)*3+1)=GetGlobalStrAddr(tstr$)
      Else
        *en2((act)*3+1)=GetGlobalStrAddr(tstr$)
      EndIf
      MUIRedrawSingle 2,act
    EndIf
  Case 5
    If ls>0
      act = MUIGet(1,#MUIA_List_Active)
      tstr$ = Peek$(MUIGet(5,#MUIA_String_Contents))
      If ls = 1
        *en((act)*3+2)=GetGlobalStrAddr(tstr$)
      Else
        *en2((act)*3+2)=GetGlobalStrAddr(tstr$)
      EndIf
      MUIRedrawSingle 2,act
    EndIf
  Case 6
    MUIChangeList 1                                     ;Clear the list
    ls=-1
    MUISetString 3," "
    MUISetString 4," "
    MUISetString 5," "
  Case 7
    MUIChangeList 1,*entr2(),#MUIV_List_Insert_Bottom   ;Replace the old list
    ls=2
    MUIDoMethod 1,#MUIM_List_GetEntry,#MUIV_List_GetEntry_Active,&*buf
    MUISet 3,#MUIA_String_Contents,*buf\entrie[0]
    MUISet 4,#MUIA_String_Contents,*buf\entrie[1]
    MUISet 5,#MUIA_String_Contents,*buf\entrie[2]
    MUISet 1,#MUIA_List_Active,#MUIV_List_Active_Top
  End Select
Until ev=-1

MUICloseWindow 11

End

