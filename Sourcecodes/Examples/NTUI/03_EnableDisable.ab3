; XTRA
; Embedded .xtra Header
; 
; General Info
; -------------------------------------------------------
; ExePath         = "RAM:"
; ExeFile         = "DisableEnable"
; CreateIcon      = 0
; Residents       = "all.res"
; 
; Compiler
; -------------------------------------------------------
; StringBuffer    = 1024
; MakeSmallest    = 0
; FuncOptimize    = 0
; Version         = 0.0.0
; NumberOfBuilds  = 0
; 
; Debugger
; -------------------------------------------------------
; CliArgs         = ""
; StackSize       = 4000
; RuntimeDebug    = 0
; DebugInfo       = 0
; CreateDbgFile   = 0
; OverflowCheck   = 0
; AssemblerCheck  = -1
; InterruptCheck  = -1
; AutoRun         = -1
; 
; Editor
; -------------------------------------------------------
; CursorLine      = 0
; CursorColumn    = 0
; LabelSearch     = ""
; LabelRemark     = 0
; LabelAll        = 0
; LabelPosition   = 0
; 
; Blitz Objects
; -------------------------------------------------------
; /XTRA
; ======================== NTUI DisableEnable Example ===========================
; 11/27/2014 Thilo Koehler
; Small demo how to manipulate NTUI objects during runtime.
; The demo disabled/enables a group with all its content, and change image and
; text of the toggle button.
; =============================================================================

; Include Dependencies ========================================================
WBStartup
Syntax 6
optimize 7
XINCLUDE "ntui.include.ab3"

; Program Initialization ======================================================
SHARED *ntui.tuiEngine

; build the GUI from XML code, and retrieve the engine object
*ntui = ntui_GetEngine{ntui_BuildFromXMLMem{#NULL, ?layout}}

; if something went wrong, display error message and exit
If (*ntui = Null)
  error{"Unable to create NTUI environment!"}
  Goto exit
End If

; show the main window
; if something went wrong, display error message and exit
If ntui_ShowWindowByID{*ntui, "MainWindow"} = False
  error{"Unable to show MainWindow!"}
  Goto exit
End If

; Notification Loop ===========================================================
ntui_PostNotify{*ntui, "TOGGLE", Null}
Repeat
  ; wait for a notification
  *notify.tuiNotify = ntui_WaitNotify{*ntui}
  
  ; evaluatie notification
  Select ntui_GetNotifyID{*notify}
    Case "QUIT" ; we should quit
      quit.b = True
      
    Case "ICONIFY"
      ntui_Iconify{*ntui}
      
    Case "POPUP"
      ntui_PopUp{*ntui}

    Case "AREXX"
      message{"Got AREXX message: "+ntui_GetNotifyString{*notify}}
      
    Case "TOGGLE"
      enabled.l = 1 - enabled
      If (enabled)
        ntui_SetAttrByID{*ntui, "Toggle", #TUIBA_IMAGE, "help"}
        ntui_SetAttrByID{*ntui, "Toggle", #TUIBA_LABEL, "Disable"}
        ntui_SetAttrByID{*ntui, "Content", #TUIA_DISABLED, False}
      Else
        ntui_SetAttrByID{*ntui, "Toggle", #TUIBA_IMAGE, "helpoff"}
        ntui_SetAttrByID{*ntui, "Toggle", #TUIBA_LABEL, "Enable"}
        ntui_SetAttrByID{*ntui, "Content", #TUIA_DISABLED, True}
      End If
  End Select
Until quit

.exit
; Cleanup =====================================================================
; free all resources allocated by NTUI
ntui_FreeEngine{*ntui}
End

.layout
; GUI Definition as XML =======================================================
Dc.b "<Engine appname='NTUI Application'"
Dc.b "  appIconFile='Application'"
Dc.b "  appIconText='NTUI Application'"
Dc.b "  appMenuText='NTUI Application'"
Dc.b "  AREXXPort='NTUIAPP'"
Dc.b "  onIconify='ICONIFY'"
Dc.b "  onPopup='POPUP'"
Dc.b "  onQuit='QUIT'"
Dc.b "  onMessage='AREXX'"
Dc.b "/>"
Dc.b "<Window id='MainWindow' title='NTUI Application' onClose='QUIT'>"
Dc.b "  <VGroup>"
Dc.b "    <HGroup id='Content'>"
Dc.b "      <Label id='Content1' text='NTUI Application'/>"
Dc.b "      <Button label='Iconify' image='iconify' onClick='ICONIFY'/>"
Dc.b "      <Button label='Quit' image='quit' onClick='QUIT'/>"
Dc.b "    </VGroup>"
Dc.b "    <Button id='Toggle' label='Enable' image='help' onClick='TOGGLE'/>"
Dc.b "  </VGroup>"
Dc.b "</Window>"
Dc.b 0
