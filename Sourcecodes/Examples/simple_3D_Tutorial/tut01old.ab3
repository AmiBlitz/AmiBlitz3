; XTRA
; Embedded .xtra Header
; 
; General Info
; -------------------------------------------------------
; ExePath         = ""
; ExeFile         = ""
; CreateIcon      = 1
; Residents       = ""
; 
; Compiler
; -------------------------------------------------------
; StringBuffer    = 10240
; MakeSmallest    = 0
; FuncOptimize    = 1
; Version         = 0.0.0
; NumberOfBuilds  = 0
; 
; Debugger
; -------------------------------------------------------
; CliArgs         = ""
; StackSize       = 80000
; RuntimeDebug    = 1
; DebugInfo       = 0
; CreateDbgFile   = 0
; OverflowCheck   = 0
; AssemblerCheck  = 0
; InterruptCheck  = 1
; AutoRun         = 1
; 
; Editor
; -------------------------------------------------------
; CursorLine      = 1
; CursorColumn    = 1
; LabelSearch     = ""
; LabelRemark     = 0
; LabelAll        = 0
; LabelPosition   = 0
; 
; Blitz Objects
; -------------------------------------------------------
; Max IconInfo    = 1
; Max NChunky     = 50
; Max MUIObject   = 50
; Max PTModule    = 5
; Max AsyncReq    = 4
; Max Req-Lib     = 5
; Max GTMenuList  = 5
; Max Console     = 5
; Max TCPSock     = 5
; Max XBSound     = 10
; Max Chunky      = 20
; Max File        = 5
; Max GadgetList  = 5
; Max Queue       = 10
; Max Screen      = 5
; Max Shape       = 100
; Max CopList     = 10
; Max Sprite      = 20
; Max Stencil     = 5
; Max Module      = 5
; Max Window      = 20
; Max Anim        = 10
; Max Sound       = 10
; Max Bank        = 5
; Max Buffer      = 10
; Max BitMap      = 10
; Max Slice       = 10
; Max Page        = 4
; Max Tape        = 5
; Max IntuiFont   = 5
; Max MedModule   = 8
; Max Palette     = 4
; Max MenuList    = 5
; Max BlitzFont   = 4
; Max GTList      = 20
; /XTRA

after;

;Hallo Freunde der Freundin!!!
;Es hatte lange gedauert aber ich habe jetzt das 3D-sample f|r euch fertiggestellt.

;Um den Code nicht mit Kommentaren (auch aus |bersichtlichkeitsgr|nden) aufzubldhen
;kvnnt auch die Doku die beiligt lesen. Dort wird alles nochmal erkldrt
;Aber auch in der AmiBlitz-ML kvnnt ihr mich direkt bei Problemen fragen

;Ich beantworte jede Frage
;Merkt euch eins: Es ist noch kein Meister vom Himmel gefallen!

;An dieser Stelle nochmals danke an Bernd Roesch fuer seine Hilfe

;SO, LOS GEHTS!




;DEFTYPE .l

NEWTYPE .vtx
  sx.l;screenX
  sy.l;screenY
  vx.f;pointX
  vy.f;pointY
  vz.f;pointZ
End NEWTYPE

Dim List points.vtx(100)



  Function .s getstring{splitstring.s, pos.l}; Funktion um den String zu teilen
                                             ;ich muss aber sagen: haette man schoener loesen koennen
    st1.l = 0
    st2.l = 0



      st1 = Instr(splitstring, ";", 1);sucht nach dem ersten seperator semikolon

      If st1 > 0 st2 = Instr(splitstring, ";", st1+1);falls in st1 schon eine position drinnsteht st2 fuellen

      Select pos
        Case 1
          Function Return Left$(splitstring, st1-1); gibt den ersten Teil des Strings
        Case 2
          Function Return Right$(Left$(splitstring, st2-1), st2-1-st1); gibt den mittleren Teil
        Case 3

          retStr.s = Right$(splitstring, Len(splitstring) - st2); gint letzten Teil des Strings zurueck

            If Right$(retStr,1) = Chr$(13); hier wird ueberprueft ob ein Enterzeichen (neue Zeile) da ist
              retStr = Left$(retStr, Len(retStr)-1); wenn ja muss es weggeschnitten werden!
            EndIf

          Function Return retStr


    End Select

  End Function


;------------------------------------------------------


  Statement loadModel {filename.s};

    SHARED points()

    If OpenF(filename); unsere Datei


      ClearList points()

      Repeat

        linestream.s = ReadLN ;liest die zeile ein

        If Len(linestream) > 1
          AddLast points()
            points()\vx = Val(getstring{linestream, 1})
            points()\vy = Val(getstring{linestream, 2})
            points()\vz = Val(getstring{linestream, 3})
        EndIf

      Until EOFile

      CloseF IOFile

    EndIf

  End Statement


;------------------------------------------------------


  Statement renderObject{onX.l, onY.l}

    SHARED points()

    For pc.l = 0 To CountItem(points()) - 1

      USEPATH points(pc)

        \sx = ((2000 * \vx) / (\vz + 400)) + onX
        \sy = ((2000 * \vy) / (\vz + 400)) + onY

    Next

  End Statement


;------------------------------------------------------


  Statement drawObject{color.l}


    SHARED points()



      pcount.l = CountItem(points()) - 1


      For pc.l = 1 To pcount

        Wline points(pc-1)\sx, points(pc-1)\sy, points(pc)\sx, points(pc)\sy, color

      Next pc

      Wline points(pcount)\sx, points(pcount)\sy, points(0)\sx, points(0)\sy, color

  End Statement


;------------------------------------------------------


  Statement scaleObject{fX.f, fY.f, fZ.f}

    SHARED points()



    For pc.l = 0 To CountItem(points()) - 1

      USEPATH points(pc)

        If fX <> 0 Then \vx = \vx * fX
        If fY <> 0 Then \vy = \vy * fY
        If fZ <> 0 Then \vz = \vz * fZ

    Next

  End Statement


;------------------------------------------------------


  Statement rotateObject{fX.f, fY.f, fZ.f}

    pifac.f = 3.1415927/180

    SHARED points()


    xcos.f = Cos(fX*pifac)
    ycos.f = Cos(fY*pifac)
    zcos.f = Cos(fZ*pifac)
    xsin.f = Sin(fX*pifac)
    ysin.f = Sin(fY*pifac)
    zsin.f = Sin(fZ*pifac)

    For pc.l = 0 To CountItem(points()) - 1

      USEPATH points(pc)

        If fX <> 0

            oY.f = \vy: oZ.f = \vz

            \vy = (oY * xcos) - (oZ * xsin)
            \vz = (oY * xsin) + (oZ * xcos)


        End If

        If fY <> 0

            oZ.f = \vz: oX.f = \vx

            \vz = (oZ * ycos) - (oX * ysin)
            \vx = (oZ * ysin) + (oX * ycos)

        End If

        If fZ <> 0

            oX = \vx: oY = \vy

            \vx = (oX * zcos) - (oY * zsin)
            \vy = (oX * zsin) + (oY * zcos)

        End If


    Next

  End Statement


;------------------------------------------------------


  Statement translateObject{fX.f, fY.f, fZ.f}

    SHARED points()

    For pc.l = 0 To CountItem(points()) - 1

      USEPATH points(pc)
        \vx = \vx + fX
        \vy = \vy + fY
        \vz = \vz + fZ

    Next

  End Statement


;------------------------------------------------------




  FindScreen 0
  Window 0, 0,0,640,480,$100F,"3D-Sample by Frank Diehl (press mouse to exit)", 120, 120
  Use Screen 0


  loadModel{"mod.txt"}


  scaleObject{10,10,10}


  Repeat

    ;InnerCls 0 ;loescht das fenster, flackert aber!

    drawObject{0} ;alte Zeichnung uebermalen, kostet aber zeit alternativ geht InnerCls 0!

    rotateObject{0.1,-0.3,0}

    translateObject{0, 0, 0.3}

    renderObject{320, 240}

    drawObject{1} ;neu zeichnen


    If Joyb(0)<>0 Then End ;bei mausklick ende

  Forever


