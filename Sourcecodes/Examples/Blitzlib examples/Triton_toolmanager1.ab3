; XTRA
; Embedded .xtra Header
; 
; General Info
; -------------------------------------------------------
; ExePath         = ""
; ExeFile         = ""
; CreateIcon      = 1
; Residents       = "triton.res"
; 
; Compiler
; -------------------------------------------------------
; StringBuffer    = 10240
; MakeSmallest    = 0
; FuncOptimize    = 1
; Version         = 0.0.0
; NumberOfBuilds  = 9
; 
; Debugger
; -------------------------------------------------------
; CliArgs         = ""
; StackSize       = 8191
; RuntimeDebug    = 0
; DebugInfo       = 0
; CreateDbgFile   = 0
; OverflowCheck   = 0
; AssemblerCheck  = 0
; InterruptCheck  = 1
; AutoRun         = 1
; 
; Editor
; -------------------------------------------------------
; CursorLine      = 209
; CursorColumn    = 11
; LabelSearch     = ""
; LabelRemark     = 0
; LabelAll        = 0
; LabelCase       = 0
; LabelPosition   = 0
; 
; Blitz Objects
; -------------------------------------------------------
; Max File        = 5
; Max GadgetList  = 5
; Max Shape       = 100
; Max Bank        = 5
; Max MenuList    = 5
; Max GTList      = 20
; Max Palette     = 4
; Max BitMap      = 10
; Max Screen      = 5
; Max IntuiFont   = 5
; Max BlitzFont   = 4
; Max Window      = 20
; Max IconInfo    = 1
; Max MUIObject   = 50
; Max AsyncReq    = 4
; Max Req-Lib     = 5
; Max GTMenuList  = 5
; Max Console     = 5
; Max TCPSock     = 5
; Max Tape        = 5
; Max TagList     = 5
; Max Sound       = 10
; Max MedModule   = 8
; Max Buffer      = 10
; Max Queue       = 10
; Max Sprite      = 20
; Max Module      = 5
; Max Slice       = 10
; Max Page        = 4
; Max CopList     = 10
; Max PTModule    = 5
; Max Anim        = 10
; Max NChunky     = 50
; Max Chunky      = 20
; Max Stencil     = 5
; Max XBSound     = 10
; /XTRA
;/*
; *  Triton - The object oriented GUI creation system For the AMIGA
; *  Written by Stefan Zeiger in 1993-1995
; *
; *  (c) 1993-1995 by Stefan Zeiger
; *  You are hereby allowed To Use this source OR parts
; *  of it For creating programs For AmigaOS which Use the
; *  Triton GUI creation system. All other rights reserved.
; *
; *  Toolmanager1.c - Looks like the original ToolManager
; *
; */

NoCli:WBStartup

; useful Runtime-Error-Routine which closes all windows if a error
; appears. So you don't need to restart your machine every time :-)

SetErr

  ErrFail
  If project.l Then TR_CloseProject_ project
  If application.l Then TR_DeleteApp_ application
  End

End SetErr

;-- not needed as used a resident file
;INCLUDE "libraries/triton.ab3"

; this newtype is needed for the ListView-Items just as
; for GTListviews.

NEWTYPE .LVItem
  num.w
  text$
End NEWTYPE

Dim cycle_entries.l(8)
Dim List LVNodes.LVItem(9)

InitTagList 1,200           ; this is our window taglist
InitTagList 2,10            ; this is our application taglist

; set the cycle gadget entries

cycle_entries(1)=GetGlobalStrAddr("Exec")
cycle_entries(2)=GetGlobalStrAddr("Image")
cycle_entries(3)=GetGlobalStrAddr("Sound")
cycle_entries(4)=GetGlobalStrAddr("Menu")
cycle_entries(5)=GetGlobalStrAddr("Icon")
cycle_entries(6)=GetGlobalStrAddr("Docs")
cycle_entries(7)=GetGlobalStrAddr("Access")
cycle_entries(8)=0

ResetList LVNodes()

; fill the listview

If AddItem(LVNodes())
  LVNodes()\text="2024View"
  If AddItem(LVNodes())
    LVNodes()\text="Add to archive"
    If AddItem(LVNodes())
      LVNodes()\text="Deletetool"
      If AddItem(LVNodes())
        LVNodes()\text="Edit text"
        If AddItem(LVNodes())
          LVNodes()\text="Env"
          If AddItem(LVNodes())
            LVNodes()\text="Exchange"
            If AddItem(LVNodes())
              LVNodes()\text="Multiview"
            EndIf
          EndIf
        EndIf
      EndIf
    EndIf
  EndIf
EndIf

ResetList LVNodes()

; tell TRITON Prefs what this application is about

Use TagList 2

AddTags #TRCA_Name,GetGlobalStrAddr("ToolManagerGUIDemo1")
AddTags #TRCA_LongName,GetGlobalStrAddr("ToolManager-GUI Demo 1")
AddTags #TRCA_Info,GetGlobalStrAddr("Looks like the original")
AddTags #TAG_END,0

; create our window layout

Use TagList 1

AddTags !WindowID{1},!WindowPosition{#TRWP_BELOWTITLEBAR}
AddTags !WindowTitle{GetGlobalStrAddr("ToolManager GUI demo 1")}
AddTags !WindowFlags{#TRWF_NOSIZEGADGET OR #TRWF_NODELZIP OR #TRWF_NOZIPGADGET OR #TRWF_NOESCCLOSE}
AddTags !WindowBackfillNone

AddTags !VertGroupA
AddTags   !Space
AddTags   !HorizGroupAC
AddTags     !Space
AddTags       !TextID{GetGlobalStrAddr("_Object Type"),1}
AddTags       !Space
AddTags       !CycleGadget{&cycle_entries(1),0,1}
AddTags       !Space
AddTags     !EndGroup
AddTags   !Space

AddTags   !HorizGroupAC
AddTags     !Space
AddTags     !VertGroupAC
AddTags       !CenteredTextID{GetGlobalStrAddr("Object List"),2}
AddTags       !Space

; this seems to be a little hackish as you access directly an
; list index but it works 100%. Don't forget to sub 36!

AddTags       !ListSSCN{&LVNodes(0)-36,2,0,0}
AddTags     !EndGroup

AddTags     !Space
AddTags     !VertGroupA
AddTags       !TextN{GetGlobalStrAddr("")}
AddTags       !Space
AddTags       !Button{GetGlobalStrAddr("Top"),3}
AddTags       !Space
AddTags       !Button{GetGlobalStrAddr("Up"),4}
AddTags       !Space
AddTags       !Button{GetGlobalStrAddr("Down"),5}
AddTags       !Space
AddTags       !Button{GetGlobalStrAddr("Bottom"),6}
AddTags       !Space
AddTags       !Button{GetGlobalStrAddr("So_rt"),7}
AddTags     !EndGroup
AddTags     !Space
AddTags   !EndGroup
AddTags   !Space
AddTags   !HorizGroupEA
AddTags     !Space
AddTags     !Button{GetGlobalStrAddr("_New..."),8}
AddTags     !Space
AddTags     !Button{GetGlobalStrAddr("_Edit..."),9}
AddTags     !Space
AddTags     !Button{GetGlobalStrAddr("Co_py"),10}
AddTags     !Space
AddTags     !Button{GetGlobalStrAddr("Remove"),11}
AddTags     !Space
AddTags   !EndGroup

AddTags   !Space
AddTags   !HorizGroupEA
AddTags     !Space
AddTags     !Button{GetGlobalStrAddr("_Save"),12}
AddTags     !Space
AddTags     !Button{GetGlobalStrAddr("_Use"),13}
AddTags     !Space
AddTags     !Button{GetGlobalStrAddr("_Test"),14}
AddTags     !Space
AddTags     !Button{GetGlobalStrAddr("_Cancel"),15}
AddTags     !Space
AddTags   !EndGroup
AddTags   !Space
AddTags !EndGroup
AddTags #TAG_END,0

; ----------------------------------------------------------------
;         Here starts the main routine
; ----------------------------------------------------------------

; never forget to set the right taglist !!

Use TagList 2

application.l = TR_CreateApp_(TagList)    ; no need for .TR_App anymore

If (application)

  Use TagList 1     ; set again the right taglist!

  project.l = TR_OpenProject_(application,TagList)

  If (project)
    user_closed=0

      While (user_closed=0)

        TR_Wait_ application,0

        *trmsg.TR_Message=TR_GetMsg_(application)

        While (*trmsg)

          If (*trmsg\trm_Project=project)
            Select *trmsg\trm_Class

              Case #TRMS_CLOSEWINDOW
                user_closed=True
              End Select
          EndIf

          TR_ReplyMsg_ *trmsg

          *trmsg=TR_GetMsg_(application)
        Wend
      Wend
    TR_CloseProject_ project
  Else
    NPrint "Unable to create the project"
  EndIf

  TR_DeleteApp_ application

Else
  NPrint "Unable to create application"
EndIf

End

