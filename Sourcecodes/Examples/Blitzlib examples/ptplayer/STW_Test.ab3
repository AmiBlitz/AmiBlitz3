; XTRA
; Embedded .xtra Header
; 
; General Info
; -------------------------------------------------------
; ExePath         = ""
; ExeFile         = ""
; CreateIcon      = 0
; Residents       = "all.res"
; 
; Compiler
; -------------------------------------------------------
; StringBuffer    = 64000
; MakeSmallest    = 1
; FuncOptimize    = 1
; Version         = 3.9.11
; NumberOfBuilds  = 3361
; 
; Debugger
; -------------------------------------------------------
; CliArgs         = ""
; StackSize       = 8192
; RuntimeDebug    = 0
; DebugInfo       = 0
; CreateDbgFile   = 0
; OverflowCheck   = 0
; AssemblerCheck  = 0
; InterruptCheck  = 1
; AutoRun         = 1
; 
; Editor
; -------------------------------------------------------
; CursorLine      = 133
; CursorColumn    = 1
; LabelSearch     = "filepattern"
; LabelRemark     = 0
; LabelAll        = 1
; LabelCase       = 0
; LabelPosition   = 0
; 
; Blitz Objects
; -------------------------------------------------------
; Max File        = 25
; Max GadgetList  = 5
; Max Shape       = 100
; Max Bank        = 5
; Max MenuList    = 5
; Max GTList      = 25
; Max Palette     = 4
; Max BitMap      = 1
; Max Screen      = 5
; Max IntuiFont   = 5
; Max Window      = 25
; Max BlitzFont   = 4
; Max IconInfo    = 1
; Max MUIObject   = 50
; Max AsyncReq    = 4
; Max Req-Lib     = 5
; Max GTMenuList  = 30
; Max Console     = 5
; Max TCPSock     = 5
; Max Tape        = 5
; Max TagList     = 5
; Max Database    = 16
; Max Sound       = 10
; Max MedModule   = 8
; Max Buffer      = 10
; Max Queue       = 10
; Max Sprite      = 20
; Max Module      = 5
; Max Slice       = 10
; Max Page        = 4
; Max CopList     = 10
; Max PTModule    = 5
; Max Anim        = 10
; Max NChunky     = 50
; Max Chunky      = 20
; Max Stencil     = 5
; Max XBSound     = 10
; /XTRA
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;; ptplayer_test
;;
;; Demonstrates all the features of ptplayer lib
;; E-Penguin, 2023-2025
;;
WBStartup

;; Include important NewTypes and convenience function
;; for converting bb Sound object to sfx structure
XINCLUDE "ptplayer_include.ab3"

; Load a mod into bank 0
Gosub gs_LoadMod

; Load some sounds into Sound 0..3
Gosub gs_LoadSfx

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; MTInstall
; Install the CIA MOD player routine (1=PAL,0=NTSC)
MTInstall 1

;; Set up the VBlank to check for E8 triggers
e8.b = 0
SetInt 5
    e8_now.b =  MTE8Trigger
    If e8_now <> e8
        Print "E8 Trigger = "
        NPrint e8_now
        e8 = e8_now
    EndIf
End SetInt


;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; MTInit
; Initialise the player with Bank 0
MTInit 0,0

; Initialise some data structures for the sfx

Gosub gs_InitSfxStructs

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; MTPlay
; Start the play loop
MTPlay On

; Wait 10s
VWait 500

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; MTIsEnabled
; Confirm it is playing
If MTIsEnabled = True
  NPrint "Music is playing"
EndIf

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; Show the three ways of playing a sound effect
;
; MTPlayFx
For i=0 To 5
*status.SfxChanStatus = MTPlayFx(&sfx0)
  Print "Playing effect on channel "
  NPrint *status\n_index
  VWait 50
Next i

; MTSoundFX (with sound object)
For i=0 To 5
  MTSoundFX 1,64
  NPrint "Playing effect with sound object 1"
  VWait 50
Next i

; MTSoundFX (with sample pointer)
*_snd.sound = Addr Sound(2)  ; Get the pointer to the sound
For i=0 To 5
  MTSoundFX *_snd\_data, *_snd\_length, *_snd\_period, 64
  NPrint "Playing effect with sound object 2"
  VWait 50
Next i

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; MTMasterVolume
; Turn down and back up
NPrint "Fade Out"
Gosub gs_FadeOut : VWait 50
NPrint "Fade In"
Gosub gs_FadeIn  : VWait 50


;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; MTMusicMask
; Reserve channels for music
MTMusicMask 5 ; Reserve channel 0 and 3
VWait 50

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; MTMusicChannels
; Set reserved channels for music
MTMusicChannels 2
VWait 50


MTEnd    ; Stop playing the current mod
MTRemove ; Remove the CIA MOD player routine
ClrInt 5 ; Clear VBlank interrupt

End

.gs_LoadMod
; Using public domain mod from
; https://modarchive.org/index.php?request=view_by_moduleid&query=178172
LoadBank 0,"drozerix_-_neon_techno.mod",2
Return

.gs_LoadSfx
; SFX Credits:
; Dinosaur roar : Groadr @ freesound.org
; Pterodactyl : dinodilopho @ freesound.org
; Ho Ho Ho : sovy @ freesound.org
; Ow : Topschool @ freesound.org
LoadSound 0,"ow-sound.8svx"
LoadSound 1,"dragon-roar.8svx"
LoadSound 2,"pterodactyl.8svx"
LoadSound 3,"ho-ho-hooo.8svx"
Return

.gs_InitSfxStructs
; Initialize a sfx pointers for ptplayer
; {*pointer.sfx, sound_index, volume, channel (-1=best), priority (>0) }
DEFTYPE .sfx sfx0
DEFTYPE .sfx sfx1
DEFTYPE .sfx sfx2
DEFTYPE .sfx sfx3

SFXInit{&sfx0,0,64,-1,1}
SFXInit{&sfx1,1,64,-1,1}
SFXInit{&sfx2,2,64,2,1}
SFXInit{&sfx3,3,64,3,1}
Return

.gs_FadeOut
MTMasterVolume 32 : VWait 50
MTMasterVolume 16 : VWait 50
MTMasterVolume 8  : VWait 50
MTMasterVolume 4  : VWait 50
MTMasterVolume 2  : VWait 50
MTMasterVolume 0  : VWait 50
Return

.gs_FadeIn
MTMasterVolume 2  : VWait 50
MTMasterVolume 4  : VWait 50
MTMasterVolume 8  : VWait 50
MTMasterVolume 16 : VWait 50
MTMasterVolume 32 : VWait 50
MTMasterVolume 64 : VWait 50
Return





