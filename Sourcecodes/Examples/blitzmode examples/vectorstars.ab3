; XTRA
; Embedded .xtra Header
; 
; General Info
; -------------------------------------------------------
; ExePath         = "Sourcecodes/Examples/blitzmode examples"
; ExeFile         = ""
; CreateIcon      = 1
; Residents       = ""
; 
; Compiler
; -------------------------------------------------------
; StringBuffer    = 10240
; MakeSmallest    = 0
; FuncOptimize    = 0
; Version         = 0.0.0
; NumberOfBuilds  = 1
; 
; Debugger
; -------------------------------------------------------
; CliArgs         = ""
; StackSize       = 8191
; RuntimeDebug    = 1
; DebugInfo       = 0
; CreateDbgFile   = 0
; OverflowCheck   = 0
; AssemblerCheck  = 0
; InterruptCheck  = 1
; AutoRun         = 1
; 
; Editor
; -------------------------------------------------------
; CursorLine      = 2
; CursorColumn    = 20
; LabelSearch     = ""
; LabelRemark     = 0
; LabelAll        = 0
; LabelCase       = 0
; LabelPosition   = 0
; 
; Blitz Objects
; -------------------------------------------------------
; Max File        = 5
; Max GadgetList  = 5
; Max Shape       = 100
; Max Bank        = 5
; Max MenuList    = 5
; Max GTList      = 20
; Max Palette     = 4
; Max BitMap      = 10
; Max Screen      = 5
; Max IntuiFont   = 5
; Max BlitzFont   = 4
; Max Window      = 20
; Max IconInfo    = 1
; Max MUIObject   = 50
; Max AsyncReq    = 4
; Max Req-Lib     = 5
; Max GTMenuList  = 5
; Max Console     = 5
; Max TCPSock     = 5
; Max Tape        = 5
; Max TagList     = 5
; Max Database    = 16
; Max Sound       = 10
; Max MedModule   = 8
; Max Buffer      = 10
; Max Queue       = 10
; Max Sprite      = 20
; Max Module      = 5
; Max Slice       = 10
; Max Page        = 4
; Max CopList     = 10
; Max PTModule    = 5
; Max Anim        = 10
; Max NChunky     = 50
; Max Chunky      = 20
; Max Stencil     = 5
; Max XBSound     = 10
; /XTRA
; -- simple vectorballs demo
; -- code by xp^tsa (found at https://www.amiblitz.de/community/thread-280.html)

Statement xCls{a.l}
  MOVE.l d0,a0 : ADD.l #40*256,a0
  MOVEQ #0,d0 : MOVEQ #0,d1 : MOVEQ #0,d2  : MOVEQ #0,d3  : MOVEQ #0,d4
  MOVEQ #0,d5 : MOVEQ #0,d6 : MOVE.l d0,a1 : MOVE.l d0,a2 : MOVE.l d0,a3
  MOVE #255,d7
  loop1: MOVEM.l d0-d6/a1-a3,-(a0) : DBRA d7,loop1
  AsmExit
End Statement

BitMap 0,320,256,1
BitMap 1,320,256,1

AGAPalRGB 0,0,32,22,33
AGAPalRGB 0,1,200,200,200

NEWTYPE.ball
  x.q
  y
  z
  nx
  ny
End NEWTYPE

Dim List balls.ball(50)
While AddLast(balls())
  balls()\x = Rnd(40) - 20
  balls()\y = Rnd(40) - 20
  balls()\z = Rnd(40) - 20
Wend

distance.w = 20


;-- building a lookup table
Dim Costable(360)
Dim Sintable(360)
For i = 0 To 359
  Costable(i)=Int( Cos(i*0.017453) *1024 )
  Sintable(i)=Int( Sin(i*0.017453) *1024 )
Next

BLITZ
InitCopList 0,44,256,$10001,8,2,0

Mouse On
CreateDisplay 0
DisplayBitMap 0,0 : DisplayPalette 0,0
Use BitMap db

While Joyb(0) = 0
  VWait
  DisplayBitMap 0,db
  db = 1 - db
  Use BitMap db

  ;-- clear the bitmap
  xCls{Peek.l (Addr BitMap(db) + 8)}

  vx + 2; X rotation speed
  vy + 1; Y rotation speed
  vz + 1; Z rotation speed

  If vx>359 : vx=1 : EndIf
  If vy>359 : vy=1 : EndIf
  If vz>359 : vz=1 : EndIf

  distance = 10 + MouseY

  cosx = Costable(vx)
  cosy = Costable(vy)
  cosz = Costable(vz)
  sinx = Sintable(vx)
  siny = Sintable(vy)
  sinz = Sintable(vz)

  ResetList balls()
  While NextItem(balls())

    ;-- simply move stars
    balls()\x + 1
    If balls()\x > 20 Then balls()\x = -20

    ;-- "3d-engine" ^^
    ty = ((balls()\y * cosx) - (balls()\z * sinx))/1024
    tz = ((balls()\y * sinx) + (balls()\z * cosx))/1024
    tx = ((balls()\x * cosy) - (tz * siny))/1024
    tz = ((balls()\x * siny) + (tz * cosy))/1024

    balls()\nx  = ( 200 * tx) / (distance - tz )  + 160
    balls()\ny  = ( 200 * ty) / (distance - tz)   + 127

    nodraw = 0
    If balls()\nx > 319 : nodraw=1 : EndIf
    If balls()\ny > 255 : nodraw=1 : EndIf
    If balls()\nx < 1   : nodraw=1 : EndIf
    If balls()\ny < 1   : nodraw=1 : EndIf

    If nodraw = 0
      Plot balls()\nx,balls()\ny,1
    EndIf

  Wend
Wend

End
