; XTRA
; Embedded .xtra Header
; 
; General Info
; -------------------------------------------------------
; ExePath         = "Development:AmiBlitz3/Sourcecodes/Examples/blitzmode examples"
; ExeFile         = "blitzosdemo"
; CreateIcon      = 1
; Residents       = "all.res"
; 
; Compiler
; -------------------------------------------------------
; StringBuffer    = 100000
; MakeSmallest    = 1
; FuncOptimize    = 1
; Version         = 0.0.0
; NumberOfBuilds  = 211
; 
; Debugger
; -------------------------------------------------------
; CliArgs         = ""
; StackSize       = 8192
; RuntimeDebug    = 1
; DebugInfo       = 0
; CreateDbgFile   = 0
; OverflowCheck   = 0
; AssemblerCheck  = 0
; InterruptCheck  = 1
; AutoRun         = 1
; 
; Editor
; -------------------------------------------------------
; CursorLine      = 52
; CursorColumn    = 2
; LabelSearch     = ""
; LabelRemark     = 0
; LabelAll        = 0
; LabelCase       = 0
; LabelPosition   = 0
; 
; Blitz Objects
; -------------------------------------------------------
; Max File        = 100
; Max GadgetList  = 100
; Max Shape       = 200
; Max Bank        = 1
; Max MenuList    = 100
; Max GTList      = 100
; Max Palette     = 4
; Max BitMap      = 10
; Max Screen      = 5
; Max IntuiFont   = 10
; Max BlitzFont   = 1
; Max Window      = 100
; Max IconInfo    = 1
; Max MUIObject   = 50
; Max AsyncReq    = 4
; Max Req-Lib     = 5
; Max GTMenuList  = 100
; Max Console     = 5
; Max TCPSock     = 5
; Max Tape        = 5
; Max TagList     = 5
; Max Database    = 16
; Max Sound       = 10
; Max MedModule   = 8
; Max Buffer      = 10
; Max Queue       = 10
; Max Sprite      = 20
; Max Module      = 5
; Max Slice       = 10
; Max Page        = 4
; Max CopList     = 10
; Max PTModule    = 5
; Max Anim        = 10
; Max NChunky     = 50
; Max Chunky      = 20
; Max Stencil     = 5
; Max XBSound     = 10
; /XTRA
;-- simple example for usage of blitzlibos commands to provide a gadget-like gui in BLITZ mode
NoCli

;-- generate a pointer sprite
Gosub makePointer

;-- load a shape
LoadShape 0,"//data/block",0
sw.w = ShapeWidth(0)
sh.w = ShapeHeight(0)

;-- init some stars for later use
Gosub initStarfield

;-- switch to blitz mode
BLITZ

;-- init a new bitmap
BitMap 0,320,256,3

;-- create a display
Slice 0,44,3
Use Palette 0

;-- show the bitmap on it
Show 0

;-- activate mouse in blitz mode
Mouse On

;-- set the pointer sprite
Pointer 0,1

;-- init a new "page" and draw something on it
BitMapOutput 0
InitPage 1,0,16     ; init a page on bitmap 0 with room for some gadgets

AddBox 1, 0,   0,  0,  54,  11,0, "Label1" : SetBoxColor 0,1
AddBox 1, 1,  56,  0, 280,  11,1, "Press both mouse buttons to exit" : SetBoxColor 1,1

AddBox 1,15,  20, 32, 126,  11,2, "give me Stars!"
AddBox 1, 2,  20, 44,  62,  11,2, "Disable"
AddBox 1, 3,  84, 44,  56,  11,2, "Enable"
AddBox 1, 9, 144, 44,  56,  11,2, "Toggle"
AddBox 1,10, 208, 44,  90,  11,2, "Activate"
AddBox 1, 4,  20, 70, 140,  11,2, "empty -->" : SetBoxColor 4,4
AddBox 1,13,  180,70, 100,  11,4
AddBox 1, 5,  20,100, 120,  18,6 : SetBox 5,0,1

;AddBox 1, 6,  20,122, 120,  11,10,"Hallo"
;AddBox 1,14,  20,140, 120,  11,14,"Hallo2"

AddBox 1,11, 160,100,sw+2,sh+2,2,0,0
AddBox 1,12, 180,100,sw+2,sh+2,3,0,0

AddBox 1, 7,  62,190, 100,  11,0,Str$(2 ASR 1) + "<MESSAGE>" : SetBoxColor 7,1
AddBox 1, 8,   0,190,  60,  11,2, "Exit"  : SetBoxColor 6,0

;-- draw all boxes of the current page
RedrawPage

exit.b = False
usestars.b  = False
selectedBox.b = 0
spot.b = 0
propx.q = 0
propy.q = 1
d.l = 1

Repeat
  VWait 1

  If usestars<>0 Then Gosub updateStarfield

  d = d + 1
  If d MOD 64 = 0
    propx + 0.1
    SetBox 5, propx, propy : RedrawBox 5
  EndIf
  ;
  If BlitzEvent
    selectedBox = BoxSelected

    If selectedBox <> -1
      Select selectedBox
      Case 2 :  DisableBox 8 : RedrawBox 8
      Case 3 :  EnableBox 8 : RedrawBox 8
      Case 4 :  SetBoxString 13,"" : RedrawBox 13
      Case 9 :  ToggleBox 8: RedrawBox 8
      Case 10:  SelectBox 8;: ActivateSelectedBox
      Case 6 :  message$ = Str$(BoxPropX(selectedBox))
      Case 8 :  exit = True
      Case 15:  usestars = 1
      Default:  message$ = "Button " + Str$(selectedBox)
      End Select
    EndIf
  Else
    spot.b = SpotBox
    If (spot <> -1)
      message$ = "over " + Str$(spot)
    Else
      message$ = ""
    EndIf
  EndIf

  If message$ <> ""
    SetBoxTitle 7, message$
    RedrawBox 7
  EndIf

  If usestars<>0 Then RedrawPage

  If Joyb(0) = 3 Then exit = True
Until exit = True

AMIGA
End

;-- subroutines
.makePointer:
  BitMap 0,8,8,2
  Use BitMap 0
  ;    x y x y c
  Line 2,0,2,4,1
  Line 0,2,4,2,1
  GetaShape 0,0,0,5,5
  Handle 0,2,2
  GetaSprite 0,0
  Free BitMap 0
Return

.initStarfield:
  ;-- setup stars
  num.b = 32
  NEWTYPE.star 
    speed.b
    x.w : y
    ox.w : oy
  End NEWTYPE
  Dim List stars.star(num)
  USEPATH stars()

  ;-- set up palette as grey scale so stars are
  ;For i = 1 To 7
  ;  br = 4 + i * 1.7
  ;  RGB i,br,br,br
  ;Next i

  For i = 0 To num
    If AddLast(stars())
      \x = Rnd(320)
      \y = Rnd(200)
      \speed = Rnd(3) + 1
    EndIf
  Next i
Return

.updateStarfield:
  ResetList stars() ;move and draw stars
  While NextItem(stars())
    Plot \x,\y,0

    \x = \x - \speed
    If \x < 0
      \x = Rnd(80)+240
      \y = Rnd(200)
    EndIf
    Plot \x,\y,4-\speed
  Wend
Return


