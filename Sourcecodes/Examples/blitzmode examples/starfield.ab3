; XTRA
; Embedded .xtra Header
; 
; General Info
; -------------------------------------------------------
; ExePath         = "ram:"
; ExeFile         = "Empty"
; CreateIcon      = 0
; Residents       = "all.res"
; 
; Compiler
; -------------------------------------------------------
; StringBuffer    = 100000
; MakeSmallest    = 1
; FuncOptimize    = 1
; Version         = 0.0.0
; NumberOfBuilds  = 138
; 
; Debugger
; -------------------------------------------------------
; CliArgs         = ""
; StackSize       = 8192
; RuntimeDebug    = 1
; DebugInfo       = 0
; CreateDbgFile   = 0
; OverflowCheck   = 0
; AssemblerCheck  = 0
; InterruptCheck  = 1
; AutoRun         = 1
; 
; Editor
; -------------------------------------------------------
; CursorLine      = 43
; CursorColumn    = 33
; LabelSearch     = ""
; LabelRemark     = 0
; LabelAll        = 0
; LabelCase       = 0
; LabelPosition   = 0
; 
; Blitz Objects
; -------------------------------------------------------
; Max File        = 100
; Max GadgetList  = 100
; Max Shape       = 200
; Max Bank        = 1
; Max MenuList    = 100
; Max GTList      = 100
; Max Palette     = 4
; Max BitMap      = 10
; Max Screen      = 5
; Max IntuiFont   = 10
; Max BlitzFont   = 1
; Max Window      = 100
; Max IconInfo    = 1
; Max MUIObject   = 50
; Max AsyncReq    = 4
; Max Req-Lib     = 5
; Max GTMenuList  = 100
; Max Console     = 5
; Max TCPSock     = 5
; Max Tape        = 5
; Max TagList     = 5
; Max Database    = 16
; Max Sound       = 10
; Max MedModule   = 8
; Max Buffer      = 10
; Max Queue       = 10
; Max Sprite      = 20
; Max Module      = 5
; Max Slice       = 10
; Max Page        = 4
; Max CopList     = 10
; Max PTModule    = 5
; Max Anim        = 10
; Max NChunky     = 50
; Max Chunky      = 20
; Max Stencil     = 5
; Max XBSound     = 10
; /XTRA
num = 128

BLITZ ;setup 8 colour display
BitMap 0,320,256,3
Slice 0,44,3
Show 0
Mouse On; turn on blitz mouse driver
MouseArea 0,0,1024,256

NEWTYPE.star ;setup stars
angle.w ;each star has 6 bits of information
dist.q:speed:acc ;
sx.w:sy ;including last screen position plotted
End NEWTYPE
Dim List stars.star(num)
USEPATH stars()

; the following sets up an array of quicks to hold Sin and Cos
; tables using 1024 to 2*pi ratio. this is very quick way for
; doing polar type geometry

Dim qsin.q(1023), qcos.q(1023) ;setup qsin and qcos arrays
For i = 0 To 1023
  qsin(i) = Sin(i * Pi / 512)
  qcos(i) = Cos(i * Pi / 512)
Next i

For i = 1 To 7 ;set up palette as grey scale so stars are
  br = 4 + i * 1.7 ;brighter as they get closer
  RGB i,br,br,br
Next i

While Joyb(O)=O ; while mouse buttons not pressed
  mx = MouseX ;read mouse for this frame
  my = MouseY /512

  ResetList stars() ;move and draw stars
  While NextItem(stars())
    Plot \sx,\sy,0           ;rub out old
    \speed + \acc,my    ;add acceleration and mousey to star's speeed
    \dist + \speed           ;then add its speed to distance
    \sx = 160 + qcos((\angle + mx) & 1023) * \dist ; calculate new sx and sy
    \sy = 128 + qsin((\angle + mx) & 1023) * \dist
    If \sx < 0 OR \sx > 319 OR \sy < 0 OR \sy > 256
      KillItem stars()       ;if reached screen borders delete from list
    Else
      Plot \sx,\sy,\dist / 20 ;else plot it on screen
    EndIf
  Wend
  If AddItem(stars())        ;add a star if theres a space in the list
    \dist = 0
    \speed= 0
    \acc = Rnd(1)/32
    \angle = Rnd(1024)
  EndIf
Wend
End
