; this is my  blitz version of
; the other basic prog. in the N.Z. AMIGA MAG. No.2
; the shapes are moved 1.5 deg on each bitmap
; written by V. A. HILL
; hope it be of interest to somebody?
; if you are wondering which other basic
; spelt backwards it is SOMA

#scw=320:#sh=256:#xoff=#scw/2:#yoff=#sh/2
#rd=110
u.f=6.28/720
DEFTYPE.w r,deh,sup,cx,dx,dy,sc,sn,cs
Dim x.w(1500),y.w(1500)
w.f=0:Print "Please Wait"
cy=0
For w=0 To 6.28 Step u
  x(cy)=Sin(w)*#rd:y(cy)=Cos(w)*#rd
  x(cy+720)=x(cy):y(cy+720)=y(cy)
  cy=cy+1
Next w
For cy=0 To 20
  x(cy+1440)=x(cy):y(cy+1440)=y(cy)
Next cy
Gosub makeshape

BLITZ
For d=0 To 7
  BitMap d,#scw,#sh,2
Next d

PalRGB 0,0,0,0,0:PalRGB 0,1,15,9,9:PalRGB 0,2,10,0,0:PalRGB 0,3,12,0,0

InitCopList 0,2
DisplayPalette 0,0
DisplayBitMap 0,0
CreateDisplay 0

Repeat
sc=0
  For deh=0 To 720 Step 24
    For sup=0 To 21 Step 3
    VWait
    DisplayBitMap 0,sc
    Use BitMap sc
    sn=deh+r+sup
    cs=deh+sup
    dx=x(sn)+#xoff
    dy=y(cs)+#yoff
    Blit 0,dx,dy
    sc=sc+1
    If Joyb(0)<>0 Then End
    Next sup
   sc=0
  Next deh
r=QWrap(r+16,0,720)
Forever

AMIGA
makeshape:
Screen 0,2
ScreensBitMap 0,0
Cls
Circlef 12,20,11,3
Circle 12,20,11,2
Plot 14,20,1
Plot 14,19,1
Circle 12,20,10,2
Circle 13,20,10,2
Circle 13,20,9,3
GetaShape 0,0,9,24,32
MidHandle 0
Cls
Free BitMap 0
Free Screen 0
Return



