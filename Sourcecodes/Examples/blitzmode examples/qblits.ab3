; XTRA
; Embedded .xtra Header
; 
; General Info
; -------------------------------------------------------
; ExePath         = ""
; ExeFile         = ""
; CreateIcon      = 1
; Residents       = ""
; 
; Compiler
; -------------------------------------------------------
; StringBuffer    = 10240
; MakeSmallest    = 0
; FuncOptimize    = 0
; Version         = 0.0.0
; NumberOfBuilds  = 0
; 
; Debugger
; -------------------------------------------------------
; CliArgs         = ""
; StackSize       = 8191
; RuntimeDebug    = 1
; DebugInfo       = 0
; CreateDbgFile   = 0
; OverflowCheck   = 0
; AssemblerCheck  = 0
; InterruptCheck  = 1
; AutoRun         = 1
; 
; Editor
; -------------------------------------------------------
; CursorLine      = 1
; CursorColumn    = 1
; LabelSearch     = ""
; LabelRemark     = 0
; LabelAll        = 0
; LabelCase       = 0
; LabelPosition   = 0
; 
; Blitz Objects
; -------------------------------------------------------
; Max File        = 5
; Max GadgetList  = 5
; Max Shape       = 100
; Max Bank        = 5
; Max MenuList    = 5
; Max GTList      = 20
; Max Palette     = 4
; Max BitMap      = 10
; Max Screen      = 5
; Max IntuiFont   = 5
; Max BlitzFont   = 4
; Max Window      = 20
; Max IconInfo    = 1
; Max MUIObject   = 50
; Max AsyncReq    = 4
; Max Req-Lib     = 5
; Max GTMenuList  = 5
; Max Console     = 5
; Max TCPSock     = 5
; Max Tape        = 5
; Max TagList     = 5
; Max Database    = 16
; Max Sound       = 10
; Max MedModule   = 8
; Max Buffer      = 10
; Max Queue       = 10
; Max Sprite      = 20
; Max Module      = 5
; Max Slice       = 10
; Max Page        = 4
; Max CopList     = 10
; Max PTModule    = 5
; Max Anim        = 10
; Max NChunky     = 50
; Max Chunky      = 20
; Max Stencil     = 5
; Max XBSound     = 10
; /XTRA
;
;A little demo of moving things using the QBlit command...

;create a 'ball' type variable
;
NEWTYPE.ball
x.q
y
xs
ys
End NEWTYPE

;dimension a 'list' of 'balls'
;
;try increasing the '2' here to '10'. Notice how things
;start to flicker? To overcome this, you'll need to use
;double buffering! See the 'doublebuffer' demo!
;
Dim List balls.ball(10)

;load in a shape and it's palette
;
LoadShape 0,"//data/ball",0

;keep adding items to list until list full
;
While AddLast(balls())
  balls()\x=Rnd(280)+10,Rnd(160)+10,(Rnd-.5)*8,(Rnd-.5)*8
Wend

;now, we go into blitz mode
;
BLITZ

;initialize a 'queue' for the qblit command to make use of:
;
;the '50' here means the queue can handle up to 50 'QBlits'
;before an 'UnQueue' is necessary.
;
Queue 0,50

;initialize a lor-res, 8 colour bitmap...
;
BitMap 0,320,200,3

;initialize a slice, and set it's colour palette
;
InitCopList 0,44,200,3,8,8,0
DisplayPalette 0,0
DisplayBitMap 0,0
CreateDisplay 0

;now, process all ballss...
;
;while mouse button not hit..
;
While Joyb(0)=0

  ;start at first item in list
  ;
  ResetList balls()

  ;wait for vertical blank
  ;
  VWait

  ;unqueue all previously drawn shapes
  ;
  UnQueue 0

  ;while more items to come...
  ;
  While NextItem(balls())

    ;update balls' position
    ;
    balls()\x+balls()\xs
    balls()\y+balls()\ys

    ;reverse direction if ball gets to edge of bitmap
    ;
    If balls()\x<10 OR balls()\x>290 Then balls()\xs=-balls()\xs
    If balls()\y<10 OR balls()\y>170 Then balls()\ys=-balls()\ys

    ;draw balls in new position
    ;
    QBlit 0,0,balls()\x,balls()\y

  Wend
Wend

End
