; XTRA
; Embedded .xtra Header
; 
; General Info
; -------------------------------------------------------
; ExePath         = ""
; ExeFile         = ""
; CreateIcon      = 1
; Residents       = ""
; 
; Compiler
; -------------------------------------------------------
; StringBuffer    = 10240
; MakeSmallest    = 0
; FuncOptimize    = 0
; Version         = 0.0.0
; NumberOfBuilds  = 0
; 
; Debugger
; -------------------------------------------------------
; CliArgs         = ""
; StackSize       = 8191
; RuntimeDebug    = 1
; DebugInfo       = 0
; CreateDbgFile   = 0
; OverflowCheck   = 0
; AssemblerCheck  = 0
; InterruptCheck  = 1
; AutoRun         = 1
; 
; Editor
; -------------------------------------------------------
; CursorLine      = 1
; CursorColumn    = 1
; LabelSearch     = ""
; LabelRemark     = 0
; LabelAll        = 0
; LabelCase       = 0
; LabelPosition   = 0
; 
; Blitz Objects
; -------------------------------------------------------
; Max File        = 5
; Max GadgetList  = 5
; Max Shape       = 100
; Max Bank        = 5
; Max MenuList    = 5
; Max GTList      = 20
; Max Palette     = 4
; Max BitMap      = 10
; Max Screen      = 5
; Max IntuiFont   = 5
; Max BlitzFont   = 4
; Max Window      = 20
; Max IconInfo    = 1
; Max MUIObject   = 50
; Max AsyncReq    = 4
; Max Req-Lib     = 5
; Max GTMenuList  = 5
; Max Console     = 5
; Max TCPSock     = 5
; Max Tape        = 5
; Max TagList     = 5
; Max Database    = 16
; Max Sound       = 10
; Max MedModule   = 8
; Max Buffer      = 10
; Max Queue       = 10
; Max Sprite      = 20
; Max Module      = 5
; Max Slice       = 10
; Max Page        = 4
; Max CopList     = 10
; Max PTModule    = 5
; Max Anim        = 10
; Max NChunky     = 50
; Max Chunky      = 20
; Max Stencil     = 5
; Max XBSound     = 10
; /XTRA
;
;This example demonstrates the principle behind 'double buffering'
;
;Double buffering is a programming technique allowing you
;to get rid of 'flicker' from graphic animation.
;
;Double buffering relies on the idea that, although graphics
;are drawn to a particular bitmap, that bitmap may not
;necessarily be 'shown' on the display.
;
;To achieve double buffering, you therefore need at least
;2 bitmaps. At any one point in time, only 1 of these bitmaps
;is displayed (using one of the 'Show' commands), whilst any
;graphic drawing is performed on THE OTHER bitmap.
;During the vertical blank period, these 2 bitmaps are 'swapped'
;ie - the newly updated bitmap is shown, and the previously
;shown bitmap is used for the next series of updates!


;create a 'ball' type variable
;
NEWTYPE.ball
x.q
y
xs
ys
End NEWTYPE

;dimension a 'list' of 'balls'
;
Dim List balls.ball(12) ; take runtime errors off and boost this value

;load in a shape and it's palette
;
LoadShape 0,"//data/ball",0

;keep adding items to list until list full
;
While AddLast(balls())
  balls()\x=Rnd(280)+10,Rnd(160)+10,(Rnd-.5)*8,(Rnd-.5)*8
Wend

;now, we go into blitz mode
;
BLITZ

;initialize a lor-res, 8 colour bitmap...
;
BitMap 0,320,200,3

;we also need another bitmap for the double buffering:
;
BitMap 1,320,200,3

;initialize a 'Queue' for the QBlit command to make use of:
;
Queue 0,50

;We will also double buffer our queue's, so we need another
;queue:
;
Queue 1,50

;initialize a slice, and set it's colour palette
;
InitCopList 0,44,200,3,8,8,0
DisplayPalette 0,0
DisplayBitMap 0,0
CreateDisplay 0

;now, process all ballss...
;
;while mouse button not hit..
;
While Joyb(0)=0

  ;start at first item in list
  ;
  ResetList balls()

  ;wait for vertical blank
  ;
  VWait

  ;this bit does the actual double buffering:
  ;the 'db=1-db' will cause the 'db' variable to switch
  ;between 0/1/0/1/0/1...
  ;
  DisplayBitMap 0,db:db=1-db:Use BitMap db

  ;unqueue items on the 'used' bitmap:
  ;
  UnQueue db

  ;while more items to come...
  ;
  While NextItem(balls())

    ;update balls' position
    ;
    balls()\x+balls()\xs
    balls()\y+balls()\ys

    ;reverse direction if ball gets to edge of bitmap
    ;
    If balls()\x<10 OR balls()\x>290 Then balls()\xs=-balls()\xs
    If balls()\y<10 OR balls()\y>170 Then balls()\ys=-balls()\ys

    ;draw balls in new position
    ;
    QBlit db,0,balls()\x,balls()\y

  Wend
Wend

End
