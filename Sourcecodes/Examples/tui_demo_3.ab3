; XTRA
; Embedded .xtra Header
; 
; General Info
; -------------------------------------------------------
; ExePath         = "ram:"
; ExeFile         = "TuiDemo3"
; CreateIcon      = 0
; Residents       = "all.res"
; 
; Compiler
; -------------------------------------------------------
; StringBuffer    = 10240
; MakeSmallest    = 1
; FuncOptimize    = 1
; Version         = 0.0.0
; NumberOfBuilds  = 20
; 
; Debugger
; -------------------------------------------------------
; CliArgs         = ""
; StackSize       = 8191
; RuntimeDebug    = 0
; DebugInfo       = 0
; CreateDbgFile   = 0
; OverflowCheck   = 0
; AssemblerCheck  = 0
; InterruptCheck  = 1
; AutoRun         = 1
; 
; Editor
; -------------------------------------------------------
; CursorLine      = 77
; CursorColumn    = 1
; LabelSearch     = ""
; LabelRemark     = 0
; LabelAll        = 0
; LabelPosition   = 0
; 
; Blitz Objects
; -------------------------------------------------------
; Max IconInfo    = 1
; Max NChunky     = 50
; Max MUIObject   = 50
; Max PTModule    = 5
; Max AsyncReq    = 4
; Max Req-Lib     = 5
; Max GTMenuList  = 5
; Max Console     = 5
; Max TCPSock     = 5
; Max XBSound     = 10
; Max Chunky      = 20
; Max File        = 5
; Max GadgetList  = 5
; Max Queue       = 10
; Max Screen      = 5
; Max Shape       = 100
; Max CopList     = 10
; Max Sprite      = 20
; Max Stencil     = 5
; Max Module      = 5
; Max Window      = 5
; Max Anim        = 10
; Max Sound       = 10
; Max Bank        = 5
; Max Buffer      = 10
; Max BitMap      = 10
; Max Slice       = 10
; Max Page        = 4
; Max Tape        = 5
; Max IntuiFont   = 5
; Max MedModule   = 8
; Max Palette     = 4
; Max MenuList    = 5
; Max BlitzFont   = 4
; Max GTList      = 5
; /XTRA
optimize 7
Syntax 2

; Konstanten fuer die Includes
#use_locklayer = 0   ; 1=die GUI mit Lokclayer sch|tzen
#max_windows   = 5   ; Max Anzahl von Fenstern
#max_tuigads   = 20 ; Max Anzahl von Gadgets pro Fenster
#max_menus     = 5   ; Max Anzahl von Menues

#tui_flag = 2
XINCLUDE "tui.include.ab3"


; Konstanten fuer das Programm
#notify_quit      = 1   ; Notify Werte, alle 32Bit Werte erlaubt
#notify_clear     = 2
#notify_viewclick = 3

#wintyp_mywin  = 1   ; ID-Nummer des Fenstertyps, alle 32Bit Werte erlaubt


; Die GUI wird komplexer, da bietet sich das autokonstanten Feature an:
Statement win_mywindow {}
SHARED tuipen(),acwin.l
If tui_win_init {#wintyp_mywin,-1,"Malkasten"}  ; Fenster vorbereiten ...
  #__autoconstantnum = 0
  tui_button     {#@tuigad_clear,"Clear",#notify_clear}
  tui_sethelp    {#tuigad_clear,"lösche das Zeichenbrett"} ; Help-Text setzen

  tui_button     {#@tuigad_quit,"Quit",#notify_quit}
  tui_sethelp    {#tuigad_quit,"hiermit wird das Programm beendet"}

  tui_addhgroup  {#@tuigad_group1,100,0,1}            ; horizontal gruppieren
  tui_settag     {#tuigad_group1,#tui_tag_samesize}  ; TUI-Tag: gleiche Groesse fuer die beiden Buttons
  tui_setsize    {#tuigad_group1,150,-1}             ; Minimum Groesse fuer Gruppe


  ; Eigene Flaechen werden "View" genannt
  tui_view       {#@tuigad_malkasten,#tui_tag_iframe,tuipen(#tpen_shine)}
  tui_setsize    {#tuigad_malkasten,200,150}
  tui_notify     {#tuigad_malkasten,#notify_viewclick}

  tui_label      {#@tuigad_penlabel,"Pen:",#tui_tag_right} ; Text-Label, rechts aligned
  tui_hslider    {#@tuigad_penslider,0,15,"",1}   ; HSlider id,min,max,text.s,default
  tui_sethelp    {#tuigad_penslider,"wähle den Pen aus, mit dem gezeichnet wird"}
  tui_addhgroup  {#@tuigad_group2,100,#tuigad_penlabel,#tuigad_penslider}

  tui_addvgroup  {#@tuigad_group3,100,#tuigad_malkasten,#tuigad_group2,#tuigad_group1}

  tui_frametitle {#tuigad_group3,"Malkasten"}          ; Gruppen Titel+Rahmen
  tui_setparent  {#tuigad_group3}                    ; oberstes GUI Element ist die HGroup #2
  tui_notify     {#tuigad_group3,#notify_quit}       ; Notify Wert fuer Close-Gadget

  tui_win_open   {}                     ; ... und oeffnen!
End If
End Statement



If screen_Open{"Workbench"}  ; Screen holen, in dem Fall WB

  ; TUI Engine initialisieren
  ; Laesst man die Schrifttypen weg, so wird fuer die normale und grosse
  ; Schrift die
  ; ScreenFont geladen, fuer die kleine Schrift Topaz 8.
  If tui_init{""}

    ; Default Tags, die jedes GUI Element bekommt
    ; Hier: InversHighLight, macht Mouseover Effekt
    tui_setdeftags {#tui_tag_ihl}

    ; Background Pattern definieren, das "image" muss vorher geladen werden
    ;image_load {0,"mypattern"}
    ;tui_setpattern{0}

    ; oder fuer kein Pattern:
    ;tui_setpattern{-1}

    ; Fenster oeffnen
    win_mywindow{}

    ; Event-Loop:
    breakme.l = False
    While breakme=False
      notify.l = tui_getnotify {0,True}

      Select notify
        Case #notify_quit
          breakme=True

        Case #notify_viewclick
          win.l = tuiev_win{}
          x.l = tuiev_mx{}    ; Koordinate relativ innerhalb des Views!
          y.l = tuiev_my{}
          pen.l = tuidat_value {win,#tuigad_penslider}

          If x>=0 AND y>=0 AND x<tuidat_xs{win,#tuigad_malkasten}-1 AND y<tuidat_ys{win,#tuigad_malkasten}-1
            x + tuidat_x {win,#tuigad_malkasten} ; Addiere Koordinaten des GUI Elements dazu
            y + tuidat_y {win,#tuigad_malkasten}
            Use Window win
            WPlot x,y,pen
            WPlot x+1,y,pen
            WPlot x,y+1,pen
            WPlot x+1,y+1,pen
          End If

         Case #notify_clear
           win.l = tuiev_win{}
           tui_draw {win,#tuigad_malkasten }    ; Neuzeichnen des Grafikfaeche, #3 ist der View

         Case #notify_tuirefresh_obj  ; Eigene Zeichen Routine fuer Background
           win.l    = tuiev_win{}
           wintyp.l = win_GetTyp {win}   ; Ermittle Fenster Typ
           tuiobj.l = tuiev_obj{}     ; Ermittle TUI Objekt
           Select wintyp
             Case #wintyp_mywin       ; In unserem Fenster !?
               Select tuiobj
                 Case #tuigad_malkasten               ; Der View #3 neuzeichnen ?
                   x1.l = tuidat_x{win,#tuigad_malkasten }
                   y1.l = tuidat_y{win,#tuigad_malkasten }
                   x2.l = x1+tuidat_xs{win,#tuigad_malkasten }
                   y2.l = y1+tuidat_ys{win,#tuigad_malkasten }
                   Use Window win
                   For x.l=x1 To x2 Step 3
                     For y.l = y1 To y2 Step 3
                       WPlot x,y,tuipen(#tpen_halfshine)
                     Next
                   Next
               End Select
           End Select

       End Select
    Wend
    tui_free{}
  End If
  screen_Close{}
End If
End

