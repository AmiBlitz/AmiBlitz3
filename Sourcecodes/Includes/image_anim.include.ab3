; XTRA
; Embedded .xtra Header
; 
; General Info
; -------------------------------------------------------
; ExePath         = "ram:"
; ExeFile         = "Empty"
; CreateIcon      = 0
; Residents       = "all.res"
; 
; Compiler
; -------------------------------------------------------
; StringBuffer    = 100000
; MakeSmallest    = 1
; FuncOptimize    = 1
; Version         = 0.0.0
; NumberOfBuilds  = 232
; 
; Debugger
; -------------------------------------------------------
; CliArgs         = ""
; StackSize       = 8191
; RuntimeDebug    = 0
; DebugInfo       = 0
; CreateDbgFile   = 0
; OverflowCheck   = 0
; AssemblerCheck  = 0
; InterruptCheck  = 1
; AutoRun         = 1
; 
; Editor
; -------------------------------------------------------
; CursorLine      = 5
; CursorColumn    = 28
; LabelSearch     = "pre"
; LabelRemark     = 0
; LabelAll        = 0
; LabelPosition   = 0
; 
; Blitz Objects
; -------------------------------------------------------
; Max IconInfo    = 1
; Max NChunky     = 50
; Max MUIObject   = 50
; Max PTModule    = 5
; Max AsyncReq    = 4
; Max Req-Lib     = 5
; Max GTMenuList  = 100
; Max Console     = 5
; Max TCPSock     = 5
; Max XBSound     = 10
; Max Chunky      = 20
; Max File        = 100
; Max GadgetList  = 100
; Max Queue       = 10
; Max Screen      = 5
; Max Shape       = 200
; Max CopList     = 10
; Max Sprite      = 20
; Max Stencil     = 5
; Max Module      = 5
; Max Window      = 100
; Max Anim        = 10
; Max Sound       = 10
; Max Bank        = 1
; Max Buffer      = 10
; Max BitMap      = 10
; Max Slice       = 10
; Max Page        = 4
; Max Tape        = 5
; Max IntuiFont   = 10
; Max MedModule   = 8
; Max Palette     = 4
; Max MenuList    = 100
; Max BlitzFont   = 1
; Max GTList      = 100
; Max ChunkyBuffer= 2
; /XTRA
CNIF #__include=0
  OPTIMIZE 7
  SYNTAX 2
CEND

XINCLUDE "image.include.ab3"

#MAX_IANIMS = 100

NEWTYPE.ianim
isused.l
framesN.l
framesP.l
width.l
height.l
dataP.l
End NEWTYPE

Dim ianimdat.ianim(#MAX_IANIMS)

#IANIM_HEAD_TRANSPARENT = %10000000
#IANIM_HEAD_OPAQUE      = %00000000
#IANIM_HEAD_MIXED       = %11000000
#IANIM_HEAD_MASK        = %11000000

#IANIM_FORMAT_RLERGB7      = 1 ; vle 0/6/7 bit argb
#IANIM_FORMAT_RLERGB14     = 2 ; vle 0/6/14 bit argb
#IANIM_FORMAT_ARGB16       = 3 ; 16bit argb
#IANIM_FORMAT_RLEYUV8      = 4 ; 8bit yuv
#IANIM_FORMAT_DIFF8        = 5 ; 8bit

USEPATH ianimdat(ianim)
Function.l ianim_Encode{rawptr.l,compptr.l,rawbpr.l,width.l,height.l,mode.l}
y.l = 0
x.l = 0
baseptr.l = compptr
head.l = -1
count.l = 0
Poke.b compptr,mode : compptr+1
Poke.b compptr,0    : compptr+1
For y.l=0 To height-1
  ptr.l = rawptr.l + (y*rawbpr)
  If mode = #IANIM_FORMAT_RLEYUV8 Then head=-1

  aa.l = 0
  rr.l = 0
  gg.l = 0
  bb.l = 0
  For x.l=0 To width-1
    a.l = Peek.b(ptr+0) & $FF
    r.l = Peek.b(ptr+1) & $FF
    g.l = Peek.b(ptr+2) & $FF
    b.l = Peek.b(ptr+3) & $FF
    ptr +4

    If mode=#IANIM_FORMAT_ARGB16
      a + %00000000 : If a>255 Then a=255
      r + %00000111 : If r>255 Then r=255
      g + %00000011 : If g>255 Then g=255
      b + %00000111 : If b>255 Then b=255
      a & %11100000
      r & %11110000
      g & %11111000
      b & %11110000
      ARGB16.l = (a LSL 8) | (r LSL 5) | (g LSL 1) | (b LSR 4)
      Poke.w compptr,ARGB16   : compptr+2
    End If

#dq = 8
#dqa = 16
    If mode=#IANIM_FORMAT_DIFF8
      If x MOD 2
        dda.l= (a-aa-(#dqa/2))/#dqa +8
        If dda>15 Then dda=15
        If dda< 0 Then dda= 0
        ddr.l= (r-rr-(#dq/2))/#dq +8
        If ddr>15 Then ddr=15
        If ddr< 0 Then ddr= 0

        DIFF8.l = (dda LSL 4) | (ddr )
        Poke.b compptr,DIFF8   : compptr+1
        aa + (dda -8)*#dqa
        rr + (ddr -8)*#dq

      Else
        ddg.l= (g-gg-(#dq/2))/#dq +8
        If ddg>15 Then ddg=15
        If ddg< 0 Then ddg= 0
        ddb.l= (b-bb-(#dq/2))/#dq +8
        If ddb>15 Then ddb=15
        If ddb< 0 Then ddb= 0

        DIFF8.l = (ddg LSL 4) | (ddb LSL 0)
        Poke.b compptr,DIFF8   : compptr+1
        gg + (ddg -8)*#dq
        bb + (ddb -8)*#dq
      End If
    End If



    If mode=#IANIM_FORMAT_RLERGB7 OR mode=#IANIM_FORMAT_RLERGB14 OR mode=#IANIM_FORMAT_RLEYUV8
      If a<10 ; totally transparent
        If head&#IANIM_HEAD_MASK=#IANIM_HEAD_TRANSPARENT
          compptr-1 : count+1
        Else
          count=0
        EndIf
        head.l = #IANIM_HEAD_TRANSPARENT | count
        Poke.b compptr,head  : compptr+1
        If count=%00111111 Then head = -1
      Else
        If a>250 ; totally opaque
          Select mode
            Case #IANIM_FORMAT_RLERGB7
              r + %00011111 : If r>255 Then r=255
              g + %00001111 : If g>255 Then g=255
              b + %00011111 : If b>255 Then b=255
              head = #IANIM_HEAD_OPAQUE
              RGB7.l = (r LSR 6) | ((g LSR 5) LSL 2) | ((b LSR 6) LSL 5)
              Poke.b compptr,RGB7   : compptr+1

            Case #IANIM_FORMAT_RLERGB14
              r + %00000111 : If r>255 Then r=255
              g + %00000001 : If g>255 Then g=255
              b + %00000111 : If b>255 Then b=255
              head = #IANIM_HEAD_OPAQUE | (g LSR 1)
              Poke.b compptr,head  : compptr+1
              RB8.l = (r LSR 4) | (b & %11110000)
              Poke.b compptr,RB8   : compptr+1

#yuvq = 4
            Case #IANIM_FORMAT_RLEYUV8
              If head&%10000000=0
                count+1
                compptr-1
              Else
                count=0
              EndIf
              head.l = #IANIM_HEAD_OPAQUE | count

              Poke.b compptr-count,head : compptr+1
              If count=%00111111 Then head = -1
              c.l = (r+g+b)/3
              u.l = (c-g)*#yuvq +128   : If u<0 Then u=0
              v.l = (c-r)*#yuvq +128   : If v<0 Then v=0

              c + %00000111 : If c>255 Then c=255
              u + %00011111 : If u>255 Then u=255
              v + %00011111 : If v>255 Then v=255
              c LSR 4
              u LSR 6
              v LSR 6
              cuv8.l = c | (u LSL 4) | (v LSL 6)
              Poke.b compptr,cuv8   : compptr+1

          End Select

        Else ; mixed
          r + %00001111 : If r>255 Then r=255
          g + %00001111 : If g>255 Then g=255
          b + %00011111 : If b>255 Then b=255
          RGB8.l = (r LSR 5) | ((g LSR 5) LSL 3) | ((b LSR 6) LSL 6)
          head = #IANIM_HEAD_MIXED | (a LSR 2)
          Poke.b compptr,head  : compptr+1
          Poke.b compptr,RGB8 : compptr+1
        End If
      End If
    End If ; RLE
  Next
Next
enclength.l = compptr - baseptr
Function Return enclength
End Function

Statement ianim_Blit{ianim.l,frameX.l,ARGBPixelArray.l,BytesPerRow.l,dx.l,dy.l}
SHARED ianimdat()
x2.l = dx + \width -1
y2.l = dy + \height-1
srcptr.l = \dataP ; + frameX ...
mode.b  = Peek.b(srcptr)  : srcptr+1
dummy.b = Peek.b(srcptr) : srcptr+1

If mode=#IANIM_FORMAT_ARGB16
  For y.l=dy To y2
    dstptr.l = ARGBPixelArray + (y*BytesPerRow) + dx*4
    For x.l=dx To x2
      ARGB16.l = Peek.w(srcptr)  : srcptr+2
      a.l = (ARGB16 & %1110000000000000) LSR 8
      r.l = (ARGB16 & %0001111000000000) LSR 5
      g.l = (ARGB16 & %0000000111110000) LSR 1
      b.l = (ARGB16 & %0000000000001111) LSL 4
      If a>0
        a.l = (a) | (a LSR 3); | (a LSR 6)
        ARGB.l = (a LSL 24) | (r LSL 16) | (g LSL 8) | (b)
        Poke.l dstptr,ARGB
      End If
      dstptr+4
    Next
  Next
End If

If mode=#IANIM_FORMAT_DIFF8
  For y.l=dy To y2
    dstptr.l = ARGBPixelArray + (y*BytesPerRow) + dx*4
    a.l = 0:r.l=0:g.l=0:b.l=0
    For x.l=dx To x2
      DIFF8.l = Peek.b(srcptr)  : srcptr+1
      If x MOD 2
        dda.l = (DIFF8 & %11110000) LSR 4
        ddr.l = (DIFF8 & %00001111)
        a + (dda -8)*#dqa
        r + (ddr -8)*#dq
      Else
        ddg.l = (DIFF8 & %11110000) LSR 4
        ddb.l = (DIFF8 & %00001111)
        g + (ddg -8)*#dq
        b + (ddb -8)*#dq
      End If

      If a>0
        xa.l = (a) | (a LSR 3)
        ARGB.l = (xa LSL 24) | (r LSL 16) | (g LSL 8) | (b)
      Else
        ARGB = $0000FF00
      End If
      Poke.l dstptr,ARGB
      dstptr+4
    Next
  Next
End If



If mode=#IANIM_FORMAT_RLERGB7 OR mode=#IANIM_FORMAT_RLERGB14 OR mode=#IANIM_FORMAT_RLEYUV8
  y = dy
  x = dx
  While y<=y2
    dstptr.l = ARGBPixelArray + (y*BytesPerRow) + x*4
    While x<=x2
      head.l = Peek.b(srcptr) : srcptr+1
      If head & %10000000
        If head & %01000000 ; it's a mix
          ARGB.l = Peek.l(dstptr)
          a.l = (head & %00111111) LSL 2
          RGB8.l = Peek.b(srcptr) : srcptr+1
          r.l = (RGB8 LSR  0) & %00000111
          g.l = (RGB8 LSR  3) & %00000111
          b.l = (RGB8 LSR  6) & %00000011
          ARGB.l = (a LSL 24) | (r LSL 21) | (g LSL 13) | (b LSL 6)
          Poke.l dstptr,ARGB  : dstptr+4 :x+1
        Else        ; its transparent
          Poke.l dstptr,$000000FF
          value.l = (head & %00111111) +1
          dstptr + 4*value :x+value
        End If
      Else     ; its opaque
        Select mode
          Case #IANIM_FORMAT_RLERGB7
            r.l = (head & %00000011) LSL 6
            g.l = (head & %00011100) LSL 3
            b.l = (head & %01100000) LSL 1
            ARGB.l = $FF000000 | (r LSL 16) | (g LSL 8) | (b)
            Poke.l dstptr,ARGB : dstptr+4 : x+1

          Case #IANIM_FORMAT_RLERGB14
            RB8.l = Peek.b(srcptr) : srcptr+1
            g.l = (head & %01111111) LSL 1
            r.l = (RB8  & %00001111) LSL 4
            b.l = (RB8  & %11110000)
            ARGB.l = $FF000000 | (r LSL 16) | (g LSL 8) | (b)
            Poke.l dstptr,ARGB : dstptr+4 : x+1

          Case #IANIM_FORMAT_RLEYUV8
            value.l = (head & %00111111)
            For n.l=0 To value
              cuv8.l =  Peek.b(srcptr) : srcptr+1
              c.l = (cuv8 & %00001111) LSL 4
              u.l = (cuv8 & %00110000) LSL 2
              v.l = (cuv8 & %11000000)

              r.l = c - (v-128)/#yuvq
              If r<0 Then r=0
              If r>255 Then r=255

              g.l = c - (u-128)/#yuvq
              If g<0 Then g=0
              If g>255 Then g=255

              b.l = c*3 -r-g
              If b<0 Then b=0
              If b>255 Then b=255

              ARGB.l = $FF000000 | (r LSL 16) | (g LSL 8) | (b)
              Poke.l dstptr,ARGB : dstptr+4 : x+1
            Next
        End Select
      End If
    Wend
    x-\width
    y+1
  Wend
End If
End Statement

Statement ianim_Free{ianim.l}
SHARED ianimdat()
If \dataP Then FreeVec_ \dataP : \dataP=0
End Statement

Statement ianim_FreeAll{}
For ianim.l = 0 To #MAX_IANIMS-1
  ianim_Free{ianim}
Next
End Statement
!autoexit{ianim_FreeAll}

Function.l ianim_Create{ianim.l,image.l,rows.l,cols.l,mode.l}
SHARED ianimdat()
; format: tt-dddddd [RGB]
; tt = 0 : transparent for d pixels, no RGB follow
;    = 1 : opaque for d pixels with d x RGB
;    = 2 : alpha with d alpha and RGB for 1 pixel
;    = 3 : reserved
succ.l = False
If image_AlphaIsUsed{image}=False Then image_SetAlpha{image,$FF}
image_FancyBlit{image,20,30,-1,-1,-1,#image_blitmode_alpha}
;image_Dither{image,12}
;image_Dither{image,17}
rawptr.l = image_GetARGBPtr{image}
rawbpr.l = image_GetARGBBpr{image}
width.l  = image_GetWidth{image}
height.l = image_GetHeight{image}
tmp.l    = tempbuffer_Get{width*height*4}
enclength.l = ianim_Encode{rawptr,tmp,rawbpr,width,height,mode}
\width  = width
\height = height
\dataP = AllocVec_(enclength,#MEMF_ANY)
CopyMem_ tmp,\dataP,enclength
;image_Draw{image,20,20}



ianim_Blit{0,0,rawptr,rawbpr,0,0}
;image_Draw{image,220,20}
xs.l = width
If xs>400 Then xs=400
image_FancyBlit{image,20+xs,30,-1,-1,-1,#image_blitmode_alpha}
error {"Encoded: "+Str$(width*height*4)+" to "+Str$(enclength)}
Function Return succ
End Function

WbToScreen 0
Window 0,0,0,1200,600,$E,"",1,0
;If image_Load{0,"sys:Programme/MapEdit/gfx/objects/House1.png"}
;If image_Load{0,"sys:Programme/MapEdit/gfx/objects/House2b.png"}
;If image_Load{0,"sys:Programme/MapEdit/gfx/objects/EE_main.png"}
If image_Load{0,"cache:EE_shot3.png"}
  image_Cut{1,0}
  image_Save{1,"ram:EE2_org.png",@"PNG"}
  ianim_Create{0,0,1,1,#IANIM_FORMAT_DIFF8}
  image_Save{0,"ram:EE2_DIFF8.png",@"PNG"}

  image_Cut{0,1}
  ianim_Create{0,0,1,1,#IANIM_FORMAT_ARGB16}
  image_Save{0,"ram:EE2_ARGB16.png",@"PNG"}

  image_Cut{0,1}
  ianim_Create{0,0,1,1,#IANIM_FORMAT_RLERGB14}
  image_Save{0,"ram:EE2_RLERGB14.png",@"PNG"}

  image_Cut{0,1}
  ianim_Create{0,0,1,1,#IANIM_FORMAT_RLERGB7}
  image_Save{0,"ram:EE2_RLERGB7.png",@"PNG"}

  image_Cut{0,1}
  ianim_Create{0,0,1,1,#IANIM_FORMAT_RLEYUV8}
  image_Save{0,"ram:EE2_RLEYUV8.png",@"PNG"}
End If
End

