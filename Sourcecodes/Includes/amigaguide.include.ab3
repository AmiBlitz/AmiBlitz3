; XTRA
; Embedded .xtra Header
; 
; General Info
; -------------------------------------------------------
; ExePath         = "/Bin"
; ExeFile         = "ViewAmigaGuideDemo"
; CreateIcon      = 0
; Residents       = "all.res"
; 
; Compiler
; -------------------------------------------------------
; StringBuffer    = 10240
; MakeSmallest    = 1
; FuncOptimize    = 1
; Version         = 0.0.0
; NumberOfBuilds  = 17
; 
; Debugger
; -------------------------------------------------------
; CliArgs         = ""
; StackSize       = 8191
; RuntimeDebug    = 0
; DebugInfo       = 0
; CreateDbgFile   = 0
; OverflowCheck   = 0
; AssemblerCheck  = 0
; InterruptCheck  = 1
; AutoRun         = 1
; 
; Editor
; -------------------------------------------------------
; CursorLine      = 7
; CursorColumn    = 27
; LabelSearch     = ""
; LabelRemark     = 61
; LabelAll        = 5
; LabelPosition   = 0
; 
; Blitz Objects
; -------------------------------------------------------
; Max IconInfo    = 1
; Max NChunky     = 50
; Max MUIObject   = 50
; Max PTModule    = 5
; Max AsyncReq    = 4
; Max Req-Lib     = 5
; Max GTMenuList  = 5
; Max Console     = 5
; Max TCPSock     = 5
; Max XBSound     = 10
; Max Chunky      = 20
; Max File        = 5
; Max GadgetList  = 50
; Max Queue       = 10
; Max Screen      = 5
; Max Shape       = 100
; Max CopList     = 10
; Max Sprite      = 20
; Max Stencil     = 5
; Max Module      = 5
; Max Window      = 50
; Max Anim        = 100
; Max Sound       = 10
; Max Bank        = 5
; Max Buffer      = 10
; Max BitMap      = 10
; Max Slice       = 10
; Max Page        = 4
; Max Tape        = 5
; Max IntuiFont   = 5
; Max MedModule   = 8
; Max Palette     = 4
; Max MenuList    = 5
; Max BlitzFont   = 4
; Max GTList      = 50
; Max ChunkyBuffer= 2
; /XTRA
;///////////////////////////////////////////////////////////////////////////////
;/                                                                             /
;/ Name: amigaguide.include                                                    /
;/                                                                             /
;/ Platforms: Classic, WinUAE, Amithlon, MorphOS, AmigaOS4                     /
;/                                                                             /
;/ Date: 19/01/2007                                                            /
;/                                                                             /
;/ Author: Thilo Koehler                                                       /
;/                                                                             /
;/ Requirements:  Amiblitz3, amigaguide.library                                /
;/                                                                             /
;/ Purpose:                                                                    /
;/ View amiga guide documents via amigaguide.library                           /
;/                                                                             /
;/ Abstract:                                                                   /
;/ This is a very easy way to integrate an amigaguide-based online help into:: /
;/  your program.                                                              /
;/ It is just one function to call with the filename of the desired amigagui:: /
;/ de.                                                                         /
;/ Future Plans: add asynchronsous viewing of amiga guide. Let the author kn:: /
;/ ow if you need this.                                                        /
;/                                                                             /
;/ User Constants:                                                             /
;/                                                                             /
;///////////////////////////////////////////////////////////////////////////////
CNIF #__include=0
Syntax 2
optimize 7
CEND

XINCLUDE "eclock.include.ab3"

;///////////////////////////////////////////////////////////////////////////////
;/                                                                             /
;/ Syntax:  result.l = amigaguide_Show {filename.s,@node.s,@pubname.s}         /
;/                                                                             /
;/ Description:                                                                /
;/ View an amigaguide document via amigaguide.library.                         /
;/ Note: This funcion blocks until the user closes the amigaguide window.      /
;/                                                                             /
;/ Inputs:                                                                     /
;/ - filename.s   : filename of the amigaguide file                            /
;/ - node.s       : optional a specific node name you want to jump-in          /
;/ - pubname.s    : optional pubscreen name                                    /
;/                                                                             /
;/ Result:                                                                     /
;/ - result.l     : -1 if the guide was open, 0 otherwise                      /
;/                                                                             /
;///////////////////////////////////////////////////////////////////////////////
Function.l amigaguide_Show{filename.s,@node.s,@pubname.s}
DEFTYPE.NewAmigaGuide NewAmigaGuide
succ.l = False
If dos_CheckLib{"amigaguide.library",0}=False Then Function Return False; Oops! we dont have amigaguide.library installed!
rec.Rectangle\MinX = 20,20,400,400
*scr.Screen = Peek.l(Addr Screen(Used Screen))
NewAmigaGuide\nag_Lock       = 0
NewAmigaGuide\nag_Name       = &filename.s
If pubname.s
  NewAmigaGuide\nag_Screen     = 0
  NewAmigaGuide\nag_PubScreen  = &pubname.s
Else
  NewAmigaGuide\nag_Screen     = *scr
  NewAmigaGuide\nag_PubScreen  = 0
End If
NewAmigaGuide\nag_HostPort   = 0
NewAmigaGuide\nag_ClientPort = 0
NewAmigaGuide\nag_BaseName   = 0
NewAmigaGuide\nag_Flags      = 0
NewAmigaGuide\nag_Context    = 0
If node.s
  NewAmigaGuide\nag_Node       = &node.s
Else
  NewAmigaGuide\nag_Node       = 0
End If
NewAmigaGuide\nag_Line       = 0
NewAmigaGuide\nag_Extens     = Tags(#HTNA_Rectangle,rec)
NewAmigaGuide\nag_Client     = 0

unit.l = eclock_ObtainUnit{}
eclock_Start{1000,unit}
AGH.l = OpenAmigaGuideA_ (NewAmigaGuide,Tags(#HTNA_Rectangle,rec))
time.l = eclock_Stop{unit}
eclock_ReleaseUnit{unit}
If AGH
  CloseAmigaGuide_ AGH
  If time>500 Then succ=-1
End If
Function Return succ
End Function

CNIF #__include=0
pubname.s ="AmiBlitz3_1"  ; if you are NOT using AB3, specify a valid pubscreen name here
                          ; e.g. "Workbench" or empty string for default pubscreen
filename.s = "Blitz3:Docs/Includes/tui.include.guide"
node.s     = "tui_cycle"
If amigaguide_Show{filename.s,node.s,pubname.s}
  ; guide found!
Else
  error {"Unable to show amigaguide! (wrong pubscreen, file or node?)"}
End If
End
CEND

