; XTRA
; Embedded .xtra Header
; 
; General Info
; -------------------------------------------------------
; ExePath         = "ram:"
; ExeFile         = "Empty"
; CreateIcon      = 0
; Residents       = "all.res"
; 
; Compiler
; -------------------------------------------------------
; StringBuffer    = 100000
; MakeSmallest    = 1
; Version         = 0.0.0
; NumberOfBuilds  = 14
; 
; Debugger
; -------------------------------------------------------
; CliArgs         = ""
; StackSize       = 8191
; RuntimeDebug    = 0
; DebugInfo       = 0
; CreateDbgFile   = 0
; OverflowCheck   = 0
; AssemblerCheck  = 0
; InterruptCheck  = 1
; AutoRun         = 1
; 
; Editor
; -------------------------------------------------------
; CursorLine      = 214
; CursorColumn    = 37
; LabelSearch     = ""
; LabelRemark     = 0
; LabelAll        = 0
; LabelPosition   = 0
; 
; Blitz Objects
; -------------------------------------------------------
; Max IconInfo    = 1
; Max NChunky     = 50
; Max MUIObject   = 50
; Max PTModule    = 5
; Max AsyncReq    = 4
; Max Req-Lib     = 5
; Max GTMenuList  = 100
; Max Console     = 5
; Max TCPSock     = 5
; Max XBSound     = 10
; Max Chunky      = 20
; Max File        = 100
; Max GadgetList  = 100
; Max Queue       = 10
; Max Screen      = 5
; Max Shape       = 200
; Max CopList     = 10
; Max Sprite      = 20
; Max Stencil     = 5
; Max Module      = 5
; Max Window      = 100
; Max Anim        = 10
; Max Sound       = 10
; Max Bank        = 1
; Max Buffer      = 10
; Max BitMap      = 10
; Max Slice       = 10
; Max Page        = 4
; Max Tape        = 5
; Max IntuiFont   = 10
; Max MedModule   = 8
; Max Palette     = 4
; Max MenuList    = 100
; Max BlitzFont   = 1
; Max GTList      = 100
; /XTRA
;///////////////////////////////////////////////////////////////////////////////
;/                                                                             /
;/ Syntax: _ntui_Layout {*tui.tuiObject,*rp.RastPort,@x.l,@y.l,@width.l,@hei:: /
;/ ght.l}                                                                      /
;/                                                                             /
;/ Description:                                                                /
;/ private: calculate the layout of the tui object given the dimensions.   /
;/                                                                             /
;/ Inputs:                                                                     /
;/ - *tui.tuiObject    : ???                                                   /
;/ - *rp.RastPort    : ???                                                     /
;/ - x.l    : ???                                                              /
;/ - y.l    : ???                                                              /
;/ - width.l    : ???                                                          /
;/ - height.l    : ???                                                         /
;/                                                                             /
;///////////////////////////////////////////////////////////////////////////////
#TUI_HAS_ENGINE = 1

#TUIEA_SKINPATH         = #TUIA_USER  + 1
#TUIEA_USESKIN          = #TUIA_USER  + 2
#TUIEA_BORDERSTYLE      = #TUIA_USER  + 3
#TUIEA_DEFLEFTMARGIN    = #TUIA_LEFTMARGIN
#TUIEA_DEFTOPMARGIN     = #TUIA_TOPMARGIN
#TUIEA_DEFRIGHTMARGIN   = #TUIA_RIGHTMARGIN
#TUIEA_DEFBOTTOMMARGIN  = #TUIA_BOTTOMMARGIN
#TUIEA_DEFLEFTPADDING   = #TUIA_LEFTPADDING
#TUIEA_DEFTOPPADDING    = #TUIA_TOPPADDING
#TUIEA_DEFRIGHTPADDING  = #TUIA_RIGHTPADDING
#TUIEA_DEFBOTTOMPADDING = #TUIA_BOTTOMPADDING
#TUIEA_USEAISS          = #TUIA_USER + 10
#TUIEA_AREXXPORT        = #TUIA_USER + 11
#TUIEA_APPNAME          = #TUIA_USER + 12
#TUIEA_MENUNAME         = #TUIA_USER + 13
#TUIEA_APPICONFILE      = #TUIA_USER + 14
#TUIEA_APPICONTEXT      = #TUIA_USER + 15
#TUIEA_PUBSCREENNAME    = #TUIA_USER + 16
#TUIEA_NORMALFONTNAME   = #TUIA_USER + 17
#TUIEA_FIXFONTNAME      = #TUIA_USER + 18
#TUIEA_SERIFFONTNAME    = #TUIA_USER + 19
#TUIEA_SMALLFONTNAME    = #TUIA_USER + 20
#TUIEA_NORMALFONTSIZE   = #TUIA_USER + 21
#TUIEA_FIXFONTSIZE      = #TUIA_USER + 22
#TUIEA_SERIFFONTSIZE    = #TUIA_USER + 23
#TUIEA_SMALLFONTSIZE    = #TUIA_USER + 24
#TUIEA_ONMESSAGE        = #TUIA_USER + 25
#TUIEA_ONICONDROP       = #TUIA_USER + 26
#TUIEA_ONQUIT           = #TUIA_USER + 27
#TUIEA_ONICONIFY        = #TUIA_USER + 28
#TUIEA_ONPOPUP          = #TUIA_USER + 29

Statement _ntui_SetEngineSkin{*tuiEngine.tuiEngine}

For n.l=0 To #TUIMAX_SKIN-1
  If *tuiEngine\skinImage[n]
    ntui_FreeTBImage{*tuiEngine\skinImage[n]}
    *tuiEngine\skinImage[n]=#NULL
  End If

  If *tuiEngine\useSkin
    If *tuiEngine\skinImageName[n]
      *tuiEngine\skinImage[n] = ntui_CreateTBImage{*tuiEngine,str_Read{&*tuiEngine\skinImageName[n]},-1,-1}
      If *tuiEngine\skinImage[n]
        If *tuiEngine\skinImage[n]\tuiImage=#TUIIMAGE_UNKNOWN
          *tuiEngine\skinImageAvail[n]=False
          ntui_FreeTBImage{*tuiEngine\skinImage[n]}
          *tuiEngine\skinImage[n] = #NULL
        Else
          *tuiEngine\skinImageAvail[n]=True
        End If
      End If
    End If
  End If
Next

End Statement


_ntui_SetEngineAttr:
Function.w _ntui_SetEngineAttr{*tuiEngine.tuiEngine,ti_Tag.l,ti_Data.l}
  !_ASSERT{*tuiEngine}
  !_ASSERT{*tuiEngine\obj\classID=#TUICLASS_ENGINE}

  done.w = True
  Select ti_Tag
    Case #TUIEA_SKINPATH
      str_WritePtr{&*tuiEngine\skinPath,ti_Data}
      _ntui_SetEngineSkin{*tuiEngine}
    Case #TUIEA_USESKIN
      *tuiEngine\useSkin = ti_Data
      _ntui_SetEngineSkin{*tuiEngine}
    Case #TUIEA_BORDERSTYLE       : *tuiEngine\borderStyle       = ti_Data
    Case #TUIEA_DEFLEFTMARGIN     : *tuiEngine\defMargin\left    = ti_Data
    Case #TUIEA_DEFTOPMARGIN      : *tuiEngine\defMargin\top     = ti_Data
    Case #TUIEA_DEFRIGHTMARGIN    : *tuiEngine\defMargin\right   = ti_Data
    Case #TUIEA_DEFBOTTOMMARGIN   : *tuiEngine\defMargin\bottom  = ti_Data
    Case #TUIEA_DEFLEFTPADDING    : *tuiEngine\defPadding\left   = ti_Data
    Case #TUIEA_DEFTOPPADDING     : *tuiEngine\defPadding\top    = ti_Data
    Case #TUIEA_DEFRIGHTPADDING   : *tuiEngine\defPadding\right  = ti_Data
    Case #TUIEA_DEFBOTTOMPADDING  : *tuiEngine\defPadding\bottom = ti_Data
    Case #TUIEA_USEAISS           : *tuiEngine\useAISS           = ti_Data
    Case #TUIEA_AREXXPORT         : str_WritePtr{&*tuiEngine\rxPortName   ,ti_Data}
    Case #TUIEA_APPNAME           : str_WritePtr{&*tuiEngine\appName      ,ti_Data}
    Case #TUIEA_MENUNAME          : str_WritePtr{&*tuiEngine\appMenuName  ,ti_Data}
    Case #TUIEA_APPICONFILE       : str_WritePtr{&*tuiEngine\appIconFile  ,ti_Data}
    Case #TUIEA_APPICONTEXT       : str_WritePtr{&*tuiEngine\appIconText  ,ti_Data}
    Case #TUIEA_PUBSCREENNAME     : str_WritePtr{&*tuiEngine\pubScreenName,ti_Data}
                                    If ti_Data
                                      string.s = Peek.s(ti_Data)
                                      If *tuiEngine\scr=#NULL
                                        ;_ntui_OpenScreen{*tuiEngine,string,#NULL}
                                      End If
                                    End If
    Case #TUIEA_NORMALFONTNAME    : str_WritePtr{&*tuiEngine\fontName[#TUIFONT_NORMAL]      ,ti_Data}
                                    str_WritePtr{&*tuiEngine\fontName[#TUIFONT_NORMALBOLD]  ,ti_Data}
                                    str_WritePtr{&*tuiEngine\fontName[#TUIFONT_NORMALITALIC],ti_Data}
                                    str_WritePtr{&*tuiEngine\fontName[#TUIFONT_NORMALIB]    ,ti_Data}
    Case #TUIEA_FIXFONTNAME       : str_WritePtr{&*tuiEngine\fontName[#TUIFONT_FIX]         ,ti_Data}
                                    str_WritePtr{&*tuiEngine\fontName[#TUIFONT_FIXBOLD]     ,ti_Data}
                                    str_WritePtr{&*tuiEngine\fontName[#TUIFONT_FIXITALIC]   ,ti_Data}
                                    str_WritePtr{&*tuiEngine\fontName[#TUIFONT_FIXIB]       ,ti_Data}
    Case #TUIEA_SERIFFONTNAME     : str_WritePtr{&*tuiEngine\fontName[#TUIFONT_SERIF]       ,ti_Data}
                                    str_WritePtr{&*tuiEngine\fontName[#TUIFONT_SERIFBOLD]   ,ti_Data}
                                    str_WritePtr{&*tuiEngine\fontName[#TUIFONT_SERIFITALIC] ,ti_Data}
                                    str_WritePtr{&*tuiEngine\fontName[#TUIFONT_SERIFIB]     ,ti_Data}
    Case #TUIEA_SMALLFONTNAME     : str_WritePtr{&*tuiEngine\fontName[#TUIFONT_SMALL]       ,ti_Data}
                                    str_WritePtr{&*tuiEngine\fontName[#TUIFONT_SMALLBOLD]   ,ti_Data}
                                    str_WritePtr{&*tuiEngine\fontName[#TUIFONT_SMALLITALIC] ,ti_Data}
                                    str_WritePtr{&*tuiEngine\fontName[#TUIFONT_SMALLIB]     ,ti_Data}
    Case #TUIEA_NORMALFONTSIZE    : *tuiEngine\fontSize[#TUIFONT_NORMAL]       = ti_Data
                                    *tuiEngine\fontSize[#TUIFONT_NORMALBOLD]   = ti_Data
                                    *tuiEngine\fontSize[#TUIFONT_NORMALITALIC] = ti_Data
                                    *tuiEngine\fontSize[#TUIFONT_NORMALIB]     = ti_Data
    Case #TUIEA_FIXFONTSIZE       : *tuiEngine\fontSize[#TUIFONT_FIX]          = ti_Data
                                    *tuiEngine\fontSize[#TUIFONT_FIXBOLD]      = ti_Data
                                    *tuiEngine\fontSize[#TUIFONT_FIXITALIC]    = ti_Data
                                    *tuiEngine\fontSize[#TUIFONT_FIXIB]        = ti_Data
    Case #TUIEA_SERIFFONTSIZE     : *tuiEngine\fontSize[#TUIFONT_SERIF]        = ti_Data
                                    *tuiEngine\fontSize[#TUIFONT_SERIFBOLD]    = ti_Data
                                    *tuiEngine\fontSize[#TUIFONT_SERIFITALIC]  = ti_Data
                                    *tuiEngine\fontSize[#TUIFONT_SERIFIB]      = ti_Data
    Case #TUIEA_SMALLFONTSIZE     : *tuiEngine\fontSize[#TUIFONT_SMALL]        = ti_Data
                                    *tuiEngine\fontSize[#TUIFONT_SMALLBOLD]    = ti_Data
                                    *tuiEngine\fontSize[#TUIFONT_SMALLITALIC]  = ti_Data
                                    *tuiEngine\fontSize[#TUIFONT_SMALLIB]      = ti_Data
    Case #TUIEA_ONMESSAGE         : str_WritePtr{&*tuiEngine\onMessage  ,ti_Data}
    Case #TUIEA_ONICONDROP        : str_WritePtr{&*tuiEngine\onIconDrop ,ti_Data}
    Case #TUIEA_ONQUIT            : str_WritePtr{&*tuiEngine\onQuit     ,ti_Data}
    Case #TUIEA_ONICONIFY         : str_WritePtr{&*tuiEngine\onIconify  ,ti_Data}
    Case #TUIEA_ONPOPUP           : str_WritePtr{&*tuiEngine\onPopup    ,ti_Data}

    Case #TUIA_XMLATTR
      *xmlA.tuiXmlAttr = ti_Data
      Select Peek.s(*xmlA\nameP)
        Case "borderstyle"
          If *xmlA\stringP
            Select Peek.s(*xmlA\stringP)
              Case "thin"  : ntui_SetAttr{*tuiEngine,#TUIEA_BORDERSTYLE,0}
              Case "thick" : ntui_SetAttr{*tuiEngine,#TUIEA_BORDERSTYLE,1}
              Default
                ntui_SetAttr{*tuiEngine,#TUIEA_BORDERSTYLE,*xmlA\value}
            End Select
          End If
        Case "skinpath"          : done=ntui_SetAttr{*tuiEngine,#TUIEA_SKINPATH         ,*xmlA\stringP}
        Case "useskin"           : done=ntui_SetAttr{*tuiEngine,#TUIEA_USESKIN          ,*xmlA\value}
        Case "defleftmargin"     : done=ntui_SetAttr{*tuiEngine,#TUIEA_DEFLEFTMARGIN    ,*xmlA\value}
        Case "deftopmargin"      : done=ntui_SetAttr{*tuiEngine,#TUIEA_DEFTOPMARGIN     ,*xmlA\value}
        Case "defrightmargin"    : done=ntui_SetAttr{*tuiEngine,#TUIEA_DEFRIGHTMARGIN   ,*xmlA\value}
        Case "defbottommargin"   : done=ntui_SetAttr{*tuiEngine,#TUIEA_DEFBOTTOMMARGIN  ,*xmlA\value}
        Case "defleftpadding"    : done=ntui_SetAttr{*tuiEngine,#TUIEA_DEFLEFTPADDING   ,*xmlA\value}
        Case "deftoppadding"     : done=ntui_SetAttr{*tuiEngine,#TUIEA_DEFTOPPADDING    ,*xmlA\value}
        Case "defrightpadding"   : done=ntui_SetAttr{*tuiEngine,#TUIEA_DEFRIGHTPADDING  ,*xmlA\value}
        Case "defbottompadding"  : done=ntui_SetAttr{*tuiEngine,#TUIEA_DEFBOTTOMPADDING ,*xmlA\value}
        Case "useaiss"           : done=ntui_SetAttr{*tuiEngine,#TUIEA_USEAISS          ,*xmlA\value}
        Case "arexxport"         : done=ntui_SetAttr{*tuiEngine,#TUIEA_AREXXPORT        ,*xmlA\stringP}
        Case "appname"           : done=ntui_SetAttr{*tuiEngine,#TUIEA_APPNAME          ,*xmlA\stringP}
        Case "menuname"          : done=ntui_SetAttr{*tuiEngine,#TUIEA_MENUNAME         ,*xmlA\stringP}
        Case "appiconfile"       : done=ntui_SetAttr{*tuiEngine,#TUIEA_APPICONFILE      ,*xmlA\stringP}
        Case "appicontext"       : done=ntui_SetAttr{*tuiEngine,#TUIEA_APPICONTEXT      ,*xmlA\stringP}
        Case "pubscreenname"     : done=ntui_SetAttr{*tuiEngine,#TUIEA_PUBSCREENNAME    ,*xmlA\stringP}
        Case "normalfontname"    : done=ntui_SetAttr{*tuiEngine,#TUIEA_NORMALFONTNAME   ,*xmlA\stringP}
        Case "fixfontname"       : done=ntui_SetAttr{*tuiEngine,#TUIEA_FIXFONTNAME      ,*xmlA\stringP}
        Case "seriffontname"     : done=ntui_SetAttr{*tuiEngine,#TUIEA_SERIFFONTNAME    ,*xmlA\stringP}
        Case "smallfontname"     : done=ntui_SetAttr{*tuiEngine,#TUIEA_SMALLFONTNAME    ,*xmlA\stringP}
        Case "normalfontsize"    : done=ntui_SetAttr{*tuiEngine,#TUIEA_NORMALFONTSIZE   ,*xmlA\value}
        Case "fixfontsize"       : done=ntui_SetAttr{*tuiEngine,#TUIEA_FIXFONTSIZE      ,*xmlA\value}
        Case "seriffontsize"     : done=ntui_SetAttr{*tuiEngine,#TUIEA_SERIFFONTSIZE    ,*xmlA\value}
        Case "smallfontsize"     : done=ntui_SetAttr{*tuiEngine,#TUIEA_SMALLFONTSIZE    ,*xmlA\value}
        Case "onmessage"         : done=ntui_SetAttr{*tuiEngine,#TUIEA_ONMESSAGE        ,*xmlA\stringP}
        Case "onquit"            : done=ntui_SetAttr{*tuiEngine,#TUIEA_ONQUIT           ,*xmlA\stringP}
        Case "onpopup"           : done=ntui_SetAttr{*tuiEngine,#TUIEA_ONPOPUP          ,*xmlA\stringP}
        Case "oniconify"         : done=ntui_SetAttr{*tuiEngine,#TUIEA_ONICONIFY        ,*xmlA\stringP}
        Case "onicondrop"        : done=ntui_SetAttr{*tuiEngine,#TUIEA_ONICONDROP       ,*xmlA\stringP}
        Default
          done=False
      End Select

    Default
      done=False

  End Select

  Function Return done
End Function


_ntui_GetEngineAttr:
Function.w _ntui_GetEngineAttr{*tuiEngine.tuiEngine,ti_Tag.l,*ti_Data.longP}
  !_ASSERT{*tuiEngine}
  !_ASSERT{*tuiEngine\obj\classID=#TUICLASS_ENGINE}

  done.w = True
  Select ti_Tag
    Case #TUIEA_SKINPATH          : *ti_Data\l = str_GetPtr{*tuiEngine\skinPath}
    Case #TUIEA_USESKIN           : *ti_Data\l = *tuiEngine\useSkin
    Case #TUIEA_BORDERSTYLE       : *ti_Data\l = *tuiEngine\borderStyle
    Case #TUIEA_DEFLEFTMARGIN     : *ti_Data\l = *tuiEngine\defMargin\left
    Case #TUIEA_DEFTOPMARGIN      : *ti_Data\l = *tuiEngine\defMargin\top
    Case #TUIEA_DEFRIGHTMARGIN    : *ti_Data\l = *tuiEngine\defMargin\right
    Case #TUIEA_DEFBOTTOMMARGIN   : *ti_Data\l = *tuiEngine\defMargin\bottom
    Case #TUIEA_DEFLEFTPADDING    : *ti_Data\l = *tuiEngine\defPadding\left
    Case #TUIEA_DEFTOPPADDING     : *ti_Data\l = *tuiEngine\defPadding\top
    Case #TUIEA_DEFRIGHTPADDING   : *ti_Data\l = *tuiEngine\defPadding\right
    Case #TUIEA_DEFBOTTOMPADDING  : *ti_Data\l = *tuiEngine\defPadding\bottom
    Case #TUIEA_USEAISS           : *ti_Data\l = *tuiEngine\useAISS
    Case #TUIEA_AREXXPORT         : *ti_Data\l = str_GetPtr{*tuiEngine\rxPortName}
    Case #TUIEA_APPNAME           : *ti_Data\l = str_GetPtr{*tuiEngine\appName}
    Case #TUIEA_MENUNAME          : *ti_Data\l = str_GetPtr{*tuiEngine\appMenuName}
    Case #TUIEA_APPICONFILE       : *ti_Data\l = str_GetPtr{*tuiEngine\appIconFile}
    Case #TUIEA_APPICONTEXT       : *ti_Data\l = str_GetPtr{*tuiEngine\appIconText}
    Case #TUIEA_PUBSCREENNAME     : *ti_Data\l = str_GetPtr{*tuiEngine\pubScreenName}
    Case #TUIEA_NORMALFONTNAME    : *ti_Data\l = str_GetPtr{*tuiEngine\fontName[#TUIFONT_NORMAL]}
    Case #TUIEA_FIXFONTNAME       : *ti_Data\l = str_GetPtr{*tuiEngine\fontName[#TUIFONT_FIX]}
    Case #TUIEA_SERIFFONTNAME     : *ti_Data\l = str_GetPtr{*tuiEngine\fontName[#TUIFONT_SERIF]}
    Case #TUIEA_SMALLFONTNAME     : *ti_Data\l = str_GetPtr{*tuiEngine\fontName[#TUIFONT_SMALL]}
    Case #TUIEA_NORMALFONTSIZE    : *ti_Data\l = *tuiEngine\fontSize[#TUIFONT_NORMAL]
    Case #TUIEA_FIXFONTSIZE       : *ti_Data\l = *tuiEngine\fontSize[#TUIFONT_FIX]
    Case #TUIEA_SERIFFONTSIZE     : *ti_Data\l = *tuiEngine\fontSize[#TUIFONT_SERIF]
    Case #TUIEA_SMALLFONTSIZE     : *ti_Data\l = *tuiEngine\fontSize[#TUIFONT_SMALL]
    Case #TUIEA_ONMESSAGE         : *ti_Data\l = str_GetPtr{*tuiEngine\onMessage}
    Case #TUIEA_ONICONDROP        : *ti_Data\l = str_GetPtr{*tuiEngine\onIconDrop}
    Case #TUIEA_ONQUIT            : *ti_Data\l = str_GetPtr{*tuiEngine\onQuit}
    Case #TUIEA_ONICONIFY         : *ti_Data\l = str_GetPtr{*tuiEngine\onIconify}
    Case #TUIEA_ONPOPUP           : *ti_Data\l = str_GetPtr{*tuiEngine\onPopup}

    Default
      done=False

  End Select

  Function Return done
End Function


_ntui_LayoutEngine:
Statement _ntui_LayoutEngine{*tuiEngine.tuiEngine,*rp.RastPort,*bbox.tuiRect}
  *child.tuiObject = *tuiEngine\obj\child
  While *child
    _ntui_Layout{*child,*tuiEngine,*rp}
    *child=*child\next_
  Wend
End Statement


_ntui_DispatchEngineEvent:
Function.l _ntui_DispatchEngineEvent{*tuiEngine.tuiEngine,*tuiEvent.tuiEvent}
USEPATH *tuiEvent
!_ASSERT{*tuiEngine}
!_ASSERT{*tuiEvent}
done.l = True
Select \evID
  Case #TUIEV_MESSAGE  : str_Dup{&\notifyID,*tuiEngine\onMessage }
  Case #TUIEV_POPUP    : str_Dup{&\notifyID,*tuiEngine\onPopup   }
  Case #TUIEV_ICONIFY  : str_Dup{&\notifyID,*tuiEngine\onIconify }
  Case #TUIEV_QUIT     : str_Dup{&\notifyID,*tuiEngine\onQuit    }
  Case #TUIEV_ICONDROP : str_Dup{&\notifyID,*tuiEngine\onIconDrop}
  Default
    done=False
End Select

If done
  If (*tuiEvent\notifyID)
    *tuiEvent\tuiObject = *tuiEngine
    _ntui_QueueEvent{*tuiEngine\outQ,*tuiEvent}
  Else
    _ntui_DestroyEvent{*tuiEvent}
  End If
End If

Function Return done
End Function


;///////////////////////////////////////////////////////////////////////////////
;/                                                                             /
;/ Syntax: ntui_ShowAppIcon {*tuiEngine.tuiEngine,@text.s}                     /
;/                                                                             /
;/ Description:                                                                /
;/                                                                             /
;/ Inputs:                                                                     /
;/ - *tuiEngine.tuiEngine    : ???                                             /
;/ - text.s    : ???                                                           /
;/                                                                             /
;///////////////////////////////////////////////////////////////////////////////
Statement ntui_ShowAppIcon{*tuiEngine.tuiEngine,@text.s}
If *tuiEngine=#NULL Then Statement Return
If *tuiEngine\appIcon=#NULL
  If *tuiEngine\appIconDO
    If text Then str_Write{&*tuiEngine\appIconText,text}
    ;MOVE.l d6,-(a7)
    ;MOVE.l d7,-(a7)
    MOVE.l a4,-(a7)
    AddAppIconA_ #APP_ICON,#NULL,&*tuiEngine\appIconText\str,*tuiEngine\appPort,#NULL,*tuiEngine\appIconDO,#NULL
    MOVE.l d0,appIcon
    MOVE.l (a7)+,a4
    ;MOVE.l (a7)+,d7
    ;MOVE.l (a7)+,d6
    *tuiEngine\appIcon = Peek.l(?appIcon)
  End If
End If
Statement Return
appIcon:
Dc.l 0
End Statement


;///////////////////////////////////////////////////////////////////////////////
;/                                                                             /
;/ Syntax: ntui_HideAppIcon {*tuiEngine.tuiEngine}                             /
;/                                                                             /
;/ Description:                                                                /
;/ * no description available *                                                /
;/                                                                             /
;/ Inputs:                                                                     /
;/ - *tuiEngine.tuiEngine    : ???                                             /
;/                                                                             /
;///////////////////////////////////////////////////////////////////////////////
Statement ntui_HideAppIcon{*tuiEngine.tuiEngine}
If *tuiEngine=#NULL Then Statement Return
If *tuiEngine\appIcon Then RemoveAppIcon_ *tuiEngine\appIcon : *tuiEngine\appIcon = #NULL
End Statement


;///////////////////////////////////////////////////////////////////////////////
;/                                                                             /
;/ Syntax: result.w = ntui_Iconify {*tuiEngine.tuiEngine}                      /
;/                                                                             /
;/ Description:                                                                /
;/ * re-open all windows, if any... */                                         /
;/                                                                             /
;/ Inputs:                                                                     /
;/ - *tuiEngine.tuiEngine    : ???                                             /
;/                                                                             /
;/ Result:                                                                     /
;/ - result.w     : ???                                                        /
;/                                                                             /
;///////////////////////////////////////////////////////////////////////////////
Function.w _ntui_CloseScreen{*tuiEngine.tuiEngine}
If *tuiEngine =#NULL Then Function Return False
If *tuiEngine\obj\classID><#TUICLASS_ENGINE Then Function Return False
If *tuiEngine\scr=#NULL Then Function Return True ; we are already iconified...

*scr.Screen = *tuiEngine\scr

;/* free all pens */
For tuipen.l=0 To #TUIMAX_PENS-1
  If *tuiEngine\penInfo[tuipen]\locked
    ReleasePen_ *scr\ViewPort\ColorMap,*tuiEngine\pen[tuipen]
    *tuiEngine\pen[tuipen]=0
    *tuiEngine\penInfo[tuipen]\locked=False
  End If
Next

For font.l=0 To #TUIMAX_FONTS-1
  If *tuiEngine\font[font] Then CloseFont_ *tuiEngine\font[font] : *tuiEngine\font[font] = #NULL
Next

; release images (maybe?)

*tuiEngine\rememberScreen = *tuiEngine\scr
*tuiEngine\scr            = #NULL

If *tuiEngine\snScreenH
  screennotify_RemCloseScreenClient{*tuiEngine\snScreenH}
  *tuiEngine\snScreenH = 0
End If

Function Return True
End Function

Function.w _ntui_ReadPrefs{*tuiEngine.tuiEngine,prefsfile.s}
*tempP.b = _ntui_ObtainTempBuffer{4096}
If *tempP=#NULL Then Function Return False
fh.l = Open_ (&prefsfile,#MODE_OLDFILE)
If fh
  *buffP.b = *tempP
  Repeat
    *buffP.b = FGets_(fh,*tempP,4096)
    If *buffP
      l.l = 0
      name.s = ""
      string.s = ""
      value.l = 0
      doName.l = True
      doValue.l = False
      nb.l = 0
      ne.l = 0
      vb.l = 0
      ve.l = 0
      While (Peek.b(*buffP+l)><0)
        If doName=True AND Peek.b(*buffP+l)=@"=" Then doValue=True : doName=False:ne=l-1:vb=l+1
      Wend
      If doValue Then ve = l-1
      If doName  Then ne = l-1
      name     = LCase$(Peeks$(*buffP+nb,ne-nb+1))
      string.s = Peeks$(*buffP+vb,ve-vb+1)
      value.l  = Vallong(string)
      Select LCase$(string)
        Case "yes"   : value=1
        Case "no"    : value=0
        Case "on"    : value=1
        Case "off"   : value=0
        Case "true"  : value=1
        Case "false" : value=0
      End Select

      ;xmlA.tuiXmlAttr\nameP = #NULL
      ;ntui_SetAttr{*tuiEngine,#TUIA_XMLATTR,xmlA}
    End If
  Until *buffP=#NULL
End If

Function Return True
End Function

;///////////////////////////////////////////////////////////////////////////////
;/                                                                             /
;/ Syntax: succ.w = ntui_PopUp {*tuiEngine.tuiEngine,@pubname.s,@*scr.Scre:: /
;/ en}                                                                         /
;/                                                                             /
;/ Description:                                                                /
;/ Pop the tui engine up on a screen.                                      /
;/ This reverses the effect of ntui_Iconify.
;/ In iconified mode (or if tui tuiEngine was just created), it is not allowed to open any window. The engine must first "popped" to a screen.
;/                                                                             /
;/ Inputs:                                                                     /
;/ - *tuiEngine.tuiEngine : tuiEngine to pop up                                             /
;/ - pubname.s            : name of the PubScreen (e.g. "Workbench")                                                       /
;/ - *scr.Screen          : pointer to a custom screen                                                     /
;/                                                                             /
;/ Result:                                                                     /
;/ - succ.w               : True if popped up, False if failed                                                        /
;/                                                                             /
;///////////////////////////////////////////////////////////////////////////////
Function.w _ntui_OpenScreen{*tuiEngine.tuiEngine,@pubname.s,@*scr.Screen}
!_ASSERT{*tuiEngine}
If (*tuiEngine\obj\classID><#TUICLASS_ENGINE) Then Function Return False
If (*tuiEngine\scr) Then Function Return True ; we are already popped-up
If *scr=-1 OR *scr=#NULL
  *scr=#NULL
  *defscr.Screen = #NULL
  If pubname=""
    MaxLen pubname=256
    GetDefaultPubScreen_ &pubname
    pubname = Peek.s(&pubname)
  End If
  *psl.List = LockPubScreenList_()
  If *psl
    *psn.PubScreenNode = *psl\lh_Head
    While *psn\psn_Node\ln_Succ
      If *defscr=#NULL Then *defscr=*psn\psn_Screen
      If (*psn\psn_Node\ln_Name)
      If (LCase$(pubname)=LCase$(Peek.s(*psn\psn_Node\ln_Name)))
        *scr=*psn\psn_Screen
        ;error {"Found screen "+pubname}
      End If
      End If
      *psn = *psn\psn_Node\ln_Succ
    Wend
  End If
  UnlockPubScreenList_
  If *scr=#NULL Then *scr=*defscr
End If


If (*scr      =#NULL) Then Function Return False
succ.w = False
*tuiEngine\scr = *scr

fontnormal.s = ""   : fontnormalsize.l = 16
fontsmall.s  = ""   : fontsmallsize.l  = 12
fontserif.s  = ""   : fontserifsize.l    = 16
fontfix.s    = ""   : fontfixsize.l    = 16

;fontnormal.s = "Vera Sans"   : fontnormalsize.l = 11
;fontsmall.s  = "Vera Sans"   : fontsmallsize.l  = 8
fontserif.s    = "DroidSerif"   : fontserifsize.l    = 16
;fontfix.s    = "Vera Mono"   : fontfixsize.l    = 16

;fontnormal.s = "thinpaz_small"   : fontnormalsize.l = 8
;fontsmall.s  = "thinpaz_small"   : fontsmallsize.l  = 8
;fontserif.s    = "Vera Sans"   : fontserifsize.l    = 24
;fontfix.s    = "thinpaz"   : fontfixsize.l    = 8

;fontnormal.s = "Helvetica"   : fontnormalsize.l = 13
*tuiEngine\font[#TUIFONT_NORMAL]       = _ntui_OpenFont{fontnormal,fontnormalsize ,#TUIF_NORMAL,*scr}
*tuiEngine\font[#TUIFONT_SMALL]        = _ntui_OpenFont{fontsmall ,fontsmallsize  ,#TUIF_SMALL ,*scr}
*tuiEngine\font[#TUIFONT_FIX]          = _ntui_OpenFont{fontfix   ,fontfixsize    ,#TUIF_FIX   ,*scr}
*tuiEngine\font[#TUIFONT_SERIF]        = _ntui_OpenFont{fontserif ,fontserifsize  ,#TUIF_SERIF   ,*scr}

*tuiEngine\font[#TUIFONT_NORMALBOLD]   = _ntui_OpenFont{fontnormal,fontnormalsize ,#TUIF_NORMAL|#TUIF_BOLD  ,*scr}
*tuiEngine\font[#TUIFONT_SMALLBOLD]    = _ntui_OpenFont{fontsmall ,fontsmallsize  ,#TUIF_SMALL |#TUIF_BOLD  ,*scr}
*tuiEngine\font[#TUIFONT_FIXBOLD]      = _ntui_OpenFont{fontfix   ,fontfixsize    ,#TUIF_FIX   |#TUIF_BOLD  ,*scr}
*tuiEngine\font[#TUIFONT_SERIFBOLD]    = _ntui_OpenFont{fontserif ,fontserifsize  ,#TUIF_SERIF |#TUIF_BOLD  ,*scr}

*tuiEngine\font[#TUIFONT_NORMALITALIC] = _ntui_OpenFont{fontnormal,fontnormalsize ,#TUIF_NORMAL|#TUIF_ITALIC,*scr}
*tuiEngine\font[#TUIFONT_SMALLITALIC]  = _ntui_OpenFont{fontsmall ,fontsmallsize  ,#TUIF_SMALL |#TUIF_ITALIC,*scr}
*tuiEngine\font[#TUIFONT_FIXITALIC]    = _ntui_OpenFont{fontfix   ,fontfixsize    ,#TUIF_FIX   |#TUIF_ITALIC,*scr}
*tuiEngine\font[#TUIFONT_SERIFITALIC]  = _ntui_OpenFont{fontserif ,fontserifsize  ,#TUIF_SERIF |#TUIF_ITALIC,*scr}

*tuiEngine\font[#TUIFONT_NORMALIB]     = _ntui_OpenFont{fontnormal,fontnormalsize ,#TUIF_NORMAL|#TUIF_BOLD|#TUIF_ITALIC,*scr}
*tuiEngine\font[#TUIFONT_SMALLIB]      = _ntui_OpenFont{fontsmall ,fontsmallsize  ,#TUIF_SMALL |#TUIF_BOLD|#TUIF_ITALIC,*scr}
*tuiEngine\font[#TUIFONT_FIXIB]        = _ntui_OpenFont{fontfix   ,fontfixsize    ,#TUIF_FIX   |#TUIF_BOLD|#TUIF_ITALIC,*scr}
*tuiEngine\font[#TUIFONT_SERIFIB]      = _ntui_OpenFont{fontserif ,fontserifsize  ,#TUIF_SERIF |#TUIF_BOLD|#TUIF_ITALIC,*scr}

_ntui_ObtainTuiPen{*tuiEngine,$C3C3C3,#TUIPEN_BACKGROUND}
_ntui_ObtainTuiPen{*tuiEngine,$000000,#TUIPEN_BLACK}
_ntui_ObtainTuiPen{*tuiEngine,$FFFFFF,#TUIPEN_WHITE}
_ntui_ObtainTuiPen{*tuiEngine,$EEEEEE,#TUIPEN_SHINE}
_ntui_ObtainTuiPen{*tuiEngine,$666666,#TUIPEN_SHADOW}
_ntui_ObtainTuiPen{*tuiEngine,$DDDDDD,#TUIPEN_HALFSHINE}
_ntui_ObtainTuiPen{*tuiEngine,$AAAAAA,#TUIPEN_HALFSHADOW}
_ntui_ObtainTuiPen{*tuiEngine,$D0D0D0,#TUIPEN_HALFHALFSHINE}
_ntui_ObtainTuiPen{*tuiEngine,$BBBBBB,#TUIPEN_HALFHALFSHADOW}
_ntui_ObtainTuiPen{*tuiEngine,(($8090D0&$FEFEFEFE) LSR 1)+(($C3C3C3&$FEFEFEFE) LSR 1),#TUIPEN_HALFMARKER}
_ntui_ObtainTuiPen{*tuiEngine,$8090D0,#TUIPEN_MARKER}
_ntui_ObtainTuiPen{*tuiEngine,$000000,#TUIPEN_TEXT}
_ntui_ObtainTuiPen{*tuiEngine,$EEEEFF,#TUIPEN_ACTIVETEXT}
_ntui_ObtainTuiPen{*tuiEngine,$FFF5D8,#TUIPEN_TOOLTIPBG}
_ntui_ObtainTuiPen{*tuiEngine,$FF0000,#TUIPEN_RED}
_ntui_ObtainTuiPen{*tuiEngine,$00FF00,#TUIPEN_GREEN}
_ntui_ObtainTuiPen{*tuiEngine,$0000FF,#TUIPEN_BLUE}

*tuiEngine\defSize[#TUISIZE_MIN          ] = #TUI_MINPIXELSIZE ; minimum possible (usually equals to 8)
*tuiEngine\defSize[#TUISIZE_SMALLFONT    ] = *tuiEngine\font[#TUIFONT_SMALL ]\tf_YSize     ; like small font
*tuiEngine\defSize[#TUISIZE_FONT         ] = *tuiEngine\font[#TUIFONT_NORMAL]\tf_YSize    ; like normal font
*tuiEngine\defSize[#TUISIZE_SERIFFONT    ] = *tuiEngine\font[#TUIFONT_SERIF ]\tf_YSize       ; like serif font
*tuiEngine\defSize[#TUISIZE_FIXFONT      ] = *tuiEngine\font[#TUIFONT_FIX   ]\tf_YSize       ; like fix font
*tuiEngine\defSize[#TUISIZE_SMALLINLINE  ] = *tuiEngine\font[#TUIFONT_SMALL ]\tf_YSize ; like small font  +2
*tuiEngine\defSize[#TUISIZE_INLINE       ] = *tuiEngine\font[#TUIFONT_NORMAL]\tf_YSize ; like normal font +2
*tuiEngine\defSize[#TUISIZE_HALFINLINE   ] = *tuiEngine\font[#TUIFONT_NORMAL]\tf_YSize/2+1 ; like normal (font +2)/2
*tuiEngine\defSize[#TUISIZE_DOUBLEINLINE ] = *tuiEngine\font[#TUIFONT_NORMAL]\tf_YSize*2+1 ; like normal (font +2)/2
*tuiEngine\defSize[#TUISIZE_ONEHALFINLINE] = *tuiEngine\font[#TUIFONT_NORMAL]\tf_YSize*3/2+1 ; like normal (font +2)/2
*tuiEngine\defSize[#TUISIZE_SERIFINLINE  ] = *tuiEngine\font[#TUIFONT_SERIF ]\tf_YSize ; like serif font    +2
*tuiEngine\defSize[#TUISIZE_FIXINLINE    ] = *tuiEngine\font[#TUIFONT_FIX   ]\tf_YSize ; like fix font    +2
*tuiEngine\defSize[#TUISIZE_BUTTON       ] = *tuiEngine\font[#TUIFONT_NORMAL]\tf_YSize ;2 + *tuiEngine\defPadding\top+*tuiEngine\defPadding\bottom ; like buttons (normal font +2 + pad\y*2)
*tuiEngine\defSize[#TUISIZE_HALFBUTTON   ] = *tuiEngine\font[#TUIFONT_NORMAL]\tf_YSize/2
*tuiEngine\defSize[#TUISIZE_KNOB         ] = *tuiEngine\font[#TUIFONT_NORMAL]\tf_YSize-3
*tuiEngine\defSize[#TUISIZE_SCROLLER     ] = *tuiEngine\font[#TUIFONT_NORMAL]\tf_YSize-1
*tuiEngine\defSize[#TUISIZE_NATIVE       ] = *tuiEngine\font[#TUIFONT_NORMAL]\tf_YSize*3/2 ; native image resolution
*tuiEngine\defSize[#TUISIZE_MAX          ] = *tuiEngine\font[#TUIFONT_NORMAL]\tf_YSize*2 ; maximum possible

If ((*tuiEngine\defSize[#TUISIZE_SCROLLER]&1) = 0) Then *tuiEngine\defSize[#TUISIZE_SCROLLER]+1

For n.l=0 To #TUISIZE_MAX
  If *tuiEngine\defSize[n]<*tuiEngine\defSize[#TUISIZE_MIN] Then *tuiEngine\defSize[n]=*tuiEngine\defSize[#TUISIZE_MIN]
Next

If *tuiEngine\snPort
  If *tuiEngine\snScreenH Then screennotify_RemCloseScreenClient{*tuiEngine\snScreenH}
  *tuiEngine\snScreenH = screennotify_AddCloseScreenClient{*tuiEngine\scr,*tuiEngine\snPort}
End If

succ=True
Function Return succ
End Function




;///////////////////////////////////////////////////////////////////////////////
;/                                                                             /
;/ Syntax: _ntui_FreeEngine {*tuiEngine.tuiEngine}                             /
;/                                                                             /
;/ Description:                                                                /
;/ private: free the tuiEngine
;/                                                                             /
;/ Inputs:                                                                     /
;/ - *tuiEngine.tuiEngine    : ???                                             /
;/                                                                             /
;///////////////////////////////////////////////////////////////////////////////
_ntui_DeinitEngine
Statement _ntui_DeinitEngine{*tuiEngine.tuiEngine}
_ntui_CloseScreen{*tuiEngine}
If *tuiEngine\rxPort       Then _ntui_FreeRexx{*tuiEngine}

If *tuiEngine\winPort      Then DeleteMsgPort_     *tuiEngine\winPort     : *tuiEngine\winPort     = #NULL

If *tuiEngine\snWorkbenchH Then screennotify_RemWorkbenchClient{*tuiEngine\snWorkbenchH} : *tuiEngine\snWorkbenchH = 0
If *tuiEngine\snScreenH    Then screennotify_RemCloseScreenClient{*tuiEngine\snScreenH}  : *tuiEngine\snScreenH    = 0
If *tuiEngine\snPort       Then DeleteMsgPort_     *tuiEngine\snPort      : *tuiEngine\snPort      = #NULL

If *tuiEngine\appIcon      Then RemoveAppIcon_     *tuiEngine\appIcon     : *tuiEngine\appIcon     = #NULL
If *tuiEngine\appIconDO    Then FreeDiskObject_    *tuiEngine\appIconDO   : *tuiEngine\appIconDO   = #NULL
If *tuiEngine\appMenu      Then RemoveAppMenuItem_ *tuiEngine\appMenu     : *tuiEngine\appMenu     = #NULL
If *tuiEngine\appPort      Then DeleteMsgPort_     *tuiEngine\appPort     : *tuiEngine\appPort     = #NULL

If *tuiEngine\outQ\sigBit  Then FreeSignal_        *tuiEngine\outQ\sigBit : *tuiEngine\outQ\sigBit = 0
If *tuiEngine\inQ\sigBit   Then FreeSignal_        *tuiEngine\inQ\sigBit  : *tuiEngine\inQ\sigBit  = 0

If *tuiEngine\aslfo        Then FreeAslRequest_ *tuiEngine\aslfo : *tuiEngine\aslfo = #NULL
If *tuiEngine\aslfr        Then FreeAslRequest_ *tuiEngine\aslfr : *tuiEngine\aslfr = #NULL
If *tuiEngine\aslsm        Then FreeAslRequest_ *tuiEngine\aslsm : *tuiEngine\aslsm = #NULL

str_Free{&*tuiEngine\appName}
str_Free{&*tuiEngine\appMenuName}
str_Free{&*tuiEngine\appIconFile}
str_Free{&*tuiEngine\appIconText}

If *tuiEngine\eventPool    Then DeletePool_ *tuiEngine\eventPool  : *tuiEngine\eventPool  = #NULL
If *tuiEngine\objectPool   Then DeletePool_ *tuiEngine\objectPool : *tuiEngine\objectPool = #NULL

str_Free{&*tuiEngine\onPopup}       ; wb menu or icon double click
str_Free{&*tuiEngine\onQuit}        ; app is requested to quit
str_Free{&*tuiEngine\onIconDrop}    ; drop icon on appicon
str_Free{&*tuiEngine\onIconify}     ; iconify
str_Free{&*tuiEngine\onMessage}     ; arexx message

End Statement


;///////////////////////////////////////////////////////////////////////////////
;/                                                                             /
;/ Syntax: result.l = ntui_CreateEngine {appname.s,@menuname.s,@icon.s,@pubn:: /
;/ ame.s,@*scr.Screen}                                                         /
;/                                                                             /
;/ Description:                                                                /
;/ * create a new tui engine */                                                /
;/                                                                             /
;/ Inputs:                                                                     /
;/ - appname.s    : name of the application                                                        /
;/ - menuname.s   : optional name for the Workbench App Menu                                                       /
;/ - icon.s       : optional icon that serves as an app icon                                                           /
;/ - pubname.s    : optional pubscreen name to popup on                                                        /
;/ - *scr.Screen  : optional screen pointer to popup on                                                      /
;/                                                                             /
;/ Result:                                                                     /
;/ - result.l     : tuiEngine                                                        /
;/                                                                             /
;///////////////////////////////////////////////////////////////////////////////

Function.l ntui_CreateEngine{appName.s,@arexxPort.s,@appMenu.s,@appIcon.s,@pubname.s,@*scr.Screen}
;ntui_BeginBuild{#NULL}
*tuiEngine.tuiEngine = _ntui_CreateObject{#TUICLASS_ENGINE,SizeOf.tuiEngine,"",0,""}

If *tuiEngine
  tuiGlobal\buildHelper\tuiEngine = *tuiEngine

  *tuiEngine\defMargin\left       = 1,1,1,1
  *tuiEngine\defPadding\left      = 2,2,2,2
  For n.l=0 To #TUIMAX_SIZES-1
    *tuiEngine\defSize[n]   = #TUI_MINPIXELSIZE  ; default size of fonts, arrows etc.
  Next
  *tuiEngine\skinPath       = #NULL
  *tuiEngine\useSkin        = False
  *tuiEngine\borderStyle    = 0

  *tuiEngine\inputTask      = #NULL
  *tuiEngine\useAISS        = False

  *tuiEngine\outQ\head      = #NULL
  *tuiEngine\outQ\tail      = #NULL
  *tuiEngine\outQ\sigBit    = AllocSignal_(-1)
  *tuiEngine\outQ\sigFlag   = 1 LSL *tuiEngine\outQ\sigBit
  *tuiEngine\outQ\sigTask   = FindTask_(#NULL)
  *tuiEngine\inQ\head       = #NULL
  *tuiEngine\inQ\tail       = #NULL
  *tuiEngine\inQ\sigBit     = AllocSignal_(-1)
  *tuiEngine\inQ\sigFlag    = 1 LSL *tuiEngine\inQ\sigBit
  *tuiEngine\inQ\sigTask    = FindTask_(#NULL)

  *tuiEngine\tuiEvent       = #NULL
  *tuiEngine\eventPool      = CreatePool_(#MEMF_ANY,SizeOf.tuiEvent*64,SizeOf.tuiEvent*2)
  *tuiEngine\objectPool     = CreatePool_(#MEMF_ANY,$8000,$4000)

  *tuiEngine\redrawMethod   = #TUIRM_CLEAR;#TUIRD_SMART
  *tuiEngine\obj\tuiEngine  = *tuiEngine

  *tuiEngine\rxPort         = #NULL
  *tuiEngine\rxPortName     = #NULL
  *tuiEngine\rxMsg          = #NULL
  *tuiEngine\rxString       = #NULL
  If arexxPort Then _ntui_InitRexx{*tuiEngine,arexxPort}

  *tuiEngine\winPort        = CreateMsgPort_()
  *tuiEngine\snPort         = CreateMsgPort_()
  *tuiEngine\snWorkbenchH   = screennotify_AddWorkbenchClient{*tuiEngine\snPort}
  *tuiEngine\snScreenH      = 0

  InitSemaphore_ *tuiEngine\lock

  *tuiEngine\mover          = #NULL
  ;*tuiEngine\active         = #NULL
  *tuiEngine\focus          = #NULL
  *tuiEngine\restingTime    = 0

  ;/* initialize App functionality (workbench.library) */
  *tuiEngine\appName        = #NULL
  *tuiEngine\appMenuName    = #NULL
  *tuiEngine\appIconFile    = #NULL
  *tuiEngine\appIconText    = #NULL
  *tuiEngine\appMenu        = #NULL
  *tuiEngine\appIcon        = #NULL
  *tuiEngine\appIconDO      = #NULL

  str_Write{&*tuiEngine\onPopup    ,!NOTIFYID_POPUP}               ; menu or icon doubleblick
  str_Write{&*tuiEngine\onIconDrop ,!NOTIFYID_ICONDROP}            ; drop icon on icon
  str_Write{&*tuiEngine\onQuit     ,!NOTIFYID_QUIT}
  str_Write{&*tuiEngine\onIconify  ,!NOTIFYID_ICONIFY}
  str_Write{&*tuiEngine\onMessage  ,!NOTIFYID_MESSAGE}
  ;If icon="" Then icon=dos_GetProgIcon{"Blitz3:Amiblitz3"}

  If appIcon Then str_Write{&*tuiEngine\appIconFile,appIcon}
  If appMenu Then str_Write{&*tuiEngine\appMenuName,appMenu}
  If appName Then str_Write{&*tuiEngine\appIconText,appName}
  If appName Then str_Write{&*tuiEngine\appName,appName}

  *tuiEngine\appPort = CreateMsgPort_()
  If *tuiEngine\appPort
    If *tuiEngine\appIconFile
      *tuiEngine\appIconDO = GetDiskObjectNew_(&*tuiEngine\appIconFile\str)
      If *tuiEngine\appIconDO=#NULL
        error{"\\__THIS_FUNCTION: Unable to get diskobject: \\22"+str_Read{&*tuiEngine\appIconFile}+"\\22!"}
      Else
        *tuiEngine\appIconDO\do_Magic       = 0
     ;   *tuiEngine\appIconDO\do_Version     = 0 ; if we do this, PNG Icons dont work anymore...
        *tuiEngine\appIconDO\do_Type        = 0
        *tuiEngine\appIconDO\do_DefaultTool = #NULL
        *tuiEngine\appIconDO\do_ToolTypes   = #NULL
        *tuiEngine\appIconDO\do_CurrentX    = #NO_ICON_POSITION
        *tuiEngine\appIconDO\do_CurrentY    = #NO_ICON_POSITION
        *tuiEngine\appIconDO\do_DrawerData  = #NULL
        *tuiEngine\appIconDO\do_ToolWindow  = #NULL
        *tuiEngine\appIconDO\do_StackSize   = #NULL
      End If
    End  If
    If *tuiEngine\appMenuName
      *tuiEngine\appMenu = AddAppMenuItemA_(#APP_MENUHIT,#NULL,&*tuiEngine\appMenuName\str,*tuiEngine\appPort,#NULL)
    End If
  End If

  *tuiEngine\obj\Draw          = #NULL
  !_GetFuncPointer{*tuiEngine\obj\Layout       ,_ntui_LayoutEngine,{0,0,0}}
  !_GetFuncPointer{*tuiEngine\obj\DispatchEvent,_ntui_DispatchEngineEvent,{0,0}}
  *tuiEngine\obj\CalculateMinSize = #NULL
  !_GetFuncPointer{*tuiEngine\obj\Deinit       ,_ntui_DeinitEngine,{0}}
  !_GetFuncPointer{*tuiEngine\obj\SetAttr      ,_ntui_SetEngineAttr,{0,0,0}}
  !_GetFuncPointer{*tuiEngine\obj\GetAttr      ,_ntui_GetEngineAttr,{0,0,0}}
  InitRastPort_ *tuiEngine\buildRP

  For n.l=0 To #TUIMAX_SKIN-1
    *tuiEngine\skinImage[n]      = #NULL
    *tuiEngine\skinImageAvail[n] = True
    *tuiEngine\skinImageName[n]  = #NULL
  Next

  str_Write{&*tuiEngine\skinImageName[#TUISKIN_FLATBORDER        ],"NTUI:flatborder.9.png"    }; flat, 1 colored vorder
  str_Write{&*tuiEngine\skinImageName[#TUISKIN_RECESSEDBORDER    ],"NTUI:recessedborder.9.png"}; recessed border
  str_Write{&*tuiEngine\skinImageName[#TUISKIN_RAISEDBORDER      ],"NTUI:raisedborder.9.png"  }; raised border
  str_Write{&*tuiEngine\skinImageName[#TUISKIN_GROUPBORDER       ],"NTUI:groupborder.9.png"   }; group style border
  str_Write{&*tuiEngine\skinImageName[#TUISKIN_BUTTON            ],"NTUI:button.9.png"        }; button border
  str_Write{&*tuiEngine\skinImageName[#TUISKIN_STRING            ],"NTUI:string.9.png"        }; string border
  str_Write{&*tuiEngine\skinImageName[#TUISKIN_TITLEBORDER       ],"NTUI:titleborder.9.png"   }; group style border with title
  str_Write{&*tuiEngine\skinImageName[#TUISKIN_TOOL              ],"NTUI:tool.9.png"          }; tool button border
  str_Write{&*tuiEngine\skinImageName[#TUISKIN_MENUBAR           ],"NTUI:menubar.9.png"       }; menu bar border
  str_Write{&*tuiEngine\skinImageName[#TUISKIN_TOOLTIP           ],"NTUI:tooltip.9.png"       }; tooltip border
  str_Write{&*tuiEngine\skinImageName[#TUISKIN_MENUITEM          ],"NTUI:menuitem.9.png"      }; menuitem
  str_Write{&*tuiEngine\skinImageName[#TUISKIN_TEXTBOX           ],"NTUI:textbox.9.png"       }; textbox
  str_Write{&*tuiEngine\skinImageName[#TUISKIN_TAB_T             ],"NTUI:tab_t.9.png"     }; page
  str_Write{&*tuiEngine\skinImageName[#TUISKIN_TABVIEWBEGIN_T    ],"NTUI:tabviewbegin_t.9.png"   }; page
  str_Write{&*tuiEngine\skinImageName[#TUISKIN_TABVIEWEND_T      ],"NTUI:tabviewend_t.9.png"     }; page
  str_Write{&*tuiEngine\skinImageName[#TUISKIN_TABCONTENT_T      ],"NTUI:tabcontent_t.9.png" }; page
  str_Write{&*tuiEngine\skinImageName[#TUISKIN_HSCROLLERCONTAINER],"NTUI:hscrollercontainer.9.png" }; page
  str_Write{&*tuiEngine\skinImageName[#TUISKIN_VSCROLLERCONTAINER],"NTUI:vscrollercontainer.9.png" }; page

  If *scr=-1 Then *scr=#NULL
  If ((pubname><"") OR (*scr><#NULL))
    _ntui_OpenScreen{*tuiEngine,pubname,*scr}
  End If
End If
;ntui_EndBuild{}
Function Return *tuiEngine
End Function



