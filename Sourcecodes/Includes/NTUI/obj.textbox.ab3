; XTRA
; Embedded .xtra Header
; 
; General Info
; -------------------------------------------------------
; ExePath         = "RAM:"
; ExeFile         = "Prog.exe"
; CreateIcon      = 0
; Residents       = "all.res"
; 
; Compiler
; -------------------------------------------------------
; StringBuffer    = 8192
; MakeSmallest    = 1
; Version         = 0.0.0
; NumberOfBuilds  = 7
; 
; Debugger
; -------------------------------------------------------
; CliArgs         = ""
; StackSize       = 32768
; RuntimeDebug    = 0
; DebugInfo       = 0
; CreateDbgFile   = 0
; OverflowCheck   = 0
; AssemblerCheck  = 1
; InterruptCheck  = 1
; AutoRun         = 1
; 
; Editor
; -------------------------------------------------------
; CursorLine      = 2604
; CursorColumn    = 53
; LabelSearch     = "set"
; LabelRemark     = 0
; LabelAll        = 0
; LabelPosition   = 0
; 
; Blitz Objects
; -------------------------------------------------------
; Max IconInfo    = 1
; Max NChunky     = 50
; Max MUIObject   = 50
; Max PTModule    = 5
; Max AsyncReq    = 4
; Max Req-Lib     = 5
; Max GTMenuList  = 5
; Max Console     = 5
; Max TCPSock     = 5
; Max XBSound     = 10
; Max Chunky      = 20
; Max File        = 5
; Max GadgetList  = 5
; Max Queue       = 10
; Max Screen      = 5
; Max Shape       = 100
; Max CopList     = 10
; Max Sprite      = 20
; Max Stencil     = 5
; Max Module      = 5
; Max Window      = 20
; Max Anim        = 10
; Max Sound       = 10
; Max Bank        = 5
; Max Buffer      = 10
; Max BitMap      = 10
; Max Slice       = 10
; Max Page        = 4
; Max Tape        = 5
; Max IntuiFont   = 5
; Max MedModule   = 8
; Max Palette     = 4
; Max MenuList    = 5
; Max BlitzFont   = 4
; Max GTList      = 20
; /XTRA
;///////////////////////////////////////////////////////////////////////////////
;/                                                                             /
;/ Name: ntui_textbox.include                                                  /
;/                                                                             /
;/ Platforms: Classic, WinUAE, Amithlon, MorphOS, AmigaOS4                     /
;/                                                                             /
;/ Date: 06/01/2009                                                            /
;/                                                                             /
;/ Author: <unknown>                                                           /
;/                                                                             /
;/ Requirements:  Amiblitz3                                                    /
;/                                                                             /
;/ Purpose:                                                                    /
;/ Extend AB3 functionality.                                                   /
;/ * no description available *                                                /
;/                                                                             /
;/ Abstract:                                                                   /
;/ * no abstract available *                                                   /
;/                                                                             /
;/ User Constants:                                                             /
;/ #max_objects   = n                                                          /
;/                                                                             /
;///////////////////////////////////////////////////////////////////////////////
#TUI_HAS_TEXTBOX = 1

;/* ntui TextBox Sub-Types */
#TUITB_TEXTBOX            = 0 ; multiline textbox
#TUITB_STRING             = 1 ; single line string gadget
#TUITB_STRING_NUMERIC     = 2 ; single line numeric gadget
#TUITB_STRING_PASSWORD    = 3 ; sinle line string gadget with password style
#TUITB_STRING_FILE        = 4 ; string gadget with file ASL
#TUITB_STRING_PATH        = 5 ; string gadget with path ASL
#TUITB_STRING_FONT        = 6 ; string gadget with font ASL
#TUITB_STRING_SCREEN      = 7 ; string gadget with screen ASL

;/* ntui TextBox Flags */
#TUITBF_LEFTWRAP          = $00000001
#TUITBF_RIGHTWRAP         = $00000002
#TUITBF_LOOSEMARKER       = $00000004
#TUITBF_DELWRAP           = $00000008
#TUITBF_REMWRAP           = $00000010
#TUITBF_DRAGCURSOR        = $00000020
#TUITBF_HIDECURSORONMARK  = $00000040
#TUITBF_SHIFTMARK         = $00000080
#TUITBF_RETURNBREAK       = $00000200
#TUITBF_BACKUP            = $00000400
#TUITBF_AUTOINDENT        = $00000800
#TUITBF_REDRAWONSCROLL    = $00002000
#TUITBF_LINECURSOR        = $00004000
#TUITBF_READONLY          = $00008000
#TUITBF_REALTAB           = $00040000
#TUITBF_WANTRETURN        = $00080000
#TUITBF_MARKONFOCUS       = $00100000
#TUITBF_LOOSEMARKONFOCUS  = $00200000
#TUITBF_WANTTAB           = $00400000
#TUITBF_BLOCKMARK         = $00800000
#TUITBF_SHOWCR            = $01000000

#TUITBF_NotePad           = #TUITBF_LEFTWRAP|#TUITBF_RIGHTWRAP|#TUITBF_LOOSEMARKER|#TUITBF_DELWRAP|#TUITBF_REMWRAP|#TUITBF_DRAGCURSOR|#TUITBF_HIDECURSORONMARK|#TUITBF_SHIFTMARK|#TUITBF_RETURNBREAK|#TUITBF_REALTAB|#TUITBF_WANTRETURN
#TUITBF_String            = #TUITBF_LEFTWRAP|#TUITBF_RIGHTWRAP|#TUITBF_LOOSEMARKER|#TUITBF_DRAGCURSOR|#TUITBF_SHIFTMARK|#TUITBF_REALTAB|#TUITBF_LOOSEMARKONFOCUS|#TUITBF_MARKONFOCUS
#TUITBF_Ped               = 0|#TUITBF_WANTRETURN|#TUITBF_RETURNBREAK
#TUITBF_Message           = #TUITBF_READONLY|#TUITBF_HIDECURSORONMARK|#TUITBF_LOOSEMARKER|#TUITBF_LINECURSOR|#TUITBF_LEFTWRAP|#TUITBF_RIGHTWRAP|#TUITBF_SHIFTMARK

;/* ntui TextBox Pens */
#TUITBPEN_BG              =  0 ; pen for background
#TUITBPEN_Text            =  1 ; pen for normal text
#TUITBPEN_Token           =  2 ; pen for Tokens
#TUITBPEN_Number          =  3 ; pen for numeric values
#TUITBPEN_Newtype         =  4 ; pen for structs
#TUITBPEN_Constant        =  5 ; pen for constants/macros
#TUITBPEN_Comment         =  6 ; pen for comments
#TUITBPEN_Macro           =  7 ; pen for macros
#TUITBPEN_String          =  8 ; pen for string constants
#TUITBPEN_Function        =  9 ; pen for functions
#TUITBPEN_Directive       = 10 ; pen for directives
#TUITBPEN_Tag             = 11 ; pen for tag
#TUITBPEN_Attr            = 12 ; pen for tag attribute
#TUITBPEN_Focus           = 13 ; pen for background with focus
#TUITBPEN_NoFocus         = 14 ; pen for background without focus
#TUITBPEN_DisabledBG      = 15 ; pen for disabled background
#TUITBPEN_DisabledFG      = 16 ; pen for disabled text
#TUITBPEN_EnabledFG       = 17 ; pen for enabled text
#TUITBPEN_MAX             = 18

;/* ntui TextBox Line Flags */
#TUITBLF_NONE             = $0   ; no flags
#TUITBLF_COMMENT          = $1   ; propagate multi-line comment
#TUITBLF_INIT             = $2
#TUITBLF_DETOKENIZED      = $4
#TUITBLF_STOLEN           = $8   ; line is not allocated by us
#TUITBLF_ALLOC            = $10  ; was allocated by us

;/* ntui TextBox Attributes */
#TUITBA_BASE               =  #TUIA_USER
#TUITBA_CURSOR_LINE        =  0 | #TUITBA_BASE ; rw
#TUITBA_CURSOR_COLUMN      =  1 | #TUITBA_BASE ; rw
#TUITBA_BEGINSELECT_LINE   =  2 | #TUITBA_BASE ; rw
#TUITBA_BEGINSELECT_COLUMN =  3 | #TUITBA_BASE ; rw
#TUITBA_ENDSELECT_LINE     =  4 | #TUITBA_BASE ; rw
#TUITBA_ENDSELECT_COLUMN   =  5 | #TUITBA_BASE ; rw
#TUITBA_TBFLAGS            =  6 | #TUITBA_BASE ; rw
#TUITBA_SETTBFLAGS         =  7 | #TUITBA_BASE ;  w
#TUITBA_CLEARTBFLAGS       =  8 | #TUITBA_BASE ;  w
#TUITBA_BLINKSPEED         =  9 | #TUITBA_BASE ; rw
#TUITBA_ROWHEIGHT          = 10 | #TUITBA_BASE ; r
#TUITBA_SPACEWIDTH         = 11 | #TUITBA_BASE ; r
#TUITBA_TABWIDTH           = 12 | #TUITBA_BASE ; rw
#TUITBA_VISIBLELINES       = 13 | #TUITBA_BASE ; r
#TUITBA_TOTALLINES         = 14 | #TUITBA_BASE ; r
#TUITBA_TOPLINE            = 15 | #TUITBA_BASE ; rw
#TUITBA_VISIBLEWIDTH       = 16 | #TUITBA_BASE ; r
#TUITBA_TOTALWIDTH         = 17 | #TUITBA_BASE ; r
#TUITBA_LEFTOFFSET         = 18 | #TUITBA_BASE ; rw
#TUITBA_MAXCOLUMNS         = 19 | #TUITBA_BASE ; rw
#TUITBA_MAXLINES           = 20 | #TUITBA_BASE ; rw
#TUITBA_FONT               = 21 | #TUITBA_BASE ; rw
#TUITBA_LINE               = 22 | #TUITBA_BASE ; rw
#TUITBA_LINELENGTH         = 23 | #TUITBA_BASE ; r
#TUITBA_SELECTION          = 24 | #TUITBA_BASE ; rw
#TUITBA_HIGHLIGHTNING      = 25 | #TUITBA_BASE ; rw

NEWTYPE.tline ; line contains ASCII text and special tokenized version
flags.l     ; like #tflag_comment

blength.l   ; allocated length
clength.l   ; character length
text.l      ; pointer to 0 terminated text string

tclength.l  ; allocated length
tblength.l  ; character length
ttext.l     ; tokenized version of text
End NEWTYPE


NEWTYPE.tuiTextBox
obj.tuiObject        ; tui object header
subTypeID.l
;tbox.tuiRect         ; real text box
;innerPos.tuiPixel
;innerSize.tuiPixel
maxChars.l
maxLines.l
*hScroller.tuiScroller
*vScroller.tuiScroller
*funcButton.tuiButton
*doc.tline           ; Zeiger auf Pointer array
*tank.b              ; pointer to text tank from load function
allocLines.l         ; Maximum Numer of Textlines
visibleLines.l
topLine.l
totalWidth.l
visibleWidth.l
leftOffset.l
totalLines.l         ;  Number of used lines in Ted
step_.tuiPixel
numMin.l             ; boundaries for numeric string
numMax.l

cursor_lpos.l
cursor_cpos.l
cursor_pixpos.l
marker1_lpos.l
marker1_cpos.l
marker2_lpos.l
marker2_cpos.l
damage_begin.l
damage_end.l
bracket1_lpos.l
bracket1_cpos.l
bracket1_char.b
bracket2_char.b
bracket2_lpos.l
bracket2_cpos.l

last_flags.l
*font.TextFont
fontstolen.l
rowheight.l
spacing.l
space_width.l
tab_width.l
editable.l
accept.l

cursor_show.l
cursor_count.l

stat_cursor_lpos.l
stat_cursor_cpos.l
range_lpos.l
range_cpos.l
search_lpos.l
search_cpos.l

scrollborder_x.l
scrollborder_y.l

edflags.l

tokenizing.l
blink_speed.l
tabsize.l ; tab size in spaces
returncode.l
max_cpos.l

validChar.b[256]

; pens
pen.l         [#TUITBPEN_MAX]
penMarker.l   [#TUITBPEN_MAX]
tuiPen.l      [#TUITBPEN_MAX]
tuiPenMarker.l[#TUITBPEN_MAX]
End NEWTYPE

USEPATH *tuiTextBox

;///////////////////////////////////////////////////////////////////////////////
;/                                                                             /
;/ Syntax: !line_use {lpos}                                                    /
;/                                                                             /
;/ Description:
;/ Helper macro to get the pointer to a text line or NULL, if out of range.                                                              /
;/                                                                             /
;/ Inputs:
;/ - *tline  : name of the varialbe that holds the pointer                                                                 /
;/ - lpos    : line position                                                             /
;/                                                                             /
;///////////////////////////////////////////////////////////////////////////////
Macro line_use ;{*tline,lpos}
If (`2) < *tuiTextBox\allocLines AND (`2)>=0
  `1.tline = *tuiTextBox\doc + ((`2) * SizeOf.tline)
Else
  `1       = #NULL
End If
End Macro


;///////////////////////////////////////////////////////////////////////////////
;/                                                                             /
;/ Syntax: _tb_CopyMem {sourceaddr.l,destaddr.l,blength.l}                     /
;/                                                                             /
;/ Description:                                                                /
;/ Copymem() reimplementation that supports overlapping memory areas                                            /
;/                                                                             /
;/ Inputs:                                                                     /
;/ - sourceaddr.l  : source memory address                                                     /
;/ - destaddr.l    : destination memory address                                                       /
;/ - blength.l     : byte length of memory area                                                        /
;/                                                                             /
;///////////////////////////////////////////////////////////////////////////////
Statement FAST _tb_CopyMem{sourceaddr.l,destaddr.l,blength.l}
UNLK a4
TST.l d2
BLE copy_exit
MOVE.l d0,a0
MOVE.l d1,a1
MOVE.l d2,d3
CMP.l d0,d1
BLT copy_normal
BEQ copy_exit
ADD.l d2,a0
ADD.l d2,a1
copy_reverse: ; --------------------------------- REVERSE
LSR.l #5,d3
BEQ copy_reverse_norow
copy_reverse_row:
MOVE.l -(a0),-(a1) ;1
MOVE.l -(a0),-(a1) ;2
MOVE.l -(a0),-(a1) ;3
MOVE.l -(a0),-(a1) ;4
MOVE.l -(a0),-(a1) ;5
MOVE.l -(a0),-(a1) ;6
MOVE.l -(a0),-(a1) ;7
MOVE.l -(a0),-(a1) ;8
SUB.l #1,d3
BGT copy_reverse_row
copy_reverse_norow:
AND.l #31,d2
BEQ copy_exit
copy_reverse_byte:
MOVE.b -(a0),-(a1)
SUB.l #1,d2
BGT copy_reverse_byte
copy_exit:
RTS
copy_normal: ; ------------------------------------ NORMAL
LSR.l #5,d3
BEQ copy_normal_norow
copy_normal_row:
MOVE.l (a0)+,(a1)+  ;1
MOVE.l (a0)+,(a1)+  ;2
MOVE.l (a0)+,(a1)+  ;3
MOVE.l (a0)+,(a1)+  ;4
MOVE.l (a0)+,(a1)+  ;5
MOVE.l (a0)+,(a1)+  ;6
MOVE.l (a0)+,(a1)+  ;7
MOVE.l (a0)+,(a1)+  ;8
SUB.l #1,d3
BGT copy_normal_row
copy_normal_norow:
AND.l #31,d2
BEQ copy_exit
copy_normal_byte:
MOVE.b (a0)+,(a1)+
SUB.l #1,d2
BGT copy_normal_byte
RTS
End Statement


;///////////////////////////////////////////////////////////////////////////////
;/                                                                             /
;/ Syntax: result.l =  _tb_Instr {textptr.l,c.b,mlen.l,@pos.l}                 /
;/                                                                             /
;/ Description:                                                                /
;/ Find position of first occurence of c in text string, mlen to search and from pos.
;/                                                                             /
;/ Inputs:                                                                     /
;/ - textptr.l  : ???                                                        /
;/ - c.b        : ???                                                              /
;/ - mlen.l     : ???                                                           /
;/ - pos.l      : ???                                                            /
;/                                                                             /
;/ Result:                                                                     /
;/ - result.l   : ???                                                        /
;/                                                                             /
;///////////////////////////////////////////////////////////////////////////////
Function.l FAST _tb_Instr{textptr.l,c.b,mlen.l,@pos.l}
If pos<0 Then pos = 0
While Peek.b(textptr+pos)><c AND pos<mlen:pos+1:Wend
If pos=mlen Then pos=-1
Function Return pos
End Function


;///////////////////////////////////////////////////////////////////////////////
;/                                                                             /
;/ Syntax: result.l = _tb_GetTextLength {*rp.RastPort,textptr.l,mlen.l}        /
;/                                                                             /
;/ Description:                                                                /
;/ Replacement for TextLength() OS function, but resects TAB sizes
;/                                                                             /
;/ Inputs:                                                                     /
;/ - *rp.RastPort    : destination RastPort                                                     /
;/ - textptr.l       : text pointer                                                        /
;/ - mlen.l          : max length to test                                                          /
;/                                                                             /
;/ Result:                                                                     /
;/ - result.l        : length in pixel of the textstring                                                        /
;/                                                                             /
;///////////////////////////////////////////////////////////////////////////////
Function.l FAST _tb_GetTextLength{*rp.RastPort,textptr.l,mlen.l}
pos.l = _tb_Instr{textptr,9,mlen}
If pos<0
  slen.l = TextLength_(*rp,textptr,mlen)
Else
  lpos.l=0
  While pos>=0
    If pos-lpos>0 Then slen.l + TextLength_(*rp,textptr+lpos,pos-lpos)
    lpos.l = pos+1
    pos.l = _tb_Instr{textptr,9,mlen,lpos}
    slen + 50
  Wend
  If mlen-lpos>0 Then slen.l + TextLength_(*rp,textptr+lpos,mlen-lpos)
End If
Function Return slen
End Function


;///////////////////////////////////////////////////////////////////////////////
;/                                                                             /
;/ Syntax: result.l = _tb_pos2x {*tuiTextBox.tuiTextBox,lpos.l,cpos.l}         /
;/                                                                             /
;/ Description:                                                                /
;/ convert cursor position to x coordinate.                                                /
;/                                                                             /
;/ Inputs:                                                                     /
;/ - *tuiTextBox.tuiTextBox    : ???                                           /
;/ - lpos.l    : ???                                                           /
;/ - cpos.l    : ???                                                           /
;/                                                                             /
;/ Result:                                                                     /
;/ - result.l     : ???                                                        /
;/                                                                             /
;///////////////////////////////////////////////////////////////////////////////
Function.l _tb_pos2x{*tuiTextBox.tuiTextBox,lpos.l,cpos.l,*rp.RastPort}
x.l = 0
cw.l = \space_width
;*tuiWindow.tuiWindow = *tuiTextBox\obj\tuiWindow
;If *tuiWindow Then *win.Window = *tuiWindow\win
;If *win Then *rp.RastPort = *win\RPort
If *rp=-1 Then *rp=#NULL
If *rp
  SetFont_ *rp,\font
  !line_use{*tline,lpos}
  If *tline
    If *tline\text
      If cpos>=*tline\clength
        If *tline\ttext AND *tline\tclength>0
          ;x.l = _tb_GetTokenLength{*rp,*tline\ttext,*tline\tclength} + \space_width * (cpos-*tline\clength)
        Else
          x.l = _tb_GetTextLength{*rp,*tline\text,*tline\clength} + \space_width * (cpos-*tline\clength)
        End If
      Else
        If *tline\ttext AND *tline\tclength>0
          ;x.l = _tb_GetTokenLength2{*rp,*tline\ttext,cpos}
        Else
          x.l = _tb_GetTextLength{*rp,*tline\text,cpos}
        End If
      End If
    Else
      x = \space_width * cpos
    End If
  Else
    x = \space_width * cpos
  End If
End If
Function Return x
End Function


;///////////////////////////////////////////////////////////////////////////////
;/                                                                             /
;/ Syntax: result.l = _tb_pos2cw {*tuiTextBox.tuiTextBox,lpos.l,cpos.l}        /
;/                                                                             /
;/ Description:                                                                /
;/ Get the cursor width of the given position                                                                            /
;/ Inputs:                                                                     /
;/ - *tuiTextBox.tuiTextBox    : ???                                           /
;/ - lpos.l    : ???                                                           /
;/ - cpos.l    : ???                                                           /
;/                                                                             /
;/ Result:                                                                     /
;/ - result.l     : ???                                                        /
;/                                                                             /
;///////////////////////////////////////////////////////////////////////////////
Function.l _tb_pos2cw{*tuiTextBox.tuiTextBox,lpos.l,cpos.l,*rp.RastPort}
cw.l = \space_width
;*tuiWindow.tuiWindow = *tuiTextBox\obj\tuiWindow
;If *tuiWindow Then *win.Window = *tuiWindow\win
;If *win Then *rp.RastPort = *win\RPort
If *rp=-1 Then *rp=#NULL
If *rp
  SetFont_ *rp,\font
  !line_use{*tline,lpos}
  If *tline
    If *tline\text
      If cpos>=*tline\clength
        cw.l = \space_width
      Else
        If *tline\ttext
          ;string.s = _tb_DeTokenize{*tline\ttext,*tline\tclength}
          ;cw.l = _tb_GetTextLength{*rp,&string.s+cpos,1}
          ;cw.l = _tb_tokenlength{*rp,*tline\ttext+cpos,1}
        Else
          cw.l = _tb_GetTextLength{*rp,*tline\text+cpos,1}
        End If
      End If
    End If
  End If
End If
Function Return cw-1
End Function


;///////////////////////////////////////////////////////////////////////////////
;/                                                                             /
;/ Syntax: result.l = _tb_x2pos {*tuiTextBox.tuiTextBox,lpos.l,x.l}            /
;/                                                                             /
;/ Description:
;/ Convert the x position at line pos to character position.                                                            /
;/                                                                             /
;/ Inputs:                                                                     /
;/ - *tuiTextBox.tuiTextBox    : ???                                           /
;/ - lpos.l    : ???                                                           /
;/ - x.l    : ???                                                              /
;/                                                                             /
;/ Result:                                                                     /
;/ - result.l     : ???                                                        /
;/                                                                             /
;///////////////////////////////////////////////////////////////////////////////
Function.l _tb_x2pos{*tuiTextBox.tuiTextBox,lpos.l,x.l,*rp.RastPort}
;*tuiWindow.tuiWindow = *tuiTextBox\obj\tuiWindow
;If *tuiWindow Then *win.Window = *tuiWindow\win
;If *win Then *rp.RastPort = *win\RPort
If *rp=-1 Then *rp=#NULL
If *rp
  SetFont_ *rp,\font
  cpos.l = 0
  !line_use{*tline,lpos}
  If *tline
    If *tline\text
      If *tline\ttext AND *tline\tclength>0
        ;string.s = _tb_DeTokenize{*tline\ttext,*tline\tclength}
      Else
        string.s = Peeks$(*tline\text,*tline\clength)
      End If

      slen.l = FLen(string.s)

      While x>0
        If cpos<slen
          x-_tb_GetTextLength{*rp,&string.s + cpos,1}
        Else
          x-\space_width
        End If
        If x>=0 Then cpos+1
      Wend
      clength.l = slen ; *tline\clength
    Else
      cpos = x / \space_width  : clength = 0
    End If
  Else
    cpos = x / \space_width  : clength = 0
  End If
  If (\edflags&#TUITBF_RIGHTWRAP)><0 AND cpos>clength Then cpos=clength
End If
Function Return cpos
End Function


;///////////////////////////////////////////////////////////////////////////////
;/                                                                             /
;/ Syntax: _tb_Text {*rp.RastPort,textptr.l,mlen.l}                           /
;/                                                                             /
;/ Description:                                                                /
;/ Sample like Text(), but respect the TAB sizes.
;/                                                                             /
;/ Inputs:                                                                     /
;/ - *rp.RastPort    : ???                                                     /
;/ - textptr.l    : ???                                                        /
;/ - mlen.l    : ???                                                           /
;/                                                                             /
;///////////////////////////////////////////////////////////////////////////////
Statement _tb_Text{*rp.RastPort,textptr.l,mlen.l}
pos.l = _tb_Instr{textptr,9,mlen}
If pos<0
  slen.l = Text_(*rp,textptr,mlen)
Else
  lpos.l=0
  apen.l = *rp\FgPen
  bpen.l = *rp\BgPen
  While pos>=0
    If pos-lpos>0
      Text_ *rp,textptr+lpos,pos-lpos
    End If
    lpos.l = pos+1
    pos.l = _tb_Instr{textptr,9,mlen,lpos}
    SetAPen_ *rp,bpen
    RectFill_ *rp,*rp\cp_x,*rp\cp_y-*rp\TxBaseline,*rp\cp_x+50,*rp\cp_y-*rp\TxBaseline+*rp\TxHeight
    SetAPen_ *rp,apen
    Move_ *rp,*rp\cp_x+50,*rp\cp_y
  Wend
  If mlen-lpos>0 Then Text_ *rp,textptr+lpos,mlen-lpos
End If
End Statement


;///////////////////////////////////////////////////////////////////////////////
;/                                                                             /
;/ Syntax: result.l =  _tb_y2line {*tuiTextBox.tuiTextBox,y.l}                 /
;/                                                                             /
;/ Description:                                                                /
;/ Convert y coordinate to line.                                                /
;/                                                                             /
;/ Inputs:                                                                     /
;/ - *tuiTextBox.tuiTextBox : TextBox Object                                           /
;/ - y.l                    : y-coordinate relative to left/top edge of TextBox                                                              /
;/                                                                             /
;/ Result:                                                                     /
;/ - result.l               : line                                                        /
;/                                                                             /
;///////////////////////////////////////////////////////////////////////////////
Function.l FAST _tb_y2line{*tuiTextBox.tuiTextBox,y.l}
If \rowheight>0
  lpos.l = ((y) / \rowheight + \topLine)
Else
  lpos = 0
End If
If lpos>=\totalLines Then lpos=\totalLines-1
If lpos<0 Then lpos=0
Function Return lpos
End Function


;///////////////////////////////////////////////////////////////////////////////
;/                                                                             /
;/ Syntax: result.l =  _tb_line2y {*tuiTextBox.tuiTextBox,lpos.l}              /
;/                                                                             /
;/ Description:                                                                /
;/ * no description available *                                                /
;/                                                                             /
;/ Inputs:                                                                     /
;/ - *tuiTextBox.tuiTextBox    : ???                                           /
;/ - lpos.l    : ???                                                           /
;/                                                                             /
;/ Result:                                                                     /
;/ - result.l     : ???                                                        /
;/                                                                             /
;///////////////////////////////////////////////////////////////////////////////
Function.l FAST _tb_line2y{*tuiTextBox.tuiTextBox,lpos.l}
Function Return (lpos-\topLine) * \rowheight
End Function


;///////////////////////////////////////////////////////////////////////////////
;/                                                                             /
;/ Syntax: _tb_DoBlink {*tuiTextBox.tuiTextBox,*rp.RastPort,mode.l}            /
;/                                                                             /
;/ Description:                                                                /
;/ View or hide the text cursor.                                                /
;/                                                                             /
;/ Inputs:                                                                     /
;/ - *tuiTextBox.tuiTextBox  : TextBox Object                                           /
;/ - *rp.RastPort            : ???                                                     /
;/ - mode.l                  : 1 = auto blink, -1=show, 0=hide                                                           /
;/                                                                             /
;///////////////////////////////////////////////////////////////////////////////
Statement _tb_DoBlink{*tuiTextBox.tuiTextBox,*rp.RastPort,mode.l}
If *tuiTextBox\obj\flags&#TUIF_ONSCREEN=0 Then Statement Return
If *rp=#NULL Then Statement Return

If (\edflags&#TUITBF_HIDECURSORONMARK=0)  OR (\marker1_lpos=-1) OR (\marker2_cpos=\marker1_cpos AND \marker1_lpos=\marker2_lpos)
  If \accept=False Then mode=Off
  If mode=1
    \cursor_count+1
    If \cursor_count>=\blink_speed
      If \cursor_show Then mode=Off : Else mode=On
      \cursor_count=0
    End If
  Else
    \cursor_count=0
  End If

  If mode=\cursor_show OR mode=1 Then Statement Return

  xs.l = _tb_pos2x {*tuiTextBox,\cursor_lpos,\cursor_cpos,*rp}
  If \edflags&#TUITBF_LINECURSOR
    cw.l = 0
  Else
    cw.l = _tb_pos2cw{*tuiTextBox,\cursor_lpos,\cursor_cpos,*rp}
  End If

  x.l = *tuiTextBox\obj\cbox\left  - \leftOffset  + xs
  y.l = *tuiTextBox\obj\cbox\top   + (\cursor_lpos-\topLine) *\rowheight ;- \view_ypos

  \cursor_show = mode
  SetDrMd_ *rp,-1
  RectFill_ *rp,x,y,x+cw,y+\rowheight-1
  SetDrMd_ *rp,0

EndIf
End Statement


;///////////////////////////////////////////////////////////////////////////////
;/                                                                             /
;/ Syntax: _tb_DrawRawLine {*tuiTextBox.tuiTextBox,*rp.RastPort,lpos.l,x.l,y:: /
;/ .l,x2.l}                                                                    /
;/                                                                             /
;/ Description:
;/ Draw a text line at the given coordinates.                                                               /
;/                                                                             /
;/ Inputs:                                                                     /
;/ - *tuiTextBox.tuiTextBox    : ???                                           /
;/ - *rp.RastPort    : ???                                                     /
;/ - lpos.l    : ???                                                           /
;/ - x.l    : ???                                                              /
;/ - y.l    : ???                                                              /
;/ - x2.l    : ???                                                             /
;/                                                                             /
;///////////////////////////////////////////////////////////////////////////////
Statement _tb_DrawRawLine{*tuiTextBox.tuiTextBox,*rp.RastPort,lpos.l,x.l,y.l,x2.l}
done.l = 0
!line_use{*tline, lpos}
If *tline
  If *tline\text
    If \marker1_lpos=lpos OR \marker2_lpos=lpos
      If \marker1_lpos=lpos Then cpos1.l = \marker1_cpos: Else cpos1=0
      If \marker2_lpos=lpos Then cpos2.l = \marker2_cpos: Else cpos2=*tline\clength

      If cpos1>0
        SetBPen_ *rp,\pen[#TUITBPEN_BG]
        SetAPen_ *rp,\pen[#TUITBPEN_Text]
        Move_ *rp,x,y+*rp\TxBaseline
        _tb_Text{ *rp,*tline\text,cpos1}
      End If

      xadd.l = _tb_pos2x{*tuiTextBox,lpos,cpos1,*rp}
      SetBPen_ *rp,\penMarker[#TUITBPEN_BG]
      SetAPen_ *rp,\penMarker[#TUITBPEN_Text]
      Move_ *rp,x+xadd,y+*rp\TxBaseline
      _tb_Text{ *rp,*tline\text+cpos1,cpos2-cpos1}

      If cpos2<*tline\clength
        xadd.l = _tb_pos2x{*tuiTextBox,lpos,cpos2,*rp}
        SetBPen_ *rp,\pen[#TUITBPEN_BG]
        SetAPen_ *rp,\pen[#TUITBPEN_Text]
        Move_ *rp,x+xadd,y+*rp\TxBaseline
        _tb_Text{ *rp,*tline\text+cpos2,*tline\clength-cpos2}
      End If

      If \marker2_lpos><lpos
        xadd.l = x + _tb_GetTextLength{*rp,*tline\text,*tline\clength}
        If *tuiTextBox\edflags&#TUITBF_SHOWCR
          If xadd<=x2
            Move_ *rp,xadd,y+*rp\TxBaseline
            If *tuiTextBox\edflags&#TUITBF_BLOCKMARK
              SetAPen_ *rp,\penMarker[#TUITBPEN_Text]
              SetBPen_ *rp,\penMarker[#TUITBPEN_BG]
            Else
              SetAPen_ *rp,*tuiTextBox\obj\tuiEngine\pen[#TUIPEN_HALFSHADOW]
              SetBPen_ *rp,\pen[#TUITBPEN_BG]
            End If
            a.s = "¶"
            Text_ *rp,&a.s,1
            xadd.l + TextLength_(*rp,&a.s,1)
          End If
        End If
          If xadd<=x2
            If *tuiTextBox\edflags&#TUITBF_BLOCKMARK
              SetAPen_ *rp,\penMarker[#TUITBPEN_BG]
            Else
              SetAPen_ *rp,\pen[#TUITBPEN_BG]
            End If
            RectFill_ *rp,xadd,y,x2,y+\rowheight-1
          End If
      Else
        xadd.l = x + _tb_GetTextLength{*rp,*tline\text,*tline\clength}
        If xadd<=x2
          SetAPen_ *rp,\pen[#TUITBPEN_BG]
          RectFill_ *rp,xadd,y,x2,y+\rowheight-1
        End If
      End If
    Else
      xadd.l = x + _tb_GetTextLength{*rp,*tline\text,*tline\clength}
      If \marker1_lpos<lpos AND \marker2_lpos>lpos
        If *tuiTextBox\edflags&#TUITBF_SHOWCR
          If xadd<=x2
            Move_ *rp,xadd,y+*rp\TxBaseline
            If *tuiTextBox\edflags&#TUITBF_BLOCKMARK
              SetAPen_ *rp,\penMarker[#TUITBPEN_Text]
              SetBPen_ *rp,\penMarker[#TUITBPEN_BG]
            Else
              SetAPen_ *rp,*tuiTextBox\obj\tuiEngine\pen[#TUIPEN_HALFSHADOW]
              SetBPen_ *rp,\pen[#TUITBPEN_BG]
            End If
            a.s = "¶"
            Text_ *rp,&a.s,1
            xadd.l + TextLength_(*rp,&a.s,1)
          End If
        End If

        If xadd<=x2
          If *tuiTextBox\edflags&#TUITBF_BLOCKMARK
            SetAPen_ *rp,\penMarker[#TUITBPEN_BG]
          Else
            SetAPen_ *rp,\pen[#TUITBPEN_BG]
          End If
          RectFill_ *rp,xadd,y,x2,y+\rowheight-1
        End If

        SetAPen_ *rp,\penMarker[#TUITBPEN_Text]
        SetBPen_ *rp,\penMarker[#TUITBPEN_BG]
      Else
        If xadd<=x2
          SetAPen_ *rp,\pen[#TUITBPEN_BG]
          RectFill_ *rp,xadd,y,x2,y+\rowheight-1
        End If
        SetAPen_ *rp,\pen[#TUITBPEN_Text]
        SetBPen_ *rp,\pen[#TUITBPEN_BG]
      End If
      Move_ *rp,x,y+*rp\TxBaseline
      _tb_Text{ *rp,*tline\text,*tline\clength}
    End If
    done.l=-1
  End If
End If
If done=False
  If (\marker1_lpos<=lpos AND \marker2_lpos>lpos)
    If *tuiTextBox\edflags&#TUITBF_SHOWCR
      Move_ *rp,x,y+*rp\TxBaseline
          If *tuiTextBox\edflags&#TUITBF_BLOCKMARK
            SetAPen_ *rp,\penMarker[#TUITBPEN_Text]
            SetBPen_ *rp,\penMarker[#TUITBPEN_BG]
          Else
            SetAPen_ *rp,*tuiTextBox\obj\tuiEngine\pen[#TUIPEN_HALFSHADOW]
            SetBPen_ *rp,\pen[#TUITBPEN_BG]
          End If

      a.s = "¶"
      Text_ *rp,&a.s,1
      x + TextLength_(*rp,&a.s,1)
    Else
      If (*tuiTextBox\edflags&#TUITBF_BLOCKMARK)=0
        SetAPen_ *rp,\penMarker[#TUITBPEN_BG]
        Move_ *rp,x,y
        Draw_ *rp,x,y+\rowheight-1 : x+1
      End If
    End If
    If *tuiTextBox\edflags&#TUITBF_BLOCKMARK
      SetAPen_ *rp,\penMarker[#TUITBPEN_BG]
    Else
      SetAPen_ *rp,\pen[#TUITBPEN_BG]
    End If
  Else
    SetAPen_ *rp,\pen[#TUITBPEN_BG]
  End If
  If x<=x2 Then RectFill_ *rp,x,y,x2,y+\rowheight-1
End If
End Statement


;///////////////////////////////////////////////////////////////////////////////
;/                                                                             /
;/ Syntax: _tb_ShowBrackets {*tuiTextBox.tuiTextBox}                           /
;/                                                                             /
;/ Description:                                                                /
;/                                                                             /
;/ Inputs:                                                                     /
;/ - *tuiTextBox.tuiTextBox    : ???                                           /
;/                                                                             /
;///////////////////////////////////////////////////////////////////////////////
Statement _tb_ShowBrackets{*tuiTextBox.tuiTextBox,*rp.RastPort}
If *rp
  If \bracket1_lpos>=0 AND \bracket2_lpos>=0
  ;  _tb_DrawLine{*tuiTextBox,\bracket1_lpos}
  ;  _tb_DrawLine{*tuiTextBox,\bracket2_lpos}
    bx1.l = _tb_pos2x{*tuiTextBox,\bracket1_lpos,\bracket1_cpos,*rp}   - \leftOffset
    bx2.l = _tb_pos2x{*tuiTextBox,\bracket2_lpos,\bracket2_cpos,*rp}   - \leftOffset
    by1.l = _tb_line2y{*tuiTextBox,\bracket1_lpos}                +*rp\TxBaseline
    by2.l = _tb_line2y{*tuiTextBox,\bracket2_lpos}                +*rp\TxBaseline
    SetAPen_ *rp,\pen[#TUITBPEN_Text]
    SetDrMd_ *rp,0
    Move_ *rp,bx1,by1
    Text_ *rp,&\bracket1_char,1

    Move_ *rp,bx1+1,by1
    Text_ *rp,&\bracket1_char,1

    Move_ *rp,bx2,by2
    Text_ *rp,&\bracket2_char,1

    Move_ *rp,bx2+1,by2
    Text_ *rp,&\bracket2_char,1

  End If
End If
End Statement

;///////////////////////////////////////////////////////////////////////////////
;/                                                                             /
;/ Syntax: ntui_CheckTextBoxString {*tuiTextBox.tuiTextBox}                    /
;/                                                                             /
;/ Description:                                                                /
;/ Copy the text from a textbox into the string/value parameter of String/NumString Objects.
;/                                                                             /
;/ Inputs:                                                                     /
;/ - *tuiTextBox.tuiTextBox    : textbox object                                          /
;/                                                                             /
;///////////////////////////////////////////////////////////////////////////////
Statement _ntui_UpdateTextBoxString{*tuiTextBox.tuiTextBox}
Select *tuiTextBox\subTypeID
  Case #TUITB_STRING_NUMERIC
    !line_use{*tline, 0}
    value.l=*tuiTextBox\obj\value
    If *tline
      If *tline\text
        string.s    = Peeks$(*tline\text,*tline\clength)
        string.s    = Replace$(string,"+","")
        value.l     = Vallong(string)
        If value<*tuiTextBox\numMin Then value = *tuiTextBox\numMin
        If value>*tuiTextBox\numMax Then value = *tuiTextBox\numMax
        newstring.s = Str$(value)
        If newstring><string
          If *tline\blength<=FLen(newstring)
            nl.l = FLen(newstring)+1+8
            FreeMem_ *tline\text,*tline\blength
            *tline\text = AllocMem_ (nl,#MEMF_ANY)
            *tline\blength = nl
          End If
          *tline\clength = FLen(newstring)
          Poke.s *tline\text,newstring
        End If
      End If
    End If
    *tuiTextBox\obj\value=value

  ;Case #TUITB_STRING
    ;!line_use{*tline, 0}
    ;If *tline
    ;  If *tline\text
    ;    str_WritePtr{&*tuiTextBox\obj\string,*tline\text,*tline\clength}
    ;  End If
    ;End If

End Select
End Statement



;///////////////////////////////////////////////////////////////////////////////
;/                                                                             /
;/ Syntax: _ntui_DrawTextBox {*tuiTextBox.tuiTextBox,x1.l,y1.l,x2.l,y2.l,*rp:: /
;/ .RastPort}                                                                  /
;/                                                                             /
;/ Description:                                                                /
;/                                                                             /
;/ Inputs:                                                                     /
;/ - *tuiTextBox.tuiTextBox    : ???                                           /
;/ - x1.l    : ???                                                             /
;/ - y1.l    : ???                                                             /
;/ - x2.l    : ???                                                             /
;/ - y2.l    : ???                                                             /
;/ - *rp.RastPort    : ???                                                     /
;/                                                                             /
;///////////////////////////////////////////////////////////////////////////////
Statement _ntui_DrawTextBoxIntern{*tuiTextBox.tuiTextBox,x1.l,y1.l,x2.l,y2.l,*rp.RastPort}
If *tuiTextBox=#NULL Then Statement Return
If *rp=#NULL OR *rp=-1 OR \rowheight<=0 Then Statement Return

*tuiEngine.tuiEngine = *tuiTextBox\obj\tuiEngine

If (*tuiTextBox\obj\flags&#TUIF_DISABLED)
  *tuiTextBox\pen[#TUITBPEN_Text]=*tuiTextBox\pen[#TUITBPEN_DisabledFG]
  *tuiTextBox\pen[#TUITBPEN_BG]  =*tuiTextBox\pen[#TUITBPEN_DisabledBG]
Else
  *tuiTextBox\pen[#TUITBPEN_Text]=*tuiTextBox\pen[#TUITBPEN_EnabledFG]
  If *tuiTextBox\obj\flags&#TUIF_FOCUS
    If *tuiTextBox\tuiPen[#TUITBPEN_Focus]>=0   Then *tuiTextBox\pen[#TUITBPEN_BG]=*tuiTextBox\pen[#TUITBPEN_Focus]
  Else
    If *tuiTextBox\tuiPen[#TUITBPEN_NoFocus]>=0 Then *tuiTextBox\pen[#TUITBPEN_BG]=*tuiTextBox\pen[#TUITBPEN_NoFocus]
    If *tuiTextBox\edflags&#TUITBF_LOOSEMARKONFOCUS
;    _tb_SetMarker{*tuiTextBox}
      *tuiTextBox\marker1_lpos  = -1
      *tuiTextBox\marker2_lpos  = -1
      _ntui_UpdateTextBoxString{*tuiTextBox.tuiTextBox}
    End If
  End If
End If
 
_tb_DoBlink{*tuiTextBox,*rp,Off}
SetFont_ *rp,\font
SetDrMd_ *rp,1

SetAPen_ *rp,*tuiTextBox\pen[#TUITBPEN_BG]
mx.l = *tuiTextBox\obj\cbox\right
my.l = *tuiTextBox\obj\cbox\bottom

If x1<*tuiTextBox\obj\cbox\left Then x1=*tuiTextBox\obj\cbox\left
If y1<*tuiTextBox\obj\cbox\top  Then y1=*tuiTextBox\obj\cbox\top
If x2>mx OR x2<0 Then x2 = mx
If y2>my OR y2<0 Then y2 = my

tbx.l = *tuiTextBox\obj\cbox\left ;innerPos\x
tby.l = *tuiTextBox\obj\cbox\top  ;innerPos\y

;If y1<tby Then y1=tby
;If y2>=tby+*tuiTextBox\obj\size\y OR y2<0 Then y2=tby + *tuiTextBox\obj\size\y-1

;If x1<tbx Then x1=tby
;If x2>=tbx+*tuiTextBox\obj\size\x OR x2<0 Then x2=tbx + *tuiTextBox\obj\size\x-1

lpos_a.l = (y1-tby)/\rowheight
If lpos_a<0 Then lpos_a=0

y.l      = lpos_a * \rowheight + tby
If y>y1
  SetAPen_ *rp,\pen[#TUITBPEN_BG]
  RectFill_ *rp,x1,y1,x2,y-1
End If

If tbx>x1
  SetAPen_ *rp,\pen[#TUITBPEN_BG]
  RectFill_ *rp,x1,y1,tbx-1,y2
End If

lpos_a   + \topLine
If lpos_a<0 Then lpos_a=0

lpos_b.l = lpos_a + ((!tuiRectHeight{*tuiTextBox\obj\cbox}-1) / \rowheight)
If lpos_b>\totalLines-1 Then lpos_b = \totalLines-1

x.l      = tbx - \leftOffset


SetAPen_ *rp,\pen[#TUITBPEN_Text]
SetBPen_ *rp,\pen[#TUITBPEN_BG]
ntui_SetClip{*rp,*tuiTextBox\obj\cbox}
If \tokenizing=False
  For lpos.l = lpos_a To lpos_b
    _tb_DrawRawLine{*tuiTextBox,*rp,lpos,x,y,x2}
    y + \rowheight
  Next
Else
;      For lpos.l = lpos_a To lpos_b
;        _tb_draw_tokenline{*tuiTextBox,lpos,x,y,x2}
;        y + \rowheight
;      Next

;    End If
End If
If *tuiTextBox\obj\flags&(#TUIF_ACTIVE|#TUIF_FOCUS)
  ;_tb_DoBlink{*tuiTextBox,*rp,On}
  _tb_ShowBrackets{*tuiTextBox,*rp}
End If

ntui_RemClip{*rp}
If y<=y2 AND *tuiTextBox\subTypeID=#TUITB_TEXTBOX

  SetAPen_ *rp,*tuiEngine\pen[#TUIPEN_HALFSHINE]
  Move_* rp,x1,y : Draw_ *rp,x2,y :y+1
End If

If y<=y2
  SetAPen_ *rp,\pen[#TUITBPEN_BG]
  RectFill_ *rp,x1,y,x2,y2
End If
End Statement


;///////////////////////////////////////////////////////////////////////////////
;/                                                                             /
;/ Syntax: _tb_Scroll {*tuiTextBox.tuiTextBox,dx.l,dy.l,upd.l}                 /
;/                                                                             /
;/ Description:                                                                /
;/ Inputs:                                                                     /
;/ - *tuiTextBox.tuiTextBox    : ???                                           /
;/ - dx.l    : ???                                                             /
;/ - dy.l    : ???                                                             /
;/ - upd.l    : ???                                                            /
;/                                                                             /
;///////////////////////////////////////////////////////////////////////////////
Statement _tb_Scroll{*tuiTextBox.tuiTextBox,dx.l,dy.l,*rp.RastPort}
If *rp  Then *win.Window  = *rp\Layer\Window : Else *win   = #NULL

If dy><0
  oldtop.l = \topLine
  \topLine + dy
 ; If \topLine>(\totalLines - *tuiTextBox\innerSize\y/\rowheight) Then \topLine=(\totalLines - *tuiTextBox\innerSize\y/\rowheight)
  If \topLine>(\totalLines - *tuiTextBox\visibleLines) Then \topLine=(\totalLines - *tuiTextBox\visibleLines)
  If \topLine<0 Then \topLine=0

  dypix.l = (\topLine-oldtop) * \rowheight
  If dypix><0 AND *rp><#NULL
    If (Abs(dypix)<!tuiRectWidth{*tuiTextBox\obj\cbox}) AND (*tuiTextBox\edflags&#TUITBF_REDRAWONSCROLL=0)
      If *win
        ScrollWindowRaster_ *win,0,dypix,*tuiTextBox\obj\cbox\left,*tuiTextBox\obj\cbox\top,*tuiTextBox\obj\cbox\right,*tuiTextBox\obj\cbox\bottom
      Else
        If *rp Then ScrollRasterBF_ *rp,0,dypix,*tuiTextBox\obj\cbox\left,*tuiTextBox\obj\cbox\top,*tuiTextBox\obj\cbox\right,*tuiTextBox\obj\cbox\bottom
      End If
      If dypix<0
        _ntui_DrawTextBoxIntern{*tuiTextBox,-1,-1,-1,*tuiTextBox\obj\cbox\top-dypix,*rp}
      Else
        _ntui_DrawTextBoxIntern{*tuiTextBox,-1,*tuiTextBox\obj\cbox\bottom+1-dypix,-1,-1,*rp}
      End If
    Else
      _ntui_DrawTextBoxIntern{*tuiTextBox,-1,-1,-1,-1,*rp}
    End If
  End If
End If

If dx><0
  oldx.l = \leftOffset
  \leftOffset + dx
  If \leftOffset<0 Then \leftOffset=0
  dxpix.l = (\leftOffset-oldx)
  If dxpix><0 AND *rp><#NULL
    If (Abs(dxpix)<!tuiRectWidth{*tuiTextBox\obj\cbox}) AND (*tuiTextBox\edflags&#TUITBF_REDRAWONSCROLL=0)
      If *win
        ScrollWindowRaster_ *win,dxpix,0,*tuiTextBox\obj\cbox\left,*tuiTextBox\obj\cbox\top,*tuiTextBox\obj\cbox\right,*tuiTextBox\obj\cbox\bottom
      Else
        If *rp Then ScrollRasterBF_ *rp,dxpix,0,*tuiTextBox\obj\cbox\left,*tuiTextBox\obj\cbox\top,*tuiTextBox\obj\cbox\right,*tuiTextBox\obj\cbox\bottom
      End If

      If dxpix<0
        _ntui_DrawTextBoxIntern{*tuiTextBox,-1,-1,*tuiTextBox\obj\cbox\left-dxpix,-1,*rp}
      Else
        _ntui_DrawTextBoxIntern{*tuiTextBox,*tuiTextBox\obj\cbox\right-dxpix+1,-1,-1,-1,*rp}
      End If
    Else
      _ntui_DrawTextBoxIntern{*tuiTextBox,-1,-1,-1,-1,*rp}
    End If
  End If
End If
End Statement



;///////////////////////////////////////////////////////////////////////////////
;/                                                                             /
;/ Syntax: _tb_ScrollTo {*tuiTextBox.tuiTextBox,lpos.l,cpos.l}                 /
;/                                                                             /
;/ Description:                                                                /
;/   ;_tb_update_scroller{*tuiTextBox,upd}                                     /
;/                                                                             /
;/ Inputs:                                                                     /
;/ - *tuiTextBox.tuiTextBox    : ???                                           /
;/ - lpos.l    : ???                                                           /
;/ - cpos.l    : ???                                                           /
;/                                                                             /
;///////////////////////////////////////////////////////////////////////////////
Statement _tb_ScrollTo{*tuiTextBox.tuiTextBox,lpos.l,cpos.l,*rp.RastPort}
If *tuiTextBox
  If lpos<\topLine+\scrollborder_y Then _tb_Scroll{*tuiTextBox,0,lpos-\topLine-\scrollborder_y,*rp}
;  If lpos>\topLine+*tuiTextBox\innerSize\y/\rowheight-1-\scrollborder_y Then _tb_Scroll{*tuiTextBox,0,lpos-\topLine-*tuiTextBox\innerSize\y/\rowheight+1+\scrollborder_y,*rp}
  If lpos>\topLine+*tuiTextBox\visibleLines-1-\scrollborder_y Then _tb_Scroll{*tuiTextBox,0,lpos-\topLine-\visibleLines+1+\scrollborder_y,*rp}

  pixpos.l = _tb_pos2x{*tuiTextBox,lpos,cpos,*rp} - \leftOffset
  If pixpos<\scrollborder_x Then _tb_Scroll{*tuiTextBox,pixpos-\scrollborder_x,0,*rp}
;  If pixpos>*tuiTextBox\innerSize\x-\rowheight-\scrollborder_x Then _tb_Scroll{*tuiTextBox,pixpos-*tuiTextBox\innerSize\x+\rowheight+\scrollborder_x,0,*rp}
  If pixpos>!tuiRectWidth{*tuiTextBox\obj\cbox}-\scrollborder_x Then _tb_Scroll{*tuiTextBox,pixpos-!tuiRectWidth{*tuiTextBox\obj\cbox}+\scrollborder_x,0,*rp}

End If
End Statement



;///////////////////////////////////////////////////////////////////////////////
;/                                                                             /
;/ Syntax: _tb_ScrollToMarker {*tuiTextBox.tuiTextBox}                         /
;/                                                                             /
;/ Description:                                                                /
;/ * no description available *                                                /
;/                                                                             /
;/ Inputs:                                                                     /
;/ - *tuiTextBox.tuiTextBox    : ???                                           /
;/                                                                             /
;///////////////////////////////////////////////////////////////////////////////
Statement _tb_ScrollToMarker{*tuiTextBox.tuiTextBox,*rp.RastPort}
If *tuiTextBox
  If \marker1_lpos>=0 Then _tb_ScrollTo{*tuiTextBox,\marker1_lpos,\marker1_cpos,*rp}
  If \marker2_lpos>=0 Then _tb_ScrollTo{*tuiTextBox,\marker2_lpos,\marker2_cpos,*rp}
End If
End Statement


;///////////////////////////////////////////////////////////////////////////////
;/                                                                             /
;/ Syntax: _tb_ScrollToCursor {*tuiTextBox.tuiTextBox}                         /
;/                                                                             /
;/ Description:                                                                /
;/ * no description available *                                                /
;/                                                                             /
;/ Inputs:                                                                     /
;/ - *tuiTextBox.tuiTextBox    : ???                                           /
;/                                                                             /
;///////////////////////////////////////////////////////////////////////////////
Statement _tb_ScrollToCursor{*tuiTextBox.tuiTextBox,*rp.RastPort}
If *tuiTextBox
  _tb_ScrollTo{*tuiTextBox,*tuiTextBox\cursor_lpos,*tuiTextBox\cursor_cpos,*rp}
End If
End Statement

;///////////////////////////////////////////////////////////////////////////////
;/                                                                             /
;/ Syntax: _tb_DrawLine {*tuiTextBox.tuiTextBox,lpos.l,*rp.RastPort}           /
;/                                                                             /
;/ Description:                                                                /
;/ * no description available *                                                /
;/                                                                             /
;/ Inputs:                                                                     /
;/ - *tuiTextBox.tuiTextBox    : ???                                           /
;/ - lpos.l    : ???                                                           /
;/ - *rp.RastPort    : ???                                                     /
;/                                                                             /
;///////////////////////////////////////////////////////////////////////////////
Statement  _tb_DrawLine{*tuiTextBox.tuiTextBox,lpos.l,*rp.RastPort}
y1.l = _tb_line2y{*tuiTextBox,lpos}
If y1>-\rowheight AND y1<!tuiRectHeight{*tuiTextBox\obj\cbox}
  y1 + *tuiTextBox\obj\cbox\top
  _ntui_DrawTextBoxIntern{*tuiTextBox,-1,y1,-1,y1+\rowheight-1,*rp}
End If
End Statement


;///////////////////////////////////////////////////////////////////////////////
;/                                                                             /
;/ Syntax: _tb_SetMarker {*tuiTextBox.tuiTextBox,@lpos1.l,@cpos1.l,@lpos2.l,:: /
;/ @cpos2.l,@*rp.RastPort}                                                     /
;/                                                                             /
;/ Description:                                                                /
;/ * no description available *                                                /
;/                                                                             /
;/ Inputs:                                                                     /
;/ - *tuiTextBox.tuiTextBox    : ???                                           /
;/ - lpos1.l    : ???                                                          /
;/ - cpos1.l    : ???                                                          /
;/ - lpos2.l    : ???                                                          /
;/ - cpos2.l    : ???                                                          /
;/ - upd.l    : ???                                                            /
;/ - *rp.RastPort    : ???                                                     /
;/                                                                             /
;///////////////////////////////////////////////////////////////////////////////
Statement _tb_SetMarker{*tuiTextBox.tuiTextBox,@lpos1.l,@cpos1.l,@lpos2.l,@cpos2.l,@*rp.RastPort}
If lpos2<lpos1 Then Exchange lpos1,lpos2 : Exchange cpos1,cpos2
If lpos2=lpos1 AND cpos1>cpos2 Then Exchange cpos1,cpos2
If cpos1<0 Then cpos1=0
If cpos2<0 Then cpos2=0
!line_use{*tline, lpos1}
If *tline
  If cpos1>*tline\clength Then cpos1=*tline\clength
End If
!line_use{*tline, lpos2}
If *tline
  If cpos2>*tline\clength Then cpos2=*tline\clength
End If

If lpos1=-1
  minlpos.l = \marker1_lpos
  maxlpos.l = \marker2_lpos
Else
  If \marker1_lpos=-1
    minlpos = lpos1
    maxlpos = lpos2
  Else

succ.l = False
If lpos1><\marker1_lpos
  minlpos.l = Min(\marker1_lpos,lpos1)
  maxlpos.l = Max(\marker1_lpos,lpos1)
  If lpos2><\marker2_lpos
    ;minlpos.l = Min(\marker2_lpos,lpos2)
    maxlpos.l = Max(\marker2_lpos,lpos2)
  End If
Else
  If lpos2><\marker2_lpos
    minlpos.l = Min(\marker2_lpos,lpos2)
    maxlpos.l = Max(\marker2_lpos,lpos2)
  Else
    If \marker1_cpos><cpos1
      minlpos = lpos1
      maxlpos = lpos1
    End If
    If \marker2_cpos><cpos2
      minlpos = lpos2
      maxlpos = lpos2
    End If
  End If
End If
End If
End If

If \marker1_cpos><cpos1 Then \marker1_cpos = cpos1  :succ.l = True
If \marker2_cpos><cpos2 Then \marker2_cpos = cpos2  :succ.l = True
If \marker1_lpos><lpos1 Then \marker1_lpos = lpos1  :succ.l = True
If \marker2_lpos><lpos2 Then \marker2_lpos = lpos2  :succ.l = True

If succ AND *rp><-1 AND *rp><#NULL
  For l.l = minlpos To maxlpos
    _tb_DrawLine{*tuiTextBox,l,*rp}
  Next
End If
End Statement


;///////////////////////////////////////////////////////////////////////////////
;/                                                                             /
;/ Syntax: _tb_RedrawDamaged {*tuiTextBox.tuiTextBox}                          /
;/                                                                             /
;/ Description:                                                                /
;/     ;minlpos.l = Min(\marker2_lpos,lpos2)                                   /
;/                                                                             /
;/ Inputs:                                                                     /
;/ - *tuiTextBox.tuiTextBox    : ???                                           /
;/                                                                             /
;///////////////////////////////////////////////////////////////////////////////
Statement _tb_RedrawDamaged{*tuiTextBox.tuiTextBox,*rp.RastPort}
If *tuiTextBox
  If *tuiTextBox\damage_begin>=*tuiTextBox\damage_end
    If *rp
      y1.l = _tb_line2y{*tuiTextBox,*tuiTextBox\damage_begin} + *tuiTextBox\obj\cbox\top ;innerPos\y
      y2.l = _tb_line2y{*tuiTextBox,*tuiTextBox\damage_end}   + *tuiTextBox\obj\cbox\top + \rowheight-1
      _ntui_DrawTextBoxIntern{*tuiTextBox,-1,y1,-1,y2,*rp}
    End If
    *tuiTextBox\damage_begin = 0
    *tuiTextBox\damage_end   = -1
  End If
End If
End Statement


;///////////////////////////////////////////////////////////////////////////////
;/                                                                             /
;/ Syntax: _tb_SetCursor {*tuiTextBox.tuiTextBox,lpos.l,cpos.l,@pixupd.l,@lo:: /
;/ ose.l,@*rp.RastPort}                                                        /
;/                                                                             /
;/ Description:                                                                /
;/ * no description available *                                                /
;/                                                                             /
;/ Inputs:                                                                     /
;/ - *tuiTextBox.tuiTextBox    : ???                                           /
;/ - lpos.l    : ???                                                           /
;/ - cpos.l    : ???                                                           /
;/ - pixupd.l    : ???                                                         /
;/ - loose.l    : ???                                                          /
;/ tb_line_settokencase{*tuiTextBox.tuiTextBox}                                /
;/ - *rp.RastPort    : ???                                                     /
;/                                                                             /
;///////////////////////////////////////////////////////////////////////////////
Statement _tb_SetCursor{*tuiTextBox.tuiTextBox,lpos.l,cpos.l,@pixupd.l,@loose.l,@*rp.RastPort}
If *rp=-1 Then *rp=#NULL
;tb_line_settokencase{*tuiTextBox.tuiTextBox}
If lpos>=\totalLines Then lpos=\totalLines-1
If lpos<0 Then lpos=0
If cpos<0 Then cpos=0
If \edflags&#TUITBF_RIGHTWRAP
  !line_use{*tline, lpos}
  If *tline
    If cpos>*tline\clength Then cpos = *tline\clength
  Else
    cpos=0
  End If
End If
\cursor_cpos     = cpos
\cursor_lpos     = lpos
If pixupd Then \cursor_pixpos = _tb_pos2x{*tuiTextBox,lpos,cpos,*rp}
If ((\edflags&#TUITBF_LOOSEMARKER)><0) AND loose Then _tb_SetMarker{*tuiTextBox,-1,-1,-1,-1,*rp}
_tb_ScrollTo{*tuiTextBox,\cursor_lpos,\cursor_cpos,*rp}
If *rp Then _tb_ShowBrackets{*tuiTextBox,*rp}
End Statement

;///////////////////////////////////////////////////////////////////////////////
;/                                                                             /
;/ Syntax: _tb_MouseDrag {*tuiTextBox.tuiTextBox,*rp.RastPort,mx.l,my.l}       /
;/                                                                             /
;/ Description:                                                                /
;/ * no description available *                                                /
;/                                                                             /
;/ Inputs:                                                                     /
;/ - *tuiTextBox.tuiTextBox    : ???                                           /
;/ - *rp.RastPort    : ???                                                     /
;/ - mx.l    : ???                                                             /
;/ - my.l    : ???                                                             /
;/                                                                             /
;///////////////////////////////////////////////////////////////////////////////
Statement _tb_MouseDrag{*tuiTextBox.tuiTextBox,*rp.RastPort,mx.l,my.l}
If *tuiTextBox
  ;mx = \xwin\MouseX - *tuiTextBox\obj\pos\x
  ;my = \xwin\MouseY - *tuiTextBox\obj\pos\y
  mx +\leftOffset

  lpos1.l = \range_lpos ;tb_y2line{*tuiTextBox,\ry}
  cpos1.l = \range_cpos ;tb_x2pos {*tuiTextBox,lpos1,\rx}

  lpos2.l = _tb_y2line{*tuiTextBox,my}
  cpos2.l = _tb_x2pos {*tuiTextBox,lpos2,mx,*rp}

  If \edflags&#TUITBF_DRAGCURSOR Then _tb_DoBlink{*tuiTextBox,*rp,Off} :_tb_SetCursor{*tuiTextBox,lpos2,cpos2,True,False}

  If lpos1><lpos2 OR cpos1><cpos2
    _tb_SetMarker{*tuiTextBox,lpos1,cpos1,lpos2,cpos2,*rp}
  End If

  If lpos2<\topLine Then _tb_Scroll{*tuiTextBox,0,-1,True}
;  If lpos2>=\topLine+*tuiTextBox\obj\size\y/\rowheight Then _tb_Scroll{*tuiTextBox,0,1,True}
  If lpos2>=\topLine+*tuiTextBox\visibleLines Then _tb_Scroll{*tuiTextBox,0,1,True}
  If mx<\leftOffset Then _tb_Scroll{*tuiTextBox,-\rowheight,0,True}
  If mx>\leftOffset+!tuiRectWidth{*tuiTextBox\obj\cbox} Then _tb_Scroll{*tuiTextBox,\rowheight,0,True}
End If
End Statement

;///////////////////////////////////////////////////////////////////////////////
;/                                                                             /
;/ Syntax: _tb_SetCursorXY {*tuiTextBox.tuiTextBox,x.l,y.l}                    /
;/                                                                             /
;/ Description:                                                                /
;/   ;mx = \xwin\MouseX - *tuiTextBox\obj\pos\x                                /
;/   ;my = \xwin\MouseY - *tuiTextBox\obj\pos\y                                /
;/   lpos1.l = \range_lpos ;tb_y2line{*tuiTextBox,\ry}                         /
;/   cpos1.l = \range_cpos ;tb_x2pos {*tuiTextBox,lpos1,\rx}                   /
;/                                                                             /
;/ Inputs:                                                                     /
;/ - *tuiTextBox.tuiTextBox    : ???                                           /
;/ - x.l    : ???                                                              /
;/ - y.l    : ???                                                              /
;/                                                                             /
;///////////////////////////////////////////////////////////////////////////////
Statement _tb_SetCursorXY{*tuiTextBox.tuiTextBox,x.l,y.l,*rp.RastPort}
lpos.l = _tb_y2line{*tuiTextBox,y}
cpos.l = _tb_x2pos{*tuiTextBox,lpos,x,*rp}
_tb_SetCursor{*tuiTextBox,lpos,cpos}
End Statement


;///////////////////////////////////////////////////////////////////////////////
;/                                                                             /
;/ Syntax: _tb_MouseClick {*tuiTextBox.tuiTextBox,*rp.RastPort,mx.l,my.l,mb.:: /
;/ l,dbl.l}                                                                    /
;/                                                                             /
;/ Description:                                                                /
;/ * no description available *                                                /
;/                                                                             /
;/ Inputs:                                                                     /
;/ - *tuiTextBox.tuiTextBox    : ???                                           /
;/ - *rp.RastPort    : ???                                                     /
;/ - mx.l    : ???                                                             /
;/ - my.l    : ???                                                             /
;/ - mb.l    : ???                                                             /
;/ - dbl.l    : ???                                                            /
;/                                                                             /
;///////////////////////////////////////////////////////////////////////////////
Statement _tb_MouseClick{*tuiTextBox.tuiTextBox,*rp.RastPort,mx.l,my.l,mb.l,dbl.l}
If *tuiTextBox
;If mb=1
  _tb_SetCursorXY{*tuiTextBox ,mx+\leftOffset,my,*rp}
  If dbl
    !line_use{*tline, \cursor_lpos}
    If *tline
      If *tline\text
      cpos.l = _tb_x2pos {*tuiTextBox,\range_lpos,mx+\leftOffset,*rp}
      breakme.l=False
      While cpos>=0 AND breakme=False
        c.b = Peek.b(*tline\text+cpos)
        If (c>=@"A" AND c<=@"Z") OR (c>=@"a" AND c<=@"z") OR (c>=@"0" AND c<=@"9") OR c=@"_" OR c<0
          cpos-1
        Else
          breakme.l = True
          cpos+1
        End If
      Wend
      If cpos<0 Then cpos=0
      apos.l = cpos
      cpos.l = _tb_x2pos {*tuiTextBox,\range_lpos,mx+\leftOffset,*rp}
      breakme.l=False
      While cpos<*tline\clength AND breakme=False
        c.b = Peek.b(*tline\text+cpos)
        If (c>=@"A" AND c<=@"Z") OR (c>=@"a" AND c<=@"z") OR (c>=@"0" AND c<=@"9") OR c=@"_" OR c<0
          cpos+1
        Else
          breakme.l = True
        End If
      Wend
      If cpos>*tline\clength Then cpos=*tline\clength
      _tb_SetMarker{*tuiTextBox,\cursor_lpos,apos,\cursor_lpos,cpos,*rp}
      End If
    End If
  Else
    _tb_SetMarker{*tuiTextBox,-1,-1,-1,-1,*rp}
  End If
  \range_lpos = _tb_y2line{*tuiTextBox,my}
  \range_cpos = _tb_x2pos {*tuiTextBox,\range_lpos,mx+\leftOffset,*rp}

;End If
End If
End Statement


;///////////////////////////////////////////////////////////////////////////////
;/                                                                             /
;/ Syntax: _tb_MoveCursor {*tuiTextBox.tuiTextBox,dx.l,dy.l,shift.l,*rp.Rast:: /
;/ Port}                                                                       /
;/                                                                             /
;/ Description:                                                                /
;/ If mb=1                                                                     /
;/ End If                                                                      /
;/                                                                             /
;/ Inputs:                                                                     /
;/ - *tuiTextBox.tuiTextBox    : ???                                           /
;/ - dx.l    : ???                                                             /
;/ - dy.l    : ???                                                             /
;/ - shift.l    : ???                                                          /
;/ - *rp.RastPort    : ???                                                     /
;/                                                                             /
;///////////////////////////////////////////////////////////////////////////////
Statement _tb_MoveCursor{*tuiTextBox.tuiTextBox,dx.l,dy.l,shift.l,*rp.RastPort}
If shift AND (\edflags&#TUITBF_SHIFTMARK><0)
  If \marker1_lpos<0 OR \marker2_lpos<0
    \range_lpos = \cursor_lpos
    \range_cpos = \cursor_cpos
  End If
End If

If dx><0
  If shift AND (\edflags&#TUITBF_SHIFTMARK=0)
    If dx>0
      clength.l = 0
      !line_use{*tline, \cursor_lpos}
      If *tline
        clength = *tline\clength
      End If
      \cursor_cpos = clength
    Else
      \cursor_cpos = 0
    End If
  Else
    \cursor_cpos+dx
  End If

  If (\edflags&#TUITBF_LEFTWRAP><0)
    While \cursor_cpos<0
      If \cursor_lpos>0
        \cursor_lpos-1
        !line_use{*tline, \cursor_lpos}
        If *tline
          \cursor_cpos + *tline\clength +1
        Else
          \cursor_cpos + 1
        End If
      Else
        \cursor_cpos = 0
      End If
    Wend
  End If

  If (\edflags&#TUITBF_RIGHTWRAP><0)
    !line_use{*tline, \cursor_lpos}
    If *tline
      clength.l=*tline\clength
    Else
      clength=0
    End If

    While \cursor_cpos>clength
      If \cursor_lpos<\totalLines-1
        \cursor_cpos-clength-1
        \cursor_lpos+1
        !line_use{*tline, \cursor_lpos}
        If *tline
          clength=*tline\clength
        Else
          clength=0
        End If
      Else
        If *tline Then \cursor_cpos = *tline\clength:Else \cursor_cpos=0
        clength = *tline\clength
      End If
    Wend
  End If
  \cursor_pixpos = _tb_pos2x{*tuiTextBox,\cursor_lpos,\cursor_cpos,*rp}
End If

If dy><0
  ;_tb_line_Settokencase{*tuiTextBox}
  If shift AND (\edflags&#TUITBF_SHIFTMARK=0) Then dy.l = *tuiTextBox\visibleLines * Sgn(dy)

  \cursor_lpos + dy
  If \cursor_lpos>=\totalLines Then \cursor_lpos=\totalLines-1
  If \cursor_lpos<0 Then \cursor_lpos=0
  \cursor_cpos = _tb_x2pos{*tuiTextBox,\cursor_lpos,\cursor_pixpos,*rp}
End If

;If \cursor_lpos<\topLine Then _tb_scroll{*tuiTextBox,0,\cursor_lpos-\topLine}
;If \cursor_lpos>\topLine + *tuiTextBox\innerSize\y/\rowheight-1 Then _tb_scroll{*tuiTextBox,0,\cursor_lpos-\topLine-*tuiTextBox\innerSize\y/\rowheight+1}

;pixpos.l = _tb_pos2x{*tuiTextBox,\cursor_lpos,\cursor_cpos} - \leftOffset
;If pixpos<0 Then _tb_scroll{*tuiTextBox,pixpos,0}
;If pixpos>*tuiTextBox\innerSize\x-\rowheight Then _tb_scroll{*tuiTextBox,pixpos-*tuiTextBox\innerSize\x+\rowheight,0}
_tb_ScrollToCursor{*tuiTextBox,*rp}

If shift AND (\edflags&#TUITBF_SHIFTMARK><0)
  _tb_SetMarker{*tuiTextBox,\range_lpos,\range_cpos,\cursor_lpos,\cursor_cpos,*rp}
  ;_tb_Drag{*tuiTextBox,\cursor_lpos,\cursor_cpos}
Else
  If (\edflags&#TUITBF_LOOSEMARKER><0) Then _tb_SetMarker{*tuiTextBox,-1,-1,-1,-1,*rp}
End If
If *rp Then _tb_ShowBrackets{*tuiTextBox,*rp}
End Statement


Function.l _tb_AllocLines{*tuiTextBox.tuiTextBox,numLines.l}
succ.l = False
If numLines>\allocLines
  newdoc.l = AllocVec_((numLines)*SizeOf.tline,#MEMF_CLEAR)
  If newdoc
    If \doc
      CopyMem_ \doc,newdoc,\allocLines*SizeOf.tline
      FreeVec_ \doc
    End If
    \doc = newdoc
    \allocLines = numLines
    succ.l = -1
  End If
End If
Function Return succ
End Function

;///////////////////////////////////////////////////////////////////////////////
;/                                                                             /
;/ Syntax: _tb_InsertLine {*tuiTextBox.tuiTextBox,lpos.l,text.l,@clength.l}    /
;/                                                                             /
;/ Description:                                                                /
;/ "Insert" at line 5 moves line 5-end to position 6 and creates new line at:: /
;/  position 5                                                                 /
;/                                                                             /
;/ Inputs:                                                                     /
;/ - *tuiTextBox.tuiTextBox    : ???                                           /
;/ - lpos.l    : ???                                                           /
;/ - text.l    : ???                                                           /
;/ - clength.l    : ???                                                        /
;/                                                                             /
;///////////////////////////////////////////////////////////////////////////////
Statement _tb_CreateLine{*tuiTextBox.tuiTextBox,lpos.l,string.l,ilength.l}
If lpos<0 OR lpos>\totalLines Then lpos = \totalLines
\bracket1_lpos=-1  ; hide brackets
\totalLines+1
If \totalLines>=\allocLines Then _tb_AllocLines{*tuiTextBox.tuiTextBox,\allocLines+32}

If \doc
  ;undo_Store{\undo,#TEDUNDO_insert_line,0,0,lpos}
  !line_use{*tline, lpos}
  If *tline
    flags.l = *tline\flags
  Else
    flags.l = \last_flags
  End If
  If \totalLines-lpos-1>0 Then _tb_CopyMem{\doc+(lpos*SizeOf.tline),\doc+((lpos+1)*SizeOf.tline),(\totalLines-lpos-1)*SizeOf.tline};:error{"Lut shift !"} ; LUT shift
  !line_use{*tline, lpos}
;  *tline = AllocMem_ (SizeOf.tline,#MEMF_CLEAR)  ; new entry
;  Poke.l \doc+lpos*4,*tline
  If *tline
    *tline\flags = flags
    If ilength<0 Then ilength = 0 : If string Then While Peek.b(string+ilength) : ilength+1:Wend
    If ilength>0
      *tline\blength = ilength+1+8
      *tline\text = AllocMem_ (*tline\blength,#MEMF_CLEAR)
      If *tline\text
        _tb_CopyMem{string,*tline\text,ilength} : Poke.b *tline\text+ilength,0 ; Null terminated
        *tline\clength = ilength
        ;_tb_Tokenize{*tuiTextBox,lpos}
        ;error{"Line insterted at: "+Str$(lpos)+" = "+Peek.s(*tline\text)}
      Else
        *tline\clength = 0
      End If
    Else
      *tline\clength = 0
      *tline\text    = #NULL
    End If
  End If
End If
End Statement



;///////////////////////////////////////////////////////////////////////////////
;/                                                                             /
;/ Syntax: _tb_Insert {*tuiTextBox.tuiTextBox,lpos.l,cpos.l,text.l,@ilength.l} /
;/                                                                             /
;/ Description:                                                                /
;/ insert at cpos 5 will move characters 5-end to cpos 6 and fill cpos with :: /
;/ inerttext                                                                   /
;/ insert at higher cpos than clength will add some spaces between             /
;/                                                                             /
;/ Inputs:                                                                     /
;/ - *tuiTextBox.tuiTextBox    : ???                                           /
;/ - lpos.l    : ???                                                           /
;/ - cpos.l    : ???                                                           /
;/ - text.l    : ???                                                           /
;/ - ilength.l    : ???                                                        /
;/ \bracket1_lpos=-1  ; hide brackets                                          /
;/                                                                             /
;///////////////////////////////////////////////////////////////////////////////
Statement _tb_InsertInLine{*tuiTextBox.tuiTextBox,lpos.l,cpos.l,string.l,ilength.l}
\bracket1_lpos=-1  ; hide brackets
If lpos<0 OR lpos>=\totalLines Then lpos=\totalLines-1
If lpos<0
  _tb_CreateLine{*tuiTextBox,0,string,ilength}
Else
  If ilength<0 Then ilength = 0 : If string Then While Peek.b(string+ilength) : ilength+1:Wend
  If cpos<0 Then cpos=0
  !line_use{*tline, lpos}
  If *tline=#NULL
    _tb_CreateLine{*tuiTextBox,lpos,#NULL,0}:\totalLines-1
    !line_use{*tline, lpos}
  End If

  If *tline
    spacelength.l  = cpos - *tline\clength : If spacelength<0 Then spacelength=0
    clength.l      = *tline\clength + spacelength + ilength
    copylength.l   = *tline\clength - cpos : If copylength<0 Then copylength=0

    ;undo_Store{\undo,#TEDUNDO_insert,*tline\text,*tline\clength,lpos}
    If clength>*tline\blength-1 OR *tline\text=#NULL ; Zeile erneuern
      blength.l = clength+1+8
      newline.l = AllocMem_ (blength,#MEMF_CLEAR)
      If *tline\text
        _tb_CopyMem{ *tline\text,newline,*tline\clength+1}
        FreeMem_ *tline\text,*tline\blength
      End If
      *tline\blength = blength
      *tline\text    = newline
    End If

    If *tline\text
      a.s = LSet$(" ",clength)
      If cpos>0 Then CopyMem_ *tline\text,&a.s,Min(cpos,*tline\clength) ; left side
      If string Then CopyMem_ string,&a.s+cpos,ilength ; insert text
      If copylength>0 Then CopyMem_ *tline\text+cpos,&a.s+cpos+ilength,copylength
      CopyMem_ &a.s,*tline\text,clength+1 ; BB Strings are 0 terminated as well
      *tline\clength = clength
    End If
;    If ilength<2 Then _tb_Tokenize{*tuiTextBox,lpos,False}:Else _tb_tokenize{*tuiTextBox,lpos,True}
  End If
End If
End Statement

;///////////////////////////////////////////////////////////////////////////////
;/                                                                             /
;/ Syntax: _tb_DeleteLine {*tuiTextBox.tuiTextBox,lpos.l}                      /
;/                                                                             /
;/ Description:                                                                /
;/ "Delete" at line 5 frees line 5 and moves line 6-end to position 5          /
;/                                                                             /
;/ Inputs:                                                                     /
;/ - *tuiTextBox.tuiTextBox    : ???                                           /
;/ - lpos.l    : ???                                                           /
;/                                                                             /
;///////////////////////////////////////////////////////////////////////////////
Statement _tb_RemoveLine{*tuiTextBox.tuiTextBox,lpos.l}
\bracket1_lpos=-1  ; hide brackets
If lpos<0 Then lpos=\cursor_lpos
If lpos<0 OR lpos>=\totalLines Then lpos=\totalLines-1
If lpos>=0
  !line_use{*tline, lpos}
  If *tline
    ;undo_Store{\undo,#TEDUNDO_delete_line,*tline\text,*tline\clength,lpos}
    If *tline\text Then FreeMem_ *tline\text ,*tline\blength : *tline\text=#NULL : *tline\blength=0 : *tline\clength=0
    ;_tb_Tokenize{*tuiTextBox ,lpos}
  End If
  If \totalLines-lpos-1>0 Then _tb_CopyMem{ \doc+((lpos+1)*SizeOf.tline),\doc+(lpos*SizeOf.tline),(\totalLines-lpos-1)*SizeOf.tline} ;:error{"Lut shift !"}
  \totalLines-1
  If \cursor_lpos>=\totalLines Then \cursor_lpos=\totalLines-1
  If \cursor_lpos<0 Then \cursor_lpos=0

End If
End Statement


;///////////////////////////////////////////////////////////////////////////////
;/                                                                             /
;/ Syntax: _tb_Delete {*tuiTextBox.tuiTextBox,lpos.l,cpos.l,elength.l}         /
;/                                                                             /
;/ Description:                                                                /
;/ "delete" at cpos -1 removes elength-1 characters and appends the rest to :: /
;/ upper line                                                                  /
;/ "delete" at cpos 0 removes elength charakters including position 0          /
;/                                                                             /
;/ Inputs:                                                                     /
;/ - *tuiTextBox.tuiTextBox    : ???                                           /
;/ - lpos.l    : ???                                                           /
;/ - cpos.l    : ???                                                           /
;/ - elength.l    : ???                                                        /
;/                                                                             /
;///////////////////////////////////////////////////////////////////////////////
Statement _tb_Delete{*tuiTextBox.tuiTextBox,lpos.l,cpos.l,elength.l}
\bracket1_lpos=-1  ; hide brackets
If lpos<0 OR lpos>=\totalLines Then lpos =\totalLines-1
If lpos>=0
  !line_use{*tline, lpos}
  If *tline
    If elength<2 Then force.l=False:Else force=True
    If elength>0
      If cpos<0
        If lpos>0 AND (\edflags&#TUITBF_DELWRAP><0)
          elength-1 ; join lines
          !line_use{*tline, lpos}
          !line_use{*tline2, lpos-1}
          If *tline2 ; \doc\base[lpos-1]
            clength.l= *tline2\clength ; \doc\base[lpos-1]\clength
            If clength>0 AND *tline\text><False
              _tb_InsertInLine{*tuiTextBox,lpos-1,clength,*tline\text,*tline\clength}
              _tb_RemoveLine{*tuiTextBox,lpos}
            Else
              If *tline\text=False
                _tb_RemoveLine{*tuiTextBox,lpos}
              Else
                _tb_RemoveLine{*tuiTextBox,lpos-1}
              End If
              clength=1
            End If
            lpos-1
            cpos+clength
          Else
            _tb_RemoveLine{*tuiTextBox,lpos-1}
            lpos-1
            cpos=0
          End If
        Else
          elength-1 : cpos=0
        End If
      End If
    End If

    If elength>0
      !line_use{*tline, lpos}
      If *tline
        If cpos>=*tline\clength AND (\edflags&#TUITBF_REMWRAP><0) ; join lines from down
          If lpos<\totalLines-1
            !line_use{*tline, lpos}
            !line_use{*tline2, lpos+1}
            If *tline2 ; \doc\base[lpos+1]
              clength.l= *tline2\clength ; \doc\base[lpos+1]\clength
              If *tline2\text><0 AND clength>0
                ;cpos+*tline\clength
                _tb_InsertInLine{*tuiTextBox,lpos,*tline\clength,*tline2\text,clength}

              End If
              _tb_RemoveLine{*tuiTextBox,lpos+1}
            End If
          End If
          elength-1 ; ... ; drag from next line
        End If
      End If
    End If

    If elength>0
      !line_use{*tline, lpos}
      If *tline
        If *tline\text
          ;undo_Store{\undo,#TEDUNDO_delete,*tline\text,*tline\clength,lpos}
          If elength<0 Then elength = *tline\clength-cpos
          elength.l = Min(elength,*tline\clength-cpos)
          clength.l = *tline\clength-elength  ; new length of string
          If clength<0 Then clength=0
          copylength.l = *tline\clength-cpos-elength+1 ; length of strin on right side
          If clength>0
            If copylength>0 ; move left ...
              _tb_CopyMem{ *tline\text+cpos+elength,*tline\text+cpos,copylength}
            End If
          Else
            If *tline\text Then FreeMem_ *tline\text,*tline\blength : *tline\text=#NULL
          End If
          *tline\clength = clength
        End If
      End If
    End If
    ;_tb_Tokenize{*tuiTextBox,lpos,force}
  End If
End If
If \cursor_lpos>\totalLines-1 Then \cursor_lpos=\totalLines-1
If \topLine>\totalLines-1 Then \topLine=\totalLines-1
End Statement


;///////////////////////////////////////////////////////////////////////////////
;/                                                                             /
;/ Syntax: result.l = _tb_ReturnBreak {*tuiTextBox.tuiTextBox,lpos.l,cpos.l,:: /
;/ @indent.l}                                                                  /
;/                                                                             /
;/ Description:                                                                /
;/                                                                             /
;/ Inputs:                                                                     /
;/ - *tuiTextBox.tuiTextBox    : ???                                           /
;/ - lpos.l    : ???                                                           /
;/ - cpos.l    : ???                                                           /
;/ - indent.l    : ???                                                         /
;/                                                                             /
;/ Result:                                                                     /
;/ - result.l     : ???                                                        /
;/                                                                             /
;///////////////////////////////////////////////////////////////////////////////
Function.l _tb_ReturnBreak{*tuiTextBox.tuiTextBox,lpos.l,cpos.l,@indent.l}
n.l=0
!line_use{*tline, lpos}
If *tline
  If *tline\text
    If (\edflags&#TUITBF_AUTOINDENT)><0 AND indent
      n.l = 0
      While Peek.b(*tline\text+n)=32 AND n<*tline\clength
        n+1
      Wend
      If n>\cursor_cpos Then n=0
      a.s = LSet$(" ",n)
    Else
      a.s = ""
    End If

    If cpos<*tline\clength
      a.s = a.s + Peeks$(*tline\text+cpos,*tline\clength-cpos)
      _tb_CreateLine{*tuiTextBox,lpos+1,&a.s,FLen(a.s)}
      !line_use{*tline, lpos}
      _tb_Delete{*tuiTextBox,lpos,cpos,*tline\clength-cpos}
    Else
      _tb_CreateLine{*tuiTextBox,lpos+1,&a.s,FLen(a.s)}
    End If
  Else
    _tb_CreateLine{*tuiTextBox,lpos+1,&a.s,FLen(a.s)}
  End If
Else
  _tb_CreateLine{*tuiTextBox,lpos+1,&a.s,FLen(a.s)}
End If
Function Return n
End Function


;///////////////////////////////////////////////////////////////////////////////
;/                                                                             /
;/ Syntax: _tb_InsertString {*tuiTextBox.tuiTextBox,lpos.l,cpos.l,str_ptr.l,:: /
;/ @blength.l,@upd.l}                                                          /
;/                                                                             /
;/ Description:                                                                /
;/ * no description available *                                                /
;/                                                                             /
;/ Inputs:                                                                     /
;/ - *tuiTextBox.tuiTextBox    : ???                                           /
;/ - lpos.l    : ???                                                           /
;/ - cpos.l    : ???                                                           /
;/ - str_ptr.l    : ???                                                        /
;/ - blength.l    : ???                                                        /
;/ - upd.l    : ???                                                            /
;/                                                                             /
;///////////////////////////////////////////////////////////////////////////////
Statement _tb_Insert{*tuiTextBox.tuiTextBox,lpos.l,cpos.l,text.l,ilength.l,@*rp.RastPort}
If *rp=-1 Then *rp=#NULL
If ilength<0 Then ilength=0 : While Peek.b(text+ilength) : ilength+1:Wend

first.l = True
epos.l = 0
apos.l = 0
bpos.l = 0
xpos.l = 0
While xpos<ilength
  Select Peek.b(text+xpos)
    Case 10
      If first
        _tb_InsertInLine{*tuiTextBox,lpos,cpos,text+apos,bpos-apos}
        cpos.l = _tb_ReturnBreak{*tuiTextBox,lpos,cpos+bpos-apos,False}
        cpos=0
        epos.l = cpos+bpos-apos
        first=False
      Else
        _tb_CreateLine{*tuiTextBox,lpos,text+apos,bpos-apos}
      End If
      apos=xpos+1
      bpos=xpos+1
      lpos+1
    Case 13
      ; .... do not insert the 13!
      If Peek.b(text+xpos+1)><10 Then bpos+1
    Default
      bpos+1

  End Select
  xpos+1
Wend
If bpos>apos
  _tb_InsertInLine{*tuiTextBox,lpos,cpos,text+apos,bpos-apos}
  epos.l = cpos+bpos-apos
End If

_tb_SetCursor{*tuiTextBox,lpos,epos,-1,-1,*rp}
End Statement



;///////////////////////////////////////////////////////////////////////////////
;/                                                                             /
;/ Syntax: _tb_FreeText {*tuiTextBox.tuiTextBox}                               /
;/                                                                             /
;/ Description:                                                                /
;/ * no description available *                                                /
;/                                                                             /
;/ Inputs:                                                                     /
;/ - *tuiTextBox.tuiTextBox    : ???                                           /
;/                                                                             /
;///////////////////////////////////////////////////////////////////////////////
Statement _tb_FreeText{*tuiTextBox.tuiTextBox}
If \doc
  For lpos.l = 0 To \totalLines-1
    !line_use {*tline, lpos}
    If *tline
      If *tline\text  Then FreeMem_ *tline\text  ,*tline\blength  : *tline\text=#NULL
      If *tline\ttext Then FreeMem_ *tline\ttext ,*tline\tblength : *tline\ttext=#NULL
  ;    ted_tokenize {*ted ,lpos}
      ;FreeMem_ *tline,SizeOf.tline
      ;Poke.l \doc+lpos*4,0
    End If
  Next
End If
\totalLines = 0
\totalWidth = 1
;undo_Flush{\undo}
;undo_SetSaved{\undo}
End Statement




Statement ntui_SetTextBoxText{*tuiTextBox.tuiTextBox,text.l,@blength.l,@*rp.RastPort}
If *tuiTextBox
  _tb_FreeText{*tuiTextBox}
  _tb_Insert{*tuiTextBox,0,0,text,blength,*rp}
  \cursor_lpos   = 0
  \cursor_cpos   = 0
  \topLine       = 0
  \cursor_pixpos = 0
End If
End Statement




;///////////////////////////////////////////////////////////////////////////////
;/                                                                             /
;/ Syntax: ntui_SetValidTextBoxChars {*tuiTextBox.tuiTextBox,chars.s}          /
;/                                                                             /
;/ Description:                                                                /
;/ Define the characters that are allowed to be typed into the textbox.                                                /
;/                                                                             /
;/ Inputs:                                                                     /
;/ - *tuiTextBox.tuiTextBox    : ???                                           /
;/ - chars.s    : ???                                                          /
;/                                                                             /
;///////////////////////////////////////////////////////////////////////////////
Statement ntui_SetValidTextBoxChars{*tuiTextBox.tuiTextBox,chars.s}
For n.l=0 To 255
  *tuiTextBox\validChar[n]=False
Next
For n.l=0 To FLen(chars)-1
  ind.l = Peek.b(&chars+n) & $FF
  *tuiTextBox\validChar[ind]=True
Next
End Statement

 




;///////////////////////////////////////////////////////////////////////////////
;/                                                                             /
;/ Syntax: result.s = ntui_GetTextBoxSelection {*tuiTextBox.tuiTextBox,@only:: /
;/ Marker.l}                                                                   /
;/                                                                             /
;/ Description:                                                                /
;/                                                                             /
;/ Inputs:                                                                     /
;/ - *tuiTextBox.tuiTextBox    : ???                                           /
;/ - onlyMarker.l    : ???                                                     /
;/                                                                             /
;/ Result:                                                                     /
;/ - result.s     : ???                                                        /
;/                                                                             /
;///////////////////////////////////////////////////////////////////////////////
Function.s ntui_GetTextBoxSelection{*tuiTextBox.tuiTextBox,@onlyMarker.l}
If onlyMarker
  lpos_a.l = \marker1_lpos
  lpos_b.l = \marker2_lpos
  cpos_a.l = \marker1_cpos
  cpos_b.l = \marker2_cpos
Else
  lpos_a.l = 0
  lpos_b.l = \totalLines-1
  cpos_a.l = 0
  cpos_b.l = $7FFFFFFF
End If
If cpos_a<0 Then cpos_a=0
If cpos_b<0 Then cpos_b=0

string.s = ""

If lpos_a>=0

  For lpos.l = lpos_a+1 To lpos_b-1
    !line_use{*tline, lpos}
    If *tline
      If *tline\text
        string.s + Peeks$(*tline\text,*tline\clength)
      End If
    End If
    string.s+Chr$(10)
    If FLen(string.s)>(1024/2-8) Then Pop For:Pop If:error{"\\__THIS_FUNCTION: Not enough string buffer for clipboard!"}:string.s = "":Goto xskipcopy
  Next

  If lpos_a=lpos_b
    !line_use{*tline, lpos_a}
    If *tline
      If cpos_b>*tline\clength Then cpos_b=*tline\clength
      elength.l = cpos_b-cpos_a
      If elength>0
        If *tline\text
          string.s = LSet$(" ",elength)+string.s
          CopyMem_ *tline\text+cpos_a,&string.s,elength
        End If
      End If
    End If
  Else
    string_add.s = ""
    !line_use{*tline, lpos_a}
    If *tline
      If *tline\text
        elength = *tline\clength-cpos_a
        If elength>0
          string_add.s = Peeks$(*tline\text+cpos_a,*tline\clength-cpos_a)
        End If
      End If
    End If
    string.s = string_add.s + Chr$(10) + string.s

    !line_use{*tline, lpos_b}
    If *tline
      If cpos_b>*tline\clength Then cpos_b=*tline\clength
      elength = cpos_b
      If *tline\text
        string.s = string.s + LSet$(" ",elength)
        CopyMem_ *tline\text,&string.s + FLen(string.s)-elength,elength
      End If
    End If
  End If

End If
xskipcopy:
Function Return string.s
End Function



;///////////////////////////////////////////////////////////////////////////////
;/                                                                             /
;/ Syntax: _tb_DeleteMarker {*tuiTextBox.tuiTextBox,*rp.RastPort}              /
;/                                                                             /
;/ Description:                                                                /
;/ * no description available *                                                /
;/                                                                             /
;/ Inputs:                                                                     /
;/ - *tuiTextBox.tuiTextBox    : ???                                           /
;/ - *rp.RastPort    : ???                                                     /
;/                                                                             /
;///////////////////////////////////////////////////////////////////////////////
Statement _tb_DeleteMarker{*tuiTextBox.tuiTextBox,*rp.RastPort}
lpos_a.l = \marker1_lpos
lpos_b.l = \marker2_lpos
If lpos_a>=0
  For lpos.l = lpos_a+1 To lpos_b-1
    _tb_RemoveLine{*tuiTextBox,lpos_a+1}
  Next

  If lpos_a=lpos_b
    elength.l = \marker2_cpos-\marker1_cpos
    _tb_Delete{*tuiTextBox,lpos_a,\marker1_cpos,elength}
  Else
    !line_use{*tline, lpos_a}
    If *tline
      elength = *tline\clength-\marker1_cpos
      If \marker1_cpos=0
        _tb_RemoveLine{*tuiTextBox,lpos_a} : lpos_a-1
      Else
        _tb_Delete{*tuiTextBox,lpos_a,\marker1_cpos,elength}
      End If
    End If

    !line_use{*tline, lpos_a+1}
    If *tline
      elength = \marker2_cpos
      ;If elength = *tline\clength
      ;  _tb_delete_line{*tuiTextBox,lpos_a+1}
      ;Else
        _tb_Delete{*tuiTextBox,lpos_a+1,0,elength}
      ;End If
    End If
  End If
  _tb_SetCursor{*tuiTextBox,\marker1_lpos,\marker1_cpos,-1,-1,False}
  _tb_SetMarker{*tuiTextBox}
  If *rp Then _ntui_DrawTextBoxIntern{*tuiTextBox,-1,-1,-1,-1,*rp}
  ;If upd Then _tb_UpdateScroller{*tuiTextBox}
End If
End Statement


;///////////////////////////////////////////////////////////////////////////////
;/                                                                             /
;/ Syntax: ntui_SetTextBoxSelection {*tuiTextBox.tuiTextBox,@*rp.RastPort,@b:: /
;/ eginLine.l,@beginCharacter.l,@endLine.l,@endCharacter.l}                    /
;/                                                                             /
;/ Description:                                                                /
;/ Set a range of characters as selected.                                                                            /
;/ Inputs:                                                                     /
;/ - *tuiTextBox.tuiTextBox    : ???                                           /
;/ - *rp.RastPort      : ???                                                     /
;/ - beginLine.l       : ???                                                      /
;/ - beginCharacter.l  : ???                                                 /
;/ - endLine.l         : ???                                                        /
;/ - endCharacter.l    : ???                                                   /
;/                                                                             /
;///////////////////////////////////////////////////////////////////////////////
Statement ntui_SetTextBoxSelection{*tuiTextBox.tuiTextBox,@*rp.RastPort,@beginLine.l,@beginCharacter.l,@endLine.l,@endCharacter.l}
If *rp=-1 Then *rp=#NULL
If beginLine<0
  _tb_SetMarker{*tuiTextBox,0,0,\totalLines-1,$7FFFFFFF,*rp}
Else
  _tb_SetMarker{*tuiTextBox,beginLine,beginCharacter,endLine,endCharacter,*rp}
End If
End Statement


;///////////////////////////////////////////////////////////////////////////////
;/                                                                             /
;/ Syntax: ntui_ClearTextBoxSelection {*tuiTextBox.tuiTextBox,@*rp.RastPort}   /
;/                                                                             /
;/ Description:                                                                /
;/ Remove the textob xselection, if any (the selected text will not be removed, only fact that it is selected)                                                /
;/                                                                             /
;/ Inputs:                                                                     /
;/ - *tuiTextBox.tuiTextBox    : ???                                           /
;/ - *rp.RastPort    : ???                                                     /
;/                                                                             /
;///////////////////////////////////////////////////////////////////////////////
Statement ntui_ClearTextBoxSelection{*tuiTextBox.tuiTextBox,@*rp.RastPort}
If *rp=-1 Then *rp=#NULL
_tb_SetMarker{*tuiTextBox,-1,-1,-1,-1,*rp}
End Statement


;///////////////////////////////////////////////////////////////////////////////
;/                                                                             /
;/ Syntax: ntui_DeleteTextBoxSelection {*tuiTextBox.tuiTextBox,@*rp.RastPort}  /
;/                                                                             /
;/ Description:                                                                /
;/ Delete the text that is currently selected.                                                /
;/                                                                             /
;/ Inputs:                                                                     /
;/ - *tuiTextBox.tuiTextBox    : ???                                           /
;/ - *rp.RastPort    : ???                                                     /
;/                                                                             /
;///////////////////////////////////////////////////////////////////////////////
Statement ntui_DeleteTextBoxSelection{*tuiTextBox.tuiTextBox,@*rp.RastPort}
If *rp=-1 Then *rp=#NULL
_tb_DeleteMarker{*tuiTextBox.tuiTextBox,*rp.RastPort}
End Statement




Function.l ntui_LoadTextBox{*tuiTextBox.tuiTextBox,filename.s,*rp.RastPort}
_tb_FreeText{*tuiTextBox}
fid.l = Open_ (&filename,#MODE_OLDFILE)
If fid
  blength.l = 0
  If Seek_ (fid,0,#OFFSET_END)><-1
    blength   = Seek_ (fid,0,#OFFSET_BEGINNING)
  End If
  If blength>0
    *tuiTextBox\tank = AllocVec_(blength,#MEMF_ANY)
    If *tuiTextBox\tank

      Read_ fid,*tuiTextBox\tank,blength

      cpos.l = 0
      allocLines.l = 32
      totalLines.l = 0
      _tb_AllocLines{*tuiTextBox.tuiTextBox,allocLines}
      While cpos<blength
        !line_use{*tline,totalLines}
        If *tline
          totalLines+1
          *tline\flags   = #TUITBLF_STOLEN
          *tline\text    = *tuiTextBox\tank+cpos

          *tline\tclength = 0
          *tline\tblength = 0
          *tline\ttext    = #NULL
          eol.l = False
          spos.l = cpos
          While eol
            c.b = Peek.b(*tuiTextBox\tank+cpos)
            If cpos>=blength Then eol=True
            If c=10 Then eol=True
            cpos+1
          Wend
          *tline\blength = cpos-spos
          *tline\clength = cpos-spos
        End If
      Wend
    End If
  End If
  Close_ fid
End If
End Function



;///////////////////////////////////////////////////////////////////////////////
;/                                                                             /
;/ Syntax: result.w = _ntui_TextBoxVanillaKey {*tuiTextBox.tuiTextBox,*tuiEv:: /
;/ ent.tuiEvent}                                                               /
;/                                                                             /
;/ Description:                                                                /
;/ * no description available *                                                /
;/                                                                             /
;/ Inputs:                                                                     /
;/ - *tuiTextBox.tuiTextBox    : ???                                           /
;/ - *tuiEvent.tuiEvent    : ???                                               /
;/                                                                             /
;/ Result:                                                                     /
;/ - result.w     : ???                                                        /
;/                                                                             /
;///////////////////////////////////////////////////////////////////////////////
Function.w _ntui_TextBoxVanillaKey{*tuiTextBox.tuiTextBox,*tuiEvent.tuiEvent,*rp.RastPort}
done.w = False
vanillakeyP.l = ntui_GetEventAttr{*tuiEvent,#TUIEVA_STRING}
If vanillakeyP=#NULL Then Function Return False
quali.l       = ntui_GetEventAttr{*tuiEvent,#TUIEVA_QUALIFIER}
Select Peek.b(vanillakeyP)
  Case 13
    If *tuiTextBox\edflags&#TUITBF_WANTRETURN
      ;undo_Start{*tuiTextBox\undo}
      _tb_DeleteMarker{*tuiTextBox,#NULL} ; no redraw
      If (quali&#TUIQUAL_SHIFT) OR \edflags&#TUITBF_RETURNBREAK
        If (quali&#TUIQUAL_SHIFT) Then indent.l=False:Else indent=True
        npos.l = _tb_ReturnBreak{*tuiTextBox,\cursor_lpos,\cursor_cpos,indent}
        _tb_SetCursor{*tuiTextBox,\cursor_lpos+1,npos}
      Else
        a.s = ""
        If \edflags&#TUITBF_AUTOINDENT
          !line_use{*tline, \cursor_lpos}
          If *tline
            If *tline\text
              n.l = 0
              While Peek.b(*tline\text+n)=32 AND n<*tline\clength
                n+1
              Wend
              a.s = LSet$(" ",n)
            End If
          End If
        End If
        _tb_CreateLine{*tuiTextBox,\cursor_lpos+1,&a.s,FLen(a.s)}
        _tb_SetCursor{*tuiTextBox,\cursor_lpos+1,FLen(a.s)}
      End If
      done.w = True
      str_Dup{&*tuiEvent\notifyID,*tuiTextBox\obj\onKeyDown}
      _ntui_DrawTextBoxIntern{*tuiTextBox,-1,-1,-1,-1,*rp}
     ; _tb_UpdateScroller{*tuiTextBox}
   Else ; single line textbox
     \accept=False
     ;*tuiEvent\notify=*tuiTextBox\obj\notify
     str_Dup{&*tuiEvent\notifyID,*tuiTextBox\obj\onKeyDown}
     _ntui_UpdateTextBoxString{*tuiTextBox}
     _ntui_DrawTextBoxIntern{*tuiTextBox,-1,-1,-1,-1,*rp}
   End If

  Case 127   ; Entfernen
    If \marker1_lpos>=0 AND \marker2_lpos>=0
      _tb_DeleteMarker{*tuiTextBox,*rp}
    Else
      ;undo_Start{*tuiTextBox\undo}
      oldlines.l = \totalLines
      _tb_Delete{*tuiTextBox,\cursor_lpos,\cursor_cpos,1}

      If \totalLines><oldlines
        _ntui_DrawTextBoxIntern{*tuiTextBox,-1,-1,-1,-1,*rp}
      Else
        _tb_DrawLine{*tuiTextBox,\cursor_lpos,*rp}
      End If
      ;_tb_UpdateScroller{*tuiTextBox}
    End If
    done.w = True
    ;If (*tuiTextBox\obj\flags&#TUIF_IMMIDIATE) Then *tuiEvent\notify=*tuiTextBox\obj\notify
    str_Dup{&*tuiEvent\notifyID,*tuiTextBox\obj\onKeyDown}

  Case 8     ; Del
    If \marker1_lpos>=0 AND \marker2_lpos>=0
      _tb_DeleteMarker{*tuiTextBox,*rp}
    Else
      ;undo_Start{*tuiTextBox\undo}
      delpos.l = \cursor_cpos-1
      delline.l = \cursor_lpos
      oldlines.l = \totalLines
      _tb_MoveCursor{*tuiTextBox,-1,0,False,#NULL}
      _tb_Delete{*tuiTextBox,delline,delpos,1}
      If \totalLines><oldlines
        If *rp Then _ntui_DrawTextBoxIntern{*tuiTextBox,-1,-1,-1,-1,*rp}
      Else
        If *rp Then _tb_DrawLine{*tuiTextBox,\cursor_lpos,*rp}
      End If
      ;_tb_UpdateDcroller{*tuiTextBox}
    End If
    done.w = True
    ;If (*tuiTextBox\obj\flags&#TUIF_IMMIDIATE) Then *tuiEvent\notify=*tuiTextBox\obj\notify
    str_Dup{&*tuiEvent\notifyID,*tuiTextBox\obj\onKeyDown}
  Default
    key.l = Peek.b(vanillakeyP)
    If (quali&#TUIQUAL_COM)
      Select key
        Case @"d"
          If ((\edflags&#TUITBF_LOOSEMARKER)><0) AND (\marker1_lpos>=0 AND \marker2_lpos>=0)
            _tb_DeleteMarker{*tuiTextBox,*rp}
          Else
            ;ntui_TBDeleteLine{*tuiTextBox}
          End If
          done.w = True
        ;Case @"v" : _tb_DoPaste{*tuiTextBox}
        ;Case @"z" : _tb_DoUndo{*tuiTextBox}
        ;Case @"x" : _tb_DoCut{*tuiTextBox}
        ;Case @"c" : _tb_DoCopy{*tuiTextBox}
      End Select
      key=0
    End If

    If (quali&#TUIQUAL_CTRL)
      Select key
        Case 4 ; D
          If ((\edflags&#TUITBF_LOOSEMARKER)><0)  AND (\marker1_lpos>=0 AND \marker2_lpos>=0)
            _tb_DeleteMarker{*tuiTextBox,*rp}
          Else
            ;ntui_TBDeleteLine{*tuiTextBox}
          End If
          done.w = True
        ;Case 22 : _tb_do_paste{*tuiTextBox} :tuievent\notify=False; V
        ;Case 26 : _tb_do_undo{*tuiTextBox}  :tuievent\notify=False; Z
        ;Case 24 : _tb_do_cut{*tuiTextBox}   :tuievent\notify=False; X
        ;Case 3  : _tb_do_copy{*tuiTextBox}   :tuievent\notify=False; C
      End Select
      key=0
    End If

    If key>31 OR key<0 OR key=9
      If *tuiTextBox\validChar[key&$FF]
        ;undo_Start{*tuiTextBox\undo}
        _tb_DeleteMarker{*tuiTextBox,*rp}
        a.s = Chr$(key)
        _tb_InsertInLine{*tuiTextBox,\cursor_lpos,\cursor_cpos,&a.s,FLen(a.s)}
        If *rp Then _tb_DrawLine{*tuiTextBox,\cursor_lpos,*rp}
        _tb_MoveCursor{*tuiTextBox,1,0,False,*rp}
        ;_tb_update_scroller{*tuiTextBox}
        done.w = True
        str_Dup{&*tuiEvent\notifyID,*tuiTextBox\obj\onKeyDown}
      End If
    End If

End Select
_tb_RedrawDamaged{*tuiTextBox,*rp}
Function Return done
End Function



;///////////////////////////////////////////////////////////////////////////////
;/                                                                             /
;/ Syntax: result.w = _ntui_KeyStrokeTextBox {*tuiTextBox.tuiTextBox,*tuiEve:: /
;/ nt.tuiEvent}                                                                /
;/                                                                             /
;/ Description:                                                                /
;/ * no description available *                                                /
;/                                                                             /
;/ Inputs:                                                                     /
;/ - *tuiTextBox.tuiTextBox    : ???                                           /
;/ - rkey.l    : ???                                                           /
;/ - vkey.l    : ???                                                           /
;/ - string.s    : ???                                                         /
;/ - *tuiEvent.tuiEvent    : ???                                               /
;/                                                                             /
;/ Result:                                                                     /
;/ - result.w     : ???                                                        /
;/                                                                             /
;///////////////////////////////////////////////////////////////////////////////
Function.w _ntui_KeyStrokeTextBox{*tuiTextBox.tuiTextBox,*tuiEvent.tuiEvent,*rp.RastPort}
rkey.l   = ntui_GetEventAttr{*tuiEvent,#TUIEVA_VALUE}
vkeyP.l  = ntui_GetEventAttr{*tuiEvent,#TUIEVA_STRING}
If vkeyP Then vkey.l   = Peek.b(vkeyP) & $FF  : Else vkey=0
quali.l  = ntui_GetEventAttr{*tuiEvent,#TUIEVA_QUALIFIER}

done.w = False
_tb_DoBlink{*tuiTextBox,*rp,Off}
If (quali&#TUIQUAL_SHIFT) Then shift.l=True:Else shift=False
If \accept=False
  If vkey=13
    \accept=1
    _tb_DoBlink{*tuiTextBox,*rp,On}
    Function Return True
  Else
    Function Return False
  End If
End If
Select rkey
  Case 78 ; >
    _tb_MoveCursor{*tuiTextBox,1,0,shift,*rp}  : done=True

  Case 79 ; <
    _tb_MoveCursor{*tuiTextBox,-1,0,shift,*rp} : done=True

  Case 76 ; up
    _tb_MoveCursor{*tuiTextBox,0,-1,shift,*rp} : done=True

  Case 77 ; down
    _tb_MoveCursor{*tuiTextBox,0,1,shift,*rp}  : done=True

  Case 66 ; TAB
    If ((*tuiTextBox\edflags&#TUITBF_READONLY)=0)
      If ((*tuiTextBox\edflags&#TUITBF_WANTTAB))
        If shift=False
          If *tuiTextBox\edflags&#TUITBF_REALTAB
            done=_ntui_TextBoxVanillaKey{*tuiTextBox,*tuiEvent,*rp}
          Else
           ; undo_Start{*tuiTextBox\undo}
            _tb_DeleteMarker{*tuiTextBox,*rp}
            a.s = LSet$(" ",\tabsize)
            _tb_InsertInLine{*tuiTextBox,\cursor_lpos,\cursor_cpos,&a.s,FLen(a.s)}
            If *rp Then _tb_DrawLine{*tuiTextBox,\cursor_lpos,*rp}
            _tb_MoveCursor{*tuiTextBox,\tabsize,0,False,*rp}
  ;          _tb_UpdateScroller{*tuiTextBox}
          End If
          done=True
          str_Dup{&*tuiEvent\notifyID,*tuiTextBox\obj\onKeyDown}
        End If
      End If
    End If

  Default
    If ((*tuiTextBox\edflags&#TUITBF_READONLY)=0)
      done = _ntui_TextBoxVanillaKey{*tuiTextBox,*tuiEvent,*rp}
    Else
      If (quali&(#TUIQUAL_COM|#TUIQUAL_CTRL)) ; ok, copy is supported in ReadOnly!
        ;Select vkey
;          Case @"c" : _tb_do_copy{*tuiTextBox} :tuievent\notify=False
;          Case 3    : _tb_do_copy{*tuiTextBox} :tuievent\notify=False; C
        ;End Select
      End If
    End If
End Select

_tb_DoBlink{*tuiTextBox,*rp,On}

Function Return done
End Function




_ntui_SetTextBoxAttr:
Function.w _ntui_SetTextBoxAttr{*tuiTextBox.tuiTextBox,ti_Tag.l,ti_Data.l}
  !_ASSERT{*tuiTextBox}
  !_ASSERT{*tuiTextBox\obj\classID=#TUICLASS_TEXTBOX}

  done.w = True

  *rp.RastPort = ntui_ObtainRastPort{*tuiTextBox}

  Select ti_Tag
    Case #TUITBA_CURSOR_LINE        : _tb_SetCursor{*tuiTextBox,ti_Data,*tuiTextBox\cursor_cpos,-1,-1,*rp}
    Case #TUITBA_CURSOR_COLUMN      : _tb_SetCursor{*tuiTextBox,*tuiTextBox\cursor_lpos,ti_Data,-1,-1,*rp}
    Case #TUITBA_BEGINSELECT_LINE   : _tb_SetMarker{*tuiTextBox,ti_Data,*tuiTextBox\marker1_cpos,*tuiTextBox\marker2_lpos,*tuiTextBox\marker2_cpos,*rp}
    Case #TUITBA_BEGINSELECT_COLUMN : _tb_SetMarker{*tuiTextBox,*tuiTextBox\marker1_lpos,ti_Data,*tuiTextBox\marker2_lpos,*tuiTextBox\marker2_cpos,*rp}
    Case #TUITBA_ENDSELECT_LINE     : _tb_SetMarker{*tuiTextBox,*tuiTextBox\marker1_lpos,*tuiTextBox\marker1_cpos,ti_Data,*tuiTextBox\marker2_cpos,*rp}
    Case #TUITBA_ENDSELECT_COLUMN   : _tb_SetMarker{*tuiTextBox,*tuiTextBox\marker1_lpos,*tuiTextBox\marker1_cpos,*tuiTextBox\marker2_lpos,ti_Data,*rp}
    Case #TUITBA_TBFLAGS            : *tuiTextBox\edflags = ti_Data
    Case #TUITBA_SETTBFLAGS         : *tuiTextBox\edflags | ti_Data
    Case #TUITBA_CLEARTBFLAGS       : *tuiTextBox\edflags | ti_Data - ti_Data
    Case #TUITBA_BLINKSPEED         : *tuiTextBox\blink_speed = ti_Data
    Case #TUITBA_TABWIDTH           : *tuiTextBox\tab_width = ti_Data
    Case #TUITBA_TOPLINE            : _tb_Scroll{*tuiTextBox,0,ti_Data-*tuiTextBox\topLine,*rp}  : _ntui_PropagateBind{*tuiTextBox,#TUIEV_BIND_VVALUE,*tuiTextBox\topLine,#NULL}
    Case #TUITBA_LEFTOFFSET         : _tb_Scroll{*tuiTextBox,ti_Data-*tuiTextBox\leftOffset,0,*rp} : _ntui_PropagateBind{*tuiTextBox,#TUIEV_BIND_HVALUE,*tuiTextBox\leftOffset,#NULL}
    Case #TUITBA_MAXCOLUMNS         : *tuiTextBox\maxChars = ti_Data
    Case #TUITBA_MAXLINES           : *tuiTextBox\maxLines = ti_Data
    Case #TUITBA_FONT               : *tuiTextBox\font = ti_Data : *tuiTextBox\fontstolen = True
    Case #TUITBA_LINE               : ; todo
    Case #TUITBA_SELECTION          : ; todo
    Case #TUITBA_HIGHLIGHTNING      : *tuiTextBox\tokenizing = ti_Data
    Case #TUIA_STRING               : ntui_SetTextBoxText{*tuiTextBox,ti_Data}

    Case #TUIA_XMLATTR
      *xmlA.tuiXmlAttr = ti_Data
      Select Peek.s(*xmlA\nameP)
        Case "text"
          ntui_SetTextBoxText{*tuiTextBox,*xmlA\stringP}
        Default
          done = False
       End Select

    Default
      done = False
  End Select

  If *rp Then ntui_ReleaseRastPort{*tuiTextBox}

  Function Return done
End Function

_ntui_GetTextBoxAttr:
Function.w _ntui_GetTextBoxAttr{*tuiTextBox.tuiTextBox,ti_Tag.l,*ti_Data.longP}
  !_ASSERT{*tuiTextBox}
  !_ASSERT{*tuiTextBox\obj\classID=#TUICLASS_TEXTBOX}

  done.w = True
  Select ti_Tag
    Case #TUITBA_CURSOR_LINE        : *ti_Data\l = *tuiTextBox\cursor_lpos
    Case #TUITBA_CURSOR_COLUMN      : *ti_Data\l = *tuiTextBox\cursor_cpos
    Case #TUITBA_BEGINSELECT_LINE   : *ti_Data\l = *tuiTextBox\marker1_lpos
    Case #TUITBA_BEGINSELECT_COLUMN : *ti_Data\l = *tuiTextBox\marker1_cpos
    Case #TUITBA_ENDSELECT_LINE     : *ti_Data\l = *tuiTextBox\marker2_lpos
    Case #TUITBA_ENDSELECT_COLUMN   : *ti_Data\l = *tuiTextBox\marker2_cpos
    Case #TUITBA_TBFLAGS            : *ti_Data\l = *tuiTextBox\edflags
    Case #TUITBA_BLINKSPEED         : *ti_Data\l = *tuiTextBox\blink_speed
    Case #TUITBA_ROWHEIGHT          : *ti_Data\l = *tuiTextBox\rowheight
    Case #TUITBA_SPACEWIDTH         : *ti_Data\l = *tuiTextBox\space_width
    Case #TUITBA_TABWIDTH           : *ti_Data\l = *tuiTextBox\tab_width
    Case #TUITBA_VISIBLELINES       : *ti_Data\l = *tuiTextBox\visibleLines
    Case #TUITBA_TOTALLINES         : *ti_Data\l = *tuiTextBox\totalLines
    Case #TUITBA_TOPLINE            : *ti_Data\l = *tuiTextBox\topLine
    Case #TUITBA_VISIBLEWIDTH       : *ti_Data\l = *tuiTextBox\visibleWidth
    Case #TUITBA_TOTALWIDTH         : *ti_Data\l = *tuiTextBox\totalWidth
    Case #TUITBA_LEFTOFFSET         : *ti_Data\l = *tuiTextBox\leftOffset
    Case #TUITBA_MAXCOLUMNS         : *ti_Data\l = *tuiTextBox\maxChars
    Case #TUITBA_MAXLINES           : *ti_Data\l = *tuiTextBox\maxLines
    Case #TUITBA_FONT               : *ti_Data\l = *tuiTextBox\font
    Case #TUITBA_LINE               : !line_use{*tline,*ti_Data\l}
                                      *ti_Data\l = #NULL
                                      If *tline
                                        If *tline\text
                                          *ti_Data\l = *tline\text
                                        End If
                                      End If
    Case #TUITBA_LINELENGTH         : !line_use{*tline,*ti_Data\l}
                                      *ti_Data\l = 0
                                      If *tline
                                        *ti_Data\l = *tline\clength
                                      End If
    Case #TUITBA_SELECTION          : *ti_Data\l = 0 ; todo
    Case #TUITBA_HIGHLIGHTNING      : *ti_Data\l = *tuiTextBox\tokenizing
    Case #TUIA_STRING               : !line_use{*tline,0}
                                      *ti_Data\l = #NULL
                                      If *tline
                                        If *tline\text
                                          *ti_Data\l = *tline\text
                                        End If
                                      End If

    Default
      done = False
  End Select

  Function Return done
End Function



;///////////////////////////////////////////////////////////////////////////////
;/                                                                             /
;/ Syntax: result.w = _ntui_DispatchTextBox {*tuiTextBox.tuiTextBox,*tuiEvent.:: /
;/ tuiEvent}                                                                   /
;/                                                                             /
;/ Description:                                                                /
;/ * do what happens if the user operates the String */                        /
;/                                                                             /
;/ Inputs:                                                                     /
;/ - *tuiTextBox.tuiTextBox    : ???                                           /
;/ - *tuiEvent.tuiEvent    : ???                                               /
;/                                                                             /
;/ Result:                                                                     /
;/ - result.w     : ???                                                        /
;/                                                                             /
;///////////////////////////////////////////////////////////////////////////////
_ntui_DispatchTextBoxEvent:
Function.w _ntui_DispatchTextBoxEvent{*tuiTextBox.tuiTextBox,*tuiEvent.tuiEvent}
  !_ASSERT{*tuiTextBox}
  !_ASSERT{*tuiTextBox\obj\classID=#TUICLASS_TEXTBOX}
  !_ASSERT{*tuiEvent}
  *tui.tuiObject = *tuiTextBox\obj
  done.w = False

  Select *tuiEvent\evID
    Case #TUIEV_MOUSEDOWN:
      If *tuiEvent\value=0
        If ntui_HitObject{*tuiTextBox,*tuiEvent\pos\x,*tuiEvent\pos\y}
          ntui_SetFocus{*tuiTextBox}
          *tuiTextBox\obj\flags | #TUIF_ACTIVE
          str_Dup{&*tuiEvent\notifyID,*tuiTextBox\obj\onTouch}
          ntui_Refresh{*tuiTextBox,#NULL}
          \accept=1

          If *tuiEvent\qualifier_&#TUIQUAL_DOUBLECLICK Then dbl.l = True: Else dbl=False
          ox.l = *tuiEvent\pos\x - *tuiTextBox\obj\cbox\left
          oy.l = *tuiEvent\pos\y - *tuiTextBox\obj\cbox\top

          *rp.RastPort = ntui_ObtainRastPort{*tuiTextBox}

          _tb_DoBlink{*tuiTextBox,*rp,Off}
          _tb_MouseClick{*tuiTextBox,*rp,ox,oy,0,dbl}

          If *tuiTextBox\edflags&#TUITBF_MARKONFOCUS
            If *tuiTextBox\marker1_lpos<0
              If *tuiTextBox\cursor_lpos>=0
                !line_use{*tline, *tuiTextBox\cursor_lpos}
                If *tline
                  If *tuiTextBox\cursor_cpos>=*tline\clength
                    ntui_SetTextBoxSelection{*tuiTextBox.tuiTextBox,*rp}
                  End If
                End If
              End If
            End If
          End If
          _tb_DoBlink{*tuiTextBox,*rp,On}
          If *rp Then ntui_ReleaseRastPort{*tuiTextBox}

          done=True
        End If
      End If

    Case #TUIEV_MOUSEUP
      If (*tuiTextBox\obj\flags | #TUIF_ACTIVE)
        str_Dup{&*tuiEvent\notifyID,*tuiTextBox\obj\onRelease}
        done.w = True
      End If

    Case #TUIEV_MOUSEMOVE
      If (*tuiTextBox\obj\flags | #TUIF_ACTIVE)
        If tuiMouseButtonMap(0)
          ox.l = *tuiEvent\pos\x - *tuiTextBox\obj\cbox\left
          oy.l = *tuiEvent\pos\y - *tuiTextBox\obj\cbox\top
          *rp.RastPort = ntui_ObtainRastPort{*tuiTextBox}
          _tb_MouseDrag{*tuiTextBox,*rp,ox,oy}
          If *rp Then ntui_ReleaseRastPort{*tuiTextBox}
          done=True
;          str_Dup{&*tuiEvent\notifyID,*tuiTextBox\obj\onMove}
        End If
      End If

    Case #TUIEV_KEYDOWN
      *rp.RastPort = ntui_ObtainRastPort{*tuiTextBox}
      done = _ntui_KeyStrokeTextBox{*tuiTextBox.tuiTextBox,*tuiEvent.tuiEvent,*rp}
      If *rp Then ntui_ReleaseRastPort{*tuiTextBox}

    Case #TUIEV_TICK
      *rp.RastPort = ntui_ObtainRastPort{*tuiTextBox}
      ntui_SetClip{*rp,*tuiTextBox\obj\cbox}
      _tb_DoBlink{*tuiTextBox,*rp,1}
      ntui_RemClip{*rp}
      If *rp Then ntui_ReleaseRastPort{*tuiTextBox}

    Case #TUIEV_HSCROLL
      If *tuiTextBox\maxLines>1
        _ntui_SetTextBoxAttr{*tuiTextBox,#TUITBA_LEFTOFFSET,*tuiTextBox\leftOffset + *tuiTextBox\step_\x * *tuiEvent\value }
        done = True
      End If

    Case #TUIEV_VSCROLL
      If *tuiTextBox\maxLines>1
        _ntui_SetTextBoxAttr{*tuiTextBox,#TUITBA_TOPLINE,*tuiTextBox\topLine + *tuiTextBox\step_\y * *tuiEvent\value }
        done = True
      End If

    Case #TUIEV_BIND_HDELTA
      If *tuiTextBox\maxLines>1
        *rp.RastPort = ntui_ObtainRastPort{*tuiTextBox}
        _tb_Scroll{*tuiTextBox,*tuiTextBox\step_\x * *tuiEvent\value, 0, *rp}
        If *rp Then ntui_ReleaseRastPort{*tuiTextBox}
        done = True
      End If

    Case #TUIEV_BIND_VDELTA
      If *tuiTextBox\maxLines>1
        *rp.RastPort = ntui_ObtainRastPort{*tuiTextBox}
        _tb_Scroll{*tuiTextBox,0,*tuiTextBox\step_\y * *tuiEvent\value,*rp}
        If *rp Then ntui_ReleaseRastPort{*tuiTextBox}
        done = True
      End If

    Case #TUIEV_BIND_HVALUE
      If *tuiTextBox\maxLines>1
        *rp.RastPort = ntui_ObtainRastPort{*tuiTextBox}
        _tb_Scroll{*tuiTextBox,*tuiEvent\value-*tuiTextBox\leftOffset,0,*rp}
        If *rp Then ntui_ReleaseRastPort{*tuiTextBox}
        done = True
      End If

    Case #TUIEV_BIND_VVALUE
      If *tuiTextBox\maxLines>1
        *rp.RastPort = ntui_ObtainRastPort{*tuiTextBox}
        _tb_Scroll{*tuiTextBox,0,*tuiEvent\value-*tuiTextBox\topLine,*rp}
        If *rp Then ntui_ReleaseRastPort{*tuiTextBox}
        done = True
      End If

    Case #TUIEV_PROPAGATE
      If *tuiTextBox\maxLines>1
        _ntui_PropagateBind{*tuiTextBox,#TUIEV_BIND_VTOTAL  ,*tuiTextBox\totalLines   ,#NULL}
        _ntui_PropagateBind{*tuiTextBox,#TUIEV_BIND_VVALUE  ,*tuiTextBox\topLine      ,#NULL}
        _ntui_PropagateBind{*tuiTextBox,#TUIEV_BIND_VVISIBLE,*tuiTextBox\visibleLines ,#NULL}
        _ntui_PropagateBind{*tuiTextBox,#TUIEV_BIND_VSTEP   ,*tuiTextBox\step_\y      ,#NULL}
        _ntui_PropagateBind{*tuiTextBox,#TUIEV_BIND_HTOTAL  ,*tuiTextBox\totalWidth   ,#NULL}
        _ntui_PropagateBind{*tuiTextBox,#TUIEV_BIND_HVALUE  ,*tuiTextBox\leftOffset   ,#NULL}
        _ntui_PropagateBind{*tuiTextBox,#TUIEV_BIND_HVISIBLE,*tuiTextBox\visibleWidth ,#NULL}
        _ntui_PropagateBind{*tuiTextBox,#TUIEV_BIND_HSTEP   ,*tuiTextBox\step_\x      ,#NULL}
        done=True
      End If

    Default
      done = False
End Select

  If done
    If (*tuiEvent\notifyID)
      *tuiEvent\tuiObject = *tuiTextBox
      _ntui_QueueEvent{*tuiTextBox\obj\tuiEngine\outQ,*tuiEvent}
    Else
      _ntui_DestroyEvent{*tuiEvent}
    End If
  End If

Function Return done
End Function


;///////////////////////////////////////////////////////////////////////////////
;/                                                                             /
;/ Syntax: ntui_DrawTextBox {*tuiTextBox.tuiTextBox,*rp.RastPort}              /
;/                                                                             /
;/ Description:                                                                /
;/       ; ....                                                                /
;/ * draw the button in all its beauty */                                      /
;/                                                                             /
;/ Inputs:                                                                     /
;/ - *tuiTextBox.tuiTextBox    : ???                                           /
;/ - *rp.RastPort    : ???                                                     /
;/                                                                             /
;///////////////////////////////////////////////////////////////////////////////
_ntui_DrawTextBox:
Statement _ntui_DrawTextBox{*tuiTextBox.tuiTextBox,*rp.RastPort,*rpclip.tuiRect}
  !_ASSERT{*tuiTextBox}
  !_ASSERT{*tuiTextBox\obj\classID=#TUICLASS_TEXTBOX}

  *tuiEngine.tuiEngine = *tuiTextBox\obj\tuiEngine
  !_ASSERT{*tuiEngine}
  !_ASSERT{*rp}

  !tuiAndRect{bclip.tuiRect,*tuiTextBox\obj\bbox,*rpclip}
  If !tuiValidRect{bclip}=False Then Statement Return

  If (*tuiTextBox\obj\borderType><#TUIBORDER_NONE)
    bgDone.l = ntui_DrawBorder{*tuiEngine,*rp,*tuiTextBox\obj\bbox,*tuiTextBox\obj\borderType,*tuiTextBox\obj\flags}
  Else
    bgDone = False
  End If

    If *tuiTextBox\hScroller
      If (*tuiTextBox\hScroller\obj\flags&#TUIF_ONSCREEN) Then _ntui_Draw{*tuiTextBox\hScroller,*rp,*rpclip}
    End If
    If *tuiTextBox\vScroller
      If (*tuiTextBox\vScroller\obj\flags&#TUIF_ONSCREEN) Then _ntui_Draw{*tuiTextBox\vScroller,*rp,*rpclip}
    End If
    If *tuiTextBox\funcButton
      If (*tuiTextBox\funcButton\obj\flags&#TUIF_ONSCREEN) Then _ntui_Draw{*tuiTextBox\funcButton,*rp,*rpclip}
    End If


  !tuiAndRect{iclip.tuiRect,*rpclip,*tuiTextBox\obj\ibox}
  If !tuiValidRect{iclip}=False Then Statement Return

  If *tuiTextBox\tuiPen[#TUITBPEN_BG]><#TUIPEN_TRANSPARENT AND bgDone=False
    CNIF #TUI_DEBUG
    SetAPen_ *rp,*tuiEngine\pen[#TUIPEN_GREEN]
    CELSE
    SetAPen_ *rp,*tuiEngine\pen[*tuiTextBox\tuiPen[#TUITBPEN_BG]]
    CEND
    !tuiAndRect{iclip.tuiRect,*tuiTextBox\obj\ibox,*rpclip}
    If !tuiValidRect{iclip}
      RectFill_ *rp,iclip\left,iclip\top,iclip\right,iclip\bottom
    End If
  End If


  !tuiAndRect{cclip.tuiRect,*rpclip,*tuiTextBox\obj\cbox}
  If !tuiValidRect{cclip}=False Then Statement Return

  x1.l = cclip\left
  x2.l = cclip\right
  y1.l = cclip\top
  y2.l = cclip\bottom

  Select *tuiTextBox\subTypeID
    Case #TUITB_TEXTBOX
      _tb_DoBlink{*tuiTextBox,*rp,Off}
      SetAPen_ *rp,*tuiEngine\pen[#TUIPEN_SHADOW]
      x.l =  *tuiTextBox\obj\cbox\right+1
      ;Move_ *rp,x,y1 : Draw_ *rp,x,y2
      _ntui_DrawTextBoxIntern{*tuiTextBox,x1,y1,x2,y2,*rp}
      If (*tuiTextBox\obj\flags&#TUIF_FOCUS) Then _tb_DoBlink{*tuiTextBox,*rp,On}

    Default
      _tb_DoBlink{*tuiTextBox,*rp,Off}
      _ntui_DrawTextBoxIntern{*tuiTextBox,x1,y1,x2,y2,*rp}
      If (*tuiTextBox\obj\flags&#TUIF_FOCUS) _tb_DoBlink{*tuiTextBox,*rp,On}

  End Select

End Statement

_ntui_LayoutTextBox:
Statement _ntui_LayoutTextBox{*tuiTextBox.tuiTextBox,*rp.RastPort,*bbox.tuiRect}
  !_ASSERT{*tuiTextBox}
  !_ASSERT{*tuiTextBox\obj\classID=#TUICLASS_TEXTBOX}

  *tui.tuiObject = *tuiTextBox
  *tuiEngine.tuiEngine = *tuiTextBox\obj\tuiEngine
  !_ASSERT{*tuiEngine}
  !_ASSERT{*rp}

  If *bbox
    !tuiCopyRect{*tui\bbox,*bbox}
  End If
  _ntui_GetBorderSize{*tui\tuiEngine,*tui\borderType,*tui\flags,*tui\border}
  !tuiRemBorder{*tui\ibox,*tui\bbox,*tui\border}
  !tuiRemBorder{*tui\cbox,*tui\ibox,*tui\padding}

  If (*tuiTextBox\fontstolen OR *tuiTextBox\font=#NULL)
    *tuiTextBox\fontstolen                = True
    tuifont.l = _ntui_SetFont{*tuiEngine,*tuiTextBox\obj\flags}
    *tuiTextBox\font                      = *tuiEngine\font[tuifont]
    *tuiTextBox\space_width               = *tuiTextBox\font\tf_XSize-1
    *tuiTextBox\rowheight                 = *tuiTextBox\font\tf_YSize
  End If

  If *tuiTextBox\vScroller
    xs.l = *tuiTextBox\vScroller\obj\minsize\x
    !tuiCopyRect{sbox.tuiRect,*tui\ibox}
    *tui\ibox\right - xs
    *tui\cbox\right = *tui\ibox\right
    sbox\left  = *tui\cbox\right +1

    If *tuiTextBox\funcButton
      ys.l = *tuiTextBox\hScroller\obj\minsize\y
      !tuiCopyRect{fbox.tuiRect,sbox}
      sbox\bottom - ys
      fbox\top    = sbox\bottom+1
      _ntui_Layout{*tuiTextBox\funcButton,*rp,fbox}
    End If

    _ntui_Layout{*tuiTextBox\vScroller,*rp,sbox}
  End If

  If *tuiTextBox\hScroller
    ys.l = *tuiTextBox\hScroller\obj\minsize\y
    !tuiCopyRect{sbox.tuiRect,*tui\ibox}
    *tui\ibox\bottom - ys
    *tui\cbox\bottom = *tui\ibox\bottom
    sbox\top  = *tui\cbox\bottom +1
    _ntui_Layout{*tuiTextBox\hScroller,*rp,sbox}
  End If

  *tuiTextBox\visibleLines = !tuiRectHeight{*tui\cbox} /*tuiTextBox\rowheight
  *tuiTextBox\visibleWidth = !tuiRectWidth{*tui\cbox}

  If *tuiTextBox\vScroller Then _ntui_UpdateScroller{*tuiTextBox\vScroller,*tuiTextBox\topLine,*tuiTextBox\visibleLines,*tuiTextBox\totalLines,1,False}
  If *tuiTextBox\hScroller Then _ntui_UpdateScroller{*tuiTextBox\hScroller,*tuiTextBox\leftOffset,*tuiTextBox\visibleWidth,*tuiTextBox\totalWidth,1,False}
End Statement


;///////////////////////////////////////////////////////////////////////////////
;/                                                                             /
;/ Syntax: result.l = _ntui_GetTextBoxMinSize {*tuiTextBox.tuiTextBox,*rp.Ra:: /
;/ stPort}                                                                     /
;/                                                                             /
;/ Description:                                                                /
;/ * calculate the minimum size for the button */                              /
;/                                                                             /
;/ Inputs:                                                                     /
;/ - *tuiTextBox.tuiTextBox    : ???                                           /
;/ - *rp.RastPort    : ???                                                     /
;/                                                                             /
;/ Result:                                                                     /
;/ - result.l     : ???                                                        /
;/                                                                             /
;///////////////////////////////////////////////////////////////////////////////
_ntui_CalculateTextBoxMinSize:
Function.l _ntui_CalculateTextBoxMinSize{*tuiTextBox.tuiTextBox,*rp.RastPort}
*tuiEngine.tuiEngine = *tuiTextBox\obj\tuiEngine : If *tuiEngine=#NULL OR *rp=#NULL Then Function Return *tuiTextBox\obj\minsize

If (*tuiTextBox\fontstolen OR *tuiTextBox\font=#NULL)
  tuifont.l                             = _ntui_SetFont{*tuiEngine,*tuiTextBox\obj\flags}
  *tuiTextBox\font                      = *tuiEngine\font[tuifont]
  *tuiTextBox\fontstolen                = True
  *tuiTextBox\space_width               = *tuiTextBox\font\tf_XSize-1
  *tuiTextBox\rowheight                 = *tuiTextBox\font\tf_YSize
End If

For n.l=0 To #TUITBPEN_MAX -1
  If *tuiTextBox\tuiPen[n]      >=0 Then *tuiTextBox\pen[n]       = *tuiEngine\pen[*tuiTextBox\tuiPen[n]]
  If *tuiTextBox\tuiPenMarker[n]>=0 Then *tuiTextBox\penMarker[n] = *tuiEngine\pen[*tuiTextBox\tuiPenMarker[n]]
Next

If *tuiTextBox\vScroller Then _ntui_CalculateScrollerMinSize{*tuiTextBox\vScroller,*rp}
If *tuiTextBox\hScroller Then _ntui_CalculateScrollerMinSize{*tuiTextBox\hScroller,*rp}

Select *tuiTextBox\subTypeID
  Case #TUITB_TEXTBOX
    minlines.l = *tuiTextBox\maxLines
    If minlines>3 Then minlines=3
    *tuiTextBox\obj\minsize\x  + *tuiTextBox\rowheight * minlines
    *tuiTextBox\obj\minsize\y  + *tuiTextBox\rowheight * minlines

  Default
    If *tuiTextBox\obj\flags&(#TUIF_SMALL|#TUIF_SERIF|#TUIF_FIX)
      *tuiTextBox\obj\minsize\y  = *tuiEngine\defSize[#TUISIZE_BUTTON]
    Else
      *tuiTextBox\obj\minsize\y  = *tuiEngine\defSize[#TUISIZE_BUTTON]
    End If
    *tuiTextBox\obj\minsize\x  = *tuiTextBox\obj\minsize\y ; *tuiTextBox\font\tf_YSize

End Select

*tuiTextBox\obj\minsize\x + !tuiBorderWidth{*tuiTextBox\obj\padding}
*tuiTextBox\obj\minsize\y + !tuiBorderHeight{*tuiTextBox\obj\padding}

_ntui_GetBorderSize{*tuiEngine,*tuiTextBox\obj\borderType,*tuiTextBox\obj\flags,*tuiTextBox\obj\border}
*tuiTextBox\obj\minsize\x + !tuiBorderWidth{*tuiTextBox\obj\border}
*tuiTextBox\obj\minsize\y + !tuiBorderHeight{*tuiTextBox\obj\border}


Function Return *tuiTextBox\obj\minsize
End Function



;///////////////////////////////////////////////////////////////////////////////
;/                                                                             /
;/ Syntax: _ntui_FreeTextBox {*tuiTextBox.tuiTextBox}                          /
;/                                                                             /
;/ Description:                                                                /
;/    ted_tokenize {*ted ,lpos}                                                /
;/ undo_Flush{\undo}                                                           /
;/ undo_SetSaved{\undo}                                                        /
;/ * free everything special that we allocated for the button */               /
;/                                                                             /
;/ Inputs:                                                                     /
;/ - *tuiTextBox.tuiTextBox    : ???                                           /
;/                                                                             /
;///////////////////////////////////////////////////////////////////////////////
_ntui_DeinitTextBox:
Statement _ntui_DeinitTextBox{*tuiTextBox.tuiTextBox}
  !_ASSERT{*tuiTextBox}
  !_ASSERT{*tuiTextBox\obj\classID=#TUICLASS_TEXTBOX}

  If \doc
    _tb_FreeText{*tuiTextBox}
    FreeVec_ \doc
    \allocLines=0
    \doc = #NULL
  End If
  ;If \undo Then undo_Free{\undo} : \undo = #NULL
  If \fontstolen = False AND \font><#NULL Then CloseFont_ \font
  \font = #NULL

End Statement


Function.l _ntui_PlainTextBox{maxLines.l,@help.s,@flags.l,@nameID.s}
If (flags=#TUI_NOFLAGS) Then flags=0
*tuiTextBox.tuiTextBox = _ntui_CreateObject{#TUICLASS_TEXTBOX,SizeOf.tuiTextBox,help,flags,nameID}
If *tuiTextBox
  *tuiTextBox\maxChars                     = 32767
  *tuiTextBox\maxLines                     = maxLines

  For n.l=0 To #TUITBPEN_MAX -1
    *tuiTextBox\tuiPen[n]       =-1
    *tuiTextBox\pen[n]          = 0
    *tuiTextBox\tuiPenMarker[n] =-1
    *tuiTextBox\penMarker[n]    = 0
  Next

  For n.l=0 To 255 ; accpet all ASCII characters
    *tuiTextBox\validChar[n]=True
  Next

  *tuiTextBox\step_\x = *tuiTextBox\obj\tuiEngine\defStep\x
  *tuiTextBox\step_\y = 1

  *tuiTextBox\tuiPen[#TUITBPEN_Text]       = #TUIPEN_TEXT
  *tuiTextBox\tuiPenMarker[#TUITBPEN_BG]   = #TUIPEN_MARKER
  *tuiTextBox\tuiPenMarker[#TUITBPEN_Text] = #TUIPEN_ACTIVETEXT
  *tuiTextBox\font                         = #NULL
  *tuiTextBox\fontstolen                   = True
  *tuiTextBox\space_width                  = 8
  *tuiTextBox\rowheight                    = 8
  *tuiTextBox\doc                          = #NULL
  *tuiTextBox\edflags                      = #TUITBF_NotePad|#TUITBF_LINECURSOR;|#TUITBF_SHOWCR ;|#TUITBF_BLOCKMARK;
  *tuiTextBox\blink_speed                  = 5
  *tuiTextBox\obj\borderType               = #TUIBORDER_RECESSED
  *tuiTextBox\leftOffset                   = 0
  *tuiTextBox\topLine                      = 0
  *tuiTextBox\obj\pointerID                = #TUIPOINTER_TEXT

  !_GetFuncPointer{*tuiTextBox\obj\Draw            ,_ntui_DrawTextBox,{0,0,0}}
  !_GetFuncPointer{*tuiTextBox\obj\Layout          ,_ntui_LayoutTextBox,{0,0,0}}
  !_GetFuncPointer{*tuiTextBox\obj\DispatchEvent   ,_ntui_DispatchTextBoxEvent,{0,0}}
  !_GetFuncPointer{*tuiTextBox\obj\CalculateMinSize,_ntui_CalculateTextBoxMinSize,{0,0}}
  !_GetFuncPointer{*tuiTextBox\obj\Deinit          ,_ntui_DeinitTextBox,{0}}
  !_GetFuncPointer{*tuiTextBox\obj\SetAttr         ,_ntui_SetTextBoxAttr,{0,0,0}}
  !_GetFuncPointer{*tuiTextBox\obj\GetAttr         ,_ntui_GetTextBoxAttr,{0,0,0}}

End If
Function Return *tuiTextBox
End Function



;///////////////////////////////////////////////////////////////////////////////
;/                                                                             /
;/ Syntax: result.l = ntui_TextBox {string.s,maxLines.l,@notify.l,@help.s,@f:: /
;/ lags.l,@userID.l}                                                           /
;/                                                                             /
;/ Description:                                                                /
;/ * create function for tui String */                                         /
;/                                                                             /
;/ Inputs:                                                                     /
;/ - string.s    : ???                                                         /
;/ - maxLines.l    : ???                                                       /
;/ - notify.l    : ???                                                         /
;/ - help.s    : ???                                                           /
;/ - flags.l    : ???                                                          /
;/ - userID.l    : ???                                                         /
;/                                                                             /
;/ Result:                                                                     /
;/ - result.l     : ???                                                        /
;/                                                                             /
;///////////////////////////////////////////////////////////////////////////////
Function.l ntui_TextBox{initialText.s,maxLines.l,@help.s,@flags.l,@nameID.s}
If (flags=#TUI_NOFLAGS) Then flags=0
flags | #TUIF_CENTER|#TUIF_MIDDLE|#TUIF_WANTMOVER|#TUIF_WANTTAB
*tuiTextBox.tuiTextBox = _ntui_PlainTextBox{maxLines,help,flags,nameID}
If *tuiTextBox
  *tuiTextBox\maxChars                     = 32767
  *tuiTextBox\maxLines                     = maxLines

  *tuiTextBox\subTypeID                    = #TUITB_TEXTBOX
  *tuiTextBox\tuiPen[#TUITBPEN_BG]         = #TUIPEN_WHITE
  *tuiTextBox\tuiPen[#TUITBPEN_Focus]      = #TUIPEN_WHITE
  *tuiTextBox\tuiPen[#TUITBPEN_NoFocus]    = #TUIPEN_WHITE
  *tuiTextBox\tuiPen[#TUITBPEN_DisabledBG] = #TUIPEN_HALFSHINE
  *tuiTextBox\tuiPen[#TUITBPEN_EnabledFG]  = #TUIPEN_TEXT
  *tuiTextBox\tuiPen[#TUITBPEN_DisabledFG] = #TUIPEN_HALFSHADOW
  *tuiTextBox\tuiPen[#TUITBPEN_Text]       = #TUIPEN_TEXT
  *tuiTextBox\tuiPenMarker[#TUITBPEN_BG]   = #TUIPEN_MARKER
  *tuiTextBox\tuiPenMarker[#TUITBPEN_Text] = #TUIPEN_ACTIVETEXT
  *tuiTextBox\font                         = #NULL
  *tuiTextBox\fontstolen                   = True
  *tuiTextBox\space_width                  = 8
  *tuiTextBox\rowheight                    = 8
  *tuiTextBox\doc                          = #NULL
  *tuiTextBox\edflags                      = #TUITBF_NotePad|#TUITBF_LINECURSOR;|#TUITBF_SHOWCR ;|#TUITBF_BLOCKMARK;
  *tuiTextBox\blink_speed                  = 5
  *tuiTextBox\obj\borderType               = #TUIBORDER_RECESSED
  *tuiTextBox\leftOffset                   = 0
  *tuiTextBox\topLine                      = 0

  ntui_SetTextBoxText{*tuiTextBox,&initialText,FLen(initialText),#NULL}

  ;tuiGlobal\buildHelper\tuiParent          = *tuiTextBox
  ;tuiGlobal\buildHelper\tuiPrev            = #NULL
  *tuiTextBox\obj\pointerID = #TUIPOINTER_TEXT

  _ntui_BeginChildren{*tuiTextBox}

  *tuiTextBox\vScroller    = ntui_VScroller{*tuiTextBox\topLine,*tuiTextBox\totalLines,*tuiTextBox\visibleLines}
  If *tuiTextBox\vScroller
    ntui_AddArrows{#NULL}
    *tuiTextBox\vScroller\obj\borderType=#TUIBORDER_NONE
  End If

  *tuiTextBox\hScroller    = ntui_HScroller{*tuiTextBox\leftOffset,*tuiTextBox\totalWidth,*tuiTextBox\visibleWidth}
  If *tuiTextBox\hScroller
    ntui_AddArrows{#NULL}
    *tuiTextBox\hScroller\obj\borderType=#TUIBORDER_NONE
  End If

  ;*tuiTextBox\funcButton   = ntui_FunctionButton{#TUIBUTTON_SAVE}

  _ntui_EndChildren{#TUICLASS_TEXTBOX}

  If (*tuiTextBox\vScroller) Then ntui_Bind{*tuiTextBox,*tuiTextBox\vScroller}
  If (*tuiTextBox\hScroller) Then ntui_Bind{*tuiTextBox,*tuiTextBox\hScroller}

End If
Function Return *tuiTextBox
End Function



;///////////////////////////////////////////////////////////////////////////////
;/                                                                             /
;/ Syntax: result.l = ntui_String {string.s,maxChars.l,@notify.l,@help.s,@fl:: /
;/ ags.l,@userID.l}                                                            /
;/                                                                             /
;/ Description:                                                                /
;/ * create function for tui String */                                         /
;/                                                                             /
;/ Inputs:                                                                     /
;/ - string.s    : ???                                                         /
;/ - maxChars.l    : ???                                                       /
;/ - notify.l    : ???                                                         /
;/ - help.s    : ???                                                           /
;/ - flags.l    : ???                                                          /
;/ - userID.l    : ???                                                         /
;/                                                                             /
;/ Result:                                                                     /
;/ - result.l     : ???                                                        /
;/                                                                             /
;///////////////////////////////////////////////////////////////////////////////
Function.l ntui_String{string.s,maxChars.l,@help.s,@flags.l,@nameID.s}
If (flags=#TUI_NOFLAGS) Then flags=0
flags | #TUIF_CENTER|#TUIF_MIDDLE |#TUIF_FIXHEIGHT|#TUIF_WANTMOVER
*tuiTextBox.tuiTextBox = _ntui_PlainTextBox{1,help,flags,nameID}
If *tuiTextBox
  *tuiTextBox\maxChars                     = maxChars
  *tuiTextBox\maxLines                     = 1
  *tuiTextBox\subTypeID                    = #TUITB_STRING
  *tuiTextBox\leftOffset                   = 0
  *tuiTextBox\topLine                      = 0
  *tuiTextBox\tuiPen[#TUITBPEN_Focus]      = #TUIPEN_HALFSHINE
  *tuiTextBox\tuiPen[#TUITBPEN_NoFocus]    = #TUIPEN_BACKGROUND
  *tuiTextBox\tuiPen[#TUITBPEN_Text]       = #TUIPEN_TEXT
  *tuiTextBox\tuiPenMarker[#TUITBPEN_BG]   = #TUIPEN_MARKER
  *tuiTextBox\tuiPenMarker[#TUITBPEN_Text] = #TUIPEN_ACTIVETEXT
  *tuiTextBox\tuiPen[#TUITBPEN_DisabledBG] = #TUIPEN_BACKGROUND
  *tuiTextBox\tuiPen[#TUITBPEN_EnabledFG]  = #TUIPEN_TEXT
  *tuiTextBox\tuiPen[#TUITBPEN_DisabledFG] = #TUIPEN_SHADOW
  *tuiTextBox\obj\pointerID                = #TUIPOINTER_TEXT
  *tuiTextBox\font                         = #NULL
  *tuiTextBox\fontstolen                   = True
  *tuiTextBox\space_width                  = 8
  *tuiTextBox\rowheight                    = 8
  *tuiTextBox\doc                          = #NULL
  *tuiTextBox\edflags                      = #TUITBF_String|#TUITBF_LINECURSOR
  *tuiTextBox\blink_speed                  = 5
  *tuiTextBox\obj\borderType               = #TUIBORDER_STRING

  ntui_SetTextBoxText{*tuiTextBox,&string,FLen(string),#NULL}

End If
Function Return *tuiTextBox
End Function

;///////////////////////////////////////////////////////////////////////////////
;/                                                                             /
;/ Syntax: result.l = ntui_EditLabel {string.s,maxChars.l,bgTuiPen.l,@notify:: /
;/ .l,@help.s,@flags.l,@userID.l}                                              /
;/                                                                             /
;/ Description:                                                                /
;/ * no description available *                                                /
;/                                                                             /
;/ Inputs:                                                                     /
;/ - string.s    : ???                                                         /
;/ - maxChars.l    : ???                                                       /
;/ - bgTuiPen.l    : ???                                                       /
;/ - notify.l    : ???                                                         /
;/ - help.s    : ???                                                           /
;/ - flags.l    : ???                                                          /
;/ - userID.l    : ???                                                         /
;/                                                                             /
;/ Result:                                                                     /
;/ - result.l     : ???                                                        /
;/                                                                             /
;///////////////////////////////////////////////////////////////////////////////
Function.l ntui_EditLabel{string.s,maxChars.l,bgTuiPen.l,@help.s,@flags.l,@nameID.s}
*tuiTextBox.tuiTextBox = ntui_String{string,maxChars,help,flags,nameID}
If *tuiTextBox
  *tuiTextBox\tuiPen[#TUITBPEN_Focus]      = bgTuiPen
  *tuiTextBox\tuiPen[#TUITBPEN_NoFocus]    = bgTuiPen
  *tuiTextBox\tuiPen[#TUITBPEN_DisabledBG] = bgTuiPen
  *tuiTextBox\obj\borderType               = #TUIBORDER_NONE
End If
Function Return *tuiTextBox
End Function



;///////////////////////////////////////////////////////////////////////////////
;/                                                                             /
;/ Syntax: result.l = ntui_NumString {value.l,minValue.l,maxValue.l,@notify.:: /
;/ l,@help.s,@flags.l,@userID.l}                                               /
;/                                                                             /
;/ Description:                                                                /
;/ * create function for tui String */                                         /
;/                                                                             /
;/ Inputs:                                                                     /
;/ - value.l    : ???                                                          /
;/ - minValue.l    : ???                                                       /
;/ - maxValue.l    : ???                                                       /
;/ - notify.l    : ???                                                         /
;/ - help.s    : ???                                                           /
;/ - flags.l    : ???                                                          /
;/ - userID.l    : ???                                                         /
;/                                                                             /
;/ Result:                                                                     /
;/ - result.l     : ???                                                        /
;/                                                                             /
;///////////////////////////////////////////////////////////////////////////////
Function.l ntui_NumString{value.l,minValue.l,maxValue.l,@help.s,@flags.l,@nameID.s}
If minValue>maxValue Then Exchange minValue,maxValue
If value<minValue Then value=minValue
If value>maxValue Then value=maxValue
string.s =  Str$(value)
*tuiTextBox.tuiTextBox = ntui_String{string,16,help,flags,nameID}
If *tuiTextBox
  *tuiTextBox\subTypeID                 = #TUITB_STRING_NUMERIC
  *tuiTextBox\numMin                    = minValue
  *tuiTextBox\numMax                    = maxValue
  *tuiTextBox\obj\value                 = value
  ntui_SetValidTextBoxChars{*tuiTextBox,"0123456789+-"}
End If
Function Return *tuiTextBox
End Function


;///////////////////////////////////////////////////////////////////////////////
;/                                                                             /
;/ Syntax: result.l = ntui_NumEditLabel {value.l,minValue.l,maxValue.l,bgTui:: /
;/ Pen.l,@notify.l,@help.s,@flags.l,@userID.l}                                 /
;/                                                                             /
;/ Description:                                                                /
;/ * no description available *                                                /
;/                                                                             /
;/ Inputs:                                                                     /
;/ - value.l    : ???                                                          /
;/ - minValue.l    : ???                                                       /
;/ - maxValue.l    : ???                                                       /
;/ - bgTuiPen.l    : ???                                                       /
;/ - notify.l    : ???                                                         /
;/ - help.s    : ???                                                           /
;/ - flags.l    : ???                                                          /
;/ - userID.l    : ???                                                         /
;/                                                                             /
;/ Result:                                                                     /
;/ - result.l     : ???                                                        /
;/                                                                             /
;///////////////////////////////////////////////////////////////////////////////
Function.l ntui_NumEditLabel{value.l,minValue.l,maxValue.l,bgTuiPen.l,@help.s,@flags.l,@nameID.s}
*tuiTextBox.tuiTextBox = ntui_NumString{value,minValue,maxValue,help,flags,nameID}
If *tuiTextBox
  *tuiTextBox\obj\borderType               = #TUIBORDER_NONE
  *tuiTextBox\tuiPen[#TUITBPEN_Focus]      = bgTuiPen
  *tuiTextBox\tuiPen[#TUITBPEN_NoFocus]    = bgTuiPen
  *tuiTextBox\tuiPen[#TUITBPEN_DisabledBG] = bgTuiPen
End If
Function Return *tuiTextBox
End Function




