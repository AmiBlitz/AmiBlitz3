; XTRA
; Embedded .xtra Header
; 
; General Info
; -------------------------------------------------------
; ExePath         = "RAM:"
; ExeFile         = "Prog.exe"
; CreateIcon      = 0
; Residents       = "all.res"
; 
; Compiler
; -------------------------------------------------------
; StringBuffer    = 8192
; MakeSmallest    = 1
; Version         = 0.0.0
; NumberOfBuilds  = 4
; 
; Debugger
; -------------------------------------------------------
; CliArgs         = ""
; StackSize       = 32768
; RuntimeDebug    = 0
; DebugInfo       = 0
; CreateDbgFile   = 0
; OverflowCheck   = 0
; AssemblerCheck  = 1
; InterruptCheck  = 1
; AutoRun         = 1
; 
; Editor
; -------------------------------------------------------
; CursorLine      = 95
; CursorColumn    = 87
; LabelSearch     = ""
; LabelRemark     = 0
; LabelAll        = 0
; LabelPosition   = 0
; 
; Blitz Objects
; -------------------------------------------------------
; Max IconInfo    = 1
; Max NChunky     = 50
; Max MUIObject   = 50
; Max PTModule    = 5
; Max AsyncReq    = 4
; Max Req-Lib     = 5
; Max GTMenuList  = 5
; Max Console     = 5
; Max TCPSock     = 5
; Max XBSound     = 10
; Max Chunky      = 20
; Max File        = 5
; Max GadgetList  = 5
; Max Queue       = 10
; Max Screen      = 5
; Max Shape       = 100
; Max CopList     = 10
; Max Sprite      = 20
; Max Stencil     = 5
; Max Module      = 5
; Max Window      = 20
; Max Anim        = 10
; Max Sound       = 10
; Max Bank        = 5
; Max Buffer      = 10
; Max BitMap      = 10
; Max Slice       = 10
; Max Page        = 4
; Max Tape        = 5
; Max IntuiFont   = 5
; Max MedModule   = 8
; Max Palette     = 4
; Max MenuList    = 5
; Max BlitzFont   = 4
; Max GTList      = 20
; /XTRA
;///////////////////////////////////////////////////////////////////////////////
;/                                                                             /
;/ Name: ntui_button.include                                                   /
;/                                                                             /
;/ Platforms: Classic, WinUAE, Amithlon, MorphOS, AmigaOS4                     /
;/                                                                             /
;/ Date: 06/01/2009                                                            /
;/                                                                             /
;/ Author: <unknown>                                                           /
;/                                                                             /
;/ Requirements:  Amiblitz3                                                    /
;/                                                                             /
;/ Purpose:                                                                    /
;/ Extend AB3 functionality.                                                   /
;/ * no description available *                                                /
;/                                                                             /
;/ Abstract:                                                                   /
;/ * no abstract available *                                                   /
;/                                                                             /
;/ User Constants:                                                             /
;/ #max_objects   = n                                                          /
;/                                                                             /
;///////////////////////////////////////////////////////////////////////////////
#TUI_HAS_BUTTON = 1
CNIF #__include=0
optimize 7
Syntax 2
CEND

#TUIBA_BASE      = #TUIA_USER
#TUIBA_CHECKED   = #TUIA_VALUE
#TUIBA_TEXT      = #TUIA_STRING
#TUIBA_IMAGE     = #TUIBA_BASE + 0
#TUIBA_IMAGE2    = #TUIBA_BASE + 1
#TUIBA_TICKDELAY = #TUIBA_BASE + 2


;/* tuiButton subtypes */
#TUIBUTTON_NORMAL       = 0
#TUIBUTTON_CHECKBOX     = 1
#TUIBUTTON_ARROWUP      = 2
#TUIBUTTON_ARROWDOWN    = 3
#TUIBUTTON_ARROWLEFT    = 4
#TUIBUTTON_ARROWRIGHT   = 5
#TUIBUTTON_PLUSMINUS    = 6
#TUIBUTTON_PATH         = 7
#TUIBUTTON_FILE         = 8
#TUIBUTTON_SAVE         = 9
#TUIBUTTON_SCREEN       = 10
#TUIBUTTON_FONT         = 11
#TUIBUTTON_INC          = 12
#TUIBUTTON_DEC          = 13
#TUIBUTTON_TOOL         = 14
#TUIBUTTON_IMAGE        = 15
#TUIBUTTON_OLARROWUP    = 16
#TUIBUTTON_OLARROWDOWN  = 17
#TUIBUTTON_OLARROWLEFT  = 18
#TUIBUTTON_OLARROWRIGHT = 19
#TUIBUTTON_POPUP        = 20
#TUIBUTTON_DROPDOWN     = 21
#TUIBUTTON_LEFT         = 22
#TUIBUTTON_RIGHT        = 23
#TUIBUTTON_RADIO        = 24
#TUIBUTTON_SPINNER      = 25

NEWTYPE.tuiButton
obj.tuiObject            ; tui object header
state.l
*tbi.tuiTBImage
tickDelay.l
tickCount.l
subClassID.l
*tbi2.tuiTBImage
End NEWTYPE

_ntui_SetButtonAttr:
Function.w _ntui_SetButtonAttr{*tuiButton.tuiButton,ti_Tag.l,ti_Data.l}
  !_ASSERT{*tuiButton}
  !_ASSERT{*tuiButton\obj\classID=#TUICLASS_BUTTON}

  done.w = True
  Select ti_Tag
    Case #TUIBA_IMAGE
      If *tuiButton\tbi Then ntui_FreeTBImage{*tuiButton\tbi}
      *tuiButton\tbi = ntui_CreateTBImage{*tuiButton\obj\tuiEngine,Peek.s(ti_Data),#TUISIZE_BUTTON,#TUISIZE_BUTTON}
    Case #TUIBA_IMAGE2
      If *tuiButton\tbi2 Then ntui_FreeTBImage{*tuiButton\tbi2}
      *tuiButton\tbi2 = ntui_CreateTBImage{*tuiButton\obj\tuiEngine,Peek.s(ti_Data),#TUISIZE_BUTTON,#TUISIZE_BUTTON}
    Case #TUIBA_TICKDELAY  : *tuiButton\tickDelay = ti_Data
    Case #TUIA_XMLATTR
      *xmlA.tuiXmlAttr = ti_Data
      Select Peek.s(*xmlA\nameP)
        Case "image"             : done=ntui_SetAttr{*tuiButton,#TUIBA_IMAGE         ,*xmlA\stringP}
        Case "image2"            : done=ntui_SetAttr{*tuiButton,#TUIBA_IMAGE2        ,*xmlA\stringP}
        Case "text"              : done=ntui_SetAttr{*tuiButton,#TUIBA_TEXT          ,*xmlA\stringP}
        Case "tickdelay"         : done=ntui_SetAttr{*tuiButton,#TUIBA_TICKDELAY     ,*xmlA\value}
        Default
          done = False
       End Select
    Default
      done = False

  End Select
  Function Return done
End Function


_ntui_GetButtonAttr:
Function.w _ntui_GetButtonAttr{*tuiButton.tuiButton,ti_Tag.l,*ti_Data.longP}
  !_ASSERT{*tuiButton}
  !_ASSERT{*tuiButton\obj\classID=#TUICLASS_BUTTON}

  done.w = True
  Select ti_Tag
    Case #TUIBA_IMAGE      : *ti_Data\l = *tuiButton\tbi
    Case #TUIBA_IMAGE2     : *ti_Data\l = *tuiButton\tbi2
    Case #TUIBA_TICKDELAY  : *ti_Data\l = *tuiButton\tickDelay
    Default
      done = False

  End Select
  Function Return done
End Function



;///////////////////////////////////////////////////////////////////////////////
;/                                                                             /
;/ Syntax: ntui_DrawButton {*tuiButton.tuiButton,*rp.RastPort}                 /
;/                                                                             /
;/ Description:                                                                /
;/         ;Case *tuiButton\hotkey                                             /
;/ * draw the button in all its beauty */                                      /
;/                                                                             /
;/ Inputs:                                                                     /
;/ - *tuiButton.tuiButton    : ???                                             /
;/ - *rp.RastPort    : ???                                                     /
;/                                                                             /
;///////////////////////////////////////////////////////////////////////////////
_ntui_DrawButton:
Statement _ntui_DrawButton{*tuiButton.tuiButton,*rp.RastPort}
!_ASSERT{*tuiButton}
!_ASSERT{*tuiButton\obj\classID=#TUICLASS_BUTTON}

*tuiEngine.tuiEngine = *tuiButton\obj\tuiEngine
!_ASSERT{*tuiEngine}
!_ASSERT{*rp}

bgDone.l   = False
bgTuiPen.l = *tuiButton\obj\bgTuiPen ; #TUIPEN_HALFSHINE
fgTuiPen.l = *tuiButton\obj\fgTuiPen ; #TUIPEN_ACTIVETEXT

Select *tuiButton\subClassID
  Case #TUIBUTTON_CHECKBOX
    If bgTuiPen><#TUIPEN_TRANSPARENT
      If *tuiButton\obj\flags&#TUIF_ACTIVE
        bgTuiPen.l = #TUIPEN_HALFSHINE
        fgTuiPen.l = #TUIPEN_ACTIVETEXT
      Else
      ;  bgTuiPen = #TUIPEN_HALFSHINE
      ;  fgTuiPen = #TUIPEN_TEXT
      End If
    End If
  Case #TUIBUTTON_TOOL
    If *tuiButton\obj\flags&#TUIF_ACTIVE
      bgTuiPen = #TUIPEN_HALFSHINE
      fgTuiPen = #TUIPEN_TEXT
    Else
    ;  bgTuiPen = #TUIPEN_TRANSPARENT;BACKGROUND ; ntui_GetBGTuiPen{*tuiButton\obj\parent}
    ;  fgTuiPen = #TUIPEN_TEXT
    End If
  Case #TUIBUTTON_RADIO

  Default
    If bgTuiPen><#TUIPEN_TRANSPARENT
    If *tuiButton\state
      bgTuiPen = #TUIPEN_MARKER
      fgTuiPen = #TUIPEN_ACTIVETEXT
    Else
    ;  bgTuiPen = #TUIPEN_HALFSHINE
    ;  fgTuiPen = #TUIPEN_TEXT
    End If
    End If
End Select


If (*tuiButton\obj\bgImage)
  ntui_DrawTBImage{*tuiButton\obj\bgImage,*rp,&*tuiButton\obj\ibox,&*tuiButton\obj\ibox,*tuiButton\obj\flags,*tuiButton\obj\bgTuiPen}
  bgDone = True
End If


If (*tuiButton\obj\borderType><#TUIBORDER_NONE)
  bgDone.l = ntui_DrawBorder{*tuiEngine,*rp,*tuiButton\obj\bbox,*tuiButton\obj\borderType,*tuiButton\obj\flags}
End If

If bgDone Then bgTuiPen = #TUIPEN_TRANSPARENT
If bgTuiPen><#TUIPEN_TRANSPARENT

CNIF #TUI_DEBUG
SetAPen_ *rp,*tuiEngine\pen[bgTuiPen] + RRnd(1,20)
CELSE
SetAPen_ *rp,*tuiEngine\pen[bgTuiPen]
CEND
EndIf

!tuiCopyRect{txbox.tuiRect,*tuiButton\obj\cbox}
!tuiCopyRect{tobox.tuiRect,*tuiButton\obj\ibox}
txflags.l          = (*tuiButton\obj\flags|#TUIF_BOTTOM|#TUIF_TOP)-(#TUIF_BOTTOM|#TUIF_TOP)

Select *tuiButton\subClassID
Case #TUIBUTTON_SPINNER
  !tuiCopyRect{imbox.tuiRect,*tuiButton\obj\cbox}
  imbox\right  = (imbox\left + imbox\right)/2
  imbox\bottom = (imbox\top + imbox\bottom)/2
  ntui_DrawTBImage{*tuiButton\tbi,*rp,tobox,imbox,txflags,bgTuiPen}

  !tuiCopyRect{imbox.tuiRect,*tuiButton\obj\cbox}
  imbox\left  = (imbox\left + imbox\right)/2
  imbox\top = (imbox\top + imbox\bottom)/2
  ntui_DrawTBImage{*tuiButton\tbi2,*rp,tobox,imbox,txflags,#TUIPEN_TRANSPARENT}
  !tuiCopyRect{imbox.tuiRect,*tuiButton\obj\ibox}
  If ((*tuiButton\obj\flags&#TUIF_DISABLED)=0) Then SetAPen_ *rp,*tuiEngine\pen[#TUIPEN_SHADOW] : Else SetAPen_ *rp,*tuiEngine\pen[#TUIPEN_HALFSHADOW]
  Move_ *rp,imbox\left,imbox\bottom-1
  Draw_ *rp,imbox\right-1,imbox\top

  SetAPen_ *rp,*tuiEngine\pen[#TUIPEN_HALFSHADOW]
  Move_ *rp,imbox\left,imbox\bottom
  Draw_ *rp,imbox\right,imbox\top

  If ((*tuiButton\obj\flags&#TUIF_DISABLED)=0) Then SetAPen_ *rp,*tuiEngine\pen[#TUIPEN_WHITE] : Else SetAPen_ *rp,*tuiEngine\pen[#TUIPEN_HALFSHINE]
  Move_ *rp,imbox\left+1,imbox\bottom
  Draw_ *rp,imbox\right,imbox\top+1
Default
If (*tuiButton\tbi)
  !tuiCopyRect{imbox.tuiRect,*tuiButton\obj\cbox}
  !tuiCopyRect{iobox.tuiRect,*tuiButton\obj\ibox}
  imflags.l          = txflags

  If (*tuiButton\obj\flags&#TUIF_BOTTOM) ; text at bottom of button
    If *tuiButton\obj\string
      tuifont.l    = _ntui_SetFont{*tuiEngine,*tuiButton\obj\flags}
      imbox\bottom = txbox\bottom-*tuiEngine\font[tuifont]\tf_YSize
      txbox\top    = imbox\bottom+1
      iobox\bottom = imbox\bottom
      tobox\top    = txbox\top
      imflags | #TUIF_BOTTOM
      txflags | (#TUIF_TOP|#TUIF_LEFT|#TUIF_RIGHT) - (#TUIF_LEFT|#TUIF_RIGHT)
    End If
  Else                                   ; text left of button
    If *tuiButton\obj\string
      imbox\right = imbox\left+ntui_GetTBImageWidth{*tuiButton\tbi}-1
      iobox\right = imbox\right + *tuiButton\obj\padding\right
      txbox\left  = iobox\right+1
      tobox\left  = txbox\left
    End If
  End If
End If

If (*tuiButton\tbi)
  If *tuiButton\obj\value
    ntui_DrawTBImage{*tuiButton\tbi,*rp,iobox,imbox,imflags,bgTuiPen}
  Else
    If *tuiButton\tbi2
      ntui_DrawTBImage{*tuiButton\tbi2,*rp,iobox,imbox,imflags,bgTuiPen}
    Else
      ntui_DrawTBImage{*tuiButton\tbi,*rp,iobox,imbox,imflags,bgTuiPen}
    End If
  End If
End If
End Select

If *tuiButton\obj\string
  Select *tuiButton\subClassID
    Case #TUIBUTTON_NORMAL
      _ntui_Print{*tuiEngine,*tuiButton\obj\string,tobox,txbox,txflags,*rp,fgTuiPen,bgTuiPen}
    Case #TUIBUTTON_TOOL
      _ntui_Print{*tuiEngine,*tuiButton\obj\string,tobox,txbox,txflags,*rp,fgTuiPen,bgTuiPen}
    Case #TUIBUTTON_RADIO
      _ntui_Print{*tuiEngine,*tuiButton\obj\string,tobox,txbox,txflags,*rp,fgTuiPen,bgTuiPen}
    Case #TUIBUTTON_CHECKBOX
      _ntui_Print{*tuiEngine,*tuiButton\obj\string,tobox,txbox,txflags,*rp,fgTuiPen,bgTuiPen}
    Case #TUIBUTTON_IMAGE
      _ntui_Print{*tuiEngine,*tuiButton\obj\string,tobox,txbox,txflags,*rp,fgTuiPen,bgTuiPen}
  End Select
End If

If (*tuiButton\obj\flags&#TUIF_FOCUS)
  If ((*tuiButton\obj\flags&#TUIF_ACTIVE)=0)
  If *tuiButton\subClassID><#TUIBUTTON_TOOL
    _ntui_DrawDottedBorder{*tuiEngine,*rp,*tuiButton\obj\cbox,*tuiButton\obj\flags}
  End If
  End If
End If

End Statement



;///////////////////////////////////////////////////////////////////////////////
;/                                                                             /
;/ Syntax: result.w = _ntui_DispatchButton {*tuiButton.tuiButton,*tuiEvent.tui:: /
;/ Event}                                                                      /
;/                                                                             /
;/ Description:                                                                /
;/ * tui button newtype */                                                     /
;/ obj.tuiObject            ; tui object header                                /
;/ * do what happens if the user operates the button */                        /
;/                                                                             /
;/ Inputs:                                                                     /
;/ - *tuiButton.tuiButton    : ???                                             /
;/ - *tuiEvent.tuiEvent    : ???                                               /
;/                                                                             /
;/ Result:                                                                     /
;/ - result.w     : ???                                                        /
;/                                                                             /
;///////////////////////////////////////////////////////////////////////////////
_ntui_DispatchButtonEvent:
Function.w _ntui_DispatchButtonEvent{*tuiButton.tuiButton,*tuiEvent.tuiEvent}
  !_ASSERT{*tuiButton}
  !_ASSERT{*tuiButton\obj\classID=#TUICLASS_BUTTON}

  done.l = False
  doFunc.l = False
  Select *tuiEvent\evID
    Case #TUIEV_BIND_VALUE
      If *tuiButton\subClassID=#TUIBUTTON_RADIO
        If *tuiEvent\value
          *tuiButton\obj\value = 0
        Else
          *tuiButton\obj\value = 1
        End If
      Else
        *tuiButton\obj\value = *tuiEvent\value
      End If
      ntui_Refresh{*tuiButton}

    Case #TUIEV_MOUSEDOWN
      If *tuiEvent\value = 0 ; we want button 0
        If ntui_HitObject{*tuiButton,*tuiEvent\pos\x,*tuiEvent\pos\y}
          ntui_SetFocus{*tuiButton\obj\tuiEngine,*tuiButton}
          done      = True
          *tuiButton\obj\flags | #TUIF_ACTIVE
          *tuiButton\tickCount = 0
          If *tuiButton\obj\flags &#TUIF_TOGGLE
            *tuiButton\state = 1-*tuiButton\obj\value
          Else
            *tuiButton\state = 1 ; pressed
          End If
          str_Dup{&*tuiEvent\notifyID,*tuiButton\obj\onTouch}
          ntui_Refresh{*tuiButton}
        End If
      End If

    Case #TUIEV_MOUSEUP
      If *tuiEvent\value = 0 ; we want button 0
        If *tuiButton\obj\flags&#TUIF_ACTIVE
          If ntui_HitObject{*tuiButton,*tuiEvent\pos\x,*tuiEvent\pos\y}
            done    = True
            If *tuiButton\obj\flags &#TUIF_TOGGLE
              If *tuiButton\subClassID=#TUIBUTTON_RADIO
                *tuiButton\obj\value = 1
              Else
                *tuiButton\obj\value = 1-*tuiButton\obj\value
              End If
              *tuiButton\state = *tuiButton\obj\value
              _ntui_PropagateBind{*tuiButton,#TUIEV_BIND_VALUE,*tuiButton\obj\value,#NULL}
            Else
              *tuiButton\state = 0 ; pressed
            End If
            str_Dup{&*tuiEvent\notifyID,*tuiButton\obj\onClick}
          Else
            str_Dup{&*tuiEvent\notifyID,*tuiButton\obj\onRelease}
          End If
          *tuiButton\obj\flags|#TUIF_ACTIVE - #TUIF_ACTIVE
          ntui_Refresh{*tuiButton}
        End If
      End If

    Case #TUIEV_MOUSEMOVE
      If *tuiButton\obj\flags&#TUIF_ACTIVE
        oldstate.l = *tuiButton\state
        If ntui_HitObject{*tuiButton,*tuiEvent\pos\x,*tuiEvent\pos\y}
          *tuiButton\state = 1
          If *tuiButton\obj\flags &#TUIF_TOGGLE
            *tuiButton\state = 1-*tuiButton\obj\value
          Else
            *tuiButton\state = 1 ; pressed
          End If
        Else
          If *tuiButton\obj\flags &#TUIF_TOGGLE
            *tuiButton\state = *tuiButton\obj\value
          Else
            *tuiButton\state = 0 ; pressed
          End If
        End If
        If oldstate><*tuiButton\state
          ntui_Refresh{*tuiButton}
        End If
        done=True
      End If

;    Case #TUIEV_REFRESH
;      If *tuiEvent\tuiObject = *tuiButton
;        If *tuiButton\obj\flags&#TUIF_VISIBLE
;          If *tuiButton\obj\tuiWindow
;            If *tuiButton\obj\tuiWindow\win
;              _ntui_DrawButton{*tuiButton,*tuiButton\obj\tuiWindow\win\RPort}
;            End If
;          End If
;        End If
;        *tuiButton\obj\flags = (*tuiButton\obj\flags|#TUIF_DIRTY)-(#TUIF_DIRTY)
;        done = True
;      End If

    Case #TUIEV_TICK
      If *tuiButton\tickDelay>0
        If *tuiButton\tickCount>*tuiButton\tickDelay
          If *tuiButton\obj\flags&#TUIF_ACTIVE
            done=True
            str_Dup{&*tuiEvent\notifyID,*tuiButton\obj\onRepeat}
          End If
        Else
          *tuiButton\tickCount+1
        End If
        done = True
      End If

    Case #TUIEV_KEYDOWN
      Select ntui_GetEventAttr{*tuiEvent,#TUIEVA_VANILLAKEY}
        Case 13 ; return
          ntui_SetFocus{*tuiButton\obj\tuiEngine,*tuiButton}
          done      = True
          *tuiButton\obj\flags | #TUIF_ACTIVE
          *tuiButton\tickCount = 0
          If *tuiButton\obj\flags &#TUIF_TOGGLE
            *tuiButton\state = 1-*tuiButton\obj\value
          Else
            *tuiButton\state = 1 ; pressed
          End If
          str_Dup{&*tuiEvent\notifyID, *tuiButton\obj\onTouch}
          ntui_Refresh{*tuiButton}
      End Select

    Case #TUIEV_KEYUP
        If *tuiButton\obj\flags&#TUIF_ACTIVE
          done = True
          If *tuiButton\obj\flags &#TUIF_TOGGLE
              If *tuiButton\subClassID=#TUIBUTTON_RADIO
                *tuiButton\obj\value = 1
              Else
                *tuiButton\obj\value = 1-*tuiButton\obj\value
              End If
            *tuiButton\state = *tuiButton\obj\value
            _ntui_PropagateBind{*tuiButton,#TUIEV_BIND_VALUE,*tuiButton\obj\value,#NULL}
          Else
            *tuiButton\state = 0 ; pressed
          End If
          str_Dup{&*tuiEvent\notifyID,*tuiButton\obj\onClick}
          *tuiButton\obj\flags|#TUIF_ACTIVE - #TUIF_ACTIVE
          ntui_Refresh{*tuiButton}
        End If

    Default
      Function Return False
  End Select

  If done
    Select str_Read{&*tuiEvent\notifyID}
      Case !NOTIFYID_ASLPATH   : ntui_SendEvent{*tuiButton,#TUIEV_ASLPATH} : str_Dup{&*tuiEvent\string,*tuiButton\obj\string}
      Case !NOTIFYID_ASLFILE   : ntui_SendEvent{*tuiButton,#TUIEV_ASLFILE} : str_Dup{&*tuiEvent\string,*tuiButton\obj\string}
      Case !NOTIFYID_ASLSAVE   : ntui_SendEvent{*tuiButton,#TUIEV_ASLSAVE} : str_Dup{&*tuiEvent\string,*tuiButton\obj\string}
      Case !NOTIFYID_ASLSCREEN : ntui_SendEvent{*tuiButton,#TUIEV_ASLSCREEN}
        str_Dup{&*tuiEvent\string,*tuiButton\obj\string}
        *tuiEvent\value = *tuiButton\obj\value
      Case !NOTIFYID_ASLFONT   : ntui_SendEvent{*tuiButton,#TUIEV_ASLFONT}
        str_Dup{&*tuiEvent\string,*tuiButton\obj\string}
        *tuiEvent\value = *tuiButton\obj\value
      Case !NOTIFYID_DELTALEFT : _ntui_PropagateBind{*tuiButton,#TUIEV_BIND_HDELTA,-1,#NULL}
      Case !NOTIFYID_DELTARIGHT: _ntui_PropagateBind{*tuiButton,#TUIEV_BIND_HDELTA, 1,#NULL}
      Case !NOTIFYID_DELTAUP   : _ntui_PropagateBind{*tuiButton,#TUIEV_BIND_VDELTA,-1,#NULL}
      Case !NOTIFYID_DELTADOWN : _ntui_PropagateBind{*tuiButton,#TUIEV_BIND_VDELTA, 1,#NULL}
      Case !NOTIFYID_DELTAINC  : _ntui_PropagateBind{*tuiButton,#TUIEV_BIND_DELTA, 1,#NULL}
      Case !NOTIFYID_DELTADEC  : _ntui_PropagateBind{*tuiButton,#TUIEV_BIND_DELTA,-1,#NULL}
    End Select

    If (*tuiEvent\notifyID)
      *tuiEvent\tuiObject = *tuiButton
      _ntui_QueueEvent{*tuiButton\obj\tuiEngine\outQ,*tuiEvent}
    Else
      _ntui_DestroyEvent{*tuiEvent}
    End If
  End If

Function Return done
End Function



;///////////////////////////////////////////////////////////////////////////////
;/                                                                             /
;/ Syntax: _ntui_FreeButton {*tuiButton.tuiButton}                             /
;/                                                                             /
;/ Description:                                                                /
;/  Case #TUIBUTTON_TOOL                                                       /
;/    *tuiButton\obj\minsize\x = iwidth                                        /
;/    *tuiButton\obj\minsize\y = iheight                                       /
;/ * free everything special that we allocated for the button */               /
;/                                                                             /
;/ Inputs:                                                                     /
;/ - *tuiButton.tuiButton    : ???                                             /
;/                                                                             /
;///////////////////////////////////////////////////////////////////////////////
_ntui_DeinitButton:
Statement _ntui_DeinitButton{*tuiButton.tuiButton}
!_ASSERT{*tuiButton}
!_ASSERT{*tuiButton\obj\classID=#TUICLASS_BUTTON}

If *tuiButton\tbi Then ntui_FreeTBImage{*tuiButton\tbi}  : *tuiButton\tbi = #NULL
End Statement



;///////////////////////////////////////////////////////////////////////////////
;/                                                                             /
;/ Syntax: result.l = _ntui_GetButtonMinSize {*tuiButton.tuiButton,*rp.RastP:: /
;/ ort}                                                                        /
;/                                                                             /
;/ Description:                                                                /
;/ * calculate the minimum size for the button */                              /
;/                                                                             /
;/ Inputs:                                                                     /
;/ - *tuiButton.tuiButton    : ???                                             /
;/ - *rp.RastPort    : ???                                                     /
;/                                                                             /
;/ Result:                                                                     /
;/ - result.l     : ???                                                        /
;/                                                                             /
;///////////////////////////////////////////////////////////////////////////////
_ntui_CalculateButtonMinSize:
Function.l _ntui_CalculateButtonMinSize{*tuiButton.tuiButton,*rp.RastPort}
!_ASSERT{*tuiButton}
!_ASSERT{*tuiButton\obj\classID=#TUICLASS_BUTTON}

*tuiEngine.tuiEngine = *tuiButton\obj\tuiEngine : If *tuiEngine=#NULL OR *rp=#NULL Then Function Return *tuiButton\obj\minsize

If *tuiButton\tbi
  iwidth.l  = ntui_GetTBImageWidth {*tuiButton\tbi}
  iheight.l = ntui_GetTBImageHeight{*tuiButton\tbi}
End If

If *tuiButton\subClassID=#TUIBUTTON_SPINNER
  iwidth.l  = ntui_GetTBImageWidth {*tuiButton\tbi} + ntui_GetTBImageWidth {*tuiButton\tbi2}
  iheight.l = ntui_GetTBImageHeight{*tuiButton\tbi} + ntui_GetTBImageHeight {*tuiButton\tbi2}
  If iwidth <*tuiEngine\defSize[#TUISIZE_BUTTON] Then iwidth  = *tuiEngine\defSize[#TUISIZE_BUTTON]
  If iheight<*tuiEngine\defSize[#TUISIZE_BUTTON] Then iheight = *tuiEngine\defSize[#TUISIZE_BUTTON]
  If iwidth<iheight Then iwidth=iheight : Else iheight=iwidth
End If

*tuiButton\obj\minsize\x  = !tuiBorderWidth{*tuiButton\obj\padding}
*tuiButton\obj\minsize\y  = !tuiBorderHeight{*tuiButton\obj\padding}

If *tuiButton\obj\string
  DEFTYPE.tuiRect tx
  _ntui_GetTextRect{*tuiEngine,*tuiButton\obj\string,tx,*tuiButton\obj\flags,*rp}
  *tuiButton\obj\minsize\x + !tuiRectWidth{tx}
  *tuiButton\obj\minsize\y + !tuiRectHeight{tx}
End If

If *tuiButton\tbi
  If (*tuiButton\obj\flags&(#TUIF_BOTTOM|#TUIF_TOP))
    *tuiButton\obj\minsize\y + iheight
    iwidth  + !tuiBorderWidth{*tuiButton\obj\padding}
    If iwidth>*tuiButton\obj\minsize\x Then *tuiButton\obj\minsize\x = iwidth
  Else
    *tuiButton\obj\minsize\x + iwidth
    If *tuiButton\obj\string Then *tuiButton\obj\minsize\x + *tuiButton\obj\padding\right
    iheight + !tuiBorderHeight{*tuiButton\obj\padding}
    If iheight>*tuiButton\obj\minsize\y Then *tuiButton\obj\minsize\y = iheight
  End If
End If

_ntui_GetBorderSize{*tuiEngine,*tuiButton\obj\borderType,*tuiButton\obj\flags,*tuiButton\obj\border}

*tuiButton\obj\minsize\x + !tuiBorderWidth {*tuiButton\obj\border}
*tuiButton\obj\minsize\y + !tuiBorderHeight{*tuiButton\obj\border}
Function Return *tuiButton\obj\minsize
End Function


;///////////////////////////////////////////////////////////////////////////////
;/                                                                             /
;/ Syntax: result.l = ntui_Button {text.s,image.s,@notify.l,@help.s,@flags.l:: /
;/ ,@userID.l}                                                                 /
;/                                                                             /
;/ Description:                                                                /
;/    iy2.l = y1+*tuiButton\tbi\height-1                                       /
;/    If *tuiButton\obj\string Then iy2+ *tuiEngine\defPad\y                   /
;/ * create function for tui button */                                         /
;/                                                                             /
;/ Inputs:                                                                     /
;/ - text.s    : ???                                                           /
;/ - image.s    : ???                                                          /
;/ - notify.l    : ???                                                         /
;/ - help.s    : ???                                                           /
;/ - flags.l    : ???                                                          /
;/ - userID.l    : ???                                                         /
;/                                                                             /
;/ Result:                                                                     /
;/ - result.l     : ???                                                        /
;/                                                                             /
;///////////////////////////////////////////////////////////////////////////////
Function.l ntui_Button{text.s,image.s,@onClick.s,@help.s,@flags.l,@nameID.s}
If (flags=#TUI_NOFLAGS) Then flags=0
flags | #TUIF_CENTER|#TUIF_MIDDLE|#TUIF_FIXHEIGHT|#TUIF_WANTMOVER|#TUIF_WANTTAB
*tuiButton.tuiButton = _ntui_CreateObject{#TUICLASS_BUTTON,SizeOf.tuiButton,help,flags,nameID}
If *tuiButton
  *tuiButton\subClassID      = #TUIBUTTON_NORMAL
;  *tuiButton\obj\bindFlags  = #TUIBIND_ABLE
  *tuiButton\obj\borderType = #TUIBORDER_BUTTON
  *tuiButton\obj\value      = 0
  *tuiButton\obj\bgTuiPen   = #TUIPEN_HALFSHINE
;  *tuiButton\obj\bgImage    = ntui_CreateTBImage{*tuiButton\obj\tuiEngine,"NTUI:button.9.png",#TUISIZE_NATIVE,#TUISIZE_NATIVE}
  *tuiButton\state          = 0
  *tuiButton\tbi            = #NULL
  str_Write{&*tuiButton\obj\string,text}
  str_Write{&*tuiButton\obj\onClick,onClick}
  If (image)
    *tuiButton\tbi          = ntui_CreateTBImage{*tuiButton\obj\tuiEngine,image,#TUISIZE_INLINE,#TUISIZE_INLINE}
  End If
  *tuiButton\tickDelay      = -1

  !_GetFuncPointer{*tuiButton\obj\Draw            ,_ntui_DrawButton,{0,0}}
;  !_GetFuncPointer{*tuiButton\obj\Layout          ,_ntui_LayoutButton,{0,0,0}}
  !_GetFuncPointer{*tuiButton\obj\DispatchEvent   ,_ntui_DispatchButtonEvent,{0,0}}
  !_GetFuncPointer{*tuiButton\obj\CalculateMinSize,_ntui_CalculateButtonMinSize,{0,0}}
  !_GetFuncPointer{*tuiButton\obj\Deinit          ,_ntui_DeinitButton,{0}}
  !_GetFuncPointer{*tuiButton\obj\SetAttr         ,_ntui_SetButtonAttr,{0,0,0}}
  !_GetFuncPointer{*tuiButton\obj\GetAttr         ,_ntui_GetButtonAttr,{0,0,0}}

End If
Function Return *tuiButton
End Function


;///////////////////////////////////////////////////////////////////////////////
;/                                                                             /
;/ Syntax: result.l = ntui_CheckBox {checked.l,@notify.l,@help.s,@flags.l,@u:: /
;/ serID.l}                                                                    /
;/                                                                             /
;/ Description:                                                                /
;/ * create function for tui button */                                         /
;/                                                                             /
;/ Inputs:                                                                     /
;/ - checked.l    : ???                                                        /
;/ - notify.l    : ???                                                         /
;/ - help.s    : ???                                                           /
;/ - flags.l    : ???                                                          /
;/ - userID.l    : ???                                                         /
;/                                                                             /
;/ Result:                                                                     /
;/ - result.l     : ???                                                        /
;/                                                                             /
;///////////////////////////////////////////////////////////////////////////////
Function.l ntui_CheckBox{checked.l,@onClick.s,@help.s,@flags.l,@nameID.s}
If (flags=#TUI_NOFLAGS) Then flags=0
flags | #TUIF_CENTER|#TUIF_MIDDLE |#TUIF_TOGGLE|#TUIF_FIXSIZE|#TUIF_WANTMOVER|#TUIF_WANTTAB
;*tuiButton.tuiButton = _ntui_CreateObject{#TUICLASS_BUTTON,SizeOf.tuiButton,notify,help,flags,userID}
*tuiButton.tuiButton = ntui_Button{"","",onClick,help,flags,nameID}
If *tuiButton
  *tuiButton\subClassID      = #TUIBUTTON_CHECKBOX
  str_Write{&*tuiButton\obj\onClick,onClick}
  *tuiButton\tbi            = ntui_CreateTBImage{*tuiButton\obj\tuiEngine,"CHECKMARK",#TUISIZE_BUTTON,#TUISIZE_BUTTON}
  *tuiButton\tbi2           = ntui_CreateTBImage{*tuiButton\obj\tuiEngine,"UNCHECKMARK",#TUISIZE_BUTTON,#TUISIZE_BUTTON}
;  *tuiButton\obj\bindFlags  = #TUIBIND_ABLE
  *tuiButton\obj\borderType = #TUIBORDER_BUTTON
  *tuiButton\state          = 0
  *tuiButton\obj\value      = checked
  *tuiButton\tickDelay      = -1
End If
Function Return *tuiButton
End Function


;///////////////////////////////////////////////////////////////////////////////
;/                                                                             /
;/ Syntax: result.l = ntui_FunctionButton {funcID.l,@notify.l,@help.s,@flags:: /
;/ .l,@userID.l}                                                               /
;/                                                                             /
;/ Description:                                                                /
;/ * create function for tui button */                                         /
;/                                                                             /
;/ Inputs:                                                                     /
;/ - funcID.l    : ???                                                         /
;/ - notify.l    : ???                                                         /
;/ - help.s    : ???                                                           /
;/ - flags.l    : ???                                                          /
;/ - userID.l    : ???                                                         /
;/                                                                             /
;/ Result:                                                                     /
;/ - result.l     : ???                                                        /
;/                                                                             /
;///////////////////////////////////////////////////////////////////////////////
Function.l ntui_FunctionButton{funcID.l,@help.s,@flags.l,@nameID.s}
If (flags=#TUI_NOFLAGS) Then flags=0
flags | #TUIF_CENTER|#TUIF_MIDDLE|#TUIF_WANTMOVER|#TUIF_FIXSIZE|#TUIF_WANTTAB

*tuiButton.tuiButton = ntui_Button{"","","",help,flags,nameID}

If *tuiButton

  *tuiEngine.tuiEngine = *tuiButton\obj\tuiEngine
  *tuiButton\subClassID = funcID
  *tuiButton\state          = 0
  *tuiButton\tickDelay      = -1
  Select funcID
    Case #TUIBUTTON_ARROWUP     : *tuiButton\tbi = ntui_CreateTBImage{*tuiButton\obj\tuiEngine,"ARROWUP",#TUISIZE_KNOB,#TUISIZE_KNOB}
                                  *tuiButton\tickDelay=3
                                  str_Write{&*tuiButton\obj\onTouch ,!NOTIFYID_DELTAUP}
                                  str_Write{&*tuiButton\obj\onRepeat,!NOTIFYID_DELTAUP}
    Case #TUIBUTTON_ARROWDOWN   : *tuiButton\tbi = ntui_CreateTBImage{*tuiButton\obj\tuiEngine,"ARROWDOWN",#TUISIZE_KNOB,#TUISIZE_KNOB}
                                  *tuiButton\tickDelay=3
                                  str_Write{&*tuiButton\obj\onTouch ,!NOTIFYID_DELTADOWN}
                                  str_Write{&*tuiButton\obj\onRepeat,!NOTIFYID_DELTADOWN}
    Case #TUIBUTTON_ARROWLEFT   : *tuiButton\tbi = ntui_CreateTBImage{*tuiButton\obj\tuiEngine,"ARROWLEFT",#TUISIZE_KNOB,#TUISIZE_KNOB}
                                  *tuiButton\tickDelay=3
                                  str_Write{&*tuiButton\obj\onTouch ,!NOTIFYID_DELTALEFT}
                                  str_Write{&*tuiButton\obj\onRepeat,!NOTIFYID_DELTALEFT}
    Case #TUIBUTTON_ARROWRIGHT  : *tuiButton\tbi = ntui_CreateTBImage{*tuiButton\obj\tuiEngine,"ARROWRIGHT",#TUISIZE_KNOB,#TUISIZE_KNOB}
                                  *tuiButton\tickDelay=3
                                  str_Write{&*tuiButton\obj\onTouch ,!NOTIFYID_DELTARIGHT}
                                  str_Write{&*tuiButton\obj\onRepeat,!NOTIFYID_DELTARIGHT}
    Case #TUIBUTTON_OLARROWUP   : *tuiButton\tbi = ntui_CreateTBImage{*tuiButton\obj\tuiEngine,"OLARROWUP",#TUISIZE_KNOB,#TUISIZE_KNOB}
                                  *tuiButton\tickDelay=3
                                  str_Write{&*tuiButton\obj\onTouch ,!NOTIFYID_DELTAUP}
                                  str_Write{&*tuiButton\obj\onRepeat,!NOTIFYID_DELTAUP}
    Case #TUIBUTTON_OLARROWDOWN : *tuiButton\tbi = ntui_CreateTBImage{*tuiButton\obj\tuiEngine,"OLARROWDOWN",#TUISIZE_KNOB,#TUISIZE_KNOB}
                                  *tuiButton\tickDelay=3
                                  str_Write{&*tuiButton\obj\onTouch ,!NOTIFYID_DELTADOWN}
                                  str_Write{&*tuiButton\obj\onRepeat,!NOTIFYID_DELTADOWN}
    Case #TUIBUTTON_OLARROWLEFT : *tuiButton\tbi = ntui_CreateTBImage{*tuiButton\obj\tuiEngine,"OLARROWLEFT",#TUISIZE_KNOB,#TUISIZE_KNOB}
                                  *tuiButton\tickDelay=3
                                  str_Write{&*tuiButton\obj\onTouch ,!NOTIFYID_DELTALEFT}
                                  str_Write{&*tuiButton\obj\onRepeat,!NOTIFYID_DELTALEFT}
    Case #TUIBUTTON_OLARROWRIGHT: *tuiButton\tbi = ntui_CreateTBImage{*tuiButton\obj\tuiEngine,"OLARROWRIGHT",#TUISIZE_KNOB,#TUISIZE_KNOB}
                                  *tuiButton\tickDelay=3
                                  str_Write{&*tuiButton\obj\onTouch ,!NOTIFYID_DELTARIGHT}
                                  str_Write{&*tuiButton\obj\onRepeat,!NOTIFYID_DELTARIGHT}
    Case #TUIBUTTON_LEFT        : *tuiButton\tbi = ntui_CreateTBImage{*tuiButton\obj\tuiEngine,"LEFT",#TUISIZE_KNOB,#TUISIZE_KNOB}
    Case #TUIBUTTON_RIGHT       : *tuiButton\tbi = ntui_CreateTBImage{*tuiButton\obj\tuiEngine,"RIGHT",#TUISIZE_KNOB,#TUISIZE_KNOB}
    Case #TUIBUTTON_POPUP       : *tuiButton\tbi = ntui_CreateTBImage{*tuiButton\obj\tuiEngine,"POPUP",#TUISIZE_KNOB,#TUISIZE_KNOB}
    Case #TUIBUTTON_DROPDOWN    : *tuiButton\tbi = ntui_CreateTBImage{*tuiButton\obj\tuiEngine,"DROPDOWN",#TUISIZE_KNOB,#TUISIZE_KNOB}
    Case #TUIBUTTON_PATH        : *tuiButton\tbi = ntui_CreateTBImage{*tuiButton\obj\tuiEngine,"PATH",#TUISIZE_BUTTON,#TUISIZE_BUTTON}
                                  str_Write{&*tuiButton\obj\onClick,!NOTIFYID_ASLPATH}
    Case #TUIBUTTON_FILE        : *tuiButton\tbi = ntui_CreateTBImage{*tuiButton\obj\tuiEngine,"FILE",#TUISIZE_BUTTON,#TUISIZE_BUTTON}
                                  str_Write{&*tuiButton\obj\onClick,!NOTIFYID_ASLFILE}
    Case #TUIBUTTON_SAVE        : *tuiButton\tbi = ntui_CreateTBImage{*tuiButton\obj\tuiEngine,"SAVE",#TUISIZE_BUTTON,#TUISIZE_BUTTON}
                                  str_Write{&*tuiButton\obj\onClick,!NOTIFYID_ASLSAVE}
    Case #TUIBUTTON_SCREEN      : *tuiButton\tbi = ntui_CreateTBImage{*tuiButton\obj\tuiEngine,"SCREEN",#TUISIZE_BUTTON,#TUISIZE_BUTTON}
                                  str_Write{&*tuiButton\obj\onClick,!NOTIFYID_ASLSCREEN}
    Case #TUIBUTTON_FONT        : *tuiButton\tbi = ntui_CreateTBImage{*tuiButton\obj\tuiEngine,"FONT",#TUISIZE_BUTTON,#TUISIZE_BUTTON}
                                  str_Write{&*tuiButton\obj\onClick,!NOTIFYID_ASLFONT}
    Case #TUIBUTTON_INC         : *tuiButton\tbi = ntui_CreateTBImage{*tuiButton\obj\tuiEngine,"DEC",#TUISIZE_BUTTON,#TUISIZE_BUTTON}
                                  *tuiButton\tickDelay=3
                                  str_Write{&*tuiButton\obj\onTouch ,!NOTIFYID_DELTAINC}
                                  str_Write{&*tuiButton\obj\onRepeat,!NOTIFYID_DELTAINC}
    Case #TUIBUTTON_DEC         : *tuiButton\tbi = ntui_CreateTBImage{*tuiButton\obj\tuiEngine,"INC",#TUISIZE_BUTTON,#TUISIZE_BUTTON}
                                  *tuiButton\tickDelay=3
                                  str_Write{&*tuiButton\obj\onTouch ,!NOTIFYID_DELTADEC}
                                  str_Write{&*tuiButton\obj\onRepeat,!NOTIFYID_DELTADEC}
    Case #TUIBUTTON_SPINNER     : *tuiButton\tbi = ntui_CreateTBImage{*tuiButton\obj\tuiEngine,"INC",#TUISIZE_HALFBUTTON,#TUISIZE_HALFBUTTON}
                                  *tuiButton\tickDelay=3
                                  str_Write{&*tuiButton\obj\onTouch ,""}
                                  str_Write{&*tuiButton\obj\onRepeat,""}
                                  *tuiButton\tbi2 = ntui_CreateTBImage{*tuiButton\obj\tuiEngine,"DEC",#TUISIZE_HALFBUTTON,#TUISIZE_HALFBUTTON}
    Default
       *tuiButton\subClassID      = #TUIBUTTON_NORMAL
       *tuiButton\tbi = ntui_CreateTBImage{*tuiButton\obj\tuiEngine,"",#TUISIZE_BUTTON,#TUISIZE_BUTTON}
  End Select

  *tuiButton\obj\borderType = #TUIBORDER_BUTTON
End If
Function Return *tuiButton
End Function

;///////////////////////////////////////////////////////////////////////////////
;/                                                                             /
;/ Syntax: result.l = ntui_ImageButton {text.s,image.s,@notify.l,@help.s,@fl:: /
;/ ags.l,@userID.l}                                                            /
;/                                                                             /
;/ Description:                                                                /
;/ * create an image tui button */                                             /
;/                                                                             /
;/ Inputs:                                                                     /
;/ - text.s    : ???                                                           /
;/ - image.s    : ???                                                          /
;/ - notify.l    : ???                                                         /
;/ - help.s    : ???                                                           /
;/ - flags.l    : ???                                                          /
;/ - userID.l    : ???                                                         /
;/                                                                             /
;/ Result:                                                                     /
;/ - result.l     : ???                                                        /
;/                                                                             /
;///////////////////////////////////////////////////////////////////////////////
Function.l ntui_ImageButton{text.s,image.s,@onClick.s,@help.s,@flags.l,@nameID.s}
If (flags=#TUI_NOFLAGS) Then flags=0
flags | #TUIF_CENTER|#TUIF_BOTTOM|#TUIF_WANTMOVER|#TUIF_FIXSIZE|#TUIF_WANTTAB
*tuiButton.tuiButton = ntui_Button{text,"",onClick,help,flags,nameID}

If *tuiButton
  *tuiEngine.tuiEngine       = *tuiButton\obj\tuiEngine
  *tuiButton\subClassID       = #TUIBUTTON_IMAGE
  *tuiButton\tickDelay       = -1
  *tuiButton\state           = 0
  *tuiButton\tbi             = ntui_CreateTBImage{*tuiButton\obj\tuiEngine,image,#TUISIZE_NATIVE,#TUISIZE_NATIVE}
  *tuiButton\obj\borderType  = #TUIBORDER_BUTTON

;  *tuiButton\obj\bgTuiPen    = #TUIPEN_HALFSHINE
End If
Function Return *tuiButton
End Function


;///////////////////////////////////////////////////////////////////////////////
;/                                                                             /
;/ Syntax: result.l = ntui_ToolButton {text.s,image.s,@notify.l,@help.s,@fla:: /
;/ gs.l,@userID.l}                                                             /
;/                                                                             /
;/ Description:                                                                /
;/ * create an image tui button */                                             /
;/                                                                             /
;/ Inputs:                                                                     /
;/ - text.s    : ???                                                           /
;/ - image.s    : ???                                                          /
;/ - notify.l    : ???                                                         /
;/ - help.s    : ???                                                           /
;/ - flags.l    : ???                                                          /
;/ - userID.l    : ???                                                         /
;/                                                                             /
;/ Result:                                                                     /
;/ - result.l     : ???                                                        /
;/                                                                             /
;///////////////////////////////////////////////////////////////////////////////
Function.l ntui_ToolButton{text.s,image.s,@tuiSize.l,@onClick.s,@help.s,@flags.l,@nameID.s}
If (flags=#TUI_NOFLAGS) Then flags=0
flags | #TUIF_CENTER|#TUIF_BOTTOM|#TUIF_WANTMOVER|#TUIF_FIXSIZE|#TUIF_WANTTAB
;*tuiButton.tuiButton = _ntui_CreateObject{#TUICLASS_BUTTON,SizeOf.tuiButton,notify,help,flags,userID}
*tuiButton.tuiButton = ntui_Button{text,"",onClick,help,flags,nameID}
If *tuiButton
  *tuiButton\subClassID        = #TUIBUTTON_TOOL
  If tuiSize=-1 Then tuiSize  = #TUISIZE_NATIVE
  *tuiButton\tbi              = ntui_CreateTBImage{*tuiButton\obj\tuiEngine,image,tuiSize,tuiSize}
  *tuiButton\state            = 0
  *tuiButton\obj\padding\left = 0,0,0,0
  *tuiButton\obj\margin\left  = 0,0,0,0
  *tuiButton\obj\borderType   = #TUIBORDER_TOOL
  *tuiButton\tickDelay        = -1
  *tuiButton\obj\bgTuiPen     = #TUIPEN_TRANSPARENT
  *tuiButton\obj\bgImage      = #NULL
End If
Function Return *tuiButton
End Function

Function.l ntui_CheckButton{text.s,checked.l,@onClick.s,@help.s,@flags.l,@nameID.s}
If (flags=#TUI_NOFLAGS) Then flags=#TUIF_LEFT
flags | #TUIF_WANTMOVER|#TUIF_FIXSIZE|#TUIF_WANTTAB|#TUIF_TOGGLE
*tuiButton.tuiButton = ntui_Button{text,"",onClick,help,flags,nameID}
If *tuiButton
  *tuiButton\subClassID        = #TUIBUTTON_CHECKBOX
  *tuiButton\obj\value        = checked
  *tuiButton\tbi              = ntui_CreateTBImage{*tuiButton\obj\tuiEngine,"CHECKON" ,#TUISIZE_INLINE,#TUISIZE_INLINE}
  *tuiButton\tbi2             = ntui_CreateTBImage{*tuiButton\obj\tuiEngine,"CHECKOFF",#TUISIZE_INLINE,#TUISIZE_INLINE}
  *tuiButton\state            = 0
  *tuiButton\obj\borderType   = #TUIBORDER_NONE
  *tuiButton\tickDelay        = -1
  *tuiButton\obj\bgTuiPen     = #TUIPEN_TRANSPARENT
  *tuiButton\obj\bgImage      = #NULL
End If
Function Return *tuiButton
End Function


Function.l ntui_RadioButton{text.s,checked.l,@onClick.s,@help.s,@flags.l,@nameID.s}
If (flags=#TUI_NOFLAGS) Then flags=#TUIF_LEFT
flags | #TUIF_WANTMOVER|#TUIF_FIXSIZE|#TUIF_WANTTAB|#TUIF_TOGGLE
*tuiButton.tuiButton = ntui_Button{text,"",onClick,help,flags,nameID}
If *tuiButton
  *tuiButton\subClassID       = #TUIBUTTON_RADIO
  *tuiButton\obj\value        = checked
  *tuiButton\tbi              = ntui_CreateTBImage{*tuiButton\obj\tuiEngine,"RADIOON" ,#TUISIZE_INLINE,#TUISIZE_INLINE}
  *tuiButton\tbi2             = ntui_CreateTBImage{*tuiButton\obj\tuiEngine,"RADIOOFF",#TUISIZE_INLINE,#TUISIZE_INLINE}
  *tuiButton\state            = 0
  *tuiButton\obj\borderType   = #TUIBORDER_NONE
  *tuiButton\tickDelay        = -1
  *tuiButton\obj\bgTuiPen     = #TUIPEN_TRANSPARENT
  *tuiButton\obj\bgImage      = #NULL
End If
Function Return *tuiButton
End Function



