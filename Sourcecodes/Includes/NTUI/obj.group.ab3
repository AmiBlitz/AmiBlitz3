; XTRA
; Embedded .xtra Header
; 
; General Info
; -------------------------------------------------------
; ExePath         = "ram:"
; ExeFile         = "Empty"
; CreateIcon      = 0
; Residents       = "all.res"
; 
; Compiler
; -------------------------------------------------------
; StringBuffer    = 100000
; MakeSmallest    = 1
; Version         = 0.0.0
; NumberOfBuilds  = 7
; 
; Debugger
; -------------------------------------------------------
; CliArgs         = ""
; StackSize       = 8191
; RuntimeDebug    = 1
; DebugInfo       = 0
; CreateDbgFile   = 0
; OverflowCheck   = 0
; AssemblerCheck  = 0
; InterruptCheck  = 1
; AutoRun         = 1
; 
; Editor
; -------------------------------------------------------
; CursorLine      = 345
; CursorColumn    = 42
; LabelSearch     = ""
; LabelRemark     = 0
; LabelAll        = 0
; LabelPosition   = 0
; 
; Blitz Objects
; -------------------------------------------------------
; Max IconInfo    = 1
; Max NChunky     = 50
; Max MUIObject   = 50
; Max PTModule    = 5
; Max AsyncReq    = 4
; Max Req-Lib     = 5
; Max GTMenuList  = 100
; Max Console     = 5
; Max TCPSock     = 5
; Max XBSound     = 10
; Max Chunky      = 20
; Max File        = 100
; Max GadgetList  = 100
; Max Queue       = 10
; Max Screen      = 5
; Max Shape       = 200
; Max CopList     = 10
; Max Sprite      = 20
; Max Stencil     = 5
; Max Module      = 5
; Max Window      = 100
; Max Anim        = 10
; Max Sound       = 10
; Max Bank        = 1
; Max Buffer      = 10
; Max BitMap      = 10
; Max Slice       = 10
; Max Page        = 4
; Max Tape        = 5
; Max IntuiFont   = 10
; Max MedModule   = 8
; Max Palette     = 4
; Max MenuList    = 100
; Max BlitzFont   = 1
; Max GTList      = 100
; /XTRA
#TUI_HAS_GROUP = 1
CNIF #__include=0
optimize 7
Syntax 2
CEND


;/* tuiGroup sub types */
#TUIGROUP_HORIZONTAL  = 0
#TUIGROUP_VERTICAL    = 1
#TUIGROUP_MENUBAR     = 2

;/* tuiGroup attributes */
#TUIGA_BASE               = #TUIA_USER
#TUIGA_WRAP               = #TUIGA_BASE  + 1 ; w
#TUIGA_TITLE              = #TUIA_STRING
#TUIGA_IMAGE              = #TUIGA_BASE  + 2
#TUIGA_ORIENT             = #TUIGA_BASE  + 3

NEWTYPE.tuiCalcSize
needed.w
same.w
sizeable.w
pad.w
weight.l
End NEWTYPE


NEWTYPE.tuiGroup
obj.tuiObject            ; tui object header
orientation.l            ; horizontal or vertical, #TUIORIENT_...
wrap.l                   ; wrap x objects per line
subClassID.l             ; subtype #TUIGROUP_...
*tbi.tuiTBImage          ; optional image in title
*tmpIn.tuiCalcSize
tmpInAlloc.l
*tmpOut.tuiCalcSize
tmpOutAlloc.l
End NEWTYPE


_ntui_SetGroupAttr:
Function.w _ntui_SetGroupAttr{*tuiGroup.tuiGroup,ti_Tag.l,ti_Data.l}
  !_ASSERT{*tuiGroup}
  !_ASSERT{*tuiGroup\obj\classID=#TUICLASS_GROUP}

  done.w = True
  Select ti_Tag
    Case #TUIGA_IMAGE
      If *tuiGroup\tbi Then ntui_FreeTBImage{*tuiGroup\tbi}
      *tuiGroup\tbi = ntui_CreateTBImage{*tuiGroup\obj\tuiEngine,Peek.s(ti_Data),#TUISIZE_INLINE,#TUISIZE_INLINE}
    Case #TUIGA_WRAP : *tuiGroup\wrap = ti_Data
    Case #TUIGA_ORIENT
      done=False
      If ti_Data = #TUIORIENT_HORIZONTAL Then *tuiGroup\subClassID = #TUIGROUP_HORIZONTAL:*tuiGroup\orientation = ti_Data
      If ti_Data = #TUIORIENT_VERTICAL   Then *tuiGroup\subClassID = #TUIGROUP_VERTICAL  :*tuiGroup\orientation = ti_Data
    Case #TUIA_XMLATTR
      *xmlA.tuiXmlAttr = ti_Data
      Select Peek.s(*xmlA\nameP)
        Case "image"             : done=ntui_SetAttr{*tuiGroup,#TUIGA_IMAGE         ,*xmlA\stringP}
        Case "bordertype"        : done=ntui_SetAttr{*tuiGroup,#TUIA_BORDERTYPE     ,*xmlA\value} ; TODO: as text
          *tuiGroup\obj\padding\left   = *tuiGroup\obj\tuiEngine\defPadding\left
          *tuiGroup\obj\padding\top    = *tuiGroup\obj\tuiEngine\defPadding\top
          *tuiGroup\obj\padding\right  = *tuiGroup\obj\tuiEngine\defPadding\right
          *tuiGroup\obj\padding\bottom = *tuiGroup\obj\tuiEngine\defPadding\bottom
          *tuiGroup\obj\margin\left    = *tuiGroup\obj\tuiEngine\defMargin\left
          *tuiGroup\obj\margin\top     = *tuiGroup\obj\tuiEngine\defMargin\top
          *tuiGroup\obj\margin\right   = *tuiGroup\obj\tuiEngine\defMargin\right
          *tuiGroup\obj\margin\bottom  = *tuiGroup\obj\tuiEngine\defMargin\bottom

        Case "title"
          done=ntui_SetAttr{*tuiGroup,#TUIGA_TITLE         ,*xmlA\stringP}
          *tuiGroup\obj\borderType   = #TUIBORDER_TITLE
          *tuiGroup\obj\padding\left   = *tuiGroup\obj\tuiEngine\defPadding\left
          *tuiGroup\obj\padding\top    = *tuiGroup\obj\tuiEngine\defPadding\top
          *tuiGroup\obj\padding\right  = *tuiGroup\obj\tuiEngine\defPadding\right
          *tuiGroup\obj\padding\bottom = *tuiGroup\obj\tuiEngine\defPadding\bottom
          *tuiGroup\obj\margin\left    = *tuiGroup\obj\tuiEngine\defMargin\left
          *tuiGroup\obj\margin\top     = *tuiGroup\obj\tuiEngine\defMargin\top
          *tuiGroup\obj\margin\right   = *tuiGroup\obj\tuiEngine\defMargin\right
          *tuiGroup\obj\margin\bottom  = *tuiGroup\obj\tuiEngine\defMargin\bottom

        Case "wrap"              : done=ntui_SetAttr{*tuiGroup,#TUIGA_WRAP          ,*xmlA\value}
        Case "orient"
          done = False
          If *xmlA\stringP
            string.s = Peek.s(*xmlA\stringP)
            If LCase$(string) = "horizontal" Then done=ntui_SetAttr{*tuiGroup,#TUIGA_ORIENT          ,#TUIORIENT_HORIZONTAL}
            If LCase$(string) = "vertical"   Then done=ntui_SetAttr{*tuiGroup,#TUIGA_ORIENT          ,#TUIORIENT_VERTICAL}
          End If
        Default
          done = False
       End Select
    Default
      done = False

  End Select
  Function Return done
End Function


_ntui_GetGroupAttr:
Function.w _ntui_GetGroupAttr{*tuiGroup.tuiGroup,ti_Tag.l,*ti_Data.longP}
  !_ASSERT{*tuiGroup}
  !_ASSERT{*tuiGroup\obj\classID=#TUICLASS_GROUP}

  done.w = True
  Select ti_Tag
    Case #TUIGA_IMAGE      : *ti_Data\l = *tuiGroup\tbi
    Case #TUIGA_WRAP       : *ti_Data\l = *tuiGroup\wrap
    Case #TUIGA_ORIENT     : *ti_Data\l = *tuiGroup\orientation
    Default
      done = False

  End Select
  Function Return done
End Function



;///////////////////////////////////////////////////////////////////////////////
;/                                                                             /
;/ Syntax: ntui_DrawGroup {*tuiGroup.tuiGroup,*rp.RastPort}                    /
;/                                                                             /
;/ Description:                                                                /
;/ private: Draw a tuiGroup object into the given rastport.
;/                                                                             /
;/ Inputs:                                                                     /
;/ - *tuiGroup.tuiGroup : tuiGroup                                              /
;/ - *rp.RastPort       : rastport                                                    /
;/                                                                             /
;///////////////////////////////////////////////////////////////////////////////
_ntui_DrawGroup:
Statement _ntui_DrawGroup{*tuiGroup.tuiGroup,*rp.RastPort,*rpclip.tuiRect}

  !_ASSERT{*tuiGroup}
  !_ASSERT{*tuiGroup\obj\classID=#TUICLASS_GROUP}
  !_ASSERT{*rp}
  *tui.tuiObject = *tuiGroup\obj
  *tuiEngine.tuiEngine = *tui\tuiEngine
  !_ASSERT{*tuiEngine}

  If ((*tui\flags&#TUIF_ONSCREEN)=0) Then Statement Return

  !tuiAndRect{bclip.tuiRect,*tui\bbox,*rpclip}
  If !tuiValidRect{bclip}=False Then Statement Return

  CNIF #TUI_DEBUG
    !tuiAndRect{iclip.tuiRect,*tui\ibox,*rpclip}
    !tuiAndRect{cclip.tuiRect,*tui\cbox,*rpclip}
    SetAPen_ *rp,1
    RectFill_ *rp,bclip\left,bclip\top,bclip\right,bclip\bottom
    SetAPen_ *rp,3
    RectFill_ *rp,iclip\left,iclip\top,iclip\right,iclip\bottom
    SetAPen_ *rp,7
    RectFill_ *rp,cclip\left,cclip\top,cclip\right,cclip\bottom
  CEND

  If (*tui\borderType><#TUIBORDER_NONE)
    If !tuiRectInside{*rpclip,*tui\cbox}=False

      bgDone.l = ntui_DrawBorder{*tuiEngine,*rp,*tui\bbox,*tui\borderType,*tui\flags}

      x1.l = *tui\bbox\left
      x2.l = *tui\bbox\right
      y1.l = *tui\bbox\top
      y2.l = *tui\bbox\bottom

      xt.l = (x1+x2) / 2
      yt2.l = y1 + *tui\border\top-1
      yt2   = y1 + *tuiEngine\font[#TUIFONT_NORMAL]\tf_YSize;+1
      If *tui\string
        ;_ntui_GetTextRect{*tuiEngine,*tui\string,tx.tuiRect,*tui\flags,*rp}
        tw.l = _ntui_GetTextWidth{*tuiEngine,*tui\string,*tui\flags,*rp}
        ;tw.l = !tuiRectWidth{tx}
        xt - tw/2
      End If

      ;bgTuiPen.l = #TUIPEN_HALFSHINE
      If *tuiGroup\tbi
        iw.l = ntui_GetTBImageWidth{*tuiGroup\tbi}
        ih.l = ntui_GetTBImageHeight{*tuiGroup\tbi}
        xt-iw/2
        tbox.tuiRect\left = xt,y1,xt+iw-1,yt2
        ntui_DrawTBImage{*tuiGroup\tbi,*rp,tbox,tbox,*tui\flags,#TUIPEN_TRANSPARENT};bgTuiPen}
        xt+iw
      End If

      If *tui\string
        If (*tui\flags&#TUIF_DISABLED)
          tbox.tuiRect\left = xt  ,y1  ,xt+tw,yt2
          _ntui_Print{*tuiEngine,*tui\string,tbox,tbox,*tui\flags,*rp,#TUIPEN_TEXT,#TUIPEN_TRANSPARENT};bgTuiPen}
        Else
          tbox.tuiRect\left =  xt+1,y1+1,xt+tw+1,yt2
          _ntui_Print{*tuiEngine,*tui\string,tbox,tbox,*tui\flags,*rp,#TUIPEN_SHADOW,#TUIPEN_TRANSPARENT};bgTuiPen}
          tbox.tuiRect\left =  xt,y1  ,xt+tw  ,yt2-1
          _ntui_Print{*tuiEngine,*tui\string,tbox,tbox,*tui\flags,*rp,#TUIPEN_WHITE,#TUIPEN_TRANSPARENT}
        End If
      End If
    End If
  End If

  If *tui\bgTuiPen><#TUIPEN_TRANSPARENT AND bgDone=False
    CNIF #TUI_DEBUG
    SetAPen_ *rp,*tuiEngine\pen[#TUIPEN_GREEN]
    CELSE
    SetAPen_ *rp,*tuiEngine\pen[*tui\bgTuiPen]
    CEND
    !tuiAndRect{iclip.tuiRect,*tui\ibox,*rpclip}
    If !tuiValidRect{iclip}
      RectFill_ *rp,iclip\left,iclip\top,iclip\right,iclip\bottom
    End If
  End If

  If *tui\child=#NULL
    !tuiRemBorder{ebox.tuiRect,*tui\cbox,*tuiEngine\defMargin}
    _ntui_DrawEmptyBox{*tuiEngine,*rp,ebox,#TUIPEN_SHADOW}
  End If

  *child.tuiObject = *tui\child
  While *child
    If !tuiRectOverlap{*child\bbox,*rpclip}
      !tuiAndRect{childclip.tuiRect,*child\bbox,*rpclip}
;    If !tuiValidRect{childclip}
      _ntui_Draw{*child,*rp,childclip}
    End If
    *child = *child\next_
  Wend

End Statement


_ntui_CalculateGroupMinSize:
Function.l _ntui_CalculateGroupMinSize{*tuiGroup.tuiGroup,*rp.RastPort}
  !_ASSERT{*tuiGroup}
  !_ASSERT{*tuiGroup\obj\classID=#TUICLASS_GROUP}

  *tui.tuiObject = *tuiGroup\obj
  !_ASSERT{*rp}

  maxmin.tuiPixel\x = 0,0
  same.tuiPixel\x   = 0,0

  _ntui_GetBorderSize{*tui\tuiEngine,*tui\borderType,*tui\flags,*tui\border}

  *tui\minsize\x    = 0,0
  numChildren.l     = 0
  *child.tuiObject = *tui\child
  While *child
    numChildren+1
    _ntui_CalculateMinSize{*child,*rp}
    If same\x<*child\minsize\x Then same\x=*child\minsize\x
    If same\y<*child\minsize\y Then same\y=*child\minsize\y
    *child=*child\next_
  Wend

  count.l = 0
  *child.tuiObject = *tui\child
  While *child
    count+1
    If (*tui\flags&#TUIF_SAMEWIDTH)  AND ((*child\flags&#TUIF_FIXWIDTH )=0) Then *child\minsize\x = same\x
    If (*tui\flags&#TUIF_SAMEHEIGHT) AND ((*child\flags&#TUIF_FIXHEIGHT)=0) Then *child\minsize\y = same\y

    inline.tuiPixel\x = 0,0
    *test.tuiObject = *tui\child
    testcount.l = 0

    While *test
      testcount+1
      If ((testcount MOD *tuiGroup\wrap)=(count MOD *tuiGroup\wrap))
        tmx.l = *test\minsize\x  + !tuiBorderWidth{*test\margin}
        tmy.l = *test\minsize\y  + !tuiBorderHeight{*test\margin}
        If inline\x<tmx Then inline\x=tmx
        If inline\y<tmy Then inline\y=tmy
      End If
      *test = *test\next_
    Wend


      If *tuiGroup\orientation = #TUIORIENT_HORIZONTAL
        ;If ((*child\flags&#TUIF_FIXWIDTH )=0) Then *child\minsize\x = inline\x ; we want them to have all the same width

      minX.l = inline\x         ;+ *child\margin\left + *child\margin\right + *child\border\west  + *child\border\east
      minY.l = *child\minsize\y + *child\margin\top + *child\margin\bottom

      maxmin\x + minX
      If maxmin\y<minY Then maxmin\y=minY
      If ((count MOD *tuiGroup\wrap)=0 OR count=numChildren)
        If maxmin\x>*tui\minsize\x Then *tui\minsize\x = maxmin\x
        *tui\minsize\y + maxmin\y
        maxmin\x = 0,0
      End If
    Else
      ;If ((*child\flags&#TUIF_FIXHEIGHT )=0) Then *child\minsize\y = inline\y ; we want all objects the same row to have the same height

      minX.l = *child\minsize\x + *child\margin\left + *child\margin\right
      minY.l = inline\y         ;+ *child\margin\top + *child\margin\bottom + *child\border\north + *child\border\south

      If maxmin\x<minX Then maxmin\x=minX
      maxmin\y + minY
      If ((count MOD *tuiGroup\wrap)=0 OR count=numChildren)
        If maxmin\y>*tui\minsize\y Then *tui\minsize\y = maxmin\y
        *tui\minsize\x + maxmin\x
        maxmin\x = 0,0
      End If
    End If
    *child=*child\next_
  Wend

  *tui\minsize\x +  !tuiBorderWidth{*tui\padding}   +!tuiBorderWidth{*tui\border}
  *tui\minsize\y +  !tuiBorderHeight{*tui\padding}  +!tuiBorderHeight{*tui\border}

  If *tui\string
  ;   _ntui_GetTextLengthEx{*tuiEngine,*tui\string,*tui\stringTE,*tui\flags,*rp}
  ;   fw.l = *tui\stringTE\te_Extent\MaxX-*tui\stringTE\te_Extent\MinX+1
  ;   If fw>*tui\minsize\x Then *tui\minsize\x = fw
  ;   *tui\border\north = *tui\stringTE\te_Extent\MaxY-*tui\stringTE\te_Extent\MinY +1+1
  End If
  Function Return *tui\minsize
End Function


_ntui_LayoutGroup:
Statement _ntui_LayoutGroup{*tuiGroup.tuiGroup,*rp.RastPort,*bbox.tuiRect}
  !_ASSERT{*tuiGroup}
  !_ASSERT{*tuiGroup\obj\classID=#TUICLASS_GROUP}
  *tui.tuiObject = *tuiGroup\obj
  *tuiEngine.tuiEngine = *tui\tuiEngine
  !_ASSERT{*tuiEngine}

  If *bbox                                                            ; update bounding box
    *tui\bbox\left = *bbox\left,*bbox\top,*bbox\right,*bbox\bottom
  Else
    ; -we are ok with bounds...
  End If
  !tuiRemBorder{*tui\ibox,*tui\bbox,*tui\border}
  !tuiRemBorder{*tui\cbox,*tui\ibox,*tui\padding}

  *tui\flags    = *tui\flags | (#TUIF_ONSCREEN|#TUIF_DIRTY)
  If *tuiGroup\wrap<1 Then *tuiGroup\wrap=32767

  numChildren.l   = 0
  *test.tuiObject = *tui\child        ; get the number of children
  While *test : *test = *test\next_ : numChildren+1 : Wend

  If numChildren<=0 Then Statement Return

  numOut.l = (numChildren+*tuiGroup\wrap-1) /  *tuiGroup\wrap ; number of wrappings
  numIn.l = *tuiGroup\wrap                 ; number of wrappings
  If numIn>numChildren Then numIn=numChildren

  If numIn>*tuiGroup\tmpInAlloc OR *tuiGroup\tmpIn=#NULL
    allocN.l = Min(16,numIn)
    If *tuiGroup\tmpIn Then FreeVec_ *tuiGroup\tmpIn
    *tuiGroup\tmpIn  = AllocVec_(allocN * SizeOf.tuiCalcSize,#MEMF_ANY)
    *tuiGroup\tmpInAlloc = allocN
  End If

  If numOut>*tuiGroup\tmpOutAlloc OR *tuiGroup\tmpOut=#NULL
    allocN.l = Min(16,numOut)
    If *tuiGroup\tmpOut Then FreeVec_ *tuiGroup\tmpOut
    *tuiGroup\tmpOut = AllocVec_(allocN*SizeOf.tuiCalcSize,#MEMF_ANY)
    *tuiGroup\tmpOutAlloc = allocN
  End If
  *tmpIn.tuiCalcSize  = *tuiGroup\tmpIn
  *tmpOut.tuiCalcSize = *tuiGroup\tmpOut

  For n.l=0 To (numOut*SizeOf.tuiCalcSize)/4-1 : Poke.l *tmpOut+n*4,0 : Next
  For n.l=0 To (numIn *SizeOf.tuiCalcSize)/4-1 : Poke.l *tmpIn +n*4,0 : Next

  *inA.tuiCalcSize  = #NULL
  *outA.tuiCalcSize = #NULL

  *test.tuiObject = *tui\child
  sizeableIn.l  = 0
  sizeableOut.l = 0
  weightIn.l    = 0
  weightOut.l   = 0
  count.l = 0

  While *test
    *inA  = *tmpIn  + (count MOD *tuiGroup\wrap)*SizeOf.tuiCalcSize
    *outA = *tmpOut + (count  /  *tuiGroup\wrap)*SizeOf.tuiCalcSize

    minX.l = *test\minsize\x + *test\margin\left + *test\margin\right
    minY.l = *test\minsize\y + *test\margin\top  + *test\margin\bottom
    *outA\weight + *test\weight

    If *tuiGroup\orientation=#TUIORIENT_HORIZONTAL
      If minX>*inA\needed  Then *inA\needed  = minX
      If minY>*outA\needed Then *outA\needed = minY
      If minX>*outA\same   Then *outA\same   = minX
      If minY>*inA\same    Then *inA\same    = minY
      If (*test\flags&#TUIF_FIXWIDTH) =0 Then If *inA\sizeable =False Then *inA\sizeable =True : sizeableIn+1
      If (*test\flags&#TUIF_FIXHEIGHT)=0 Then If *outA\sizeable=False Then *outA\sizeable=True : sizeableOut+1
    Else
      If minX>*outA\needed Then *outA\needed = minX
      If minY>*inA\needed  Then *inA\needed  = minY
      If minX>*inA\same    Then *inA\same    = minX
      If minY>*outA\same   Then *outA\same   = minY
      If (*test\flags&#TUIF_FIXWIDTH) =0 Then If *outA\sizeable=False Then *outA\sizeable=True : sizeableOut+1
      If (*test\flags&#TUIF_FIXHEIGHT)=0 Then If *inA\sizeable =False Then *inA\sizeable =True : sizeableIn+1
    End If
    *test = *test\next_  : count+1
  Wend

  If sizeableOut<=0 Then sizeableOut=numOut
  If sizeableIn <=0 Then sizeableIn=numIn

  ;If *tui\userID = @"TEST" Then error{"Groupsize: "+Str$(width)+" / "+Str$(*tui\minsize\x)}
  *child.tuiObject = *tui\child
  If *tuiGroup\orientation=#TUIORIENT_HORIZONTAL ; horizontal orientation
    spaceIn.l  = !tuiRectWidth{*tui\cbox} ;width
    minIn.l    = *tui\minsize\x - !tuiBorderWidth{*tui\padding} - !tuiBorderWidth{*tui\border}
    inP.l      = *tui\cbox\left ; x
    spaceOut.l = !tuiRectHeight{*tui\cbox} ;height
    minOut.l   = *tui\minsize\y - !tuiBorderHeight{*tui\padding} - !tuiBorderHeight{*tui\border}
    outX.l     = *tui\cbox\top ; y
  Else
    spaceIn.l  = !tuiRectHeight{*tui\cbox}; height
    minIn.l    = *tui\minsize\y  - !tuiBorderHeight{*tui\padding} - !tuiBorderHeight{*tui\border}
    inP        = *tui\cbox\top ; y
    spaceOut.l = !tuiRectWidth{*tui\cbox} ;width
    minOut.l   = *tui\minsize\x  - !tuiBorderWidth{*tui\padding} - !tuiBorderWidth{*tui\border}
    outX       = *tui\cbox\left ; x
  End If

  ;SetAPen_ *rp,*tuiEngine\pen[#TUIPEN_BLUE]
  ;RectFill_ *rp,*tui\cbox\left,*tui\cbox\top,*tui\cbox\right,*tui\cbox\bottom
  morespaceOut.l = spaceOut - minOut

  nOut.l       = outX       ; start here
  count.l      = 0
  so.l         = sizeableOut
  While *child
    morespaceIn.l = spaceIn- minIn ; space we have left over
    totalspaceIn.l = morespaceIn
    si.l          = sizeableIn
    inX.l         = inP
    c.l           = *tuiGroup\wrap
    addspaceOut.l = 0
    While *child AND c>0
      c-1
      *outA = *tmpOut + (count  /  *tuiGroup\wrap)*SizeOf.tuiCalcSize
      *inA  = *tmpIn  + (count MOD *tuiGroup\wrap)*SizeOf.tuiCalcSize
      count+1

      inS.l = *inA\needed
      If (*inA\sizeable OR sizeableIn=numIn)
        If *outA\weight<=0
          addspaceIn.l = morespaceIn/si
        Else
          addspaceIn.l = totalspaceIn * *child\weight/*outA\weight
          If addspaceIn>morespaceIn Then addspaceIn=morespaceIn
          If addspaceIn<0 Then addspaceIn=0
        End If
        inS + addspaceIn : si-1 : morespaceIn-addspaceIn
      End If

      outS.l = *outA\needed
      If (*outA\sizeable OR sizeableOut=numOut)
        addspaceOut.l = morespaceOut/so : outS + addspaceOut
      End If

      If (*child\flags&#TUIF_FIXWIDTH) AND False
        gap.l = nw.l-*child\minsize\x
        If (*child\flags&#TUIF_RIGHT)
          *child\margin\left = gap : *child\margin\right = 0
        Else
          If (*child\flags&#TUIF_LEFT)
            *child\margin\left = 0 : *child\margin\right = gap
          Else
            *child\margin\left = gap/2 : *child\margin\right = gap-*child\margin\left
          End If
        End If
      Else
        ;*child\gapNW\x = *child\margin\left : *child\gapSE\x = *child\margin\right
      End If

      If (*child\flags&#TUIF_FIXHEIGHT) AND False
        gap.l = nh.l-*child\minsize\y
        If (*child\flags&#TUIF_BOTTOM)
          *child\margin\top = gap : *child\margin\bottom = 0
        Else
          If (*child\flags&#TUIF_TOP)
             *child\margin\top = 0 : *child\margin\bottom = gap
          Else
            *child\margin\top = gap/2 : *child\margin\bottom = gap-*child\margin\top
          End If
        End If
      Else
        ;*child\gapNW\y = *child\margin\top : *child\gapSE\y = *child\margin\bottom
      End If

      If *tuiGroup\orientation=#TUIORIENT_HORIZONTAL
        lbox.tuiRect\left   = inX           + *child\margin\left
        lbox\top            = outX          + *child\margin\top
        lbox\right          = inX  + inS -1 - *child\margin\right
        lbox\bottom         = outX + outS-1 - *child\margin\bottom
        _ntui_Layout{*child,*rp,lbox}
        ;inX + *child\margin\left + *child\margin\right
      Else
        lbox.tuiRect\left   = outX            + *child\margin\left
        lbox\top            = inX             + *child\margin\top
        lbox\right          = outX  + outS -1 - *child\margin\right
        lbox\bottom         = inX   + inS  -1 - *child\margin\bottom
        _ntui_Layout{*child,*rp,lbox}
        ;outX + *child\margin\top + *child\margin\bottom
      End If
      inX + inS
      *child=*child\next_
    Wend
    If (*outA\sizeable OR sizeableOut=numOut)
      morespaceOut - addspaceOut : so-1
    End If
    outX + outS
  Wend

End Statement


_ntui_DeinitGroup:
Statement _ntui_DeinitGroup{*tuiGroup.tuiGroup}
If *tuiGroup\tmpIn  Then FreeVec_ *tuiGroup\tmpIn  : *tuiGroup\tmpIn  = #NULL
If *tuiGroup\tmpOut Then FreeVec_ *tuiGroup\tmpOut : *tuiGroup\tmpOut = #NULL
End Statement


;///////////////////////////////////////////////////////////////////////////////
;/                                                                             /
;/ Syntax: result.l = ntui_BeginVGroup {wrap.l,@title.s,@flags.l,@userID.l}    /
;/                                                                             /
;/ Description:                                                                /
;/ Begin a new tuiGroup that arranges its sub-tuiObjects vertically. After all sub-tuiObjects are created, you must call ntui_EndGroup.                                           /
;/ Note: a suitable parent object must have been created before you can call this function, such as another tuiGroup or tuiWindow.
;/ Note: a value of 0 or a negativ number for wrapping will switch wrapping off. All sub-tuiObjects will be vertically in-line.
;/                                                                             /
;/ Inputs:                                                                     /
;/ - wrap.l    : maximum number of tuiObject in-line before wrapping occures                                                           /
;/ - title.s   : optional title of the group (group will get a frame then)                                                          /
;/ - flags.l   : flags to control the group                                                         /
;/ - userID.l  : userID of the group                                                         /
;/                                                                             /
;/ Result:                                                                     /
;/ - *tuiGroup.tuiGroup  : pointer to the created group                                                       /
;/                                                                             /
;///////////////////////////////////////////////////////////////////////////////
Function.l ntui_BeginVGroup{wrap.l,@title.s,@imagefile.s,@flags.l,@nameID.s}
If flags=-1 Then flags=0
flags.l | #TUIF_FIXSIZE
If title Then flags=(flags|#TUIF_CENTER)
If wrap<1 Then wrap=32767
*tuiGroup.tuiGroup = _ntui_CreateObject{#TUICLASS_GROUP,SizeOf.tuiGroup,"",flags,nameID}
If *tuiGroup
  If title
    str_Write{&*tuiGroup\obj\string,title}
    *tuiGroup\obj\borderType   = #TUIBORDER_TITLE
  Else
    *tuiGroup\obj\borderType   = #TUIBORDER_NONE
    *tuiGroup\obj\padding\left = 0,0,0,0
    *tuiGroup\obj\margin\left  = 0,0,0,0
  End If
  If imagefile
    *tuiGroup\tbi = ntui_CreateTBImage{*tuiGroup\obj\tuiEngine,imagefile,#TUISIZE_FONT,#TUISIZE_FONT}
  End If
  *tuiGroup\subClassID     = #TUIGROUP_VERTICAL
  *tuiGroup\orientation    = #TUIORIENT_VERTICAL
  *tuiGroup\wrap           = wrap
  *tuiGroup\obj\bgTuiPen   = #TUIPEN_TRANSPARENT
  *tuiGroup\tmpIn          = #NULL
  *tuiGroup\tmpInAlloc     = 0
  *tuiGroup\tmpOut         = #NULL
  *tuiGroup\tmpOutAlloc    = 0

  !_GetFuncPointer{*tuiGroup\obj\Draw            ,_ntui_DrawGroup,{0,0,0}}
  !_GetFuncPointer{*tuiGroup\obj\Layout          ,_ntui_LayoutGroup,{0,0,0}}
;  !_GetFuncPointer{*tuiGroup\obj\DispatchEvent   ,_ntui_DispatchGroupEvent,{0,0}}
  !_GetFuncPointer{*tuiGroup\obj\CalculateMinSize,_ntui_CalculateGroupMinSize,{0,0}}
  !_GetFuncPointer{*tuiGroup\obj\Deinit          ,_ntui_DeinitGroup,{0}}
  !_GetFuncPointer{*tuiGroup\obj\SetAttr         ,_ntui_SetGroupAttr,{0,0,0}}
  !_GetFuncPointer{*tuiGroup\obj\GetAttr         ,_ntui_GetGroupAttr,{0,0,0}}
  _ntui_BeginChildren{*tuiGroup}
  ;tuiGlobal\buildHelper\tuiParent              = *tuiGroup
  ;tuiGlobal\buildHelper\tuiPrev                = #NULL
End If
Function Return *tuiGroup
End Function


;///////////////////////////////////////////////////////////////////////////////
;/                                                                             /
;/ Syntax: result.l = ntui_BeginHGroup {wrap.l,@title.s,@flags.l,@userID.l}    /
;/                                                                             /
;/ Description:                                                                /
;/ Same as ntui_BeginVGroup, but sub-tuiObjects are arranged horizontally.                                                /
;/                                                                             /
;/ Inputs:                                                                     /
;/ - wrap.l    : maximum number of tuiObject in-line before wrapping occures                                                           /
;/ - title.s   : optional title of the group (group will get a frame then)                                                          /
;/ - flags.l   : flags to control the group                                                         /
;/ - userID.l  : userID of the group                                                         /
;/                                                                             /
;/ Result:                                                                     /
;/ - *tuiGroup.tuiGroup  : pointer to the created group                                                       /
;/                                                                             /
;///////////////////////////////////////////////////////////////////////////////
Function.l ntui_BeginHGroup{wrap.l,@title.s,@imagefile.s ,@flags.l,@nameID.s}
*tuiGroup.tuiGroup = ntui_BeginVGroup{wrap,title,imagefile,flags,nameID}
If *tuiGroup
  *tuiGroup\orientation = #TUIORIENT_HORIZONTAL
  *tuiGroup\subClassID   = #TUIGROUP_HORIZONTAL
End If
Function Return *tuiGroup
End Function


;///////////////////////////////////////////////////////////////////////////////
;/                                                                             /
;/ Syntax: result.l = ntui_BeginMenuBar {}                                     /
;/                                                                             /
;/ Description:                                                                /
;/ Same as ntui_BeginHGroup, but the sub-tuiObjects will work as a menubar that might appear in the screen-titlebar.
;/* Typically, you will create ntui_MenuTitle objects within this group.                                               /
;/                                                                             /
;/ Inputs:                                                                     /
;/ - wrap.l    : maximum number of tuiObject in-line before wrapping occures                                                           /
;/ - title.s   : optional title of the group (group will get a frame then)                                                          /
;/ - flags.l   : flags to control the group                                                         /
;/ - userID.l  : userID of the group                                                         /
;/                                                                             /
;/ Result:                                                                     /
;/ - *tuiGroup.tuiGroup  : pointer to the created group                                                       /
;/                                                                             /
;///////////////////////////////////////////////////////////////////////////////
Function.l ntui_BeginMenuBar{}
flags.l = #TUIF_FIXHEIGHT
*tuiGroup.tuiGroup = ntui_BeginVGroup{0,"","",flags}
If *tuiGroup
  *tuiGroup\orientation = #TUIORIENT_HORIZONTAL
  *tuiGroup\subClassID   = #TUIGROUP_MENUBAR
  *tuiGroup\obj\borderType = #TUIBORDER_MENUBAR
End If
Function Return *tuiGroup
End Function


;///////////////////////////////////////////////////////////////////////////////
;/                                                                             /
;/ Syntax: result.l = ntui_EndGroup {}                                         /
;/                                                                             /
;/ Description:                                                                /
;/ End a group definition.                                                 /
;/                                                                             /
;/ Result:                                                                     /
;/ - result.l     : tuiGroup Object that was just terminated                                                        /
;/                                                                             /
;///////////////////////////////////////////////////////////////////////////////
Function.l ntui_EndGroup{}
Function Return _ntui_EndChildren{#TUICLASS_GROUP}
;tuiGlobal\buildHelper\tuiPrev    = tuiGlobal\buildHelper\tuiParent
;tuiGlobal\buildHelper\tuiCurrent = tuiGlobal\buildHelper\tuiParent
;If tuiGlobal\buildHelper\tuiParent Then tuiGlobal\buildHelper\tuiParent = tuiGlobal\buildHelper\tuiParent\parent : Else tuiGlobal\buildHelper\tuiParent = #NULL
;If tuiGlobal\buildHelper\tuiCurrent
;  If tuiGlobal\buildHelper\tuiCurrent\classID><#TUICLASS_GROUP Then error{"\\__THIS_FUNCTION: EndGroup does not match a BeginGroup!"}
;Else
;  error{"\\__THIS_FUNCTION: EndGroup without BeginGroup!"}
;End If
;Function Return tuiGlobal\buildHelper\tuiCurrent
End Function


