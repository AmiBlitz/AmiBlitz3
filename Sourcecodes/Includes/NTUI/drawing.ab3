; XTRA
; Embedded .xtra Header
; 
; General Info
; -------------------------------------------------------
; ExePath         = "ram:"
; ExeFile         = "Empty"
; CreateIcon      = 0
; Residents       = "all.res"
; 
; Compiler
; -------------------------------------------------------
; StringBuffer    = 100000
; MakeSmallest    = 1
; FuncOptimize    = 0
; Version         = 0.0.0
; NumberOfBuilds  = 4
; 
; Debugger
; -------------------------------------------------------
; CliArgs         = ""
; StackSize       = 8191
; RuntimeDebug    = 0
; DebugInfo       = 0
; CreateDbgFile   = 0
; OverflowCheck   = 0
; AssemblerCheck  = 0
; InterruptCheck  = 1
; AutoRun         = 1
; 
; Editor
; -------------------------------------------------------
; CursorLine      = 603
; CursorColumn    = 47
; LabelSearch     = "pen"
; LabelRemark     = 0
; LabelAll        = 0
; LabelPosition   = 0
; 
; Blitz Objects
; -------------------------------------------------------
; Max IconInfo    = 1
; Max NChunky     = 50
; Max MUIObject   = 50
; Max PTModule    = 5
; Max AsyncReq    = 4
; Max Req-Lib     = 5
; Max GTMenuList  = 100
; Max Console     = 5
; Max TCPSock     = 5
; Max XBSound     = 10
; Max Chunky      = 20
; Max File        = 100
; Max GadgetList  = 100
; Max Queue       = 10
; Max Screen      = 5
; Max Shape       = 200
; Max CopList     = 10
; Max Sprite      = 20
; Max Stencil     = 5
; Max Module      = 5
; Max Window      = 100
; Max Anim        = 10
; Max Sound       = 10
; Max Bank        = 1
; Max Buffer      = 10
; Max BitMap      = 10
; Max Slice       = 10
; Max Page        = 4
; Max Tape        = 5
; Max IntuiFont   = 10
; Max MedModule   = 8
; Max Palette     = 4
; Max MenuList    = 100
; Max BlitzFont   = 1
; Max GTList      = 100
; /XTRA


;///////////////////////////////////////////////////////////////////////////////
;/                                                                             /
;/ Syntax: ntui_RemClip {*rp.RastPort}                                         /
;/                                                                             /
;/ Description:                                                                /
;/* remove a clip region from a rastport previously set with ntui_SetClip.
;/                                                                             /
;/ Inputs:                                                                     /
;/ - *rp.RastPort    : rastport                                                     /
;/                                                                             /
;///////////////////////////////////////////////////////////////////////////////
Statement ntui_RemClip_deprecated{*rp.RastPort}
*old_region.Region =Null
*new_region.Region = InstallClipRegion_(*rp\Layer, *old_region)
DisposeRegion_ *new_region
*old_region = -1
End Statement


;///////////////////////////////////////////////////////////////////////////////
;/                                                                             /
;/ Syntax: ntui_SetClip {*rp.RastPort,x1.l,y1.l,x2.l,y2.l}                     /
;/                                                                             /
;/ Description:                                                                /
;/ Set a rectangle clip region to the rastport. Remember to always call ntui_RemClip once you are done.
;/ Note: This is a low-level helper function if you are drawing into a tuiView.
;/                                                                             /
;/ Inputs:                                                                     /
;/ - *rp.RastPort : rastport to manipulate                                                     /
;/ - x1.l         : coordinates of the rectangle                                                             /
;/ - y1.l         :                                                              /
;/ - x2.l         :                                                             /
;/ - y2.l         :                                                             /
;/                                                                             /
;///////////////////////////////////////////////////////////////////////////////
Statement ntui_SetClip_deprecated{*rp.RastPort,*cbox.tuiRect}
cliprect.Rectangle\MinX = Max(*cbox\left,0)
cliprect.Rectangle\MinY = Max(*cbox\top ,0)
cliprect.Rectangle\MaxX = Min(*cbox\right ,*rp\Layer\Width-1)
cliprect.Rectangle\MaxY = Min(*cbox\bottom,*rp\Layer\Height-1)
*new_region.Region = NewRegion_
If *new_region
  If OrRectRegion_(*new_region, &cliprect)
    *old_region.Region = InstallClipRegion_(*rp\Layer, *new_region)
  End If
End If
End Statement

Function.Region ntui_SetClip{*rp.RastPort,*cbox.tuiRect}

  If *cbox
    cliprect.Rectangle\MinX = Max(*cbox\left,0)
    cliprect.Rectangle\MinY = Max(*cbox\top ,0)
    cliprect.Rectangle\MaxX = Min(*cbox\right ,*rp\Layer\Width-1)
    cliprect.Rectangle\MaxY = Min(*cbox\bottom,*rp\Layer\Height-1)
  Else
    cliprect.Rectangle\MinX = 0
    cliprect.Rectangle\MinY = 0
    cliprect.Rectangle\MaxX = *rp\Layer\Width-1
    cliprect.Rectangle\MaxY = *rp\Layer\Height-1
  End If

  refresh.l = False
  If *rp\Layer
    If *rp\Layer\Window
      *oswin.Window = *rp\Layer\Window
      If *oswin\Flags&#WFLG_WINDOWREFRESH
        EndRefresh_ *rp\Layer\Window,0
        refresh = True
      End If
    End If
  End If

  *new_region.Region = NewRegion_
  If *new_region
    If OrRectRegion_(*new_region, &cliprect)
      *old_region.Region = InstallClipRegion_(*rp\Layer, *new_region)
    End If
  End If

  If refresh
    BeginRefresh_ *rp\Layer\Window
  End If

  Function Return *old_region

End Function


Function.Region ntui_AddClip{*rp.RastPort,*cbox.tuiRect}

  cliprect.Rectangle\MinX = Max(*cbox\left,0)
  cliprect.Rectangle\MinY = Max(*cbox\top ,0)
  cliprect.Rectangle\MaxX = Min(*cbox\right ,*rp\Layer\Width-1)
  cliprect.Rectangle\MaxY = Min(*cbox\bottom,*rp\Layer\Height-1)

  refresh.l = False
  If *rp\Layer
    If *rp\Layer\Window
      *oswin.Window = *rp\Layer\Window
      If *oswin\Flags&#WFLG_WINDOWREFRESH
        EndRefresh_ *rp\Layer\Window,0
        refresh = True
      End If
    End If
  End If

  *new_region.Region = NewRegion_
  If *new_region
    If OrRectRegion_(*new_region, &cliprect)
      If *rp\Layer\ClipRegion Then AndRegionRegion_ *rp\Layer\ClipRegion,*new_region
      *old_region.Region = InstallClipRegion_(*rp\Layer, *new_region)
    End If
  End If

  If refresh
    BeginRefresh_ *rp\Layer\Window
  End If

  Function Return *old_region

End Function


Statement ntui_RemClip{*rp.RastPort,*old_region.Region}

  refresh.l = False
  If *rp\Layer
    If *rp\Layer\Window
      *oswin.Window = *rp\Layer\Window
      If *oswin\Flags&#WFLG_WINDOWREFRESH
        EndRefresh_ *rp\Layer\Window,0
        refresh = True
      End If
    End If
  End If

  *new_region.Region = InstallClipRegion_(*rp\Layer, *old_region)
  If *new_region Then DisposeRegion_ *new_region

  If refresh
    BeginRefresh_ *rp\Layer\Window
  End If

End Statement


;///////////////////////////////////////////////////////////////////////////////
;/                                                                             /
;/ Syntax: result.l = _ntui_OpenFont {fontname.s,size.l,style.l,*scr.Screen}   /
;/                                                                             /
;/ Description:                                                                /
;/ private: open a font for later use in NTUI                                  /
;/                                                                             /
;/ Inputs:                                                                     /
;/ - fontname.s   : name of the font (e.g. "topaz.font")                       /
;/ - size.l       : size of the font (e.g. 8)                                  /
;/ - style.l      : style flags of the font (e.g. #FSF_BOLD)                   /
;/ - *scr.Screen  : optional screen from were to get the default if this fails /
;/                                                                             /
;/ Result:                                                                     /
;/ - *font.TextFont : font handle                                              /
;/                                                                             /
;///////////////////////////////////////////////////////////////////////////////
Function.TextFont _ntui_OpenFont{*fontnameS.str,size.l,flags.l,*scr.Screen}
DEFTYPE.TextAttr ta

*font.TextFont=Null

style.b = 0
If flags&#TUIF_BOLD    Then style | #FSF_BOLD
If flags&#TUIF_ITALIC  Then style | #FSF_ITALIC
If size<6 Then size=6
fontname.s = str_Read{&*fontnameS}

If fontname
  If (LCase$(Right$(fontname,5))><".font") Then fontname = fontname+".font"
  ta\ta_Name  = &fontname
  ta\ta_YSize = size          ;/* height of the font */
  ta\ta_Style = style         ;/* intrinsic font style */
  ta\ta_Flags = #FPF_DISKFONT ;|#FPF_PROPORTIONAL      ;/* font preferences and flags */
  *font = OpenDiskFont_(ta)
End If

If (*font=Null) AND ((flags&#TUIF_FIX)=0) ;/* if font cannot be opened, try the screen font */
  If *scr><Null
    size = *scr\Font\ta_YSize
    If (flags&#TUIF_SMALL) Then size*2/3 : If size<6 Then size=6
    ta\ta_Name  = *scr\Font\ta_Name
    ta\ta_YSize = size
    ta\ta_Style = style
    ta\ta_Flags = *scr\Font\ta_Flags
    *font = OpenDiskFont_(ta)
  End If
End If

If (*font=Null) AND ((flags&#TUIF_FIX)=0);/* if font cannot be opened, try the screen font wihtout styles*/
  If *scr><Null
    *font = OpenDiskFont_(*scr\Font)
  End If
End If

;If *font=null ;/* if font still cannot be opened, try the default gfx font */
;  *gfxbase.GfxBase = OpenLibrary_ ("graphics.library",0)
;  If *gfxbase
;    *font = *gfxbase\DefaultFont
;  End If
;  CloseLibrary_ *gfxbase
;End If

If (*font=Null) ;/* if font still cannot be opened, try the default gfx font */
  InitRastPort_ rp.RastPort
  AskFont_ rp,ta ; might be better
  ta\ta_Style = style
  *font = OpenDiskFont_(ta)
End If

If (*font=Null) ;/* if font still cannot be opened, try the default gfx font wihtout styles*/
  InitRastPort_ rp.RastPort
  AskFont_ rp,ta ; might be better
  *font = OpenDiskFont_(ta)
End If

Function Return *font
End Function


;///////////////////////////////////////////////////////////////////////////////
;/                                                                             /
;/ Syntax: result.l = _ntui_SetFont {*engine.tuiEngine,flags.l,@*rp.RastP:: /
;/ ort}                                                                        /
;/                                                                             /
;/ Description:                                                                /
;/ private: select a font from the given flags and optionally attach it to:: /
;/  the rastport                                                             /
;/                                                                             /
;/ Inputs:                                                                     /
;/ - *engine.tuiEngine : tuiEngine                                             /
;/ - flags.l              : tuiFlags                                                          /
;/ - *rp.RastPort         : a RastPort                                                    /
;/                                                                             /
;/ Result:                                                                     /
;/ - result.l             : tuiFont index                                                        /
;/                                                                             /
;///////////////////////////////////////////////////////////////////////////////
Function.l _ntui_SetFont{*engine.tuiEngine,flags.l,@*rp.RastPort}
tuifont.l = #TUIFONT_NORMAL
If (flags&#TUIF_BOLD)
  If (flags&#TUIF_ITALIC)
    tuifont = #TUIFONT_NORMALIB
  Else
    tuifont = #TUIFONT_NORMALBOLD
  End If
Else
  If (flags&#TUIF_ITALIC)
    tuifont = #TUIFONT_NORMALITALIC
  Else
    tuifont = #TUIFONT_NORMAL
  End If
End If
If (flags&#TUIF_SMALL)
  tuifont+4
Else
  If (flags&#TUIF_FIX)
    tuifont+8
  Else
    If (flags&#TUIF_SERIF)
      tuifont+12
    End If
  EndIf
End If

If *engine\font[tuifont]=Null
  *engine\font[tuifont] = _ntui_OpenFont{*engine\fontName[tuifont],*engine\fontSize[tuifont],flags,*engine\screen_}
End If

If *rp><Null AND *rp><-1
  If (*engine\font[tuifont]) Then SetFont_ *rp,*engine\font[tuifont]
  If flags&#TUIF_BOLD   Then SetSoftStyle_ *rp,#FSF_BOLD,#FSF_BOLD
  If flags&#TUIF_ITALIC Then SetSoftStyle_ *rp,#FSF_ITALIC,#FSF_ITALIC
End If
Function Return tuifont
End Function



;///////////////////////////////////////////////////////////////////////////////
;/                                                                             /
;/ Syntax: result.l = _ntui_GetTuiPen {*engine.tuiEngine,argb.l,@tuipen.l}  /
;/                                                                             /
;/ Description:                                                                /
;/ private
;/                                                                             /
;/ Inputs:                                                                     /
;/ - *engine.tuiEngine    : ???                                             /
;/ - argb.l    : ???                                                           /
;/ - tuipen.l    : ???                                                         /
;/                                                                             /
;/ Result:                                                                     /
;/ - result.l     : ???                                                        /
;/                                                                             /
;///////////////////////////////////////////////////////////////////////////////
Function.l ntui_ObtainTuiPen{*engine.tuiEngine,argb.l,@tuiPen.l}

  rgb_.l = argb&$00FFFFFF
  If tuiPen<0 ; we want a new pen
    For n.l=0 To *engine\pensUsed-1 ; check if it is already there...
      If *engine\penInfo[n]\useCount>0
        If rgb_=*engine\penInfo[n]\rgb_
          *engine\penInfo[n]\useCount+1
          Function Return n
        End If
      Else
        tuiPen = n ; remember unused pen
      End If
    Next
    If tuiPen<0 Then tuiPen=*engine\pensUsed
  Else
    If *engine\penInfo[tuiPen]\useCount>0 ; we are overwriting an existing one

    End If
  End If

  If tuiPen>=#TUIMAX_PENS Then Function Return #TUIPEN_TEXT ; no more pens!
  If tuiPen>=*engine\pensUsed Then *engine\pensUsed=tuiPen+1

  *engine\penInfo[tuiPen]\rgb_ = rgb_
  *engine\penInfo[tuiPen]\useCount +1

  If *engine\screen_=Null Then Function Return tuiPen

  If *engine\penInfo[tuiPen]\useCount>1 Then ReleasePen_ *engine\screen_\ViewPort\ColorMap,*engine\pen[tuiPen]

  R.l = (rgb_ & $00FF0000) LSL  8
  G.l = (rgb_ & $0000FF00) LSL 16
  B.l = (rgb_ & $000000FF) LSL 24
  *engine\pen[tuiPen] = ObtainBestPenA_(*engine\screen_\ViewPort\ColorMap,R,G,B,Null)

  Function Return tuiPen

End Function


Function.l ntui_ReleaseTuiPen{*engine.tuiEngine,tuiPen.l}
  !_ASSERT{tuiPen>=0}
  !_ASSERT{tuiPen<*engine\pensUsed}
  !_ASSERT{*engine\penInfo[tuiPen]\useCount>0}

  If *engine\penInfo[tuiPen]\useCount>0 ; was it used at all?
    *engine\penInfo[tuiPen]\useCount-1 ; remove one
    If *engine\penInfo[tuiPen]\useCount=0 ; is it unused now?
      If *engine\screen_   ; if we are on screen, release system pen
        ReleasePen_ *engine\screen_\ViewPort\ColorMap,*engine\pen[tuiPen]
      End If
      Repeat
        done.l = True
        If *engine\pensUsed>0
          If *engine\penInfo[*engine\pensUsed-1]\useCount<=0 Then *engine\pensUsed-1 : done=False
        End If
      Until done
    End If
  End If

  Function Return done

End Function


;///////////////////////////////////////////////////////////////////////////////
;/                                                                             /
;/ Syntax: result.l = _ntui_GetTextLength {*engine.tuiEngine,*text.str,fl:: /
;/ ags.l,*rp.RastPort}                                                         /
;/                                                                             /
;/ Description:                                                                /
;/ * no description available *                                                /
;/                                                                             /
;/ Inputs:                                                                     /
;/ - *engine.tuiEngine : ???                                             /
;/ - *text.str            : ???                                                        /
;/ - flags.l              : ???                                                          /
;/ - *rp.RastPort         : ???                                                     /
;/                                                                             /
;/ Result:                                                                     /
;/ - result.l     : ???                                                        /
;/                                                                             /
;///////////////////////////////////////////////////////////////////////////////
Function.l _ntui_GetTextWidth{*engine.tuiEngine,*text.str,flags.l,*rp.RastPort}
If *text=Null OR *rp=Null Then Function Return 0
_ntui_SetFont{*engine,flags,*rp}
TextExtent_ *rp,&*text\str,*text\size,tx.TextExtent
Function Return tx\te_Extent\MaxX-tx\te_Extent\MinX+1
End Function


Function.l _ntui_GetTextSkip{*engine.tuiEngine,*text.str,flags.l,*rp.RastPort}
If *text=Null OR *rp=Null Then Function Return 0
_ntui_SetFont{*engine,flags,*rp}
Function Return TextLength_(*rp,&*text\str,*text\size)
End Function



Function.l _ntui_GetFontHeight{*engine.tuiEngine,flags.l}
!_ASSERT{*engine}
tuifont.l = _ntui_SetFont{*engine,flags}
Function Return *engine\font[tuifont]\tf_YSize
End Function


Statement _ntui_DrawEmptyBox{*engine.tuiEngine,*rp.RastPort,*bbox.tuiRect,tuiPen.l}
  SetAPen_ *rp,*engine\pen[tuiPen]
  x1.l = *bbox\left
  x2.l = *bbox\right
  y1.l = *bbox\top
  y2.l = *bbox\bottom
  w.l = x2-x1+1
  h.l = y2-y1+1
  For x.l = x1 To x2+h-1 Step 4
    dx.l = x
    dy.l = y1
    If dx>=w+x1 Then dy- (w+x1)+dx+1 : dx = x2
    Move_ *rp,dx,dy
    dx.l = x-h+1
    dy.l = y2
    If dx<x1 Then dy+dx-x1 : dx=x1
    Draw_ *rp,dx,dy
  Next
End Statement

Statement _ntui_DrawDottedLine{*engine.tuiEngine,*rp.RastPort,*bbox.tuiRect,tuiPen.l,stepping.l}
x1.l = *bbox\left
x2.l = *bbox\right
y1.l = *bbox\top
y2.l = *bbox\bottom
If stepping<1 Then stepping=2

If tuiPen>=0 Then SetAPen_ *rp,*engine\pen[tuiPen]
For x.l=x1+1 To x2-1 Step stepping
  WritePixel_ *rp,x,y1
Next

For y.l=y1+1 To y2-1 Step stepping
  WritePixel_ *rp,x2,y
Next

End Statement


Statement _ntui_DrawGradientBox{*engine.tuiEngine,*rp.RastPort,*bbox.tuiRect,tuiPen.l,orient.l}

  If tuiPen<0 Then Statement Return
  
  If *engine\cModel >< #TUICM_TRUECOLOR
    SetAPen_ *rp,*engine\pen[tuiPen]
    RectFill_ *rp,*bbox\left,*bbox\top,*bbox\right,*bbox\bottom
    Statement Return
  End If

  x1.l = *bbox\left
  x2.l = *bbox\right
  y1.l = *bbox\top
  y2.l = *bbox\bottom
  w.l = x2-x1+1
  h.l = y2-y1+1
  argb.l = *engine\penInfo[tuiPen]\rgb_

  r1.l = (argb LSR 16) & $FF
  g1.l = (argb LSR  8) & $FF
  b1.l = (argb       ) & $FF

  r2.l = (r1 * 14) ASR 4
  g2.l = (g1 * 14) ASR 4
  b2.l = (b1 * 14) ASR 4

  r1.l = (r1 * 17) ASR 4
  g1.l = (g1 * 17) ASR 4
  b1.l = (b1 * 17) ASR 4

  If r1>255 Then r1=255
  If g1>255 Then g1=255
  If b1>255 Then b1=255

  Select (orient)
    Case #TUIORIENT_VERTICAL
      For x.l = 0 To w-1
        r.l = (r2*x + r1*(w-x)) / w
        g.l = (g2*x + g1*(w-x)) / w
        b.l = (b2*x + b1*(w-x)) / w
        argb.l = r LSL 16 | g LSL 8 | b
        FillPixelArray_ *rp,x1+x,y1,1,h,argb
      Next

    Case #TUIORIENT_HORIZONTAL
      For y.l = 0 To h-1
        r.l = (r2*y + r1*(h-y)) / h
        g.l = (g2*y + g1*(h-y)) / h
        b.l = (b2*y + b1*(h-y)) / h
        argb.l = r LSL 16 | g LSL 8 | b
        FillPixelArray_ *rp,x1,y+y1,w,1,argb
      Next

    Case #TUIORIENT_INVVERTICAL
      For x.l = 0 To w-1
        r.l = (r1*x + r2*(w-x)) / w
        g.l = (g1*x + g2*(w-x)) / w
        b.l = (b1*x + b2*(w-x)) / w
        argb.l = r LSL 16 | g LSL 8 | b
        FillPixelArray_ *rp,x1+x,y1,1,h,argb
      Next

    Case #TUIORIENT_INVHORIZONTAL
      For y.l = 0 To h-1
        r.l = (r1*y + r2*(h-y)) / h
        g.l = (g1*y + g2*(h-y)) / h
        b.l = (b1*y + b2*(h-y)) / h
        argb.l = r LSL 16 | g LSL 8 | b
        FillPixelArray_ *rp,x1,y+y1,w,1,argb
      Next

    Default
      FillPixelArray_ *rp,x1,y1,w,h,argb

  End Select

End Statement


Statement _ntui_DrawGlossBox{*engine.tuiEngine,*rp.RastPort,*bbox.tuiRect}

  x1.l = *bbox\left
  x2.l = *bbox\right
  y1.l = *bbox\top
  y2.l = *bbox\bottom
  w.l = x2-x1+1
  h.l = y2-y1+1

  tmpP.l = _ntui_ObtainTempBuffer{w*h*4}
  ReadPixelArray_ tmpP,0,0,w*4,*rp,x1,y1,w,h,#RECTFMT_ARGB

  For y.l = 1 To h/2
    tmp.l = tmpP + (y*w*4)
    For x.l = 1 To w-2
      argb.l = ((Peek.l(tmp+x*4) & $FEFEFEFE) LSR 1) + $80808080
      Poke.l tmp+x*4,argb
    Next
  Next

  WritePixelArray_ tmpP,0,0,w*4,*rp,x1,y1,w,h,#RECTFMT_ARGB
  _ntui_ReleaseTempBuffer{}

End Statement


;///////////////////////////////////////////////////////////////////////////////
;/                                                                             /
;/ Syntax: _ntui_GetTextLengthEx {*engine.tuiEngine,*text.str,*tx.TextExt:: /
;/ ent,flags.l,*rp.RastPort}                                                   /
;/                                                                             /
;/ Description:                                                                /
;/ * no description available *                                                /
;/                                                                             /
;/ Inputs:                                                                     /
;/ - *engine.tuiEngine : ???                                             /
;/ - *text.str            : ???                                                        /
;/ - *tx.TextExtent       : ???                                                   /
;/ - flags.l              : ???                                                          /
;/ - *rp.RastPort         : ???                                                     /
;/                                                                             /
;///////////////////////////////////////////////////////////////////////////////
Statement _ntui_GetTextRect{*engine.tuiEngine,*text.str,*tbox.tuiRect,flags.l,*rp.RastPort}
If *text=Null OR *tbox=Null OR *rp=Null
  *tbox\left = 0,0,0,0
  Statement Return
End If
_ntui_SetFont{*engine,flags,*rp}
TextExtent_ *rp,&*text\str,*text\size,tx.TextExtent
*tbox\left   = tx\te_Extent\MinX
*tbox\top    = tx\te_Extent\MinY
*tbox\right  = tx\te_Extent\MaxX
*tbox\bottom = tx\te_Extent\MaxY
End Statement



;///////////////////////////////////////////////////////////////////////////////
;/                                                                             /
;/ Syntax: _ntui_Print {*engine.tuiEngine,*text.str,x1.l,y1.l,x2.l,y2.l,f:: /
;/ lags.l,*rp.RastPort,fgTuiPen.l,bgTuiPen.l}                                  /
;/                                                                             /
;/ Description:                                                                /
;/ * no description available *                                                /
;/                                                                             /
;/ Inputs:                                                                     /
;/ - *engine.tuiEngine    : ???                                             /
;/ - *text.str    : ???                                                        /
;/ - x1.l    : ???                                                             /
;/ - y1.l    : ???                                                             /
;/ - x2.l    : ???                                                             /
;/ - y2.l    : ???                                                             /
;/ - flags.l    : ???                                                          /
;/ - *rp.RastPort    : ???                                                     /
;/ - fgTuiPen.l    : ???                                                       /
;/ - bgTuiPen.l    : ???                                                       /
;/                                                                             /
;///////////////////////////////////////////////////////////////////////////////
Statement _ntui_Print{*engine.tuiEngine,*text.str,*bbox.tuiRect,*tbox.tuiRect,flags.l,*rp.RastPort,fgTuiPen.l,bgTuiPen.l}
If *text Then If *text\size<=0 Then *text=Null
If *text.str=Null
  If bgTuiPen>=0
    bgpen.l = *engine\pen[bgTuiPen]
    SetAPen_ *rp,bgpen
    RectFill_ *rp,*bbox\left,*bbox\top,*bbox\right,*bbox\bottom
  End If
  Statement Return
End If

;_ntui_DrawGradientBox{*engine,*rp,*bbox,bgTuiPen,1} : bgTuiPen = #TUIPEN_TRANSPARENT

osize.l = *text\size
If osize>4096
  CNIF #TUI_DEBUG
  error{"Lengthy string! :"+Str$(osize)+" chars!!!"}
  CEND
  Statement Return
End If
xs.l = !tuiRectWidth{*tbox}
Repeat
  _ntui_GetTextRect{*engine,*text,tx.tuiRect,flags,*rp}
  lx.l = !tuiRectWidth{tx}
  If xs<lx Then *text\size-1
Until xs>=lx OR *text\size<=0

ly.l = !tuiRectHeight{tx}

If (flags&#TUIF_LEFT)
  px.l = *tbox\left-tx\left
Else
  If (flags&#TUIF_RIGHT)
    px = *tbox\right-lx-tx\left +1
  Else
    px = (*tbox\left+*tbox\right-lx+1)/2-tx\left
  End If
End If

If (flags&#TUIF_TOP)
  py.l = *tbox\top-tx\top
Else
  If (flags&#TUIF_BOTTOM)
    py = *tbox\bottom-ly-tx\top +1
  Else
    py = (*tbox\top+*tbox\bottom-ly+1)/2-tx\top
  End If
End If

If bgTuiPen>=0 Then bgpen.l = *engine\pen[bgTuiPen] : Else bgpen=-1
fgpen.l = *engine\pen[fgTuiPen]
CNIF #TUI_DEBUG
fgpen =  RRnd(1,255)
bgpen = RRnd(1,255)
CEND

If bgTuiPen>=0
  If *engine\redrawMethod=#TUIRM_SMART
    xa.l = px + tx\left   -1
    xb.l = px + tx\right  +1
    ya.l = py + tx\top    -1
    yb.l = py + tx\bottom +1
    SetAPen_ *rp,bgpen
    If ya>=*bbox\top    Then RectFill_ *rp,*bbox\left,*bbox\top,*bbox\right,ya
    If yb<=*bbox\bottom Then RectFill_ *rp,*bbox\left,       yb,*bbox\right,*bbox\bottom
    If xa>=*bbox\left   Then RectFill_ *rp,*bbox\left,     ya+1,         xa,yb-1
    If xb<=*bbox\right  Then RectFill_ *rp,        xb,     ya+1,*bbox\right,yb-1
  Else
    CNIF #TUI_DEBUG
      SetAPen_ *rp,9
    CELSE
      SetAPen_ *rp,bgpen
    CEND
    RectFill_ *rp,*bbox\left,*bbox\top,*bbox\right,*bbox\bottom
  End If
End If

    CNIF #TUI_DEBUG
      SetAPen_ *rp,4
      RectFill_ *rp,*tbox\left,*tbox\top,*tbox\right,*tbox\bottom
    CEND


If (flags&#TUIF_DISABLED)
  If *engine\redrawMethod=#TUIRM_SMART
    If bgpen>=0
      Move_ *rp,xa+1,ya+1 : Draw_ *rp,xb-1,ya+1
      Move_ *rp,xa+1,ya+1 : Draw_ *rp,xa+1,yb-1
    End If
  End If
  fgpen=*engine\pen[#TUIPEN_WHITE]
  If fgpen>=0 Then SetAPen_ *rp,fgpen
  If bgpen>=0 Then SetBPen_ *rp,bgpen : SetDrMd_ *rp,#JAM2: Else SetDrMd_ *rp,#JAM1
  yb.l = py + tx\bottom +1
  If yb<=*bbox\bottom
    Move_    *rp,px+1,py+1
  Else
    Move_    *rp,px+1,py
  End If
  Text_    *rp,&*text\str,*text\size
  fgpen=*engine\pen[#TUIPEN_SHADOW]
  SetAPen_ *rp,fgpen
  SetDrMd_ *rp,#JAM1
  Move_    *rp,px,py
  Text_    *rp,&*text\str,*text\size
Else
  If fgpen>=0 Then SetAPen_ *rp,fgpen
  If bgpen>=0 Then SetBPen_ *rp,bgpen : SetDrMd_ *rp,#JAM2: Else SetDrMd_ *rp,#JAM1
  Move_    *rp,px,py
  Text_    *rp,&*text\str,*text\size
End If

If *text\size>0
If (flags&#TUIF_UNDERLINED)
  yb.l = py + tx\bottom +1
  If yb<*bbox\bottom
    Move_ *rp,px,py+2
    Draw_ *rp,px+lx-1,py+2
  Else
    Move_ *rp,px,py+1
    Draw_ *rp,px+lx-1,py+1
  End If
End If
End If

SetDrMd_ *rp,#JAM2
If *text\size<osize
  ya.l = py + tx\top-1  +2
  yb.l = py + tx\bottom+1  -2
  s.l = 2;(yb-ya+1)/5 : If s<2 Then s=2

  For y.l=ya To yb Step s
    WritePixel_ *rp,*tbox\right,y
  Next
  *text\size=osize
End If

;_ntui_DrawGlossBox{*engine,*rp,*bbox}
End Statement


Function.l _ntui_DrawBG{*engine.tuiEngine,*rp.RastPort,*bbox.tuiRect,*ibox.tuiRect,*rpclip.tuiRect,*bgImage.tuiTBImage,bgTuiPen.l,borderType.l,flags.l,orient.l}

  *reg.Region = ntui_AddClip{*rp,*rpclip}

  If (*bgImage)
    ;Stop
    ntui_DrawTBImage{*bgImage,*rp,*bbox,*bbox,flags,#TUIPEN_TRANSPARENT}
    bgTuiPen = #TUIPEN_TRANSPARENT
  Else
    If (borderType><#TUIBORDER_NONE)
      If ntui_DrawBorder{*engine,*rp,*bbox,borderType,flags} Then bgTuiPen = #TUIPEN_TRANSPARENT
    End If
    If *engine\fillStyle=1 AND bgTuiPen><#TUIPEN_TRANSPARENT
      If flags&#TUIF_ACTIVE
        If orient=#TUIORIENT_HORIZONTAL    Then orient=#TUIORIENT_INVHORIZONTAL : Else If orient=#TUIORIENT_INVHORIZONTAL Then orient=#TUIORIENT_HORIZONTAL
        If orient=#TUIORIENT_VERTICAL      Then orient=#TUIORIENT_INVVERTICAL   : Else If orient=#TUIORIENT_INVVERTICAL   Then orient=#TUIORIENT_VERTICAL
      End If
      _ntui_DrawGradientBox{*engine,*rp,*ibox,bgTuiPen,orient}
      bgTuiPen = #TUIPEN_TRANSPARENT
    End If
  EndIf

  If bgTuiPen><#TUIPEN_TRANSPARENT
    CNIF #TUI_DEBUG
      SetAPen_ *rp,RNDPen{}
    CELSE
      SetAPen_ *rp,*engine\pen[bgTuiPen]
    CEND
    !tuiAndRect{iclip.tuiRect,*ibox,*rpclip}
    RectFill_ *rp,iclip\left,iclip\top,iclip\right,iclip\bottom
  EndIf
  ntui_RemClip{*rp,*reg}

  Function Return bgTuiPen
End Function



;///////////////////////////////////////////////////////////////////////////////
;/                                                                             /
;/ Syntax: ntui_Draw {*obj.tuiObject,*rp.RastPort}                             /
;/                                                                             /
;/ Description:                                                                /
;/ Draw a tuiObject into the given rastport.
;/                                                                             /
;/ Inputs:                                                                     /
;/ - *obj.tuiObject  : tuiObject                                                   /
;/ - *rp.RastPort    : destination rastport                                                     /
;/                                                                             /
;///////////////////////////////////////////////////////////////////////////////
Statement FAST _ntui_Draw{*obj.tuiObject,*rp.RastPort,*clip.tuiRect}
  !_ASSERT{*obj}
  !_ASSERT{*rp}
  If ((*obj\flags&#TUIF_ONSCREEN)=0) Then Statement Return
  ;Delay_ 1  ; for debugging
  If *obj\Draw Then !_CallFuncPointer{*obj\Draw,res.l,*obj,*rp,*clip}

End Statement



Statement ntui_ScrollRect{*obj.tuiObject,dx.l,dy.l,*rp.RastPort,*rpclip.tuiRect}
!_ASSERT{*rp}
!_ASSERT{*obj}

If *rpclip=Null Then *rpclip = *obj\cbox
*win.Window  = *rp\Layer\Window

If dy><0
  If (Abs(dy)<!tuiRectWidth{*rpclip})
    If *win
      ScrollWindowRaster_ *win,0,dy,*rpclip\left,*rpclip\top,*rpclip\right,*rpclip\bottom
    Else
      ScrollRasterBF_ *rp,0,dy,*rpclip\left,*rpclip\top,*rpclip\right,*rpclip\bottom
    End If

    If dy<0
      *rpclip\bottom = *rpclip\top-dy
      _ntui_Draw{*obj,*rp,*rpclip}
    Else
      *rpclip\top = *rpclip\bottom+1-dy
      _ntui_Draw{*obj,*rp,*rpclip}
    End If
  Else
    _ntui_Draw{*obj,*rp,*rpclip}
  End If
End If

If dx><0
  If (Abs(dx)<!tuiRectWidth{*rpclip})
    If *win
      ScrollWindowRaster_ *win,dx,0,*rpclip\left,*rpclip\top,*rpclip\right,*rpclip\bottom
    Else
      ScrollRasterBF_ *rp,dx,0,*rpclip\left,*rpclip\top,*rpclip\right,*rpclip\bottom
    End If
    If dx<0
      *rpclip\right = *rpclip\left-dx
      _ntui_Draw{*obj,*rp,*rpclip}
    Else
      *rpclip\left = *rpclip\right-dx+1
      _ntui_Draw{*obj,*rp,*rpclip}
    End If
  Else
    _ntui_Draw{*obj,*rp,*rpclip}
  End If
End If
End Statement



