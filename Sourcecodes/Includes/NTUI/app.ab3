; XTRA
; Embedded .xtra Header
; 
; General Info
; -------------------------------------------------------
; ExePath         = "ram:"
; ExeFile         = "Empty"
; CreateIcon      = 0
; Residents       = "all.res"
; 
; Compiler
; -------------------------------------------------------
; StringBuffer    = 100000
; MakeSmallest    = 1
; Version         = 0.0.0
; NumberOfBuilds  = 11
; 
; Debugger
; -------------------------------------------------------
; CliArgs         = ""
; StackSize       = 8192
; RuntimeDebug    = 0
; DebugInfo       = 0
; CreateDbgFile   = 0
; OverflowCheck   = 0
; AssemblerCheck  = 0
; InterruptCheck  = 1
; AutoRun         = 1
; 
; Editor
; -------------------------------------------------------
; CursorLine      = 119
; CursorColumn    = 7
; LabelSearch     = ""
; LabelRemark     = 0
; LabelAll        = 0
; LabelPosition   = 0
; 
; Blitz Objects
; -------------------------------------------------------
; Max IconInfo    = 1
; Max NChunky     = 50
; Max MUIObject   = 50
; Max PTModule    = 5
; Max AsyncReq    = 4
; Max Req-Lib     = 5
; Max GTMenuList  = 100
; Max Console     = 5
; Max TCPSock     = 5
; Max XBSound     = 10
; Max Chunky      = 20
; Max File        = 100
; Max GadgetList  = 100
; Max Queue       = 10
; Max Screen      = 5
; Max Shape       = 200
; Max CopList     = 10
; Max Sprite      = 20
; Max Stencil     = 5
; Max Module      = 5
; Max Window      = 100
; Max Anim        = 10
; Max Sound       = 10
; Max Bank        = 1
; Max Buffer      = 10
; Max BitMap      = 10
; Max Slice       = 10
; Max Page        = 4
; Max Tape        = 5
; Max IntuiFont   = 10
; Max MedModule   = 8
; Max Palette     = 4
; Max MenuList    = 100
; Max BlitzFont   = 1
; Max GTList      = 100
; /XTRA
#TUI_HAS_APP = 1

Statement ntui_ShowAppIcon{*engine.tuiEngine,@text.s}
  !_ASSERT{*engine}
  !_ASSERT{*engine\obj\classID = #TUICLASS_ENGINE}

  If *engine\appIcon=Null
    If *engine\appIconDO
      If text Then str_Write{&*engine\appIconText,text}
      textP.l = str_GetPtr{*engine\appIconText}
      *engine\appIcon = AddAppIconA_ (#APP_ICON,Null,textP,*engine\appPort,Null,*engine\appIconDO,Null)
    End If
  End If

End Statement


Statement ntui_HideAppIcon{*engine.tuiEngine}
  !_ASSERT{*engine}
  !_ASSERT{*engine\obj\classID = #TUICLASS_ENGINE}

  If *engine\appIcon Then RemoveAppIcon_ *engine\appIcon : *engine\appIcon = Null

End Statement

Statement _ntui_FreeAppIcon{*engine.tuiEngine}
  !_ASSERT{*engine}
  !_ASSERT{*engine\obj\classID = #TUICLASS_ENGINE}

  ntui_HideAppIcon{*engine}
  If *engine\appIconDO    Then FreeDiskObject_    *engine\appIconDO   : *engine\appIconDO   = Null

End Statement

Statement _ntui_FreeAppMenu{*engine.tuiEngine}
  !_ASSERT{*engine}
  !_ASSERT{*engine\obj\classID = #TUICLASS_ENGINE}

  If *engine\appMenu      Then RemoveAppMenuItem_ *engine\appMenu     : *engine\appMenu     = Null

End Statement


Statement _ntui_FreeApp{*engine.tuiEngine}
  !_ASSERT{*engine}
  !_ASSERT{*engine\obj\classID = #TUICLASS_ENGINE}

  _ntui_FreeAppMenu{*engine}
  _ntui_FreeAppIcon{*engine}
  If *engine\appPort      Then DeleteMsgPort_     *engine\appPort     : *engine\appPort     = Null

End Statement


Function.l _ntui_InitAppIcon{*engine.tuiEngine,appIconFile.s}
  !_ASSERT{*engine}
  !_ASSERT{*engine\obj\classID = #TUICLASS_ENGINE}

  _ntui_FreeAppIcon{*engine}

  done.l = False

  If *engine\appPort
    str_Write{&*engine\appIconFile,appIconFile}

    If *engine\appIconFile
      *engine\appIconDO = GetDiskObjectNew_(&*engine\appIconFile\str)
      If *engine\appIconDO=Null
        error{"\\__THIS_FUNCTION: Unable to get diskobject: \\22"+str_Read{&*engine\appIconFile}+"\\22!"}
      Else
        ;*engine\appIconDO\do_Magic       = 0 ; leave it
        ;*engine\appIconDO\do_Version     = 0 ; if we do this, PNG Icons dont work anymore...
        *engine\appIconDO\do_Type        = #WBAPPICON
        *engine\appIconDO\do_DefaultTool = Null
        *engine\appIconDO\do_ToolTypes   = Null
        *engine\appIconDO\do_CurrentX    = #NO_ICON_POSITION
        *engine\appIconDO\do_CurrentY    = #NO_ICON_POSITION
        ;*engine\appIconDO\do_DrawerData  = Null ; this is not a pointer!
        *engine\appIconDO\do_ToolWindow  = Null
        *engine\appIconDO\do_StackSize   = Null
        done = True
      End If
    End  If
  End If

  Function Return done

End Function

Function.l _ntui_InitAppMenu{*engine.tuiEngine,appMenuName.s}
  !_ASSERT{*engine}
  !_ASSERT{*engine\obj\classID = #TUICLASS_ENGINE}

  _ntui_FreeAppMenu{*engine}

  done.l = False

  If *engine\appPort
    str_Write{&*engine\appMenuName,appMenuName}

    If *engine\appMenuName
      *engine\appMenu = AddAppMenuItemA_(#APP_MENUHIT,Null,&*engine\appMenuName\str,*engine\appPort,Null)
      If *engine\appMenu Then done=True
    End If

  End If

  Function Return done

End Function


Function.l _ntui_InitApp{*engine.tuiEngine}
  !_ASSERT{*engine}
  !_ASSERT{*engine\obj\classID = #TUICLASS_ENGINE}

  _ntui_FreeApp{*engine}

  done.l = False
  *engine\appPort = CreateMsgPort_()
  If *engine\appPort Then done=True

  Function Return done

End Function

