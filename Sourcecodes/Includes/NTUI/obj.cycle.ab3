; XTRA
; Embedded .xtra Header
; 
; General Info
; -------------------------------------------------------
; ExePath         = "RAM:"
; ExeFile         = "Prog.exe"
; CreateIcon      = 0
; Residents       = "all.res,SDSTCP2.res,xpk.res,ttengine.res"
; 
; Compiler
; -------------------------------------------------------
; StringBuffer    = 8192
; MakeSmallest    = 1
; Version         = 0.0.0
; NumberOfBuilds  = 1
; 
; Debugger
; -------------------------------------------------------
; CliArgs         = ""
; StackSize       = 32768
; RuntimeDebug    = 0
; DebugInfo       = 0
; CreateDbgFile   = 0
; OverflowCheck   = 0
; AssemblerCheck  = 1
; InterruptCheck  = 1
; AutoRun         = 1
; 
; Editor
; -------------------------------------------------------
; CursorLine      = 575
; CursorColumn    = 93
; LabelSearch     = ""
; LabelRemark     = 0
; LabelAll        = 0
; LabelPosition   = 0
; 
; Blitz Objects
; -------------------------------------------------------
; Max IconInfo    = 1
; Max NChunky     = 50
; Max MUIObject   = 50
; Max PTModule    = 5
; Max AsyncReq    = 4
; Max Req-Lib     = 5
; Max GTMenuList  = 5
; Max Console     = 5
; Max TCPSock     = 5
; Max XBSound     = 10
; Max Chunky      = 20
; Max File        = 5
; Max GadgetList  = 5
; Max Queue       = 10
; Max Screen      = 5
; Max Shape       = 100
; Max CopList     = 10
; Max Sprite      = 20
; Max Stencil     = 5
; Max Module      = 5
; Max Window      = 20
; Max Anim        = 10
; Max Sound       = 10
; Max Bank        = 5
; Max Buffer      = 10
; Max BitMap      = 10
; Max Slice       = 10
; Max Page        = 4
; Max Tape        = 5
; Max IntuiFont   = 5
; Max MedModule   = 8
; Max Palette     = 4
; Max MenuList    = 5
; Max BlitzFont   = 4
; Max GTList      = 20
; /XTRA
;///////////////////////////////////////////////////////////////////////////////
;/                                                                             /
;/ Name: ntui_cycle.include                                                    /
;/                                                                             /
;/ Platforms: Classic, WinUAE, Amithlon, MorphOS, AmigaOS4                     /
;/                                                                             /
;/ Date: 06/01/2009                                                            /
;/                                                                             /
;/ Author: <unknown>                                                           /
;/                                                                             /
;/ Requirements:  Amiblitz3                                                    /
;/                                                                             /
;/ Purpose:                                                                    /
;/ Extend AB3 functionality.                                                   /
;/ * no description available *                                                /
;/                                                                             /
;/ Abstract:                                                                   /
;/ * no abstract available *                                                   /
;/                                                                             /
;/ User Constants:                                                             /
;/ #max_objects   = n                                                          /
;/                                                                             /
;///////////////////////////////////////////////////////////////////////////////
#TUI_HAS_CYCLE = 1
CNIF #__include=0
optimize 7
Syntax 2
CEND

#TUICYA_BASE      = #TUIA_USER
#TUICYA_SELECTED  = #TUIA_VALUE
#TUICYA_OPTIONSTR = #TUICYA_BASE + 0

NEWTYPE.tuiCycle
obj.tuiObject            ; tui object header
state.l
*optionS.str
*tbi.tuiTBImage
*optionW.Window
optionHeight.l
numOptions.l
lastOption.l
tmpOption.l
End NEWTYPE


;///////////////////////////////////////////////////////////////////////////////
;/                                                                             /
;/ Syntax: result.s = _ntui_GetCycleOption {*cycle_.tuiCycle,option.l}       /
;/                                                                             /
;/ Description:                                                                /
;/ * tui Cycle newtype */                                                      /
;/ obj.tuiObject            ; tui object header                                /
;/                                                                             /
;/ Inputs:                                                                     /
;/ - *cycle_.tuiCycle    : ???                                               /
;/ - option.l    : ???                                                         /
;/                                                                             /
;/ Result:                                                                     /
;/ - result.s     : ???                                                        /
;/                                                                             /
;///////////////////////////////////////////////////////////////////////////////
Function.s _ntui_GetCycleOption{*cycle_.tuiCycle,option.l}
If option<0 OR option>=*cycle_\numOptions Then Function Return ""
res.s = ""
If *cycle_\optionS
  ptr.l = &*cycle_\optionS\str
  While Peek.b(ptr)><0 AND option>=0
    If Peek.b(ptr)=@"|"
      option-1
    Else
      If option=0 Then res + Chr$(Peek.b(ptr))
    End If
    ptr+1
  Wend
End If
Function Return res
End Function


;///////////////////////////////////////////////////////////////////////////////
;/                                                                             /
;/ Syntax: _ntui_CloseCycleOptions {*cycle_.tuiCycle}                        /
;/                                                                             /
;/ Description:                                                                /
;/       ;case hotkey                                                          /
;/   option = -1;*cycle_\obj\value                                           /
;/                                                                             /
;/ Inputs:                                                                     /
;/ - *cycle_.tuiCycle    : ???                                               /
;/                                                                             /
;///////////////////////////////////////////////////////////////////////////////
Statement _ntui_CloseCycleOptions{*cycle_.tuiCycle}
  If *cycle_\optionW
    Forbid_
    *msg.IntuiMessage = (.IntuiMessage)*cycle_\optionW\UserPort\mp_MsgList\lh_Head
    While (*msg\ExecMessage\mn_Node\ln_Succ)
      *succ.Node = *msg\ExecMessage\mn_Node\ln_Succ
      If( *msg\IDCMPWindow=*cycle_\optionW)
        Remove_ *msg : ReplyMsg_ *msg
      End If
      *msg = (.IntuiMessage)*succ
    Wend
    *cycle_\optionW\UserPort = Null
    ModifyIDCMP_ *cycle_\optionW,0
    Permit_
    CloseWindow_ *cycle_\optionW
    *cycle_\optionW = Null
  End If
End Statement

;///////////////////////////////////////////////////////////////////////////////
;/                                                                             /
;/ Syntax: _ntui_OpenCycleOptions {*cycle_.tuiCycle}                         /
;/                                                                             /
;/ Description:                                                                /
;/   *cycle_\optionW\UserPort = Null;                                      /
;/                                                                             /
;/ Inputs:                                                                     /
;/ - *cycle_.tuiCycle    : ???                                               /
;/                                                                             /
;///////////////////////////////////////////////////////////////////////////////
Statement _ntui_OpenCycleOptions{*cycle_.tuiCycle}
If *cycle_\optionW=Null
  *tuiWindow.tuiWindow = ntui_GetObjectWindow{*cycle_}
  *tuiEngine.tuiEngine = *cycle_\obj\tuiEngine
  If *tuiWindow
    DEFTYPE.NewWindow nw
    _ntui_GetBorderSize{*tuiEngine,#TUIBORDER_FLAT,0,bs.tuiRect}

   If *cycle_\tbi
     iw.l = ntui_GetTBImageWidth{*cycle_\tbi}
     px.l = iw+*cycle_\obj\padding\left+*cycle_\obj\padding\right + 2-1
   Else
     px.l = 0
   End If
    *cycle_\tmpOption = *cycle_\obj\value

    nw\LeftEdge    = px +*cycle_\obj\cbox\left + *tuiWindow\oswin\LeftEdge  - bs\left
    ; below:
    ;nw\TopEdge     = *cycle_\obj\pos\y+*cycle_\obj\size\y + *tuiWindow\win\TopEdge + 1
    option.l = *cycle_\obj\value
    If option<-1 OR option>=*cycle_\numOptions Then option=-1

    nw\TopEdge     = *cycle_\obj\cbox\top-(*cycle_\optionHeight * option) + *tuiWindow\oswin\TopEdge - bs\top ;+ *cycle_\obj\padding\top

    nw\Width       = !tuiRectWidth{*cycle_\obj\cbox} - px + bs\left + bs\right
    nw\Height      = *cycle_\optionHeight * *cycle_\numOptions + bs\top+bs\bottom
    nw\DetailPen   = -1 ; use intuitions default
    nw\BlockPen    = -1
    nw\IDCMPFlags  = 0
    IDCMPFlags.l   = #IDCMP_REFRESHWINDOW|#IDCMP_MOUSEBUTTONS|#IDCMP_MOUSEMOVE|#IDCMP_INTUITICKS|#IDCMP_MENUVERIFY|#IDCMP_RAWKEY
    nw\Flags       = #WFLG_BORDERLESS|#WFLG_SIMPLE_REFRESH|#WFLG_NOCAREREFRESH
    nw\FirstGadget = Null
    nw\CheckMark   = Null
    nw\Title       = Null
    nw\Screen      = *tuiEngine\scr
    nw\BitMap      = Null
    nw\MinWidth    = 0
    nw\MinHeight   = 0
    nw\MaxWidth    = 0
    nw\MaxHeight   = 0
    nw\Type        = #CUSTOMSCREEN
    DEFTYPE.tag5 wtags
    wtags\ti_Tag =#WA_AutoAdjust,1,#WA_BackFill,Null,#TAG_DONE,0
    *cycle_\optionW = OpenWindowTagList_(nw,wtags)
    If *cycle_\optionW
      *cycle_\optionW\UserData = *tuiWindow
      *cycle_\optionW\UserPort = *tuiEngine\winPort
      ModifyIDCMP_ *cycle_\optionW,IDCMPFlags
    End If
  End If
End If
End Statement



Statement _ntui_UpdateCycleString{*cycle_.tuiCycle,option.l,bind.w}
  *cycle_\obj\value = option
  text.s = _ntui_GetCycleOption{*cycle_,*cycle_\obj\value}
  str_Write{&*cycle_\obj\string,text}
  If bind
    _ntui_PropagateBind{*cycle_,#TUIEV_BIND_VALUE,*cycle_\obj\value,Null}
    _ntui_PropagateBind{*cycle_,#TUIEV_BIND_STRING,0,*cycle_\obj\string}
  End If
End Statement

_ntui_SetCycleAttr:
Function.w _ntui_SetCycleAttr{*cycle_.tuiCycle,ti_Tag.l,ti_Data.l}
  !_ASSERT{*cycle_}
  !_ASSERT{*cycle_\obj\classID=#TUICLASS_CYCLE}

  done.w = True
  Select ti_Tag
    Case #TUICYA_OPTIONSTR
      _ntui_CloseCycleOptions{*cycle_}
      str_WritePtr{&*cycle_\optionS,ti_Data}

      If *cycle_\optionS
        *cycle_\numOptions=1
        ptr.l = &*cycle_\optionS\str
        While Peek.b(ptr)><0
          If Peek.b(ptr)=@"|" Then *cycle_\numOptions+1
          ptr+1
        Wend
      Else
        *cycle_\numOptions=0
      End If
      _ntui_UpdateCycleString{*cycle_,ti_Data,False}
      ntui_Refresh{*cycle_,Null}

    Case #TUICYA_SELECTED
      If *cycle_\obj\value><ti_Data
        _ntui_UpdateCycleString{*cycle_,ti_Data,True}
        ntui_Refresh{*cycle_,Null}
      End If

    Case #TUIA_XMLATTR
      *xmlA.tuiXmlAttr = ti_Data
      Select Peek.s(*xmlA\nameP)
        Case "options"           : done=ntui_SetAttr{*cycle_,#TUICYA_OPTIONSTR      ,*xmlA\stringP}
        Case "selected"          : done=ntui_SetAttr{*cycle_,#TUICYA_SELECTED       ,*xmlA\value}
        Default
          done = False
       End Select
    Default
      done = False

  End Select
  Function Return done
End Function


_ntui_GetCycleAttr:
Function.w _ntui_GetCycleAttr{*cycle_.tuiCycle,ti_Tag.l,*ti_Data.longP}
  !_ASSERT{*cycle_}
  !_ASSERT{*cycle_\obj\classID=#TUICLASS_CYCLE}

  done.w = True
  Select ti_Tag
    Case #TUICYA_OPTIONSTR    : *ti_Data\l = str_GetPtr{*cycle_\optionS}
    Default
      done = False

  End Select
  Function Return done
End Function






;///////////////////////////////////////////////////////////////////////////////
;/                                                                             /
;/ Syntax: result.w = _ntui_DispatchCycle {*cycle_.tuiCycle,*tuiEvent.tuiEve:: /
;/ nt}                                                                         /
;/                                                                             /
;/ Description:                                                                /
;/ * do what happens if the user operates the Cycle */                         /
;/                                                                             /
;/ Inputs:                                                                     /
;/ - *cycle_.tuiCycle    : ???                                               /
;/ - *tuiEvent.tuiEvent    : ???                                               /
;/                                                                             /
;/ Result:                                                                     /
;/ - result.w     : ???                                                        /
;/                                                                             /
;///////////////////////////////////////////////////////////////////////////////
_ntui_DispatchCycleEvent:
Function.w _ntui_DispatchCycleEvent{*cycle_.tuiCycle,*tuiEvent.tuiEvent}

  done.l = False
  If *cycle_\optionW
    option.l = *tuiEvent\pos\y
    *tuiWindow.tuiWindow = ntui_GetObjectWindow{*cycle_}
    If *tuiWindow
      If *tuiWindow\oswin
        option.l = (*tuiEvent\pos\y + *tuiWindow\oswin\TopEdge - *cycle_\optionW\TopEdge)
      End If
    End If
    If option>0 Then option / *cycle_\optionHeight
  Else
    option = -1;*cycle_\obj\value
  End If

  Select *tuiEvent\evID
    Case #TUIEV_MOUSEDOWN
      If *tuiEvent\value = 0 ; we want button 0
        If ntui_HitObject{*cycle_,*tuiEvent\pos\x,*tuiEvent\pos\y}
          ntui_SetFocus{*cycle_}
          done      = True
          *cycle_\obj\flags | #TUIF_ACTIVE
          str_Dup{*tuiEvent\notifyID,*cycle_\obj\onTouch}
          If *cycle_\tbi Then iw.l = ntui_GetTBImageWidth{*cycle_\tbi}
          If (*tuiEvent\pos\x-*cycle_\obj\cbox\left) > iw + *cycle_\obj\padding\left + *cycle_\obj\padding\right+3
            _ntui_OpenCycleOptions{*cycle_}
            *cycle_\state = 2
          Else
            *cycle_\state = 1
          End If
          ntui_Refresh{*cycle_,Null}
        End If
      End If

    Case #TUIEV_MOUSEUP
      If *tuiEvent\value = 0 ; we want Cycle 0
        If *cycle_\obj\flags&#TUIF_ACTIVE
          done = True
          If *cycle_\optionW
            _ntui_CloseCycleOptions{*cycle_}
          Else
            If ntui_HitObject{*cycle_,*tuiEvent\pos\x,*tuiEvent\pos\y}
              If (*tuiEvent\qualifier_&#TUIQUAL_SHIFT)=0
               option = *cycle_\obj\value+1
               If option>=*cycle_\numOptions OR option<0 Then option=0
              Else
                option = *cycle_\obj\value-1
                If option>=*cycle_\numOptions OR option<0 Then option=*cycle_\numOptions-1
              End If
            Else
              option=-1
            End If
          End If
          If option>=0 AND option<*cycle_\numOptions
            _ntui_UpdateCycleString{*cycle_,option,True}
          End If
          *cycle_\state = 0
          str_Dup{*tuiEvent\notifyID,*cycle_\obj\onClick}
          *cycle_\obj\flags|#TUIF_ACTIVE - #TUIF_ACTIVE
          ntui_Refresh{*cycle_,Null}
        End If
      End If

    Case #TUIEV_MOUSEMOVE
      If *cycle_\obj\flags&#TUIF_ACTIVE
        If *cycle_\optionW
          ooption.l = *cycle_\tmpOption
          *cycle_\tmpOption = option
          If ooption><option
            ntui_Refresh{*cycle_,Null}
          End If
        Else
          oldstate.l = *cycle_\state
          If ntui_HitObject{*cycle_,*tuiEvent\pos\x,*tuiEvent\pos\y}
            *cycle_\state = 1 ; pressed
          Else
            *cycle_\state = 0 ; pressed
          End If
          If oldstate><*cycle_\state
            ntui_Refresh{*cycle_,Null}
          End If
        End If
        done=True
      End If

    Case #TUIEV_KEYDOWN
      Select ntui_GetEventAttr{*tuiEvent,#TUIEVA_VANILLAKEY,0}
        Case 13 ; return
          ntui_SetFocus{*cycle_}
          done      = True
          *cycle_\obj\flags | #TUIF_ACTIVE
          *cycle_\state = 1 ; pressed
          str_Dup{*tuiEvent\notifyID,*cycle_\obj\onTouch}
          ntui_Refresh{*cycle_,Null}
      End Select

    Case #TUIEV_KEYUP
      If *cycle_\obj\flags&#TUIF_ACTIVE
        done = True
        *cycle_\state = 0 ; pressed
        str_Dup{*tuiEvent\notifyID,*cycle_\obj\onClick}
        *cycle_\obj\flags|#TUIF_ACTIVE - #TUIF_ACTIVE
        If (*tuiEvent\qualifier_&#TUIQUAL_SHIFT)=0
          option = *cycle_\obj\value+1
          If option>=*cycle_\numOptions OR option<0 Then option=0
        Else
          option = *cycle_\obj\value-1
          If option>=*cycle_\numOptions OR option<0 Then option=*cycle_\numOptions-1
        End If
        _ntui_UpdateCycleString{*cycle_,option,True}
        ntui_Refresh{*cycle_,Null}
      End If

    Case #TUIEV_BIND_VALUE
      If *cycle_\obj\value><*tuiEvent\value
        _ntui_UpdateCycleString{*cycle_,*tuiEvent\value,False}
        ;While *cycle_\obj\value>=*cycle_\numOptions Then *cycle_\obj\value-*cycle_\numOptions
        ;While *cycle_\obj\value<0 Then *cycle_\obj\value+*cycle_\numOptions
        ntui_Refresh{*cycle_,Null}
      End If
      done=True

    Case #TUIEV_BIND_DELTA
      *cycle_\obj\value + *tuiEvent\value
      While *cycle_\obj\value>=*cycle_\numOptions Then *cycle_\obj\value-*cycle_\numOptions
      While *cycle_\obj\value<0 Then *cycle_\obj\value+*cycle_\numOptions
      _ntui_UpdateCycleString{*cycle_,*tuiEvent\value,False}
      ntui_Refresh{*cycle_,Null}
      done=True

    Case #TUIEV_PROPAGATE
      _ntui_PropagateBind{*cycle_,#TUIEV_BIND_MINVALUE  ,0   ,Null}
      _ntui_PropagateBind{*cycle_,#TUIEV_BIND_MAXVALUE  ,*cycle_\numOptions  ,Null}
      _ntui_PropagateBind{*cycle_,#TUIEV_BIND_VALUE     ,*cycle_\obj\value,Null}
      _ntui_PropagateBind{*cycle_,#TUIEV_BIND_STRING    ,0,*cycle_\obj\string}
      done=True

    Default
      Function Return False
  End Select

  If done
    If (*tuiEvent\notifyID)
      *tuiEvent\tuiObject = *cycle_
      _ntui_QueueEvent{*cycle_\obj\tuiEngine\outQ,*tuiEvent}
    Else
      _ntui_DestroyEvent{*tuiEvent}
    End If
  End If

Function Return done
End Function



;///////////////////////////////////////////////////////////////////////////////
;/                                                                             /
;/ Syntax: ntui_DrawCycle {*cycle_.tuiCycle,*rp.RastPort}                    /
;/                                                                             /
;/ Description:                                                                /
;/     nw\DetailPen   = -1 ; use intuitions default                            /
;/ * draw the Cycle in all its beauty */                                       /
;/                                                                             /
;/ Inputs:                                                                     /
;/ - *cycle_.tuiCycle    : ???                                               /
;/ - *rp.RastPort    : ???                                                     /
;/                                                                             /
;///////////////////////////////////////////////////////////////////////////////
_ntui_DrawCycle:
Statement _ntui_DrawCycle{*cycle_.tuiCycle,*rp.RastPort,*rpclip.tuiRect}
*tuiEngine.tuiEngine = *cycle_\obj\tuiEngine : If *tuiEngine=Null OR *rp=Null Then Statement Return
*tui.tuiObject = *cycle_\obj

If (*cycle_\obj\borderType><#TUIBORDER_NONE)
  bgDone.l = ntui_DrawBorder{*tuiEngine,*rp,*tui\bbox,*tui\borderType,*tui\flags}
End If


If *cycle_\state=1
  bgTuiPen.l = #TUIPEN_MARKER
  fgTuiPen.l = #TUIPEN_ACTIVETEXT
Else
  bgTuiPen.l = #TUIPEN_HALFSHINE
  fgTuiPen.l = #TUIPEN_TEXT
End If

If bgDone Then bgTuiPen = #TUIPEN_TRANSPARENT

!tuiCopyRect{ibox.tuiRect,*tui\ibox}
!tuiCopyRect{cbox.tuiRect,*tui\cbox}

If *cycle_\tbi

  iw.l = ntui_GetTBImageWidth{*cycle_\tbi}

  ibox\right = ibox\left + iw + *tui\padding\left + *tui\padding\right -1
  cbox\right = cbox\left + iw -1
  If cbox\right>*tui\cbox\right Then cbox\right=*tui\cbox\right
  If ibox\right>*tui\ibox\right Then ibox\right=*tui\ibox\right

  ntui_DrawTBImage{*cycle_\tbi,*rp,ibox,cbox,*cycle_\obj\flags,bgTuiPen}

  ;If *tui\string
    SetAPen_ *rp,*tuiEngine\pen[#TUIPEN_HALFSHADOW]  ; seperator
    Move_ *rp,ibox\right+1,ibox\top
    Draw_ *rp,ibox\right+1,ibox\bottom

    SetAPen_ *rp,*tuiEngine\pen[#TUIPEN_SHINE]
    Move_ *rp,ibox\right+2,ibox\top
    Draw_ *rp,ibox\right+2,ibox\bottom
  ;End If

  cbox\left  = ibox\right+3 + *tui\padding\left
  ibox\left  = ibox\right+3

  cbox\right = *tui\cbox\right
  ibox\right = *tui\ibox\right
End If

If *cycle_\state=2
  bgTuiPen.l = #TUIPEN_MARKER
  fgTuiPen.l = #TUIPEN_ACTIVETEXT
Else
  bgTuiPen.l = #TUIPEN_HALFSHINE
  fgTuiPen.l = #TUIPEN_TEXT
End If
  If bgDone Then bgTuiPen = #TUIPEN_TRANSPARENT

  If *cycle_\obj\string
    _ntui_Print{*tuiEngine,*tui\string,ibox,cbox,*tui\flags,*rp,fgTuiPen,bgTuiPen}
  Else
    If bgTuiPen><#TUIPEN_TRANSPARENT
      SetAPen_ *rp,*tuiEngine\pen[bgTuiPen]
      RectFill_ *rp,ibox\left,ibox\top,ibox\right,ibox\bottom
    End If
    _ntui_DrawEmptyBox{*tuiEngine,*rp,cbox,#TUIPEN_HALFSHADOW}
  End If

  If *cycle_\optionW
    *rpo.RastPort = *cycle_\optionW\RPort

    bbox.tuiRect\left = 0
    bbox\top          = 0
    bbox\right        = *cycle_\optionW\Width-1
    bbox\bottom       = *cycle_\optionW\Height-1

    ntui_DrawBorder{*tuiEngine,*rpo,bbox,#TUIBORDER_FLAT,*cycle_\obj\flags}
    _ntui_GetBorderSize{*tuiEngine,#TUIBORDER_FLAT,*tui\flags,bs.tuiRect}

    bbox\left         + bs\left
    bbox\top          + bs\top
    bbox\right        - bs\right
    bbox\bottom       = *cycle_\optionHeight + bs\top -1

    *textP.str = Null

    ; draw the options ...
    For n.l=0 To *cycle_\numOptions-1
      text.s = _ntui_GetCycleOption{*cycle_,n}
      If n><*cycle_\tmpOption
        If n><*cycle_\obj\value
          bgTuiPen.l = #TUIPEN_HALFSHINE
          fgTuiPen.l = #TUIPEN_TEXT
        Else
          bgTuiPen.l = #TUIPEN_BACKGROUND
          fgTuiPen.l = #TUIPEN_TEXT
        End If
      Else
        bgTuiPen.l = #TUIPEN_MARKER
        fgTuiPen.l = #TUIPEN_ACTIVETEXT
      End If


      str_Write{&*textP,text}
      _ntui_Print{*tuiEngine,*textP,bbox,bbox,*cycle_\obj\flags,*rpo,fgTuiPen,bgTuiPen}

      bbox\top    + *cycle_\optionHeight
      bbox\bottom + *cycle_\optionHeight
    Next
    str_Free{&*textP}
  End If
End Statement


;///////////////////////////////////////////////////////////////////////////////
;/                                                                             /
;/ Syntax: result.l = _ntui_GetCycleMinSize {*cycle_.tuiCycle,*rp.RastPort}  /
;/                                                                             /
;/ Description:                                                                /
;/   *cycle_\optionHeight   = 8 ;*tuiEngine\defSize+*tuiEngine\defPad\y      /
;/ * calculate the minimum size for the Cycle */                               /
;/                                                                             /
;/ Inputs:                                                                     /
;/ - *cycle_.tuiCycle    : ???                                               /
;/ - *rp.RastPort    : ???                                                     /
;/                                                                             /
;/ Result:                                                                     /
;/ - result.l     : ???                                                        /
;/                                                                             /
;///////////////////////////////////////////////////////////////////////////////
_ntui_CalculateCycleMinSize:
Statement _ntui_CalculateCycleMinSize{*cycle_.tuiCycle,*rp.RastPort}
*tuiEngine.tuiEngine = *cycle_\obj\tuiEngine : If *tuiEngine=Null OR *rp=Null Then Statement Return
*cycle_\optionHeight   = *tuiEngine\defSize[#TUISIZE_INLINE]

*cycle_\obj\minsize\x  = !tuiBorderWidth{*cycle_\obj\padding}
*cycle_\obj\minsize\y  = !tuiBorderHeight{*cycle_\obj\padding}

DEFTYPE.tuiRect tx
If *cycle_\obj\string
  _ntui_GetTextRect{*tuiEngine,*cycle_\obj\string,tx,*cycle_\obj\flags,*rp}
  *cycle_\obj\minsize\x  + !tuiRectWidth {tx}
  *cycle_\obj\minsize\y  + !tuiRectHeight{tx}
End If

If *cycle_\tbi
  iw.l = ntui_GetTBImageWidth{*cycle_\tbi}
  *cycle_\obj\minsize\x + iw + *cycle_\obj\padding\left + *cycle_\obj\padding\right + 2
End If

_ntui_GetBorderSize{*tuiEngine,*cycle_\obj\borderType,*cycle_\obj\flags,*cycle_\obj\border}
*cycle_\obj\minsize\x + !tuiBorderWidth{*cycle_\obj\border}
*cycle_\obj\minsize\y + !tuiBorderHeight{*cycle_\obj\border}

End Statement


;///////////////////////////////////////////////////////////////////////////////
;/                                                                             /
;/ Syntax: _ntui_FreeCycle {*cycle_.tuiCycle}                                /
;/                                                                             /
;/ Description:                                                                /
;/ *cycle_\optionHeight   = *tuiEngine\defSize[#TUISIZE_INLINE];+*tuiEngin:: /
;/ e\defPad\y                                                                  /
;/   *cycle_\obj\minsize\x + *tuiEngine\defSize[#TUISIZE_BUTTON]  + 2;<=sp:: /
;/ acer                                                                        /
;/ * free everything special that we allocated for the Cycle */                /
;/                                                                             /
;/ Inputs:                                                                     /
;/ - *cycle_.tuiCycle    : ???                                               /
;/                                                                             /
;///////////////////////////////////////////////////////////////////////////////
_ntui_DeinitCycle:
Statement _ntui_DeinitCycle{*cycle_.tuiCycle}
If *cycle_\tbi     Then ntui_FreeTBImage{*cycle_\tbi}  : *cycle_\tbi = Null
If *cycle_\optionW Then _ntui_CloseCycleOptions{*cycle_} : *cycle_\optionW = Null
End Statement


;///////////////////////////////////////////////////////////////////////////////
;/                                                                             /
;/ Syntax: result.l = ntui_Cycle {option.s,initial.l,@notify.l,@help.s,@flag:: /
;/ s.l,@itemID.l}                                                              /
;/                                                                             /
;/ Description:                                                                /
;/     ; draw the options ...                                                  /
;/ * create function for tui Cycle */                                          /
;/                                                                             /
;/ Inputs:                                                                     /
;/ - option.s    : ???                                                         /
;/ - initial.l    : ???                                                        /
;/ - notify.l    : ???                                                         /
;/ - help.s    : ???                                                           /
;/ - flags.l    : ???                                                          /
;/ - itemID.l    : ???                                                         /
;/                                                                             /
;/ Result:                                                                     /
;/ - result.l     : ???                                                        /
;/                                                                             /
;///////////////////////////////////////////////////////////////////////////////
Function.tuiCycle ntui_Cycle{options.s,initial.l,@help.s,@flags.l,@nameID.s}
If (flags=#TUI_NOFLAGS) Then flags=0
flags | #TUIF_CENTER|#TUIF_MIDDLE|#TUIF_FIXHEIGHT|#TUIF_WANTMOVER|#TUIF_WANTTAB
*cycle_.tuiCycle = (.tuiCycle)_ntui_CreateObject{#TUICLASS_CYCLE,SizeOf.tuiCycle,help,flags,nameID}
If *cycle_
  *tuiEngine.tuiEngine = *cycle_\obj\tuiEngine
  *cycle_\optionW = Null
  *cycle_\optionS = Null
  _ntui_SetCycleAttr{*cycle_,#TUICYA_OPTIONSTR,&options}
  *cycle_\optionHeight   = 8 ;*tuiEngine\defSize+*tuiEngine\defPad\y
  *cycle_\tbi            = ntui_CreateTBImage{*cycle_\obj\tuiEngine,"CYCLE",#TUISIZE_BUTTON,#TUISIZE_BUTTON}
  *cycle_\obj\borderType = #TUIBORDER_BUTTON
  _ntui_UpdateCycleString{*cycle_,initial,False}

  !_GetFuncPointer{*cycle_\obj\Draw            ,_ntui_DrawCycle,{0,0,0}}
;  !_GetFuncPointer{*cycle_\obj\Layout          ,_ntui_LayoutCycle,{0,0,0}}
  !_GetFuncPointer{*cycle_\obj\DispatchEvent   ,_ntui_DispatchCycleEvent,{0,0}}
  !_GetFuncPointer{*cycle_\obj\CalculateMinSize,_ntui_CalculateCycleMinSize,{0,0}}
  !_GetFuncPointer{*cycle_\obj\Deinit          ,_ntui_DeinitCycle,{0}}
  !_GetFuncPointer{*cycle_\obj\SetAttr         ,_ntui_SetCycleAttr,{0,0,0}}
  !_GetFuncPointer{*cycle_\obj\GetAttr         ,_ntui_GetCycleAttr,{0,0,0}}

End If
Function Return *cycle_
End Function



