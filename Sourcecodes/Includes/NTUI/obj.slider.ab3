; XTRA
; Embedded .xtra Header
; 
; General Info
; -------------------------------------------------------
; ExePath         = "RAM:"
; ExeFile         = "Prog.exe"
; CreateIcon      = 0
; Residents       = "all.res,SDSTCP2.res,xpk.res,ttengine.res"
; 
; Compiler
; -------------------------------------------------------
; StringBuffer    = 8192
; MakeSmallest    = 1
; Version         = 0.0.0
; NumberOfBuilds  = 0
; 
; Debugger
; -------------------------------------------------------
; CliArgs         = ""
; StackSize       = 32768
; RuntimeDebug    = 0
; DebugInfo       = 0
; CreateDbgFile   = 0
; OverflowCheck   = 0
; AssemblerCheck  = 1
; InterruptCheck  = 1
; AutoRun         = 1
; 
; Editor
; -------------------------------------------------------
; CursorLine      = 734
; CursorColumn    = 19
; LabelSearch     = ""
; LabelRemark     = 0
; LabelAll        = 0
; LabelPosition   = 0
; 
; Blitz Objects
; -------------------------------------------------------
; Max IconInfo    = 1
; Max NChunky     = 50
; Max MUIObject   = 50
; Max PTModule    = 5
; Max AsyncReq    = 4
; Max Req-Lib     = 5
; Max GTMenuList  = 5
; Max Console     = 5
; Max TCPSock     = 5
; Max XBSound     = 10
; Max Chunky      = 20
; Max File        = 5
; Max GadgetList  = 5
; Max Queue       = 10
; Max Screen      = 5
; Max Shape       = 100
; Max CopList     = 10
; Max Sprite      = 20
; Max Stencil     = 5
; Max Module      = 5
; Max Window      = 20
; Max Anim        = 10
; Max Sound       = 10
; Max Bank        = 5
; Max Buffer      = 10
; Max BitMap      = 10
; Max Slice       = 10
; Max Page        = 4
; Max Tape        = 5
; Max IntuiFont   = 5
; Max MedModule   = 8
; Max Palette     = 4
; Max MenuList    = 5
; Max BlitzFont   = 4
; Max GTList      = 20
; /XTRA
;///////////////////////////////////////////////////////////////////////////////
;/                                                                             /
;/ Name: ntui_slider.include                                                   /
;/                                                                             /
;/ Platforms: Classic, WinUAE, Amithlon, MorphOS, AmigaOS4                     /
;/                                                                             /
;/ Date: 06/01/2009                                                            /
;/                                                                             /
;/ Author: <unknown>                                                           /
;/                                                                             /
;/ Requirements:  Amiblitz3                                                    /
;/                                                                             /
;/ Purpose:                                                                    /
;/ Extend AB3 functionality.                                                   /
;/ * no description available *                                                /
;/                                                                             /
;/ Abstract:                                                                   /
;/ * no abstract available *                                                   /
;/                                                                             /
;/ User Constants:                                                             /
;/ #max_objects   = n                                                          /
;/                                                                             /
;///////////////////////////////////////////////////////////////////////////////
#TUI_HAS_SLIDER=1

CNIF #__include=0
optimize 7
Syntax 2
CEND


NEWTYPE.tuiSlider
obj.tuiObject            ; tui object header
minValue.l               ; minimum value of slider
maxValue.l               ; maximum value of slider
step_.l                  ; step
orientation.l
grab.tuiPixel
grabValue.l
grabbed.l
knobSize.tuiPixel
labelSize.tuiPixel
subTypeID.l
End NEWTYPE

;///////////////////////////////////////////////////////////////////////////////
;/                                                                             /
;/ Syntax: !_ntui_VSlider_Value2Pixel                                          /
;/                                                                             /
;/ Description:                                                                /
;/                                                                             /
;///////////////////////////////////////////////////////////////////////////////
Macro _ntui_VSlider_Value2Pixel ; {value, size}
If *tuiSlider\maxValue>*tuiSlider\minValue
  `1 = (*tuiSlider\maxValue -(`2)-*tuiSlider\minValue) * (!tuiRectHeight{*tuiSlider\obj\cbox} - *tuiSlider\labelSize\y - *tuiSlider\knobSize\y)  / (*tuiSlider\maxValue - *tuiSlider\minValue)
Else
  `1 = *tuiSlider\obj\cbox\bottom
End If
End Macro

Macro _ntui_HSlider_Value2Pixel ; {value, size}
If  *tuiSlider\maxValue>*tuiSlider\minValue
  `1 = (((`2)-*tuiSlider\minValue) * (!tuiRectWidth{*tuiSlider\obj\cbox} - *tuiSlider\labelSize\x - *tuiSlider\knobSize\x) )/ (*tuiSlider\maxValue - *tuiSlider\minValue)
Else
   `1 = *tuiSlider\obj\cbox\left
End If
End Macro
 

Macro _ntui_VSlider_Pixel2Value ; {pixel, size}
If ((!tuiRectHeight{*tuiSlider\obj\cbox}- *tuiSlider\labelSize\y - *tuiSlider\knobSize\y)>0)
  `1 = ((`2)) * (*tuiSlider\maxValue - *tuiSlider\minValue+1) / (!tuiRectHeight{*tuiSlider\obj\cbox}- *tuiSlider\labelSize\y - *tuiSlider\knobSize\y) + *tuiSlider\minValue
Else
  `1 = *tuiSlider\minValue
End If
End Macro

Macro _ntui_HSlider_Pixel2Value ; {pixel, size}
If ((!tuiRectWidth{*tuiSlider\obj\cbox} - *tuiSlider\labelSize\x - *tuiSlider\knobSize\x)>0)
  `1 = ((`2)) * (*tuiSlider\maxValue - *tuiSlider\minValue+1) / (!tuiRectWidth{*tuiSlider\obj\cbox} - *tuiSlider\labelSize\x - *tuiSlider\knobSize\x) + *tuiSlider\minValue
Else
  `1 = *tuiSlider\minValue
End If
End Macro
 

#TUISLA_BASE       = #TUIA_USER
#TUISLA_VALUE      = #TUIA_VALUE       ; top value of Slider
#TUISLA_MINVALUE   = 0 | #TUISLA_BASE   ; visible logic units
#TUISLA_MAXVALUE   = 1 | #TUISLA_BASE   ; total logic units
#TUISLA_STEP       = 2 | #TUISLA_BASE
#TUISLA_LABEL      = #TUIA_STRING

;///////////////////////////////////////////////////////////////////////////////
;/                                                                             /
;/ Syntax: _ntui_UpdateSlider {*tuiSlider.tuiSlider,value.l,minvalue.l,maxva:: /
;/ lue.l}                                                                      /
;/                                                                             /
;/ Description:                                                                /
;/ Update the slider values.                                        /
;/                                                                             /
;/ Inputs:                                                                     /
;/ - *tuiSlider.tuiSlider    : ???                                             /
;/ - value.l       : ???                                                          /
;/ - minvalue.l    : ???                                                       /
;/ - maxvalue.l    : ???                                                       /
;/                                                                             /
;///////////////////////////////////////////////////////////////////////////////
Statement _ntui_UpdateSlider{*tuiSlider.tuiSlider,value.l,minvalue.l,maxvalue.l}
  !_ASSERT{*tuiSlider}
  !_ASSERT{*tuiSlider\obj\classID=#TUICLASS_SLIDER}

  If value><*tuiSlider\obj\value OR minvalue><*tuiSlider\minValue OR maxvalue><*tuiSlider\maxValue
    If maxvalue<minvalue Then x.l=minvalue:minvalue=maxvalue:maxvalue=x
    If value>maxvalue Then value=maxvalue
    If value<minvalue Then value=minvalue
    *tuiSlider\obj\value = value
    *tuiSlider\minValue  = minvalue
    *tuiSlider\maxValue  = maxvalue
    ntui_Refresh{*tuiSlider,*tuiSlider\obj\cbox}
  End If

End Statement


_ntui_SetSliderAttr:
Function.w _ntui_SetSliderAttr{*tuiSlider.tuiSlider,ti_Tag.l,ti_Data.l}
  !_ASSERT{*tuiSlider}
  !_ASSERT{*tuiSlider\obj\classID=#TUICLASS_SLIDER}

  done.w = True
  Select ti_Tag
    Case #TUISLA_VALUE    : _ntui_UpdateSlider{*tuiSlider,ti_Data,*tuiSlider\minValue,*tuiSlider\maxValue}
    Case #TUISLA_MINVALUE : _ntui_UpdateSlider{*tuiSlider,*tuiSlider\obj\value,ti_Data,*tuiSlider\maxValue}
    Case #TUISLA_MAXVALUE : _ntui_UpdateSlider{*tuiSlider,*tuiSlider\obj\value,*tuiSlider\minValue,ti_Data}
    Case #TUISLA_STEP     : *tuiSlider\step_ = ti_Data

    Case #TUIA_XMLATTR
      *xmlA.tuiXmlAttr = ti_Data
      Select Peek.s(*xmlA\nameP)
        Case "value"       : done=ntui_SetAttr{*tuiSlider,#TUISLA_VALUE     ,*xmlA\value}
        Case "maxvalue"    : done=ntui_SetAttr{*tuiSlider,#TUISLA_MAXVALUE  ,*xmlA\value}
        Case "minvalue"    : done=ntui_SetAttr{*tuiSlider,#TUISLA_MINVALUE  ,*xmlA\value}
        Case "step"        : done=ntui_SetAttr{*tuiSlider,#TUISLA_STEP      ,*xmlA\value}
        Case "label"       : done=ntui_SetAttr{*tuiSlider,#TUISLA_LABEL     ,*xmlA\stringP}
        Default
          done = False
       End Select

    Default
      done=False

  End Select

  Function Return done
End Function


_ntui_GetSliderAttr:
Function.w _ntui_GetSliderAttr{*tuiSlider.tuiSlider,ti_Tag.l,*ti_Data.longP}
  !_ASSERT{*tuiSlider}
  !_ASSERT{*tuiSlider\obj\classID=#TUICLASS_SLIDER}

  done.w = True
  Select ti_Tag
    Case #TUISLA_VALUE     : *ti_Data\l = *tuiSlider\obj\value
    Case #TUISLA_MINVALUE  : *ti_Data\l = *tuiSlider\minValue
    Case #TUISLA_MAXVALUE  : *ti_Data\l = *tuiSlider\maxValue
    Case #TUISLA_STEP      : *ti_Data\l = *tuiSlider\step_

    Default
      done=False

  End Select

  Function Return done
End Function



;///////////////////////////////////////////////////////////////////////////////
;/                                                                             /
;/ Syntax: !_ntui_Slider_Clip                                                  /
;/                                                                             /
;/ Description:                                                                /
;/ * no description available *                                                /
;/                                                                             /
;///////////////////////////////////////////////////////////////////////////////
Macro _ntui_Slider_Clip ;
If ((`1)>*tuiSlider\maxValue) Then `1 = *tuiSlider\maxValue
If ((`1)<*tuiSlider\minValue) Then `1 = *tuiSlider\minValue
End Macro

Function.s _ntui_CreateSliderLabel{*tuiSlider.tuiSlider,value.l}
  !_ASSERT{*tuiSlider}
  !_ASSERT{*tuiSlider\obj\classID=#TUICLASS_SLIDER}

  If *tuiSlider\obj\string
    text.s = str_Read{&*tuiSlider\obj\string}
    text = Replace$(text,"%d",Str$(value))
    text = Replace$(text,"%m",Str$(*tuiSlider\minValue))
    text = Replace$(text,"%M",Str$(*tuiSlider\maxValue))
  Else
    text = ""
  End If

Function Return text
End Function


;///////////////////////////////////////////////////////////////////////////////
;/                                                                             /
;/ Syntax: result.w = _ntui_DispatchSlider {*tuiSlider.tuiSlider,*tuiEvent.tui:: /
;/ Event}                                                                      /
;/                                                                             /
;/ Description:                                                                /
;/ * do what happens if the user operates the Slider */                        /
;/                                                                             /
;/ Inputs:                                                                     /
;/ - *tuiSlider.tuiSlider    : ???                                             /
;/ - *tuiEvent.tuiEvent    : ???                                               /
;/                                                                             /
;/ Result:                                                                     /
;/ - result.w     : ???                                                        /
;/                                                                             /
;///////////////////////////////////////////////////////////////////////////////
_ntui_DispatchSliderEvent:
Function.w _ntui_DispatchSliderEvent{*tuiSlider.tuiSlider,*tuiEvent.tuiEvent}
  !_ASSERT{*tuiSlider}
  !_ASSERT{*tuiSlider\obj\classID=#TUICLASS_SLIDER}
  !_ASSERT{*tuiEvent}
  done.w = False

  Select *tuiEvent\evID
    Case #TUIEV_MOUSEDOWN:
      If *tuiEvent\value=0
;        If !tuiRectHit{*tui\cbox,*tuiEvent\pos\x,*tuiEvent\pos\y}
        If ntui_HitObject{*tuiSlider,*tuiEvent\pos\x,*tuiEvent\pos\y}
          done=True
          ntui_SetFocus{*tuiSlider}
          *tuiSlider\obj\flags | #TUIF_ACTIVE
          str_Dup{&*tuiEvent\notifyID,*tuiSlider\obj\onTouch}
          *tuiSlider\grab\x = *tuiEvent\pos\x - *tuiSlider\obj\cbox\left
          *tuiSlider\grab\y = *tuiEvent\pos\y - *tuiSlider\obj\cbox\top
          *tuiSlider\grabValue = *tuiSlider\obj\value
          *tuiSlider\grabbed = True

          Select *tuiSlider\orientation
            Case #TUIORIENT_HORIZONTAL
              !_ntui_HSlider_Value2Pixel{kx1.l,*tuiSlider\grabValue}
              kx2.l = kx1 + *tuiSlider\knobSize\x-1
              If *tuiSlider\grab\x<kx1
                _ntui_SetSliderAttr{*tuiSlider,#TUISLA_VALUE,*tuiSlider\obj\value-*tuiSlider\step_}
                *tuiSlider\grabbed = False
              End If
              If *tuiSlider\grab\x>kx2
                _ntui_SetSliderAttr{*tuiSlider,#TUISLA_VALUE,*tuiSlider\obj\value+*tuiSlider\step_}
                *tuiSlider\grabbed = False
              End If

            Case #TUIORIENT_VERTICAL
              !_ntui_VSlider_Value2Pixel{ky1.l,*tuiSlider\grabValue}
              ky2.l = ky1 + *tuiSlider\knobSize\y-1
              If *tuiSlider\grab\y<ky1
                _ntui_SetSliderAttr{*tuiSlider,#TUISLA_VALUE,*tuiSlider\obj\value+*tuiSlider\step_}
                *tuiSlider\grabbed = False
              End If
              If *tuiSlider\grab\y>ky2
                _ntui_SetSliderAttr{*tuiSlider,#TUISLA_VALUE,*tuiSlider\obj\value-*tuiSlider\step_}
                *tuiSlider\grabbed = False
              End If
          End Select
          ntui_Refresh{*tuiSlider,#NULL}
        End If
      End If

    Case #TUIEV_MOUSEUP
      If *tuiEvent\value=0
        str_Dup{&*tuiEvent\notifyID,*tuiSlider\obj\onRelease}
        *tuiSlider\obj\flags|#TUIF_ACTIVE - #TUIF_ACTIVE
        *tuiSlider\grabbed=False
        done=True
        ntui_Refresh{*tuiSlider,#NULL}
      End If

    Case #TUIEV_VSCROLL
      _ntui_SetSliderAttr{*tuiSlider,#TUISLA_VALUE,*tuiSlider\obj\value - *tuiSlider\step_* *tuiEvent\value}
      done=True

    Case #TUIEV_HSCROLL
      _ntui_SetSliderAttr{*tuiSlider,#TUISLA_VALUE,*tuiSlider\obj\value - *tuiSlider\step_* *tuiEvent\value}
      done=True

    Case #TUIEV_KEYDOWN
      Select *tuiEvent\value
        Case #RAWKEY_LEFT      : _ntui_SetSliderAttr{*tuiSlider,#TUISLA_VALUE,*tuiSlider\obj\value - *tuiSlider\step_} : done=True
        Case #RAWKEY_UP        : _ntui_SetSliderAttr{*tuiSlider,#TUISLA_VALUE,*tuiSlider\obj\value + *tuiSlider\step_} : done=True
        Case #RAWKEY_RIGHT     : _ntui_SetSliderAttr{*tuiSlider,#TUISLA_VALUE,*tuiSlider\obj\value + *tuiSlider\step_} : done=True
        Case #RAWKEY_DOWN      : _ntui_SetSliderAttr{*tuiSlider,#TUISLA_VALUE,*tuiSlider\obj\value - *tuiSlider\step_} : done=True
        Case #RAWKEY_WHEELUP   : _ntui_SetSliderAttr{*tuiSlider,#TUISLA_VALUE,*tuiSlider\obj\value + *tuiSlider\step_} : done=True
        Case #RAWKEY_WHEELDOWN : _ntui_SetSliderAttr{*tuiSlider,#TUISLA_VALUE,*tuiSlider\obj\value - *tuiSlider\step_} : done=True
      End Select

    Case #TUIEV_MOUSEMOVE
      If *tuiSlider\obj\flags&#TUIF_ACTIVE
        If *tuiSlider\grabbed
          Select *tuiSlider\orientation
            Case #TUIORIENT_HORIZONTAL
              newPos.l   = *tuiEvent\pos\x - *tuiSlider\obj\cbox\left - *tuiSlider\grab\x
              !_ntui_HSlider_Pixel2Value{newValue.l,newPos}
              newValue + (*tuiSlider\grabValue-*tuiSlider\minValue)
            Case #TUIORIENT_VERTICAL
              newPos.l   = *tuiEvent\pos\y - *tuiSlider\obj\cbox\top - *tuiSlider\grab\y
              !_ntui_VSlider_Pixel2Value{newValue,-newPos}
              newValue + (*tuiSlider\grabValue-*tuiSlider\minValue)

          End Select
          str_Dup{&*tuiEvent\notifyID,*tuiSlider\obj\onClick}
          _ntui_SetSliderAttr{*tuiSlider,#TUISLA_VALUE,newValue}
        End If
        done=True
      End If

  End Select

  If done
    If (*tuiEvent\notifyID)
      *tuiEvent\tuiObject = *tuiSlider
      _ntui_QueueEvent{*tuiSlider\obj\tuiEngine\outQ,*tuiEvent}
    Else
      _ntui_DestroyEvent{*tuiEvent}
    End If
  End If


  Function Return done
End Function


;///////////////////////////////////////////////////////////////////////////////
;/                                                                             /
;/ Syntax: result.l = _ntui_GetSliderMinSize {*tuiSlider.tuiSlider,*rp.RastP:: /
;/ ort}                                                                        /
;/                                                                             /
;/ Description:                                                                /
;/ * calculate the minimum size for the Slider */                              /
;/                                                                             /
;/ Inputs:                                                                     /
;/ - *tuiSlider.tuiSlider    : ???                                             /
;/ - *rp.RastPort    : ???                                                     /
;/                                                                             /
;/ Result:                                                                     /
;/ - result.l     : ???                                                        /
;/                                                                             /
;///////////////////////////////////////////////////////////////////////////////
_ntui_CalculateSliderMinSize:
Statement _ntui_CalculateSliderMinSize{*tuiSlider.tuiSlider,*rp.RastPort}
  !_ASSERT{*tuiSlider}
  !_ASSERT{*rp}
  !_ASSERT{*tuiSlider\obj\classID=#TUICLASS_SLIDER}
  *tuiEngine.tuiEngine = *tuiSlider\obj\tuiEngine
  !_ASSERT{*tuiEngine}

;  *tuiSlider\obj\minsize\x = *tuiSlider\obj\padding\left + *tuiSlider\obj\padding\right
;  *tuiSlider\obj\minsize\y = *tuiSlider\obj\padding\top  + *tuiSlider\obj\padding\bottom

  *tuiSlider\knobSize\x = *tuiEngine\defSize[#TUISIZE_KNOB]
  *tuiSlider\knobSize\y = *tuiEngine\defSize[#TUISIZE_KNOB]

  Select *tuiSlider\orientation
    Case #TUIORIENT_HORIZONTAL
      *tuiSlider\obj\minsize\x  = *tuiSlider\knobSize\x
      *tuiSlider\obj\minsize\y  = *tuiEngine\defSize[#TUISIZE_KNOB]
    Case #TUIORIENT_VERTICAL
      *tuiSlider\obj\minsize\x  = *tuiEngine\defSize[#TUISIZE_KNOB]
      *tuiSlider\obj\minsize\y  = *tuiSlider\knobSize\y
  End Select

  If *tuiSlider\obj\string
    text.s = _ntui_CreateSliderLabel{*tuiSlider,*tuiSlider\minValue}
    *tmp.str=#NULL
    str_Write{&*tmp,text}
    DEFTYPE.tuiRect tx
    _ntui_GetTextRect{*tuiEngine,*tmp,tx,*tuiSlider\obj\flags,*rp}
    xsmin.l = !tuiRectWidth{tx} + 1

    text.s = _ntui_CreateSliderLabel{*tuiSlider,*tuiSlider\maxValue}
    str_Write{&*tmp,text}
    _ntui_GetTextRect{*tuiEngine,*tmp,tx,*tuiSlider\obj\flags,*rp}
    xsmax.l = !tuiRectWidth{tx} + 1
    str_Free{&*tmp}
    If xsmin>xsmax Then xsmax=xsmin

    *tuiSlider\labelSize\x  = xsmax
    *tuiSlider\labelSize\y  = *tuiEngine\defSize[#TUISIZE_INLINE]

    Select *tuiSlider\orientation
      Case #TUIORIENT_HORIZONTAL
        *tuiSlider\obj\minsize\x + *tuiSlider\labelSize\x
        If (*tuiSlider\obj\minsize\y< *tuiSlider\labelSize\y) Then  *tuiSlider\obj\minsize\y =  *tuiSlider\labelSize\y

      Case #TUIORIENT_VERTICAL
        *tuiSlider\obj\minsize\y + *tuiSlider\labelSize\y
        If (*tuiSlider\obj\minsize\x< *tuiSlider\labelSize\x) Then  *tuiSlider\obj\minsize\x =  *tuiSlider\labelSize\x
    End Select

    If *tuiSlider\obj\flags&#TUIF_UNDERLINED
      _ntui_GetBorderSize{*tuiEngine,#TUIBORDER_BUTTON,*tuiSlider\obj\flags,bs.tuiRect}
      *tuiSlider\knobSize\x     = *tuiSlider\labelSize\x+ !tuiBorderWidth{bs},*tuiSlider\labelSize\y+ !tuiBorderHeight{bs}
      *tuiSlider\labelSize\x    = 0,0
      *tuiSlider\obj\minsize\x  = *tuiSlider\knobSize\x
      *tuiSlider\obj\minsize\y  = *tuiSlider\knobSize\y
    End If
  Else
    *tuiSlider\labelSize\x = 0
    *tuiSlider\labelSize\y = 0
  End If

  _ntui_GetBorderSize{*tuiEngine,*tuiSlider\obj\borderType,*tuiSlider\obj\flags,*tuiSlider\obj\border}
  *tuiSlider\obj\minsize\x + !tuiBorderWidth{*tuiSlider\obj\border}  +  !tuiBorderWidth{*tuiSlider\obj\padding}
  *tuiSlider\obj\minsize\y + !tuiBorderHeight{*tuiSlider\obj\border} +  !tuiBorderHeight{*tuiSlider\obj\padding}

End Statement


;///////////////////////////////////////////////////////////////////////////////
;/                                                                             /
;/ Syntax: ntui_DrawSlider {*tuiSlider.tuiSlider,*rp.RastPort}                 /
;/                                                                             /
;/ Description:                                                                /
;/ Draw the slider in all its beauty                                           /
;/                                                                             /
;/ Inputs:                                                                     /
;/ - *tuiSlider.tuiSlider    : ???                                             /
;/ - *rp.RastPort    : ???                                                     /
;/                                                                             /
;///////////////////////////////////////////////////////////////////////////////
_ntui_DrawSlider:
Statement _ntui_DrawSlider{*tuiSlider.tuiSlider,*rp.RastPort,*rpclip.tuiRect}
  !_ASSERT{*tuiSlider}
  !_ASSERT{*tuiSlider\obj\classID=#TUICLASS_SLIDER}
  *tuiEngine.tuiEngine = *tuiSlider\obj\tuiEngine
  *tui.tuiObject = *tuiSlider\obj
  !_ASSERT{*tuiEngine}

  ;/* draw the border */
  If (*tuiSlider\obj\borderType><#TUIBORDER_NONE)
    bgDone.l = ntui_DrawBorder{*tuiEngine,*rp,*tui\bbox,*tui\borderType,*tui\flags}
  End If

  flags.l = (*tuiSlider\obj\flags&(#TUIF_DISABLED|#TUIF_MOVER|#TUIF_FOCUS))

  ;/* get the colors */
  If *tuiSlider\grabbed;*tuiSlider\obj\flags&#TUIF_ACTIVE ;AND False
    bgTuiPen.l = #TUIPEN_MARKER
    fgTuiPen.l = #TUIPEN_ACTIVETEXT
    flags.l | #TUIF_ACTIVE
  Else
    bgTuiPen.l = #TUIPEN_HALFSHINE
    fgTuiPen.l = #TUIPEN_TEXT
  End If

  If (*tuiSlider\obj\borderType=#TUIBORDER_NONE)
    containerPen.l = #TUIPEN_BACKGROUND;_ntui_GetBGTuiPen{*tuiSlider}
  Else
    If (*tuiSlider\obj\flags&#TUIF_DISABLED)
      containerPen = #TUIPEN_BACKGROUND
    Else
      containerPen = #TUIPEN_HALFSHADOW
    End If
  End If

  If (*tuiSlider\obj\flags&#TUIF_DISABLED)
    sliceBright.l = #TUIPEN_HALFSHINE
    sliceDark.l   = #TUIPEN_HALFSHADOW
  Else
    sliceBright.l = #TUIPEN_HALFSHINE
    sliceDark.l   = #TUIPEN_SHADOW
  End If

  ;/* calculate layout */
  cbox.tuiRect\left = *tui\cbox\left,*tui\cbox\top,*tui\cbox\right,*tui\cbox\bottom
  kbox.tuiRect\left = *tui\cbox\left,*tui\cbox\top,*tui\cbox\right,*tui\cbox\bottom
  lbox.tuiRect\left = *tui\cbox\left,*tui\cbox\top,*tui\cbox\right,*tui\cbox\bottom

  Select *tuiSlider\orientation
    Case #TUIORIENT_HORIZONTAL
      lbox\left    = lbox\right - *tuiSlider\labelSize\x +1
      cbox\right   = lbox\left - 1
      ;sliderSize.l = !tuiRectWidth{cbox} - *tuiSlider\knobSize\x
      ;kbox\left    =
      !_ntui_HSlider_Value2Pixel{kbox\left,*tui\value} ;,sliderSize}
      kbox\left + cbox\left
      kbox\right   = kbox\left+*tuiSlider\knobSize\x-1

    Case #TUIORIENT_VERTICAL
      lbox\top     = lbox\bottom - *tuiSlider\labelSize\y +1
      cbox\bottom  = lbox\top - 1
;      sliderSize.l = !tuiRectHeight{cbox} - *tuiSlider\knobSize\y
      ;kbox\top     =
      !_ntui_VSlider_Value2Pixel{kbox\top,*tui\value} ;,sliderSize}
      kbox\top + cbox\top
      kbox\bottom  = kbox\top+*tuiSlider\knobSize\y-1

  End Select

  ;/* draw the container */
  If kbox\left  <*tui\cbox\left   Then kbox\left   = *tui\cbox\left
  If kbox\top   <*tui\cbox\top    Then kbox\top    = *tui\cbox\top
  If kbox\right >*tui\cbox\right  Then kbox\right  = *tui\cbox\right
  If kbox\bottom>*tui\cbox\bottom Then kbox\bottom = *tui\cbox\bottom

  If bgDone=False
  SetAPen_ *rp,*tuiEngine\pen[containerPen]
  If kbox\left  >cbox\left   Then RectFill_ *rp,cbox\left   ,cbox\top     ,kbox\left-1 ,cbox\bottom
  If kbox\top   >cbox\top    Then RectFill_ *rp,cbox\left   ,cbox\top     ,cbox\right  ,kbox\top-1
  If kbox\right <cbox\right  Then RectFill_ *rp,kbox\right+1,cbox\top     ,cbox\right  ,cbox\bottom
  If kbox\bottom<cbox\bottom Then RectFill_ *rp,cbox\left   ,kbox\bottom+1,cbox\right  ,cbox\bottom
  End If

  xm.l = (cbox\left+cbox\right )/2
  ym.l = (cbox\top +cbox\bottom)/2

  Select *tuiSlider\orientation
    Case #TUIORIENT_HORIZONTAL

      SetAPen_ *rp,*tuiEngine\pen[sliceBright]
      Move_ *rp,cbox\left +*tuiSlider\knobSize\x/2+1,ym-1
      Draw_ *rp,cbox\left +*tuiSlider\knobSize\x/2+1,ym+2

      Move_ *rp,cbox\left +*tuiSlider\knobSize\x/2,ym+1
      Draw_ *rp,cbox\right-*tuiSlider\knobSize\x/2,ym+1

      Move_ *rp,cbox\right-*tuiSlider\knobSize\x/2+1,ym-1
      Draw_ *rp,cbox\right-*tuiSlider\knobSize\x/2+1,ym+2

      ;WritePixel_ *rp,x1+*tuiSlider\knobSize/2,ym+2
      ;WritePixel_ *rp,x2-*tuiSlider\knobSize/2,ym+2

      SetAPen_ *rp,*tuiEngine\pen[sliceDark]
      Move_ *rp,cbox\left +*tuiSlider\knobSize\x/2,ym
      Draw_ *rp,cbox\right-*tuiSlider\knobSize\x/2,ym

      Move_ *rp,cbox\right-*tuiSlider\knobSize\x/2,ym-1
      Draw_ *rp,cbox\right-*tuiSlider\knobSize\x/2,ym+1

      Move_ *rp,cbox\left+*tuiSlider\knobSize\x/2,ym-1
      Draw_ *rp,cbox\left+*tuiSlider\knobSize\x/2,ym+1

    Case #TUIORIENT_VERTICAL

      SetAPen_ *rp,*tuiEngine\pen[sliceBright]
      Move_ *rp,xm-1,cbox\top+*tuiSlider\knobSize\y/2+1
      Draw_ *rp,xm+2,cbox\top+*tuiSlider\knobSize\y/2+1

      Move_ *rp,xm+1,cbox\top+*tuiSlider\knobSize\y/2
      Draw_ *rp,xm+1,cbox\bottom-*tuiSlider\knobSize\y/2

      Move_ *rp,xm-1,cbox\bottom-*tuiSlider\knobSize\y/2+1
      Draw_ *rp,xm+2,cbox\bottom-*tuiSlider\knobSize\y/2+1

      ;WritePixel_ *rp,xm+2,cbox\top+*tuiSlider\knobSize/2
      ;WritePixel_ *rp,xm+2,cbox\bottom-*tuiSlider\knobSize/2

      SetAPen_ *rp,*tuiEngine\pen[sliceDark]
      Move_ *rp,xm,cbox\top+*tuiSlider\knobSize\y/2
      Draw_ *rp,xm,cbox\bottom-*tuiSlider\knobSize\y/2

      Move_ *rp,xm-1,cbox\bottom-*tuiSlider\knobSize\y/2
      Draw_ *rp,xm+1,cbox\bottom-*tuiSlider\knobSize\y/2

      Move_ *rp,xm-1,cbox\top+*tuiSlider\knobSize\y/2
      Draw_ *rp,xm+1,cbox\top+*tuiSlider\knobSize\y/2

  End Select


  ;/* draw the knob */
  _ntui_GetBorderSize{*tuiEngine,#TUIBORDER_BUTTON,*tuiSlider\obj\flags,bs.tuiRect}
  bgDone = ntui_DrawBorder{*tuiEngine,*rp,kbox,#TUIBORDER_BUTTON,flags}
  !tuiRemBorder{kbox,kbox,bs}
  If bgDone Then bgTuiPen = #TUIPEN_TRANSPARENT
  If (*tuiSlider\obj\flags&#TUIF_UNDERLINED><0) AND (*tuiSlider\obj\string><#NULL)
    text.s = _ntui_CreateSliderLabel{*tuiSlider,*tuiSlider\obj\value}
    *tmp.str = #NULL
    str_Write{&*tmp,text}
    _ntui_Print{*tuiEngine,*tmp,kbox,kbox,*tuiSlider\obj\flags-#TUIF_UNDERLINED,*rp,fgTuiPen,bgTuiPen}
    str_Free{&*tmp}
  Else
    If bgTuiPen>=0
    SetAPen_ *rp,*tuiEngine\pen[bgTuiPen]
    RectFill_ *rp,kbox\left,kbox\top,kbox\right,kbox\bottom
    End If

    Select *tuiSlider\orientation
      Case #TUIORIENT_HORIZONTAL
        kx.l = (kbox\left+kbox\right)/2
        ky1.l = kbox\top  + 4
        ky2.l = kbox\bottom - 4

        If (!tuiRectWidth{kbox}>8)
          SetAPen_ *rp,*tuiEngine\pen[#TUIPEN_HALFSHADOW]
          Move_ *rp,kx    , ky1
          Draw_ *rp,kx    , ky2

          SetAPen_ *rp,*tuiEngine\pen[#TUIPEN_WHITE]
          Move_ *rp,kx +1 , ky1
          Draw_ *rp,kx +1 , ky2
        End If

      Case #TUIORIENT_VERTICAL
        kx1.l = kbox\left  + 4
        kx2.l = kbox\right - 4
        ky.l = (kbox\top+kbox\bottom)/2

        If (!tuiRectHeight{kbox}>8)
          SetAPen_ *rp,*tuiEngine\pen[#TUIPEN_HALFSHADOW]
          Move_ *rp,kx1 , ky
          Draw_ *rp,kx2 , ky

          SetAPen_ *rp,*tuiEngine\pen[#TUIPEN_WHITE]
          Move_ *rp,kx1 , ky +1
          Draw_ *rp,kx2 , ky +1
        End If

    End Select


  End If

  ;/* draw the label */
  If *tuiSlider\obj\string><#NULL AND *tuiSlider\labelSize\x>0
    text.s = _ntui_CreateSliderLabel{*tuiSlider,*tuiSlider\obj\value}
    *tmp.str = #NULL
    str_Write{&*tmp,text}
    _ntui_Print{*tuiEngine,*tmp,lbox,lbox,*tuiSlider\obj\flags,*rp,#TUIPEN_TEXT,#TUIPEN_HALFSHINE}
    str_Free{&*tmp}
  End If

End Statement

;///////////////////////////////////////////////////////////////////////////////
;/                                                                             /
;/ Syntax: _ntui_FreeSlider {*tuiSlider.tuiSlider}                             /
;/                                                                             /
;/ Description:                                                                /
;/ * free everything special that we allocated for the Slider */               /
;/ If *tuiSlider\img Then ntui_FreeImage{*tuiSlider\img}  : *tuiSlider\img =:: /
;/  #NULL                                                                     /
;/                                                                             /
;/ Inputs:                                                                     /
;/ - *tuiSlider.tuiSlider    : ???                                             /
;/                                                                             /
;///////////////////////////////////////////////////////////////////////////////
_ntui_DeinitSlider:
Statement _ntui_DeinitSlider{*tuiSlider.tuiSlider}
  !_ASSERT{*tuiSlider}
  !_ASSERT{*tuiSlider\obj\classID=#TUICLASS_SLIDER}
  ;If *tuiSlider\img Then ntui_FreeImage{*tuiSlider\img}  : *tuiSlider\img = #NULL
End Statement


;///////////////////////////////////////////////////////////////////////////////
;/                                                                             /
;/ Syntax: result.l = ntui_VSlider {value.l,minValue.l,maxValue.l,@notify.l,:: /
;/ @help.s,@flags.l,@userID.l}                                                 /
;/                                                                             /
;/ Description:                                                                /
;/ Create function for tuiSlider.                                         /
;/                                                                             /
;/ Inputs:                                                                     /
;/ - value.l    : ???                                                          /
;/ - minValue.l    : ???                                                       /
;/ - maxValue.l    : ???                                                       /
;/ - notify.l    : ???                                                         /
;/ - help.s    : ???                                                           /
;/ - flags.l    : ???                                                          /
;/ - userID.l    : ???                                                         /
;/                                                                             /
;/ Result:                                                                     /
;/ - result.l     : ???                                                        /
;/                                                                             /
;///////////////////////////////////////////////////////////////////////////////
Function.tuiSlider ntui_VSlider{value.l,minValue.l,maxValue.l,@label.s,@onClick.s,@help.s,@flags.l,@nameID.s}
If (flags=#TUI_NOFLAGS) Then flags=0
flags | #TUIF_CENTER|#TUIF_MIDDLE |#TUIF_FIXWIDTH|#TUIF_WANTTAB
*tuiSlider.tuiSlider = (.tuiSlider)_ntui_CreateObject{#TUICLASS_SLIDER,SizeOf.tuiSlider,help,flags,nameID}
If *tuiSlider
  If minValue>maxValue Then x.l=minValue:minValue=maxValue:maxValue=x
  _ntui_UpdateSlider{*tuiSlider,value,minValue,maxValue}
  *tuiSlider\step_          = 1
  *tuiSlider\knobSize\x     = *tuiSlider\obj\tuiEngine\defSize[#TUISIZE_KNOB],*tuiSlider\obj\tuiEngine\defSize[#TUISIZE_KNOB]
  *tuiSlider\obj\padding\left    = 0,0,0,0
  If label Then str_Write{&*tuiSlider\obj\string,label}
  *tuiSlider\orientation    = #TUIORIENT_VERTICAL
  *tuiSlider\obj\borderType = #TUIBORDER_NONE
  str_Write{&*tuiSlider\obj\onClick,onClick}

  !_GetFuncPointer{*tuiSlider\obj\Draw            ,_ntui_DrawSlider,{0,0,0}}
;  !_GetFuncPointer{*tuiSlider\obj\Layout          ,_ntui_LayoutSlider,{0,0,0}}
  !_GetFuncPointer{*tuiSlider\obj\DispatchEvent   ,_ntui_DispatchSliderEvent,{0,0}}
  !_GetFuncPointer{*tuiSlider\obj\CalculateMinSize,_ntui_CalculateSliderMinSize,{0,0}}
  !_GetFuncPointer{*tuiSlider\obj\Deinit          ,_ntui_DeinitSlider,{0}}
  !_GetFuncPointer{*tuiSlider\obj\SetAttr         ,_ntui_SetSliderAttr,{0,0,0}}
  !_GetFuncPointer{*tuiSlider\obj\GetAttr         ,_ntui_GetSliderAttr,{0,0,0}}

End If
Function Return *tuiSlider
End Function


;///////////////////////////////////////////////////////////////////////////////
;/                                                                             /
;/ Syntax: result.l = ntui_HSlider {value.l,minValue.l,maxValue.l,@notify.l,:: /
;/ @help.s,@flags.l,@userID.l}                                                 /
;/                                                                             /
;/ Description:                                                                /
;/ * create function for tui Slider */                                         /
;/                                                                             /
;/ Inputs:                                                                     /
;/ - value.l    : ???                                                          /
;/ - minValue.l    : ???                                                       /
;/ - maxValue.l    : ???                                                       /
;/ - notify.l    : ???                                                         /
;/ - help.s    : ???                                                           /
;/ - flags.l    : ???                                                          /
;/ - userID.l    : ???                                                         /
;/                                                                             /
;/ Result:                                                                     /
;/ - result.l     : ???                                                        /
;/                                                                             /
;///////////////////////////////////////////////////////////////////////////////
Function.tuiSlider ntui_HSlider{value.l,minValue.l,maxValue.l,@label.s,@onClick.s,@help.s,@flags.l,@nameID.s}
*tuiSlider.tuiSlider = ntui_VSlider{value,minValue,maxValue,label,onClick,help,flags,nameID}
If *tuiSlider
  *tuiSlider\orientation = #TUIORIENT_HORIZONTAL
  *tuiSlider\obj\flags   = (*tuiSlider\obj\flags| #TUIF_FIXSIZE) - #TUIF_FIXWIDTH
End If
Function Return *tuiSlider
End Function




