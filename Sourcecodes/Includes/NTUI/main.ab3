; XTRA
; Embedded .xtra Header
; 
; General Info
; -------------------------------------------------------
; ExePath         = "//Bin"
; ExeFile         = "NTUIDemo"
; CreateIcon      = 0
; Residents       = "all.res,SDSTCP2.res,xpk.res,ttengine.res"
; 
; Compiler
; -------------------------------------------------------
; StringBuffer    = 10240
; MakeSmallest    = 1
; Version         = 0.1.0
; NumberOfBuilds  = 3327
; 
; Debugger
; -------------------------------------------------------
; CliArgs         = ""
; StackSize       = 100000
; RuntimeDebug    = 1
; DebugInfo       = 0
; CreateDbgFile   = 0
; OverflowCheck   = 0
; AssemblerCheck  = 0
; InterruptCheck  = 1
; AutoRun         = 1
; 
; Editor
; -------------------------------------------------------
; CursorLine      = 1735
; CursorColumn    = 64
; LabelSearch     = "too"
; LabelRemark     = 0
; LabelAll        = 0
; LabelPosition   = 0
; 
; Blitz Objects
; -------------------------------------------------------
; Max IconInfo    = 1
; Max NChunky     = 50
; Max MUIObject   = 50
; Max PTModule    = 5
; Max AsyncReq    = 4
; Max Req-Lib     = 5
; Max GTMenuList  = 5
; Max Console     = 5
; Max TCPSock     = 5
; Max XBSound     = 10
; Max Chunky      = 20
; Max File        = 5
; Max GadgetList  = 5
; Max Queue       = 10
; Max Screen      = 5
; Max Shape       = 100
; Max CopList     = 10
; Max Sprite      = 20
; Max Stencil     = 5
; Max Module      = 5
; Max Window      = 20
; Max Anim        = 10
; Max Sound       = 10
; Max Bank        = 5
; Max Buffer      = 10
; Max BitMap      = 10
; Max Slice       = 10
; Max Page        = 4
; Max Tape        = 5
; Max IntuiFont   = 5
; Max MedModule   = 8
; Max Palette     = 4
; Max MenuList    = 5
; Max BlitzFont   = 4
; Max GTList      = 20
; /XTRA
;///////////////////////////////////////////////////////////////////////////////
;/                                                                             /
;/ Name: ntui.include                                                          /
;/                                                                             /
;/ Platforms: Classic, WinUAE, Amithlon, MorphOS, AmigaOS4                     /
;/                                                                             /
;/ Date: 06/01/2009                                                            /
;/                                                                             /
;/ Author: Thilo Koehler                                                       /
;/                                                                             /
;/ Requirements:  Amiblitz3                                                    /
;/                                                                             /
;/ Purpose:                                                                    /
;/ Implementation of NTUI Toolkit (New Thilo's User Interface)                 /
;/                                                                             /
;/ Abstract:                                                                   /
;/ * no abstract available *                                                   /
;/                                                                             /
;/ User Constants:                                                             /
;/ #TUIINQUEUE_MAX = 16 ; define the maximum events in input queue                                                          /
;/ #TUI_DEBUG      = 0  ; define debug mode                                         /
;/                                                                             /
;///////////////////////////////////////////////////////////////////////////////
optimize 7
Syntax 6
#NTUI_DEBUG = 0
#NTUI_SAFERESIZE = 0

XINCLUDE "NTUI/definitions.ab3"
Syntax 6

CNIF #NTUI_DEBUG
Function.l RNDPen{}

  seed.l = Peek.l(?seed) * 1234567 + 12345
  Poke.l ?seed,seed
  Function Return ((seed ASR 8)&$0F)

  .seed
  Dc.l 0

End Function
CEND

;/* get the window of a tuiObject, if it has any */
Function.tuiWindow FAST ntui_GetWindow{*obj.tuiObject}
  !_ASSERT{*obj}

  *win.tuiWindow = Null
  breakme.l = False

  Repeat
    If *obj
      If (*obj\classID = #TUICLASS_WINDOW) Then *win = (.tuiWindow)*obj : breakme=True
      *obj = *obj\parent
    Else
      breakme=True
    End If
  Until breakme

  Function Return *win

End Function


Function.tuiWindow FAST ntui_GetRootWindow{*obj.tuiObject}
  !_ASSERT{*obj}

  *win.tuiWindow = Null

  While *obj
    If *obj\classID = #TUICLASS_WINDOW Then *win = (.tuiWindow)*obj
    *obj = *obj\parent
  Wend

  Function Return *win

End Function


;/* get the rastport of a tuiObject */
Function.RastPort FAST ntui_ObtainRastPort{*obj.tuiObject}
  !_ASSERT{*obj}

  *rp.RastPort = Null

  *win.tuiWindow = ntui_GetWindow{*obj}
  If *win
    If *win\oswin
      CNIF #NTUI_SAFERESIZE
        ObtainSemaphore_ *win\lock
        CNIF #__debug=0
        ;LockLayer_ 0,*win\oswin\WLayer
        LockLayerInfo_ *win\oswin\WScreen\LayerInfo
        CEND
        *rp = *win\rp;win\RPort
      CELSE
        *rp = *win\oswin\RPort
      CEND
    End If
  Else
    CNIF #NTUI_SAFERESIZE
    ObtainSemaphore_ *obj\engine\lock
    CEND
    *rp = *obj\engine\buildRP
  End If

  Function Return *rp

End Function


;/* release the rastport of a tuiObject */
Statement FAST ntui_ReleaseRastPort{*obj.tuiObject}
  !_ASSERT{*obj}

  *win.tuiWindow = ntui_GetWindow{*obj}
  If *win
    If *win\oswin
      CNIF #NTUI_SAFERESIZE
        ReleaseSemaphore_ *win\lock
        CNIF #__debug=0
        ;UnlockLayer_ *win\oswin\WLayer
        UnlockLayerInfo_ *win\oswin\WScreen\LayerInfo
        CEND
      CEND
    End If
  Else
    CNIF #NTUI_SAFERESIZE
    ReleaseSemaphore_ *obj\engine\lock
    CEND
  End If

End Statement


;/ include utility functions */
XINCLUDE "mousepointer.ab3"
XINCLUDE "tempbuffer.ab3"
XINCLUDE "file.ab3"
XINCLUDE "png.ab3"
XINCLUDE "bitmap.ab3"
XINCLUDE "border.ab3"
XINCLUDE "asl.ab3"
XINCLUDE "arexx.ab3"
XINCLUDE "app.ab3"
XINCLUDE "event.ab3"
XINCLUDE "global.ab3"
XINCLUDE "drawing.ab3"
XINCLUDE "tree.ab3"
XINCLUDE "clipboard.ab3"
XINCLUDE "undo.ab3"
Syntax 6
;///////////////////////////////////////////////////////////////////////////////
;/                                                                             /
;/ Syntax: _ntui_FreeObject {*obj.tuiObject}                                   /
;/                                                                             /
;/ Description:                                                                /
;/ private: free a tui object and all children                                 /
;/                                                                             /
;/ Inputs:                                                                     /
;/ - *obj.tuiObject    : tuiObject to free                                     /
;/                                                                             /
;///////////////////////////////////////////////////////////////////////////////
Statement FAST ntui_FreeObject{*obj.tuiObject}
  !_ASSERT{*obj}

  ntui_FlushObjEvents{*obj}

  While (*obj\child)      ; free all children
    ntui_FreeObject{*obj\child}
  Wend

  If *obj\Deinit Then !_CallFuncPointer{*obj\Deinit,ret.l,*obj}

  If *obj\child Then error{"\\__THIS_FUNCTION: Tui object still has children after free! classID="+Str$(*obj\classID)+"!"} : Statement Return

  If *obj\prev  Then *obj\prev\next_ = *obj\next_   ; relink previous
  If *obj\next_ Then *obj\next_\prev = *obj\prev    ; relink next

  If *obj\parent                                    ; relink parent
    If *obj\parent\child = *obj
      *obj\parent\child = *obj\prev
      If *obj\parent\child = Null Then *obj\parent\child = *obj\next_  ;: *obj\next_\parent = *obj\parent
    End If
  End If

  str_Free{&*obj\string}
  str_Free{&*obj\help}
  str_Free{&*obj\nameID}
  ;str_Free{&*obj\onClick}
  ;str_Free{&*obj\onMouseOver}
  ;str_Free{&*obj\onDoubleClick}
  ;str_Free{&*obj\onTouch}
  ;str_Free{&*obj\onRelease}
  ;str_Free{&*obj\onKeyDown}
  ;str_Free{&*obj\onKeyRepeat}
  ;str_Free{&*obj\onKeyUp}
  ;str_Free{&*obj\onBeginRefresh}
  ;str_Free{&*obj\onEvent\notifyID}
  ;str_Free{&*obj\onRefresh\notifyID}
  ;str_Free{&*obj\onFocus\notifyID}
  ;str_Free{&*obj\onLoseFocus\notifyID}
  ;str_Free{&*obj\onHelp}

  If *obj\fgImage Then ntui_FreeTBImage{*obj\fgImage}  : *obj\fgImage = Null
  If *obj\bgImage Then ntui_FreeTBImage{*obj\bgImage}  : *obj\bgImage = Null

  FreeVec_ *obj

End Statement


;///////////////////////////////////////////////////////////////////////////////
;/                                                                             /
;/ Syntax: result.l = _ntui_GetMinSize {*obj.tuiObject,*rp.RastPort}           /
;/                                                                             /
;/ Description:                                                                /
;/ private: calculate the minimum size needed by the tui object                /
;/                                                                             /
;/ Inputs:                                                                     /
;/ - *obj.tuiObject  : ???                                                     /
;/ - *rp.RastPort    : ???                                                     /
;/                                                                             /
;/ Result:                                                                     /
;/ - result.l     : ???                                                        /
;/                                                                             /
;///////////////////////////////////////////////////////////////////////////////
Statement _ntui_CalculateMinSize{*obj.tuiObject,*rp.RastPort}
  !_ASSERT{*obj}
  !_ASSERT{*rp}

  initsize.tuiPixel\x = *obj\minsize\x,*obj\minsize\y
  If (*obj\CalculateMinSize)
    !_CallFuncPointer{*obj\CalculateMinSize,ret.l,*obj,*rp}
  Else
    _ntui_GetBorderSize{*obj\engine,*obj\borderType,*obj\flags,*obj\border}
    *obj\minsize\x = *obj\border\left + *obj\border\right + *obj\padding\left + *obj\padding\right
    *obj\minsize\y = *obj\border\top  + *obj\border\bottom+ *obj\padding\top  + *obj\padding\bottom
  End If

  If initsize\x>*obj\minsize\x Then *obj\minsize\x = initsize\x
  If initsize\y>*obj\minsize\y Then *obj\minsize\y = initsize\y

End Statement



; re-draw a tuiObject and recursively all its child objects dependent on the #TUIF_DIRTY flag.
; The tuiObject must be visible, otherwise it will be ignored.
Statement _ntui_Redraw{*obj.tuiObject,*clip.tuiRect}
  !_ASSERT{*obj}

  If (*obj\flags&#TUIF_ONSCREEN=0) Then Statement Return

  *obj\flags = (*obj\flags | #TUIF_DIRTY) - #TUIF_DIRTY
  If *clip
    !tuiAndRect{rpclip.tuiRect,*obj\bbox,*clip}
  Else
    !tuiCopyRect{rpclip.tuiRect,*obj\bbox}
  End If

  ; TODO: while #TUIF_WANTBG, and set clip
  While *obj\classID><#TUICLASS_WINDOW AND *obj\parent
    *obj = *obj\parent
    !tuiAndRect{rpclip,rpclip,*obj\bbox}
  Wend

  If (*obj\classID=#TUICLASS_WINDOW)
    *rp.RastPort = ntui_ObtainRastPort{*obj}
    If *rp
      _ntui_Draw{*obj,*rp,rpclip}
      ntui_ReleaseRastPort{*obj}
    End If
  End If

End Statement



; /* calculate the layout of a tui Object and all its children */
Statement FAST _ntui_Layout{*obj.tuiObject,*rp.RastPort,*bbox.tuiRect}
  !_ASSERT{*obj}

  If *rp=Null
    If *obj\engine\screen_
      *rp = *obj\engine\screen_\RastPort
    End If
  End If
  If *rp=Null Then Statement Return ; there is no rastport!
  ;!_ASSERT{*rp}
  ; !_ASSERT{*bbox} <= might be NULL if not defined yet!

  *obj\flags = *obj\flags | (#TUIF_ONSCREEN|#TUIF_DIRTY)

;  If Left$(str_Read{&*obj\nameID},1)="x"
;    error{"Layout object: "+str_Read{&*obj\nameID}+"("+Str$(!tuiRectWidth{*bbox})+"/"+Str$(!tuiRectHeight{*bbox})+")"}
;  End If

  If (*obj\Layout)
    !_CallFuncPointer{*obj\Layout,ret.l,*obj,*rp,*bbox}
  Else ; default layout function
    If *bbox
      !tuiCopyRect{*obj\bbox,*bbox}
    End If
    !tuiRemBorder{*obj\ibox,*obj\bbox,*obj\border}
    !tuiRemBorder{*obj\cbox,*obj\ibox,*obj\padding}
  End If

  If (*obj\flags & #TUIF_INVISIBLE) Then *obj\flags = (*obj\flags | #TUIF_ONSCREEN) - #TUIF_ONSCREEN
  ; TODO: Check if bbox is actually on window

End Statement


;///////////////////////////////////////////////////////////////////////////////
;/                                                                             /
;/ Syntax: result.l = ntui_GetObjectByID {*obj.tuiObject,nameID.s}
;/                                                                             /
;/ Description:                                                                /
;/ Find a tui object by nameID and return its pointer
;/                                                                             /
;/ Inputs:                                                                     /
;/ - *obj.tuiObject : tuiObject from were to start the search (typically a tuiEngine or tuiWindow)                                                   /
;/ - nameID.s       : nameID of the tuiObject                                                         /
;/                                                                             /
;/ Result:                                                                     /
;/ - result.l       : pointer to the tuiObject or NULL if not found                                                       /
;/                                                                             /
;///////////////////////////////////////////////////////////////////////////////
Function.tuiObject ntui_GetObjectByID{*obj.tuiObject,ID.s}
  !_ASSERT{*obj}

  l.l = &ID-4
  *IDs.str = l ; this is ugly; I know
  If (str_Comp{*obj\nameID,*IDs}=0) Then Function Return *obj

  *obj = *obj\child
  While (*obj><Null)
    *found.tuiObject = ntui_GetObjectByID{*obj,ID}
    If *found Then Function Return *found
    *obj=*obj\next_
  Wend

  Function Return Null

End Function



; get the previos object in the tab-cycle chain
Function.tuiObject ntui_GetPrevTabObject{*obj.tuiObject}

  While *obj><Null
    If *obj\child            ; descend
      *obj = *obj\child
      While *obj\next_ : *obj=*obj\next_ : Wend ; go to the last child
      Function Return *obj
    Else
      If *obj\prev
        Function Return *obj\prev  ; go side
      Else
        While *obj\parent><Null ; go up
          *obj = *obj\parent
          If *obj\prev
            Function Return *obj\prev  ; go side
          End If
        Wend
        *obj=Null
      End If
    End If
  Wend

  Function Return *obj

End Function


; get the next object in the tab-cycle chain
Function.tuiObject ntui_GetNextTabObject{*obj.tuiObject}

  While *obj><Null
    If *obj\child            ; descend
      Function Return *obj\child
    Else
      If *obj\next_
        Function Return *obj\next_  ; go side
      Else
        While *obj\parent><Null
          *obj = *obj\parent
          If *obj\next_
            Function Return *obj\next_  ; go side
          End If
        Wend
        *obj=Null
      End If
    End If
  Wend

  Function Return *obj

End Function


; begin the building process.
; This need to be called always before building any new tuiObject
Statement ntui_BeginBuild{*parent.tuiObject}

  ObtainSemaphore_ tuiGlobal\buildHelper\lock

  If *parent
    tuiGlobal\buildHelper\engine = *parent\engine
    tuiGlobal\buildHelper\prev   = Null
    *child.tuiObject = *parent\child
    While *child
      tuiGlobal\buildHelper\prev = *child
      *child = *child\next_
    Wend
  Else
    tuiGlobal\buildHelper\engine = Null
    tuiGlobal\buildHelper\prev   = Null
  End If
  tuiGlobal\buildHelper\parent   = *parent
  tuiGlobal\buildHelper\current  = Null
  tuiGlobal\buildHelper\windowDepth = 0
  tuiGlobal\buildHelper\rootWin  = Null

End Statement


Statement ntui_EndBuild{}

  tuiGlobal\buildHelper\engine   = Null
  tuiGlobal\buildHelper\parent   = Null
  tuiGlobal\buildHelper\current  = Null
  tuiGlobal\buildHelper\prev     = Null
  tuiGlobal\buildHelper\windowDepth = 0
  tuiGlobal\buildHelper\rootWin  = Null

  ReleaseSemaphore_ tuiGlobal\buildHelper\lock

End Statement



;///////////////////////////////////////////////////////////////////////////////
;/                                                                             /
;/ Syntax: result.l = ntui_GetObjectByID {*obj.tuiObject,@userID.l,@typeID.l:: /
;/ ,@notify.l}                                                                 /
;/                                                                             /
;/ Description:                                                                /
;/ Find a tui object by userID, optionally add typeID and notify value for more restricted search */    /
;/                                                                             /
;/ Inputs:                                                                     /
;/ - *obj.tuiObject : tuiObject from were to start the search (typically a tuiEngine or tuiWindow)                                                   /
;/ - userID.l       : userID of the tuiObject                                                         /
;/ - typeID.l       : typeID of the tuiObject                                                         /
;/ - notify.l       : notify value of the tuiObject                                                         /
;/                                                                             /
;/ Result:                                                                     /
;/ - result.l       : pointer to the tuiObject or NULL if not found                                                       /
;/                                                                             /
;///////////////////////////////////////////////////////////////////////////////
;Function.l _ntui_GetObjectByID{*obj.tuiObject,*objID.str}
;!_ASSERT{*obj}
;If str_Comp{*obj\objID,*objID}=0 Then Function Return *obj
;*obj = *obj\child
;While *obj><Null
;  *objfind.tuiObject = _ntui_GetObjectByID{*obj,*objID}
;  If *objfind Then Function Return *objfind
;  *obj=*obj\next_
;Wend
;Function Return Null
;End Function
;
;; Amiblitz Stub
;Function.l ntui_GetObjectByID{*obj.tuiObject,userID.s}
;str_Write{&*userID.str,userID}
;*obj = _ntui_GetObjectByID{*obj,*userID}
;str_Free{&*userID}
;Function Return *obj
;End Function



;///////////////////////////////////////////////////////////////////////////////
;/                                                                             /
;/ Syntax: ntui_SetFlags {flags.l,@nochildren.l,@*obj.tuiObject}               /
;/                                                                             /
;/ Description:                                                                /
;/ Set flags of a tuiObject                              /
;/                                                                             /
;/ Inputs:                                                                     /
;/ - flags.l        : tuiFlags to set (#TUIF_...)                                                         /
;/ - nochildren.l   : if false, the flags are recursively set for all its children tuiObjects                                                     /
;/ - *obj.tuiObject : tuiObject, if not during build-time                                                   /
;/                                                                             /
;///////////////////////////////////////////////////////////////////////////////
Statement FAST ntui_SetFlags{*obj.tuiObject,flags.l,@nochildren.l}
  !_ASSERT{*obj}

  *obj\flags|flags
  If nochildren=False
    *obj = *obj\child
    While *obj
      ntui_SetFlags{*obj,flags,nochildren}
      *obj = *obj\next_
    Wend
  End If

End Statement



;///////////////////////////////////////////////////////////////////////////////
;/                                                                             /
;/ Syntax: ntui_ClearFlags {flags.l,@nochildren.l,@*obj.tuiObject}             /
;/                                                                             /
;/ Description:                                                                /
;/ Reset flags of a tui object                          /
;/                                                                             /
;/ Inputs:                                                                     /
;/ - flags.l        : tuiFlags to reset (#TUIF_...)                                                         /
;/ - nochildren.l   : if false, the flags are recursively set for all its children tuiObjects                                                     /
;/ - *obj.tuiObject : tuiObject, if not during build-time                                                   /
;/                                                                             /
;///////////////////////////////////////////////////////////////////////////////
Statement FAST ntui_ClearFlags{*obj.tuiObject,flags.l,@nochildren.l}
  !_ASSERT{*obj}

  *obj\flags = (*obj\flags|flags) - flags
  If nochildren=False
    *obj = *obj\child
    While *obj
      ntui_ClearFlags{*obj,flags,nochildren}
      *obj = *obj\next_
    Wend
  End If

End Statement



;///////////////////////////////////////////////////////////////////////////////
;/                                                                             /
;/ Syntax: ntui_Refresh {*obj.tuiObject}                                       /
;/                                                                             /
;/ Description:                                                                /
;/ Make a tuiObject dirty, so that it gets refreshed the next time the ntui Engine gets the focus.
;/                                                                             /
;/ Inputs:                                                                     /
;/ - *obj.tuiObject    : tuiObject to refresh                                                  /
;/                                                                             /
;///////////////////////////////////////////////////////////////////////////////
Statement ntui_Refresh{*obj.tuiObject,*clip.tuiRect}
  ;If *obj=Null Then Statement Return
  !_ASSERT{*obj}

  If (*obj\flags&#TUIF_ONSCREEN)
  ;If ((*obj\flags&(#TUIF_ONSCREEN|#TUIF_DIRTY))=#TUIF_ONSCREEN )
  ;  If (*obj\flags&(#TUIF_DIRTY)=0) Then ntui_SetFlags{*obj,#TUIF_DIRTY,False}
    *obj\flags|#TUIF_DIRTY
    If *clip
      ntui_PostEvent{*obj,#TUIEV_REFRESH,0,"",*clip\left,*clip\top,!tuiRectWidth{*clip},!tuiRectHeight{*clip}}
    Else
      ntui_PostEvent{*obj,#TUIEV_REFRESH}
    End If
  End If

End Statement


;///////////////////////////////////////////////////////////////////////////////
;/                                                                             /
;/ Syntax: ntui_SetFocus {*obj.tuiObject}                                      /
;/                                                                             /
;/ Description:                                                                /
;/ Set the foucs to a given tuiObject.
;/ The tuiObject will receive the user input then. Most tuiObjects change their visual appearance slightly.                                                /
;/ Note: This is the same as if the tuiObject was hit with the mouse. If the user selects another tuiObject, the foucs is gone.
;/                                                                             /
;/ Inputs:                                                                     /
;/ - *obj.tuiObject    : tuiObject                                                   /
;/                                                                             /
;///////////////////////////////////////////////////////////////////////////////
Statement ntui_SetFocus{*obj.tuiObject}
  ;If *obj=Null Then Statement Return
  !_ASSERT{*obj}
  *engine.tuiEngine=*obj\engine
  !_ASSERT{*engine}
  If *obj=*engine\focus Then Statement Return ; has already the focus

  *win.tuiWindow = ntui_GetWindow{*obj}

  If *win=Null Then Statement Return

  If *obj=*win\focus Then Statement Return ; has already the focus

  If *win\focus ; remove the focus
    If (*win\focus\flags&#TUIF_FOCUS)
      *win\focus\flags|#TUIF_FOCUS - #TUIF_FOCUS
      ntui_PostEvent{*win\focus,#TUIEV_LOSEFOCUS}
      ;ntui_Refresh{*win\focus,Null} ; this is up to the widget that should react on focus if it needs a refresh
    End If
    *win\focus = Null
  End If

  If *obj><*engine ; set the focus
    ;If ((*obj\flags&#TUIF_DISABLED)=0)
      *win\focus = *obj
      If (*obj\flags&#TUIF_FOCUS)=0
        *obj\flags|#TUIF_FOCUS
        ntui_PostEvent{*obj,#TUIEV_FOCUS}
        ;ntui_Refresh{*obj,Null} ; this is up to the widget that should react on focus if it needs a refresh
      End If
    ;End If
  End If

  *engine\focus = *win\focus

End Statement



;///////////////////////////////////////////////////////////////////////////////
;/                                                                             /
;/ Syntax: _ntui_SetMover {*win.tuiWindow,*obj.tuiObject}                /
;/                                                                             /
;/ Description:                                                                /
;/ private
;/                                                                             /
;/ Inputs:                                                                     /
;/ - *win.tuiWindow    : ???                                             /
;/ - *obj.tuiObject    : ???                                                   /
;/                                                                             /
;///////////////////////////////////////////////////////////////////////////////
Statement _ntui_SetMover{*obj.tuiObject}
  If *obj=Null Then Statement Return
  !_ASSERT{*obj}
  *engine.tuiEngine=*obj\engine
  !_ASSERT{*engine}

  If *obj=*engine\mover Then Statement Return ; has already the mover

  *engine\restingTime=0
  If *engine\mover
    *engine\mover\flags|#TUIF_MOVER - #TUIF_MOVER
    ntui_Refresh{*engine\mover,Null}
    *engine\mover = Null
  End If

  If *obj><*engine
    If ((*obj\flags&#TUIF_WANTMOVER)><0)
     *engine\mover = *obj
     *engine\mover\flags|#TUIF_MOVER
      ntui_Refresh{*engine\mover,Null}
    End If
    *win.tuiWindow = ntui_GetWindow{*obj}
    If *win
      _ntui_UsePointer{*win,*obj\pointerID}
    End If
  End If

End Statement


;///////////////////////////////////////////////////////////////////////////////
;/                                                                             /
;/ Syntax: _ntui_PropagateBind {*objMaster.tuiObject}                          /
;/                                                                             /
;/ Description:                                                                /
;/ private                                                                     /
;/                                                                             /
;/ Inputs:                                                                     /
;/ - *objMaster.tuiObject    : ???                                             /
;/                                                                             /
;///////////////////////////////////////////////////////////////////////////////
Statement _ntui_PropagateBind{*master.tuiObject,bindEventID.l,value.l,*string.str}

  *obj.tuiObject =*master\bind
  While *obj><Null AND *obj><*master
    *objEvent.tuiEvent = _ntui_CreateEvent{*master,bindEventID}
    *objEvent\value = value
    If *string Then str_WritePtr{&*objEvent\string,&*string\str,*string\size}
    If *obj\DispatchEvent Then !_CallFuncPointer{*obj\DispatchEvent,done.w,*obj,*objEvent}
    If done=False Then _ntui_DestroyEvent{*objEvent}
    *obj=*obj\bind
  Wend

End Statement


;///////////////////////////////////////////////////////////////////////////////
;/                                                                             /
;/ Syntax: result.l = ntui_SetAttr {*obj.tuiObject,ti_Tag.l,ti_Data.l}         /
;/                                                                             /
;/ Description:                                                                /
;/ Set an attribute of a tuiObject.
;/ Note: This is a very powerful function that can change almost any aspect of the tuiObject.
;/
;/ Supported attributes:
;/ #TUIA_STRING      Get/Set
;/                   Pointer to 0-terminated string value of tuiObject (if set, a cooy of the string will be made)
;/ #TUIA_VALUE       Get/Set
;/                   32bit integer value of tuiObject
;/ #TUIA_HELP        Get/Set
;/                   Pointer to 0-terminated string that holds the short help text (if set, a copy is made)
;/ #TUIA_NOTIFY      Get/Set
;/                   Notify value of the tuiObject
;/ #TUIA_FLAGS       Get/Set
;/                   All tuiFlags at once - use TUIA_SETFLAGS to manipulate single flags
;/                   Note: not all flags have an effect if changed during life-time of the tuiObject
;/ #TUIA_WIDTH       Get
;/                   Current width in layout (might be 0 if the tuiObject is not visibke)
;/ #TUIA_HEIGHT      Get
;/                   Current height in layout (might be 0 if the tuiObject is not visible)
;/ #TUIA_MINWIDTH    Get/Set
;/                   Minimum width of the tuiObject (might be 0 if the tuiObject was never visible)
;/ #TUIA_MINHEIGHT   Get/Set
;/                   Minimum height of the tuiObject (might be 0 if the tuiObject was never visible)
;/ #TUIA_MAXWIDTH    Get/Set
;/                   Maximum width of the tuiObject (might be 0 if the tuiObject was never visible)
;/ #TUIA_MAXHEIGHT   Get/Set
;/                   Minimum height of the tuiObject (might be 0 if the tuiObject was never visible)
;/ #TUIA_XPOS        Get
;/                   X-position in layout of the tuiObject
;/ #TUIA_YPOS        Get
;/                   Y-position in layout of the tuiObject
;/ #TUIA_BORDERTYPE  Get/Set
;/                   Type of border, see #TUIBORDER_... for possible border types
;/ #TUIA_GAPNORTH    Get
;/                   Gap at north side of the tuiObject
;/ #TUIA_GAPSOUTH    Get
;/                   Gap at south side of the tuiObject
;/ #TUIA_GAPEAST     Get
;/                   Gap at east side of the tuiObject
;/ #TUIA_GAPWEST     Get
;/                   Gap at west side of the tuiObject
;/ #TUIA_FOCUS       Get/Set
;/                   True, if the tuiObject currently holds the focus, False otherwise
;/ #TUIA_TYPEID      Get
;/                   TypeID of the tuiObject (#TUICLASS_...)
;/ #TUIA_USERID      Get/Set
;/                   UserID of the tuiObject
;/ #TUIA_BGPEN       Get/Set
;/                   Background tuiPen (NOT AmigaOS pen!)
;/ #TUIA_SETFLAGS    Set
;/                   Set the flags stated in ti_Data
;/ #TUIA_CLEARFLAGS  Set
;/                   Clear the flags stated in ti_Data
;/ #TUIA_HIDDEN      Get/Set
;/                   True if the Object is hidden, False otherwise
;/ #TUIA_ENABLED     Get/Set
;/                   True, if the tuiObject is enabled, False otherwise
;/ #TUIA_DISABLED    Get/Set
;/                   True, if the tuiObject is disabled, False otherwise
;/ #TUIA_VISIBLE     Get/Set
;/                   True, if the tuiObject is visible, False otherwise
;/ #TUIA_ACTIVE      Get/Set
;/                   True, if the tuiObject is active, False otherwise
;/ #TUIA_MOVER       Get/Set
;/                   True, if the tuiObject is the current Mouse-Over object, False otherwise
;/
; tuiWindow attributes
;/ #TUIWA_WFLAGS     Get/Set
;/                   AmigaOS Window flags of the tuiWindow (#WFLG_...)
;/ #TUIWA_APPNOTIFY  Get/Set
;/                   Notify value for an app event of this tuiWindow, default is #NOTIFY_WINDROP
;
;/* tuiListView attributes */
;/ #TUILVA_MULTISELECT Get/Set
;/                   Defines if the tuiListView can select multiple ListItems at a time
;/                                                                             /
;/ Inputs:                                                                     /
;/ - *obj.tuiObject  : tuiObject                                                   /
;/ - ti_Tag.l        : tuiAttribute (#TUIA_...)                                                         /
;/ - ti_Data.l       : value                                                        /
;/                                                                             /
;/ Result:                                                                     /
;/ - result.l        : true if the attribute was successfuly changed, false otherwise                                                        /
;/                                                                             /
;///////////////////////////////////////////////////////////////////////////////
Function.w ntui_SetAttr{*obj.tuiObject,ti_Tag.l,ti_Data.l}
  !_ASSERT{*obj}

  If (*obj\SetAttr)  ; try the objects SetAttr first
    !_CallFuncPointer{*obj\SetAttr,done.w,*obj,ti_Tag,ti_Data}
    If done Then Function Return done
  End If

  done=True
  Select ti_Tag
    Case #TUIA_VALUE
      *obj\value = ti_Data
      _ntui_PropagateBind{*obj,#TUIEV_BIND_VALUE,*obj\value,Null}
      ntui_Refresh{*obj,Null}

    Case #TUIA_STRING
      str_WritePtr{&*obj\string,ti_Data}
      ntui_Refresh{*obj,Null}
      _ntui_PropagateBind{*obj,#TUIEV_BIND_STRING,0,*obj\string}

    ;Case #TUIA_ONCLICK        : str_WritePtr{&*obj\onClick       ,ti_Data} ; notifyID when the object is clicked
    ;Case #TUIA_ONMOUSEOVER    : str_WritePtr{&*obj\onMouseOver   ,ti_Data} ; notifyID when mouse gets over the object
    ;Case #TUIA_ONDOUBLECLICK  : str_WritePtr{&*obj\onDoubleClick ,ti_Data} ; notifyID when the object is double clicked
    ;Case #TUIA_ONTOUCH        : str_WritePtr{&*obj\onTouch       ,ti_Data} ; notifyID when the object is touched (mouse down)
    ;Case #TUIA_ONRELEASE      : str_WritePtr{&*obj\onRelease     ,ti_Data} ; notifyID when the object is released (mouse up, doesn't need to be a valid click)
    ;Case #TUIA_ONKEY          : str_WritePtr{&*obj\onKeyDown     ,ti_Data} ; notifyID when a key is pressed
    ;Case #TUIA_ONBEGINREFRESH : str_WritePtr{&*obj\onBeginRefresh,ti_Data} ; notifyID before internal refresh
    ;Case #TUIA_ONREFRESH      : str_WritePtr{&*obj\onRefresh\notifyID     ,ti_Data} ; notifyID after internal refresh
    ;Case #TUIA_ONFOCUS        : str_WritePtr{&*obj\onFocus\notifyID       ,ti_Data} ; notifyID when object gains focus
    ;Case #TUIA_ONLOSEFOCUS    : str_WritePtr{&*obj\onLoseFocus\notifyID   ,ti_Data} ; notifyID when object loses focus
    ;Case #TUIA_ONHELP         : str_WritePtr{&*obj\onHelp\notifyID        ,ti_Data} ; notifyID when mouse rests over object or HELP key is pressed
    ;Case #TUIA_ONEVENT        : str_WritePtr{&*obj\onEvent\notifyID       ,ti_Data}

    Case #TUIA_HELP       : str_WritePtr{&*obj\help,ti_Data} ; TODO: Update Tooltip!
    Case #TUIA_FLAGS      : *obj\flags       = ti_Data                        : ntui_Refresh{*obj,Null} ;: _ntui_PropagateBind{*obj}
    Case #TUIA_SETFLAGS   : *obj\flags       = *obj\flags|ti_Data             : ntui_Refresh{*obj,Null} ;: _ntui_PropagateBind{*obj}
    Case #TUIA_CLEARFLAGS : *obj\flags       = (*obj\flags|ti_Data) - ti_Data : ntui_Refresh{*obj,Null} ;: _ntui_PropagateBind{*obj}
    Case #TUIA_HEIGHT     : *obj\bbox\bottom = *obj\bbox\top +ti_Data-1 : ntui_Refresh{*obj,Null}
    Case #TUIA_WIDTH      : *obj\bbox\right  = *obj\bbox\left+ti_Data-1 : ntui_Refresh{*obj,Null}
    Case #TUIA_XPOS       : *obj\bbox\left   = ti_Data : ntui_Refresh{*obj,Null}
    Case #TUIA_YPOS       : *obj\bbox\top    = ti_Data : ntui_Refresh{*obj,Null}
    Case #TUIA_MINHEIGHT  : *obj\minsize\y   = ti_Data
    Case #TUIA_MINWIDTH   : *obj\minsize\x   = ti_Data
    Case #TUIA_MAXHEIGHT  : *obj\maxsize\y   = ti_Data
    Case #TUIA_MAXWIDTH   : *obj\maxsize\x   = ti_Data
    Case #TUIA_BORDERTYPE : *obj\borderType  = ti_Data : ntui_Refresh{*obj,Null}
    Case #TUIA_FOCUS      : If ti_Data
                              ntui_SetFocus{*obj}
                            Else
                              If *obj\engine\focus = *obj Then ntui_SetFocus{*obj\engine}
                            End If
    Case #TUIA_MOVER      : If ti_Data
                              _ntui_SetMover{*obj}
                            Else
                              If *obj\engine\focus = *obj Then _ntui_SetMover{*obj\engine}
                            End If

    Case #TUIA_CLASSID    : ;/* we cant set the typeID - read only! */
    Case #TUIA_INVISIBLE  : *obj\flags|#TUIF_INVISIBLE : If ti_Data=False Then *obj\flags-#TUIF_INVISIBLE
                            ntui_Refresh{*obj,Null} ;: _ntui_PropagateBind{*obj}
    Case #TUIA_ENABLED    : *obj\flags|#TUIF_DISABLED  : If ti_Data       Then *obj\flags-#TUIF_DISABLED
                            ntui_Refresh{*obj,Null} ;: _ntui_PropagateBind{*obj}
    Case #TUIA_ACTIVE     : *obj\flags|#TUIF_ACTIVE    : If ti_Data=False Then *obj\flags-#TUIF_ACTIVE
    Case #TUIA_DISABLED   : *obj\flags|#TUIF_DISABLED  : If ti_Data=False Then *obj\flags-#TUIF_DISABLED
                            ntui_Refresh{*obj,Null} ;: _ntui_PropagateBind{*obj}


    Case #TUIA_SAMEWIDTH  : *obj\flags|#TUIF_SAMEWIDTH   : If ti_Data=False Then *obj\flags-#TUIF_SAMEWIDTH
    Case #TUIA_SAMEHEIGHT : *obj\flags|#TUIF_SAMEHEIGHT  : If ti_Data=False Then *obj\flags-#TUIF_SAMEHEIGHT
    Case #TUIA_SAMESIZE   : *obj\flags|#TUIF_SAMESIZE    : If ti_Data=False Then *obj\flags-#TUIF_SAMESIZE
    Case #TUIA_FIXWIDTH   : *obj\flags|#TUIF_FIXWIDTH    : If ti_Data=False Then *obj\flags-#TUIF_FIXWIDTH
    Case #TUIA_FIXHEIGHT  : *obj\flags|#TUIF_FIXHEIGHT   : If ti_Data=False Then *obj\flags-#TUIF_FIXHEIGHT
    Case #TUIA_FIXSIZE    : *obj\flags|#TUIF_FIXSIZE     : If ti_Data=False Then *obj\flags-#TUIF_FIXSIZE
    Case #TUIA_ALIGN      :
      Select ti_Data
        Case #TUIALIGN_LEFT   : *obj\flags = *obj\flags|(#TUIF_RIGHT|#TUIF_LEFT|#TUIF_CENTER) - (#TUIF_RIGHT |#TUIF_CENTER) : ntui_Refresh{*obj,Null}
        Case #TUIALIGN_RIGHT  : *obj\flags = *obj\flags|(#TUIF_RIGHT|#TUIF_LEFT|#TUIF_CENTER) - (#TUIF_LEFT  |#TUIF_CENTER) : ntui_Refresh{*obj,Null}
        Case #TUIALIGN_CENTER : *obj\flags = *obj\flags|(#TUIF_RIGHT|#TUIF_LEFT|#TUIF_CENTER) - (#TUIF_RIGHT |#TUIF_CENTER) : ntui_Refresh{*obj,Null}
        Case #TUIALIGN_TOP    : *obj\flags = *obj\flags|(#TUIF_TOP|#TUIF_BOTTOM|#TUIF_MIDDLE) - (#TUIF_BOTTOM|#TUIF_MIDDLE) : ntui_Refresh{*obj,Null}
        Case #TUIALIGN_BOTTOM : *obj\flags = *obj\flags|(#TUIF_TOP|#TUIF_BOTTOM|#TUIF_MIDDLE) - (#TUIF_TOP   |#TUIF_MIDDLE) : ntui_Refresh{*obj,Null}
        Case #TUIALIGN_MIDDLE : *obj\flags = *obj\flags|(#TUIF_TOP|#TUIF_BOTTOM|#TUIF_MIDDLE) - (#TUIF_BOTTOM|#TUIF_TOP   ) : ntui_Refresh{*obj,Null}
      End Select

    Case #TUIA_ID         : str_WritePtr{&*obj\nameID,ti_Data}
    Case #TUIA_BGPEN      : *obj\bgTuiPen  = ti_Data : ntui_Refresh{*obj,Null}
    Case #TUIA_FGPEN      : *obj\fgTuiPen  = ti_Data : ntui_Refresh{*obj,Null}
    Case #TUIA_BGRGB      : *obj\bgRGB  = ti_Data : ntui_Refresh{*obj,Null}
    Case #TUIA_FGRGB      : *obj\fgRGB  = ti_Data : ntui_Refresh{*obj,Null}
    Case #TUIA_BGIMAGE    : imagefile.s = Peek.s(ti_Data)
                            *obj\bgImage = ntui_CreateTBImage{*obj\engine,imagefile,#TUISIZE_NATIVE,#TUISIZE_NATIVE}
    Case #TUIA_FGIMAGE    : imagefile.s = Peek.s(ti_Data)
                            *obj\fgImage = ntui_CreateTBImage{*obj\engine,imagefile,#TUISIZE_NATIVE,#TUISIZE_NATIVE}
    Case #TUIA_PADDING       : *obj\padding\left   = ti_Data,ti_Data,ti_Data,ti_Data : ntui_Refresh{*obj,Null}
    Case #TUIA_LEFTPADDING   : *obj\padding\left   = ti_Data : ntui_Refresh{*obj,Null}
    Case #TUIA_TOPPADDING    : *obj\padding\top    = ti_Data : ntui_Refresh{*obj,Null}
    Case #TUIA_RIGHTPADDING  : *obj\padding\right  = ti_Data : ntui_Refresh{*obj,Null}
    Case #TUIA_BOTTOMPADDING : *obj\padding\bottom = ti_Data : ntui_Refresh{*obj,Null}
    Case #TUIA_MARGIN        : *obj\margin\left    = ti_Data,ti_Data,ti_Data,ti_Data : ntui_Refresh{*obj,Null}
    Case #TUIA_LEFTMARGIN    : *obj\margin\left    = ti_Data : ntui_Refresh{*obj,Null}
    Case #TUIA_TOPMARGIN     : *obj\margin\top     = ti_Data : ntui_Refresh{*obj,Null}
    Case #TUIA_RIGHTMARGIN   : *obj\margin\right   = ti_Data : ntui_Refresh{*obj,Null}
    Case #TUIA_BOTTOMMARGIN  : *obj\margin\bottom  = ti_Data : ntui_Refresh{*obj,Null}

    Case #TUIA_MD_CALCULATEMINSIZE : *obj\CalculateMinSize = ti_Data ; rw (*func) function pointer to ntui_CalculateMinSize method
    Case #TUIA_MD_DISPATCHEVENT    : *obj\DispatchEvent    = ti_Data ; rw (*func) function pointer to ntui_DispatchEvent method
    Case #TUIA_MD_DRAW             : *obj\Draw             = ti_Data ; rw (*func) function pointer to ntui_Draw method
    Case #TUIA_MD_DEINIT           : *obj\Deinit           = ti_Data ; rw (*func) function pointer to ntui_Deinit method
    ;Case #TUIA_MD_INIT             : *obj\Init             = ti_Data ; rw (*func) function pointer to ntui_Init method
    Case #TUIA_MD_GETATTR          : *obj\GetAttr          = ti_Data ; rw (*func) function pointer to ntui_GetAttr method
    Case #TUIA_MD_SETATTR          : *obj\SetAttr          = ti_Data ; rw (*func) function pointer to ntui_SetAttr method
    Case #TUIA_MD_LAYOUT           : *obj\Layout           = ti_Data ; rw (*func) function pointer to ntui_Layout method

    Case #TUIA_USERDATA      : *obj\userData       = ti_Data
    Case #TUIA_MOUSEPOINTER  : ntui_SetPointer{*obj,ti_Data}
    Case #TUIA_GRADIENT      : *obj\gradient       = ti_Data

    Default
      !_ASSERT{False}
      done = False ; unknown attr
  End Select

  Function  Return done

End Function

;///////////////////////////////////////////////////////////////////////////////
;/                                                                             /
;/ Syntax: result.l = ntui_SetAttrs {*obj.tuiObject,*taglist.TagItem}          /
;/                                                                             /
;/ Description:                                                                /
;/ Same like ntui_SetAttr, but set multiple attributes at once.                                                /
;/                                                                             /
;/ Inputs:                                                                     /
;/ - *obj.tuiObject    : tuiObject                                                   /
;/ - *taglist.TagItem  : list of tagItems                                                 /
;/                                                                             /
;/ Result:                                                                     /
;/ - result.l     : count of attributes that have been successfully changed                                                        /
;/                                                                             /
;///////////////////////////////////////////////////////////////////////////////
Function.l ntui_SetAttrs{*obj.tuiObject,*tagList.TagItem}

  count.l = 0
  If *tagList
    While *tagList\ti_Tag><#TAG_DONE
      If ntui_SetAttr{*obj,*tagList\ti_Tag,*tagList\ti_Data} Then count+1
      *tagList + SizeOf.TagItem
    Wend
  End If

  Function Return count

End Function


;///////////////////////////////////////////////////////////////////////////////
;/                                                                             /
;/ Syntax: result.l = ntui_GetAttr {*obj.tuiObject,ti_Tag.l,*tagData.l}        /
;/                                                                             /
;/ Description:                                                                /
;/ Get an attribute of a tuiObject.                                                /
;/ See ntui_SetAttr for possible attributes.
;/                                                                             /
;/ Inputs:                                                                     /
;/ - *obj.tuiObject : tuiObject                                                  /
;/ - ti_Tag.l       : tuiAttribute (#TUIA_...)                                                         /
;/ - tagData.l      : pointer to data to be filled out                                         /
;/                                                                             /
;/ Result:                                                                     /
;/ - result.w       : success if attribute has been get                                                        /
;/                                                                             /
;///////////////////////////////////////////////////////////////////////////////
Function.w ntui_GetAttr{*obj.tuiObject,ti_Tag.l,*ti_Data_.l}
  !_ASSERT{*obj}
  !_ASSERT{*ti_Data_}

  If (*obj\GetAttr)  ; try the objects GetAttr first
    !_CallFuncPointer{*obj\GetAttr,done.w,*obj,ti_Tag,*ti_Data_}
    If done Then Function Return done
  End If

  *ti_Data.longP = *ti_Data_

  done = True
  Select ti_Tag
    Case #TUIA_CLASSID       : *ti_Data\l = *obj\classID
    Case #TUIA_ID            : *ti_Data\l = str_GetPtr{*obj\nameID        }
    Case #TUIA_STRING        : *ti_Data\l = str_GetPtr{*obj\string        }
    ;Case #TUIA_ONCLICK       : *ti_Data\l = str_GetPtr{*obj\onClick       } ; notifyID when the object is clicked
    ;Case #TUIA_ONMOUSEOVER   : *ti_Data\l = str_GetPtr{*obj\onMouseOver   } ; notifyID when mouse gets over the object
    ;Case #TUIA_ONDOUBLECLICK : *ti_Data\l = str_GetPtr{*obj\onDoubleClick } ; notifyID when the object is double clicked
    ;Case #TUIA_ONTOUCH       : *ti_Data\l = str_GetPtr{*obj\onTouch       } ; notifyID when the object is touched (mouse down)
    ;Case #TUIA_ONRELEASE     : *ti_Data\l = str_GetPtr{*obj\onRelease     } ; notifyID when the object is released (mouse up, doesn't need to be a valid click)
    ;Case #TUIA_ONKEY         : *ti_Data\l = str_GetPtr{*obj\onKeyDown     } ; notifyID when a key is pressed
    ;Case #TUIA_ONBEGINREFRESH: *ti_Data\l = str_GetPtr{*obj\onBeginRefresh} ; notifyID before internal refresh
    ;Case #TUIA_ONREFRESH     : *ti_Data\l = str_GetPtr{*obj\onRefresh\notifyID     } ; notifyID after internal refresh
    ;Case #TUIA_ONEVENT       : *ti_Data\l = str_GetPtr{*obj\onEvent\notifyID       } ;
    ;Case #TUIA_ONFOCUS       : *ti_Data\l = str_GetPtr{*obj\onFocus       } ; notifyID when object gains focus
    ;Case #TUIA_ONLOSEFOCUS   : *ti_Data\l = str_GetPtr{*obj\onLoseFocus   } ; notifyID when object loses focus
    ;Case #TUIA_ONHELP        : *ti_Data\l = str_GetPtr{*obj\onHelp        } ; notifyID when mouse rests over object or HELP key is pressed
    Case #TUIA_HELP          : *ti_Data\l = str_GetPtr{*obj\help          }
    Case #TUIA_VALUE         : *ti_Data\l = *obj\value
    Case #TUIA_FLAGS         : *ti_Data\l = *obj\flags
    Case #TUIA_WIDTH         : *ti_Data\l = !tuiRectWidth{*obj\cbox}
    Case #TUIA_HEIGHT        : *ti_Data\l = !tuiRectHeight{*obj\cbox}
    Case #TUIA_MINWIDTH      : *ti_Data\l = *obj\minsize\x
    Case #TUIA_MINHEIGHT     : *ti_Data\l = *obj\minsize\y
    Case #TUIA_MAXWIDTH      : *ti_Data\l = *obj\maxsize\x
    Case #TUIA_MAXHEIGHT     : *ti_Data\l = *obj\maxsize\y
    Case #TUIA_XPOS          : *ti_Data\l = *obj\cbox\left
    Case #TUIA_YPOS          : *ti_Data\l = *obj\cbox\top
    Case #TUIA_BORDERTYPE    : *ti_Data\l = *obj\borderType
    Case #TUIA_FOCUS         : If (*obj\flags&#TUIF_FOCUS    ) Then *ti_Data\l=True:Else *ti_Data\l=False
    Case #TUIA_MOVER         : If (*obj\flags&#TUIF_MOVER    ) Then *ti_Data\l=True:Else *ti_Data\l=False
    Case #TUIA_ACTIVE        : If (*obj\flags&#TUIF_ACTIVE   ) Then *ti_Data\l=True:Else *ti_Data\l=False
    Case #TUIA_INVISIBLE     : If (*obj\flags&#TUIF_INVISIBLE) Then *ti_Data\l=True:Else *ti_Data\l=False
    Case #TUIA_ONSCREEN      : If (*obj\flags&#TUIF_ONSCREEN ) Then *ti_Data\l=True:Else *ti_Data\l=False
    Case #TUIA_ENABLED       : If (*obj\flags&#TUIF_DISABLED ) Then *ti_Data\l=False:Else *ti_Data\l=True
    Case #TUIA_DISABLED      : If (*obj\flags&#TUIF_DISABLED ) Then *ti_Data\l=True:Else *ti_Data\l=False
    Case #TUIA_LEFTMARGIN    : *ti_Data\l = *obj\margin\left
    Case #TUIA_TOPMARGIN     : *ti_Data\l = *obj\margin\top
    Case #TUIA_RIGHTMARGIN   : *ti_Data\l = *obj\margin\right
    Case #TUIA_BOTTOMMARGIN  : *ti_Data\l = *obj\margin\bottom
    Case #TUIA_LEFTPADDING   : *ti_Data\l = *obj\padding\left
    Case #TUIA_TOPPADDING    : *ti_Data\l = *obj\padding\top
    Case #TUIA_RIGHTPADDING  : *ti_Data\l = *obj\padding\right
    Case #TUIA_BOTTOMPADDING : *ti_Data\l = *obj\padding\bottom
    Case #TUIA_BGPEN         : *ti_Data\l = *obj\bgTuiPen
    Case #TUIA_FGPEN         : *ti_Data\l = *obj\fgTuiPen
    Case #TUIA_BGRGB         : *ti_Data\l = *obj\bgRGB
    Case #TUIA_FGRGB         : *ti_Data\l = *obj\fgRGB
    Case #TUIA_BGIMAGE       : *ti_Data\l = *obj\bgImage
    Case #TUIA_FGIMAGE       : *ti_Data\l = *obj\fgImage
    Case #TUIA_CBOX          : *ti_Data\l = &*obj\cbox ; r  (*tuiRect) content box
    Case #TUIA_IBOX          : *ti_Data\l = &*obj\ibox ; r  (*tuiRect) innerbox box
    Case #TUIA_BBOX          : *ti_Data\l = &*obj\bbox ; r  (*tuiRect) bounding box
    Case #TUIA_MD_CALCULATEMINSIZE : *ti_Data\l = *obj\CalculateMinSize ; rw (*func) function pointer to ntui_CalculateMinSize method
    Case #TUIA_MD_DISPATCHEVENT    : *ti_Data\l = *obj\DispatchEvent    ; rw (*func) function pointer to ntui_DispatchEvent method
    Case #TUIA_MD_DRAW             : *ti_Data\l = *obj\Draw             ; rw (*func) function pointer to ntui_Draw method
    Case #TUIA_MD_DEINIT           : *ti_Data\l = *obj\Deinit           ; rw (*func) function pointer to ntui_Deinit method
    ;Case #TUIA_MD_INIT             : *ti_Data\l = *obj\Init             ; rw (*func) function pointer to ntui_Init method
    Case #TUIA_MD_GETATTR          : *ti_Data\l = *obj\GetAttr          ; rw (*func) function pointer to ntui_GetAttr method
    Case #TUIA_MD_SETATTR          : *ti_Data\l = *obj\SetAttr          ; rw (*func) function pointer to ntui_SetAttr method
    Case #TUIA_MD_LAYOUT           : *ti_Data\l = *obj\Layout           ; rw (*func) function pointer to ntui_Layout method
    Case #TUIA_NEXTOBJECT    : *ti_Data\l = *obj\next_ ; r  (*obj) pointer to next object in hirarchie
    Case #TUIA_PREVOBJECT    : *ti_Data\l = *obj\prev  ; r  (*obj) pointer to previous object in hirarchie
    Case #TUIA_PARENTOBJECT  : *ti_Data\l = *obj\parent; r  (*obj) pointer to parent object in hirarchie
    Case #TUIA_CHILDOBJECT   : *ti_Data\l = *obj\child ; r  (*obj) pointer to child object in hirarchie
    Case #TUIA_NEXTTABOBJECT : *ti_Data\l = ntui_GetNextTabObject{*obj}; r  (*obj) pointer to next object in tab chain
    Case #TUIA_PREVTABOBJECT : *ti_Data\l = ntui_GetPrevTabObject{*obj}; r  (*obj) pointer to prevouis object in tab chain
    Case #TUIA_MOUSEPOINTER  : *ti_Data\l = *obj\pointerID
    Case #TUIA_USERDATA      : *ti_Data\l = *obj\userData
    Case #TUIA_ENGINE        : *ti_Data\l = *obj\engine
    Case #TUIA_GRADIENT      : *ti_Data\l = *obj\gradient
    Default
      done=False
  End Select

  Function Return done

End Function


Statement _ntui_DispatchEvent{*ev.tuiEvent}
  !_ASSERT{*ev}
  *obj.tuiObject = *ev\obj
  !_ASSERT{*obj}

  If *obj\DispatchEvent Then !_CallFuncPointer{*obj\DispatchEvent,done.w,*obj,*ev}
  If done = False

  ;/* generic handling of unhandled events */
  *engine.tuiEngine = *obj\engine

  Select *ev\evID
    Case #TUIEV_FOCUS
      ntui_Refresh{*obj,Null}

    Case #TUIEV_LOSEFOCUS
      ntui_Refresh{*obj,Null}

    Case #TUIEV_REFRESH
      If (*obj\flags&(#TUIF_DIRTY|#TUIF_ONSCREEN) = (#TUIF_DIRTY|#TUIF_ONSCREEN))
        clip.tuiRect\left = *ev\pos\x,*ev\pos\y,*ev\size\x+*ev\pos\x-1,*ev\size\y+*ev\pos\y-1
        If !tuiValidRect{clip}
          _ntui_Redraw{*obj,clip}
        Else
          _ntui_Redraw{*obj,Null}
        End If
      End If

    Case #TUIEV_PROPAGATE
      ;_ntui_PropagateBind{*obj}

    Case #TUIEV_LAYOUT
      *win.tuiWindow = ntui_GetWindow{*obj}
      If *win
        *rp.RastPort = ntui_ObtainRastPort{*win}
        If *rp
          _ntui_Layout{*obj,*rp,Null}
          ntui_ReleaseRastPort{*win}
        End If
      End If

;    Case #TUIEV_ASLFONT
;      fontname.s = str_Read{&*ev\string}
;      fontsize.l = *ev\value
;      If ntui_ASLFontRequest{*engine,fontname,fontsize}
;        ntui_SetAttr{*ev\obj,#TUIA_STRING,*engine\aslfo\fo_TAttr\tta_Name}
;        ntui_SetAttr{*ev\obj,#TUIA_VALUE,*engine\aslfo\fo_TAttr\tta_YSize}
;      End If
;
;    Case #TUIEV_ASLFILE
;      filename.s = str_Read{&*ev\string}
;      path.s = _ntui_PathPart{filename}
;      fil.s  = _ntui_FilePart{filename}
;      patt.s = "#?"
;      If ntui_ASLFileRequest{*engine,path,fil,patt,False,False,False,True}
;        fil  = Peek.s(*engine\aslfr\fr_File)
;        path = Peek.s(*engine\aslfr\fr_Drawer)
;        patt = Peek.s(*engine\aslfr\fr_Pattern)
;        filename = _ntui_AddPart{path,fil}
;        str_Write{&*ev\string,&filename}
;        ;ntui_SetAttr{*ev\obj,#TUIA_STRING,&filename}
;      End If
;
;    Case #TUIEV_ASLSAVE
;      filename.s = str_Read{&*ev\string}
;      path.s = _ntui_PathPart{filename}
;      fil.s  = _ntui_FilePart{filename}
;      patt.s = "#?"
;      If ntui_ASLFileRequest{*engine,path,fil,patt,True,False,False,True}
;        fil  = Peek.s(*engine\aslfr\fr_File)
;        path = Peek.s(*engine\aslfr\fr_Drawer)
;        patt = Peek.s(*engine\aslfr\fr_Pattern)
;        filename = _ntui_AddPart{path,fil}
;        str_Write{&*ev\string,&filename}
;        ;ntui_SetAttr{*ev\obj,#TUIA_STRING,&filename}
;      End If
;
;    Case #TUIEV_ASLPATH
;      path.s = str_Read{&*ev\string}
;      If ntui_ASLFileRequest{*engine,path,"","",False,False,True,True}
;        path = Peek.s(*engine\aslfr\fr_Drawer)
;        str_Write{&*ev\string,&path}
;        ;ntui_SetAttr{*ev\obj,#TUIA_STRING,&path}
;      End If
;
;    Case #TUIEV_ASLSCREEN
;      name.s = str_Read{&*ev\string}
;      modeID.l = *ev\value
;      width.l  = 0
;      height.l = 0
;      depth_.l = 0
;      If ntui_ASLScreenModeRequest{*engine,modeID,width,height,depth_}
;        modeID = *engine\aslsm\sm_DisplayID
;        depth_ = *engine\aslsm\sm_DisplayDepth
;        width  = *engine\aslsm\sm_DisplayWidth
;        height = *engine\aslsm\sm_DisplayHeight
;        *ev\obj\value = modeID
;        mode_ptr.l = FindDisplayInfo_ (modeID)
;        If mode_ptr
;          count.l = GetDisplayInfoData_ (mode_ptr,&naminfo.NameInfo,SizeOf.NameInfo,#DTAG_NAME,modeID)
;          If count Then modename.s = Peek.s(&naminfo\Name) : Else modename = "???"
;        End If
;
;        modename.s + " ("+Str$(width)+"x"+Str$(height)+"x"+Str$(depth_)+")"
;        ;ntui_SetAttr{*ev\obj,#TUIA_STRING,&modename}
;        str_Write{&*ev\string,&modename}
;        *ev\value = modeID
;      End If

    Case #TUIEV_TOOLTIP
      !_CallFuncPointer{tuiGlobal\showToolTip,ret.l,*engine,*ev\obj\help,*engine\screen_\MouseX,*engine\screen_\MouseY}
;      ntui_ShowTooltip{*engine,str_Read{&*engine\mover\help},*engine\screen_\MouseX,*engine\screen_\MouseY}

  End Select
  End If

  If (*ev\queued=False)
    _ntui_DestroyEvent{*ev}
  End If

End Statement


;///////////////////////////////////////////////////////////////////////////////
;/                                                                             /
;/ Syntax: result.l = ntui_CauseEvent {notify.l,*win.tuiWindow,@*objOb:: /
;/ ject.tuiObject,@x.l,@y.l,@width.l,@height.l}                                /
;/                                                                             /
;/ Description:                                                                /
;/ Cause an event to happen manually. The event will be passed to the tuiEngines input queue.                                      /
;/                                                                             /
;/ Inputs:                                                                     /
;/ - notify.l               : notify value                                                         /
;/ - *win.tuiWindow   : tuiWindow of the event                                             /
;/ - *objObject.tuiObject   : tuiObject of the event (if any)                                             /
;/ - x/y.l                  : x/y position of event                                                    /
;/ - width.l                : ???                                                          /
;/ - height.l               : ???                                                         /
;/                                                                             /
;/ Result:                                                                     /
;/ - result.l               : pointer to created tuiEvent struture                                                        /
;/                                                                             /
;///////////////////////////////////////////////////////////////////////////////
Statement ntui_SendEvent{*obj.tuiObject,evID.l,@value.l,@string.s,@*pos.tuiPixel,@*size.tuiPixel,@notifyID.s}
  !_ASSERT{*obj}
  *engine.tuiEngine = *obj\engine
  !_ASSERT{*engine}

  *ev.tuiEvent = _ntui_CreateEvent{*obj,evID}
  If *ev
    If string Then str_Write{&*ev\string,string}
    *ev\value  = value
    If *pos><-1  AND *pos ><Null Then *ev\pos\x  = *pos\x,*pos\y
    If *size><-1 AND *size><Null Then *ev\size\x = *size\x,*size\y
    If notifyID Then str_Write{&*ev\notifyID,string}
    _ntui_DispatchEvent{*ev}
  End If

End Statement


;///////////////////////////////////////////////////////////////////////////////
;/                                                                             /
;/ Syntax: result.l = _ntui_CreateObject {typeID.l,sizeof_obj.l,notify.l,hel:: /
;/ p.s,flags.l,userID.l}                                                       /
;/                                                                             /
;/ Description:                                                                /
;/ private: generic create function for every tui object. This is called by every tuiObject creation function.                 /
;/ This function allocates the memory for the tuiObject and fills out the mandatory fields.
;/ It also links the object according the buildHelper and updates the buildHelper.
;/                                                                             /
;/ Inputs:                                                                     /
;/ - typeID.l      : typeID of the tuiObject                                                         /
;/ - sizeof_obj.l  : size of the tuiObject specific structure                                                     /
;/ - notify.l      : notify value                                                         /
;/ - help.s        : short help text for this tuiObject                                                          /
;/ - flags.l       : initial tuiFlags (#TUIF_...)                                                         /
;/ - userID.l      : userID to find the object later                                                         /
;/                                                                             /
;/ Result:                                                                     /
;/ - result.l      : pointer to the tuiObject or NULL if failed                                                        /
;/                                                                             /
;///////////////////////////////////////////////////////////////////////////////
Function.tuiObject _ntui_CreateObject_deprecated{typeID.l,sizeof_obj.l,help.s,flags.l,nameID.s}
  *obj.tuiObject = Null

  If sizeof_obj<SizeOf.tuiObject
    error{"\\__THIS_FUNCTION: The size of an tuiObject can not be smaller than the basic object structure size!"} : Function Return Null
  End If

  If typeID<1 OR typeID>=#TUIMAX_CLASSES
    error{"\\__THIS_FUNCTION: Trying to create an tui object with invalid ID="+Str$(typeID)+", name=\\22"+nameID+"\\22!"} : Function Return Null
  End If

  If (flags = #TUI_NOFLAGS) Then flags=0

  *obj.tuiObject = AllocVec_(sizeof_obj,#MEMF_CLEAR)
  If *obj
    If typeID = #TUICLASS_ENGINE
      *engine.tuiEngine = (.tuiEngine)*obj
    Else
      *engine.tuiEngine =  tuiGlobal\buildHelper\engine
      If *engine = Null
        error{"\\__THIS_FUNCTION: Trying to create an tui object without tuiEngine!"} : Function Return Null
      End If
    End If

    *obj\classID        = typeID
    *obj\engine         = *engine
    *obj\flags          = (flags|#TUIF_ONSCREEN) - #TUIF_ONSCREEN
    *obj\value          = 0
    *obj\string         = Null
    *obj\help           = Null
    *obj\nameID         = Null
    *obj\pointerID      = #TUIPOINTER_DEFAULT
    ;*obj\bindFlags      = 0

    *obj\bbox\left      = 0,0,0,0
    *obj\cbox\left      = 0,0,0,0
    *obj\minsize\x      = 0,0
    *obj\maxsize\x      = 0,0
    *obj\border\left    = 0,0,0,0
    *obj\padding\left   = *engine\defPadding\left
    *obj\padding\top    = *engine\defPadding\top
    *obj\padding\right  = *engine\defPadding\right
    *obj\padding\bottom = *engine\defPadding\bottom

    *obj\borderType     = #TUIBORDER_NONE
    *obj\bgTuiPen       = #TUIPEN_TRANSPARENT
    *obj\fgTuiPen       = #TUIPEN_TEXT
    *obj\bgImage        = Null
    *obj\fgImage        = Null
    *obj\bgRGB          = $00000000
    *obj\fgRGB          = $00000000

    *obj\Deinit           = Null
    *obj\DispatchEvent    = Null
    *obj\Draw             = Null
    *obj\CalculateMinSize = Null
    *obj\Layout           = Null
    *obj\SetAttr          = Null
    *obj\GetAttr          = Null

    *obj\margin\left   = *engine\defMargin\left
    *obj\margin\top    = *engine\defMargin\top
    *obj\margin\right  = *engine\defMargin\right
    *obj\margin\bottom = *engine\defMargin\bottom

    *obj\parent      = tuiGlobal\buildHelper\parent               ; parent/child
    If (tuiGlobal\buildHelper\parent)
      If (tuiGlobal\buildHelper\parent\child=Null) Then tuiGlobal\buildHelper\parent\child = *obj
    End If

    If (tuiGlobal\buildHelper\prev) ; previous/next
      *obj\prev       = tuiGlobal\buildHelper\prev
      *obj\next_      = tuiGlobal\buildHelper\prev\next_
      tuiGlobal\buildHelper\prev\next_ = *obj
      If *obj\next_ Then *obj\next_\prev = *obj
    Else
      *obj\prev        = Null
      *obj\next_       = Null                    ; there is no next yet
    End If
    *obj\child       = Null                    ; there is no child yet
    *obj\bind        = Null

    If (help)   Then str_Write{&*obj\help   ,help}
    If (nameID) Then str_Write{&*obj\nameID, nameID}

;    bpflags.l = 0
;    If (*obj\flags&#TUIF_FIXWIDTH  = 0) Then bpflags | #TUIF_FIXWIDTH
;    If (*obj\flags&#TUIF_FIXHEIGHT = 0) Then bpflags | #TUIF_FIXHEIGHT
;    If (bpflags) ; backpropagate the sizing
;      *parent.tuiObject = *obj\parent
;      While *parent
;        If (*parent\flags&bpflags)
;          *parent\flags = (*parent\flags|bpflags) - bpflags
;          If *parent\classID><#TUICLASS_WINDOW AND *parent\classID><#TUICLASS_ENGINE Then *parent = *parent\parent : Else *parent=Null
;        Else
;          *parent=Null
;        End If
;      Wend
;    End If

    tuiGlobal\buildHelper\current = *obj
    tuiGlobal\buildHelper\prev    = *obj
  EndIf

  Function Return *obj

End Function

Function.tuiObject _ntui_CreateObject{typeID.l,sizeof_obj.l,flags.l}
  *obj.tuiObject = Null

  If sizeof_obj<SizeOf.tuiObject
    error{"\\__THIS_FUNCTION: The size of an tuiObject can not be smaller than the basic object structure size!"} : Function Return Null
  End If

  If typeID<=#TUICLASS_INVALID OR typeID>=#TUIMAX_CLASSES
    error{"\\__THIS_FUNCTION: Trying to create an tui object with invalid ID="+Str$(typeID)+"!"} : Function Return Null
  End If

  If (flags = #TUI_NOFLAGS) Then flags=0

  *obj.tuiObject = AllocVec_(sizeof_obj,#MEMF_CLEAR)
  If *obj
    If typeID = #TUICLASS_ENGINE
      *engine.tuiEngine = (.tuiEngine)*obj
    Else
      *engine.tuiEngine =  tuiGlobal\buildHelper\engine
      If *engine = Null
        error{"\\__THIS_FUNCTION: Trying to create an tui object without tuiEngine!"} : Function Return Null
      End If
    End If

    *obj\classID          = typeID
    *obj\engine           = *engine
    *obj\flags            = (flags|#TUIF_ONSCREEN) - #TUIF_ONSCREEN
    *obj\value            = 0
    *obj\string           = Null
    *obj\help             = Null
    *obj\nameID           = Null
    *obj\pointerID        = #TUIPOINTER_DEFAULT

    *obj\bbox\left        = 0,0,0,0
    *obj\cbox\left        = 0,0,0,0
    *obj\minsize\x        = 0,0
    *obj\maxsize\x        = 0,0
    *obj\border\left      = 0,0,0,0
    *obj\padding\left     = *engine\defPadding\left
    *obj\padding\top      = *engine\defPadding\top
    *obj\padding\right    = *engine\defPadding\right
    *obj\padding\bottom   = *engine\defPadding\bottom

    *obj\borderType       = #TUIBORDER_NONE
    *obj\bgTuiPen         = #TUIPEN_TRANSPARENT
    *obj\fgTuiPen         = #TUIPEN_TEXT
    *obj\gradient         = #TUIORIENT_NONE
    *obj\bgImage          = Null
    *obj\fgImage          = Null
    *obj\bgRGB            = $00000000
    *obj\fgRGB            = $00000000

    *obj\Deinit           = Null
    *obj\DispatchEvent    = Null
    *obj\Draw             = Null
    *obj\CalculateMinSize = Null
    *obj\Layout           = Null
    *obj\SetAttr          = Null
    *obj\GetAttr          = Null

    *obj\margin\left      = *engine\defMargin\left
    *obj\margin\top       = *engine\defMargin\top
    *obj\margin\right     = *engine\defMargin\right
    *obj\margin\bottom    = *engine\defMargin\bottom

    *obj\parent           = tuiGlobal\buildHelper\parent               ; parent/child
    If (tuiGlobal\buildHelper\parent)
      If (tuiGlobal\buildHelper\parent\child=Null) Then tuiGlobal\buildHelper\parent\child = *obj
    End If

    If (tuiGlobal\buildHelper\prev) ; previous/next
      *obj\prev       = tuiGlobal\buildHelper\prev
      *obj\next_      = tuiGlobal\buildHelper\prev\next_
      tuiGlobal\buildHelper\prev\next_ = *obj
      If *obj\next_ Then *obj\next_\prev = *obj
    Else
      *obj\prev        = Null
      *obj\next_       = Null                    ; there is no next yet
    End If
    *obj\child       = Null                    ; there is no child yet
    *obj\bind        = Null

    tuiGlobal\buildHelper\current = *obj
    tuiGlobal\buildHelper\prev    = *obj
  EndIf

  Function Return *obj

End Function


Statement _ntui_BeginChildren{*obj.tuiObject}

  tuiGlobal\buildHelper\parent              = *obj
  tuiGlobal\buildHelper\prev                = Null

End Statement

Function.tuiObject _ntui_EndChildren{classID.l}

  tuiGlobal\buildHelper\prev    = tuiGlobal\buildHelper\parent
  tuiGlobal\buildHelper\current = tuiGlobal\buildHelper\parent
  If tuiGlobal\buildHelper\parent Then tuiGlobal\buildHelper\parent = tuiGlobal\buildHelper\parent\parent : Else tuiGlobal\buildHelper\parent = Null
  If tuiGlobal\buildHelper\current
    If tuiGlobal\buildHelper\current\classID><classID Then error{"\\__THIS_FUNCTION: EndChildren does not match a BeginChildren!"}
  Else
    error{"\\__THIS_FUNCTION: EndChildren without BeginChildren!"}
  End If

  Function Return tuiGlobal\buildHelper\current

End Function

;///////////////////////////////////////////////////////////////////////////////
;/                                                                             /
;/ Syntax: result.l = ntui_HitObject {*obj.tuiObject,x.l,y.l}                  /
;/                                                                             /
;/ Description:                                                                /
;/ Test the x/y coordinates which tuiObject they would hit.                                              /
;/ note: This function does also hit disalbed tuiObjects.
;/* note: This function returns NULL, if no object is hit.
;/                                                                             /
;/ Inputs:                                                                     /
;/ - *obj.tuiObject  : parent tuiObject, usually a tuiWindow                                                   /
;/ - x.l             : x coordinate                                                             /
;/ - y.l             : y coordinate                                                             /
;/                                                                             /
;/ Result:                                                                     /
;/ - result.l        : pointer to tuiObject or NULL                                                       /
;/                                                                             /
;///////////////////////////////////////////////////////////////////////////////
Function.tuiObject ntui_HitObject{*obj.tuiObject,x.l,y.l}
  !_ASSERT{*obj}

  *hit.tuiObject=Null
  If (*obj\flags&#TUIF_ONSCREEN)
    If x>=*obj\bbox\left AND x<=*obj\bbox\right
      If y>=*obj\bbox\top AND y<=*obj\bbox\bottom
        *hit = *obj ; we hit the object!
        *child.tuiObject = *obj\child ; but do we have any children?
        While *child
          *hitchild.tuiObject = ntui_HitObject{*child,x,y}
          If *hitchild Then Function Return *hitchild
          *child = *child\next_
        Wend
      End If
    End If
  End If

  Function Return *hit

End Function


;///////////////////////////////////////////////////////////////////////////////
;/                                                                             /
;/ Syntax: ntui_Bind {*objMaster.tuiObject,*objSlave.tuiObject}                /
;/                                                                             /
;/ Description:
;/ Bind (connect) the two tuiObjects.                                                       /
;/ Master and Slave are only important during the call of this function and determines from which tuiObject to take the initial paramters.
;/* Lateron, the two tuiObject will stay synchron, means a change on one of the tuiObjects will case a change on the other tuiObject.
;/* Note: Which parameters will be synchronized is hardcoded in the tuiObject. E.g. if you bind a horizonal scroller with a vertical scroller, nothing will happen since they change different parameters (vertical vs. horizontal scroll offset).
;/* Note: If you bind A+B, and B+C ,A is automatically bound with C too.
;/* Note: The typical usage is to bind a custom view with a horizontal and vertical scroller. Once they are binded, you only have to take care about the custom view, and the scrollers will follow all changes.
;/                                                                             /
;/ Inputs:                                                                     /
;/ - *objMaster.tuiObject : first tuiObject                                             /
;/ - *objSlave.tuiObject  : second tuiObject                                              /
;/                                                                             /
;///////////////////////////////////////////////////////////////////////////////
Statement ntui_Bind{*master.tuiObject,*slave.tuiObject}
  !_ASSERT{*master}
  !_ASSERT{*slave}

  If *master\bind = Null AND *slave\bind=Null
   *master\bind = *slave
   *slave\bind = *master
   ntui_SendEvent{*master,#TUIEV_PROPAGATE}
   Statement Return
  End If

  If *master\bind=Null AND *slave\bind><Null
    *master\bind = *slave\bind
    *slave\bind  = *master
    ntui_SendEvent{*master,#TUIEV_PROPAGATE}
    Statement Return
  End If

  If *master\bind><Null AND *slave\bind=Null
    *slave\bind  = *master\bind
    *master\bind = *slave
    ntui_SendEvent{*master,#TUIEV_PROPAGATE}
    Statement Return
  End If

  If *master\bind><Null AND *slave\bind><Null
    *objExchange.tuiObject = *master\bind
    *master\bind = *slave\bind
    *slave\bind  = *objExchange
    ntui_SendEvent{*master,#TUIEV_PROPAGATE}
  End If

End Statement

;/* auto init */
If _ntui_Init{} =False Then End ; and call it

Statement ntui_InitDefaultClass{}

  classID.l = ntui_RegisterClass{"object",Null,Null}

  ntui_RegisterAttr{classID ,"string"        ,#TUIA_STRING        ,#TUIAT_STRING ,0,0}
  ntui_RegisterAttr{classID ,"value"         ,#TUIA_VALUE         ,#TUIAT_INT    ,0,0}
  ntui_RegisterAttr{classID ,"help"          ,#TUIA_HELP          ,#TUIAT_STRING ,0,0}
  ntui_RegisterAttr{classID ,"flags"         ,#TUIA_SETFLAGS      ,#TUIAT_INT    ,0,0}
  ntui_RegisterAttr{classID ,"width"         ,#TUIA_WIDTH         ,#TUIAT_INT    ,0,0}
  ntui_RegisterAttr{classID ,"height"        ,#TUIA_HEIGHT        ,#TUIAT_INT    ,0,0}
  ntui_RegisterAttr{classID ,"minwidth"      ,#TUIA_MINWIDTH      ,#TUIAT_INT    ,0,0}
  ntui_RegisterAttr{classID ,"minheight"     ,#TUIA_MINHEIGHT     ,#TUIAT_INT    ,0,0}
  ntui_RegisterAttr{classID ,"maxwidth"      ,#TUIA_MAXWIDTH      ,#TUIAT_INT    ,0,0}
  ntui_RegisterAttr{classID ,"maxheight"     ,#TUIA_MAXHEIGHT     ,#TUIAT_INT    ,0,0}
  ntui_RegisterAttr{classID ,"xpos"          ,#TUIA_XPOS          ,#TUIAT_INT    ,0,0}
  ntui_RegisterAttr{classID ,"ypos"          ,#TUIA_YPOS          ,#TUIAT_INT    ,0,0}
  ntui_RegisterAttr{classID ,"bordertype"    ,#TUIA_BORDERTYPE    ,#TUIAT_INT    ,#TUIBORDER_NONE,#TUIMAX_BORDER}
  ntui_RegisterAttr{classID ,"border"        ,#TUIA_BORDERTYPE    ,#TUIAT_INT    ,#TUIBORDER_NONE,#TUIMAX_BORDER}
  ntui_RegisterAttr{classID ,"focus"         ,#TUIA_FOCUS         ,#TUIAT_BOOL   ,0,0}
  ntui_RegisterAttr{classID ,"classid"       ,#TUIA_CLASSID       ,#TUIAT_INT    ,0,0}
  ntui_RegisterAttr{classID ,"id"            ,#TUIA_ID            ,#TUIAT_STRING ,0,0}
  ntui_RegisterAttr{classID ,"bgpen"         ,#TUIA_BGPEN         ,#TUIAT_INT    ,0,#TUIMAX_PENS}
  ntui_RegisterAttr{classID ,"fgpen"         ,#TUIA_FGPEN         ,#TUIAT_INT    ,0,#TUIMAX_PENS}
  ntui_RegisterAttr{classID ,"bgrgb"         ,#TUIA_BGRGB         ,#TUIAT_INT    ,0,0}
  ntui_RegisterAttr{classID ,"fgrgb"         ,#TUIA_FGRGB         ,#TUIAT_INT    ,0,0}
  ntui_RegisterAttr{classID ,"bgimage"       ,#TUIA_BGIMAGE       ,#TUIAT_STRING ,0,0}
  ntui_RegisterAttr{classID ,"fgimage"       ,#TUIA_FGIMAGE       ,#TUIAT_STRING ,0,0}
  ntui_RegisterAttr{classID ,"setflags"      ,#TUIA_SETFLAGS      ,#TUIAT_INT    ,0,0}
  ntui_RegisterAttr{classID ,"clearflags"    ,#TUIA_CLEARFLAGS    ,#TUIAT_INT    ,0,0}
  ntui_RegisterAttr{classID ,"invisible"     ,#TUIA_INVISIBLE     ,#TUIAT_BOOL   ,0,0}
  ntui_RegisterAttr{classID ,"enabled"       ,#TUIA_ENABLED       ,#TUIAT_BOOL   ,0,0}
  ntui_RegisterAttr{classID ,"onscreen"      ,#TUIA_ONSCREEN      ,#TUIAT_BOOL   ,0,0}
  ntui_RegisterAttr{classID ,"active"        ,#TUIA_ACTIVE        ,#TUIAT_BOOL   ,0,0}
  ntui_RegisterAttr{classID ,"mover"         ,#TUIA_MOVER         ,#TUIAT_BOOL   ,0,0}
  ntui_RegisterAttr{classID ,"disabled"      ,#TUIA_DISABLED      ,#TUIAT_BOOL   ,0,0}
  ntui_RegisterAttr{classID ,"samewidth"     ,#TUIA_SAMEWIDTH     ,#TUIAT_BOOL   ,0,0}
  ntui_RegisterAttr{classID ,"sameheight"    ,#TUIA_SAMEHEIGHT    ,#TUIAT_BOOL   ,0,0}
  ntui_RegisterAttr{classID ,"samesize"      ,#TUIA_SAMESIZE      ,#TUIAT_BOOL   ,0,0}
  ntui_RegisterAttr{classID ,"fixwidth"      ,#TUIA_FIXWIDTH      ,#TUIAT_BOOL   ,0,0}
  ntui_RegisterAttr{classID ,"fixheight"     ,#TUIA_FIXHEIGHT     ,#TUIAT_BOOL   ,0,0}
  ntui_RegisterAttr{classID ,"fixsize"       ,#TUIA_FIXSIZE       ,#TUIAT_BOOL   ,0,0}
  ntui_RegisterAttr{classID ,"leftmargin"    ,#TUIA_LEFTMARGIN    ,#TUIAT_INT    ,0,0}
  ntui_RegisterAttr{classID ,"rightmargin"   ,#TUIA_RIGHTMARGIN   ,#TUIAT_INT    ,0,0}
  ntui_RegisterAttr{classID ,"topmargin"     ,#TUIA_TOPMARGIN     ,#TUIAT_INT    ,0,0}
  ntui_RegisterAttr{classID ,"bottommargin"  ,#TUIA_BOTTOMMARGIN  ,#TUIAT_INT    ,0,0}
  ntui_RegisterAttr{classID ,"leftpadding"   ,#TUIA_LEFTPADDING   ,#TUIAT_INT    ,0,0}
  ntui_RegisterAttr{classID ,"rightpadding"  ,#TUIA_RIGHTPADDING  ,#TUIAT_INT    ,0,0}
  ntui_RegisterAttr{classID ,"toppadding"    ,#TUIA_TOPPADDING    ,#TUIAT_INT    ,0,0}
  ntui_RegisterAttr{classID ,"bottompadding" ,#TUIA_BOTTOMPADDING ,#TUIAT_INT    ,0,0}
  ntui_RegisterAttr{classID ,"cbox"          ,#TUIA_CBOX          ,#TUIAT_PTR    ,0,0}
  ntui_RegisterAttr{classID ,"ibox"          ,#TUIA_IBOX          ,#TUIAT_PTR    ,0,0}
  ntui_RegisterAttr{classID ,"bbox"          ,#TUIA_BBOX          ,#TUIAT_PTR    ,0,0}
  ntui_RegisterAttr{classID ,"userdata"      ,#TUIA_USERDATA      ,#TUIAT_INT    ,0,0}
  ntui_RegisterAttr{classID ,"padding"       ,#TUIA_PADDING       ,#TUIAT_INT    ,0,0}
  ntui_RegisterAttr{classID ,"margin"        ,#TUIA_MARGIN        ,#TUIAT_INT    ,0,0}
  ntui_RegisterAttr{classID ,"mousepointer"  ,#TUIA_MOUSEPOINTER  ,#TUIAT_INT    ,0,0}
  ntui_RegisterAttr{classID ,"bind"          ,#TUIA_NONE          ,#TUIAT_BIND   ,0,0}
  ntui_RegisterAttr{classID ,"align"         ,#TUIA_ALIGN         ,#TUIAT_INT    ,0,0}
  ntui_RegisterAttr{classID ,"gradient"      ,#TUIA_GRADIENT      ,#TUIAT_INT    ,0,0}

  ntui_RegisterValue{classID,"left"          ,#TUIALIGN_LEFT}
  ntui_RegisterValue{classID,"right"         ,#TUIALIGN_RIGHT}
  ntui_RegisterValue{classID,"center"        ,#TUIALIGN_CENTER}
  ntui_RegisterValue{classID,"top"           ,#TUIALIGN_TOP}
  ntui_RegisterValue{classID,"bottom"        ,#TUIALIGN_BOTTOM}
  ntui_RegisterValue{classID,"middle"        ,#TUIALIGN_MIDDLE}

  ntui_RegisterValue{classID,"vertical"      ,#TUIORIENT_VERTICAL}
  ntui_RegisterValue{classID,"horizontal"    ,#TUIORIENT_HORIZONTAL}
  ntui_RegisterValue{classID,"invvertical"      ,#TUIORIENT_INVVERTICAL}
  ntui_RegisterValue{classID,"invhorizontal"    ,#TUIORIENT_INVHORIZONTAL}

  ntui_RegisterValue{classID,"true"          ,True}
  ntui_RegisterValue{classID,"false"         ,False}
  ntui_RegisterValue{classID,"yes"           ,True}
  ntui_RegisterValue{classID,"no"            ,False}
  ntui_RegisterValue{classID,"on"            ,True}
  ntui_RegisterValue{classID,"off"           ,False}

  ntui_RegisterValue{classID,"transparent"   ,#TUIPEN_TRANSPARENT}
  ntui_RegisterValue{classID,"background"    ,#TUIPEN_BACKGROUND }
  ntui_RegisterValue{classID,"bg"            ,#TUIPEN_BG         }
  ntui_RegisterValue{classID,"black"         ,#TUIPEN_BLACK      }
  ntui_RegisterValue{classID,"white"         ,#TUIPEN_WHITE      }
  ntui_RegisterValue{classID,"shine"         ,#TUIPEN_SHINE      }
  ntui_RegisterValue{classID,"shadow"        ,#TUIPEN_SHADOW     }
  ntui_RegisterValue{classID,"halfshine"     ,#TUIPEN_HALFSHINE  }
  ntui_RegisterValue{classID,"halfshadow"    ,#TUIPEN_HALFSHADOW }
  ntui_RegisterValue{classID,"marker"        ,#TUIPEN_MARKER     }
  ntui_RegisterValue{classID,"halfmarker"    ,#TUIPEN_HALFMARKER }
  ntui_RegisterValue{classID,"text"          ,#TUIPEN_TEXT       }
  ntui_RegisterValue{classID,"activetext"    ,#TUIPEN_ACTIVETEXT }
  ntui_RegisterValue{classID,"tooltipbg"     ,#TUIPEN_TOOLTIPBG  }
  ntui_RegisterValue{classID,"halfhalfshine" ,#TUIPEN_HALFHALFSHINE}
  ntui_RegisterValue{classID,"red"           ,#TUIPEN_RED        }
  ntui_RegisterValue{classID,"green"         ,#TUIPEN_GREEN      }
  ntui_RegisterValue{classID,"blue"          ,#TUIPEN_BLUE       }
  ntui_RegisterValue{classID,"halfhalfshadow",#TUIPEN_HALFHALFSHADOW}

  ntui_RegisterValue{classID,"none"          ,#TUIBORDER_NONE     }
  ntui_RegisterValue{classID,"flat"          ,#TUIBORDER_FLAT     }
  ntui_RegisterValue{classID,"recessed"      ,#TUIBORDER_RECESSED }
  ntui_RegisterValue{classID,"raised"        ,#TUIBORDER_RAISED   }
  ntui_RegisterValue{classID,"group"         ,#TUIBORDER_GROUP    }
  ntui_RegisterValue{classID,"button"        ,#TUIBORDER_BUTTON   }
  ntui_RegisterValue{classID,"string"        ,#TUIBORDER_STRING   }
  ntui_RegisterValue{classID,"title"         ,#TUIBORDER_TITLE    }
  ntui_RegisterValue{classID,"tool"          ,#TUIBORDER_TOOL     }
  ntui_RegisterValue{classID,"menubar"       ,#TUIBORDER_MENUBAR  }
  ntui_RegisterValue{classID,"tooltip"       ,#TUIBORDER_TOOLTIP  }
  ntui_RegisterValue{classID,"menu"          ,#TUIBORDER_MENU     }
  ntui_RegisterValue{classID,"window"        ,#TUIBORDER_WINDOW   }
  ntui_RegisterValue{classID,"tabcontent"    ,#TUIBORDER_TABCONTENT}
  ntui_RegisterValue{classID,"tab"           ,#TUIBORDER_TAB      }
  ntui_RegisterValue{classID,"tabviewend"    ,#TUIBORDER_TABVIEWEND}
  ntui_RegisterValue{classID,"tabviewbegin"  ,#TUIBORDER_TABVIEWBEGIN}
  ntui_RegisterValue{classID,"hscrollercontainer" ,#TUIBORDER_HSCROLLERCONTAINER}
  ntui_RegisterValue{classID,"vscrollercontainer" ,#TUIBORDER_VSCROLLERCONTAINER}

  ntui_RegisterValue{classID,"filerequest"    ,#TUIFUNC_FILE}
  ntui_RegisterValue{classID,"saverequest"    ,#TUIFUNC_SAVE}
  ntui_RegisterValue{classID,"pathrequest"    ,#TUIFUNC_PATH}
  ntui_RegisterValue{classID,"screenrequest"  ,#TUIFUNC_SCREEN}
  ntui_RegisterValue{classID,"fontrequest"    ,#TUIFUNC_FONT}

End Statement



ntui_InitDefaultClass{}
XINCLUDE "obj.engine.ab3" ; done
XINCLUDE "obj.group.ab3"  ; done
XINCLUDE "obj.space.ab3"  ; done
XINCLUDE "obj.button.ab3" ; ok, just subclasses
XINCLUDE "obj.label.ab3"  ; done
XINCLUDE "obj.scroller.ab3" ; ok, some Flag issues
XINCLUDE "obj.slider.ab3"   ; done
XINCLUDE "obj.cycle.ab3"
XINCLUDE "obj.customview.ab3"
XINCLUDE "obj.scrollview.ab3"
XINCLUDE "obj.progressbar.ab3"
XINCLUDE "obj.tabview.ab3" ; done
XINCLUDE "obj.textbox.ab3"
XINCLUDE "obj.seperator.ab3" ; => fuse with Balancer
XINCLUDE "obj.balancer.ab3"
XINCLUDE "obj.treeview.ab3"
XINCLUDE "obj.window.ab3"   ; done, needs WFLags fix
XINCLUDE "obj.menuitem.ab3"
XINCLUDE "obj.listview.ab3"
XINCLUDE "obj.imageview.ab3"


;///////////////////////////////////////////////////////////////////////////////
;/                                                                             /
;/ Syntax: ntui_ShowBubble {*engine.tuiEngine,*text.str,x.l,y.l}            /
;/                                                                             /
;/ Description:                                                                /
;/ Show a help bubble over the given coordinates.                                                /
;/                                                                             /
;/ Inputs:                                                                     /
;/ - *engine.tuiEngine    : ???                                             /
;/ - *text.str    : ???                                                        /
;/ - x.l    : ???                                                              /
;/ - y.l    : ???                                                              /
;/                                                                             /
;///////////////////////////////////////////////////////////////////////////////
_ntui_ShowToolTip:
Statement _ntui_ShowToolTip{*engine.tuiEngine,*text.str,x.l,y.l}

  If *engine=Null Then Statement Return   ; no engine!
  If *engine\screen_=Null Then Statement Return  ; engine not popped up on screen
  If *engine\toolTip Then ntui_FreeObject{*engine\toolTip} : *engine\toolTip=Null

  ntui_BeginBuild{*engine}
  DEFTYPE.tag10 tagList
  id.s = "TOOLTIPWIN"
  tagList\ti_Tag = #TUIA_ID,&id,#TUIA_BGPEN,#TUIPEN_BG,#TUIA_MARGIN,0,#TUIA_PADDING,0,#TUIA_FIXSIZE,1,#TAG_DONE,0
  *toolTip.tuiWindow = ntui_BeginWindow{(.TagItem)tagList}

  If *toolTip
    tagList\ti_Tag = #TUIGA_ORIENTATION,#TUIORIENT_HORIZONTAL,#TUIA_BORDERTYPE,#TUIBORDER_TOOLTIP,#TUIA_BGPEN,#TUIPEN_TOOLTIPBG,#TUIA_MARGIN,0,#TUIA_PADDING,0,#TAG_DONE,0
    *group.tuiGroup = ntui_BeginGroup{(.TagItem)tagList}

    CNIF @#TUI_HAS_IMAGEVIEW
    If *engine\useAISS
      ntuib_ImageView{"TBImages:help",#TUISIZE_INLINE}
    End If
    CEND

    id = "TOOLTIP"
    tagList\ti_Tag = #TUIA_ID,&id,#TUILA_TEXT,str_GetPtr{*text},#TAG_DONE,0
    *label.tuiLabel = ntui_Label{(.TagItem)tagList}
    ntui_EndGroup{}
    ntui_EndWindow{}

    *toolTip\oswinFlags = #WFLG_BORDERLESS|#WFLG_NOCAREREFRESH|#WFLG_SIMPLE_REFRESH

    ;Forbid_
    ;*oldFont.TextFont =*engine\screen_\RastPort\Font
    _ntui_CalculateMinSize{*toolTip,*engine\buildRP}
    ;SetFont_ *engine\screen_\RastPort,*oldFont
    ;Permit_

    x = x -*toolTip\obj\minsize\x/2
   ; y = y + *engine\font[#TUIFONT_NORMAL]\tf_YSize-1

    addy.l = *engine\font[#TUIFONT_NORMAL]\tf_YSize - *group\obj\border\bottom

    y = y -*toolTip\obj\minsize\y - addy
    If y + *engine\font[#TUIFONT_NORMAL]\tf_YSize +10 >*engine\screen_\Height
      ;y = y - *engine\font[#TUIFONT_NORMAL]\tf_YSize -15-15
      y = y -*toolTip\obj\minsize\y - *engine\font[#TUIFONT_NORMAL]\tf_YSize*2+2
    End If
    *engine\toolTip = *toolTip
    ntui_ShowWindow{*toolTip,x,y}
  End If
  ntui_EndBuild{}

End Statement
!_MakeGlobalFuncPointer{_ntui_ShowToolTip,{Null,Null,0,0}}
tuiGlobal\showToolTip = *_ntui_ShowToolTip
;!_GetFuncPointer{tuiGlobal\showToolTip     ,_ntui_ShowToolTip,{Null,Null,0,0}}


Statement ntui_ShowToolTip{*engine.tuiEngine,text.s,@x.l,@y.l}
  str_Write{&*text.str,text}

  If (*engine\screen_ = Null) Then _ntui_OpenScreen{*engine,Null}
  If (*engine\screen_ = Null) Then Statement Return ; no screen

  If x<0 Then x=*engine\screen_\MouseX
  If y<0 Then y=*engine\screen_\MouseY
  _ntui_ShowToolTip{*engine,*text,x,y}
  str_Free{&*text}
End Statement


;///////////////////////////////////////////////////////////////////////////////
;/                                                                             /
;/ Syntax: succ.w = ntui_PopUp {*engine.tuiEngine,@pubname.s,@*scr.Scre:: /
;/ en}                                                                         /
;/                                                                             /
;/ Description:                                                                /
;/ Pop the tui engine up on a screen.                                      /
;/ This reverses the effect of ntui_Iconify.
;/ In iconified mode (or if tui tuiEngine was just created), it is not allowed to open any window. The engine must first "popped" to a screen.
;/                                                                             /
;/ Inputs:                                                                     /
;/ - *engine.tuiEngine : tuiEngine to pop up                                             /
;/ - pubname.s            : name of the PubScreen (e.g. "Workbench")                                                       /
;/ - *scr.Screen          : pointer to a custom screen                                                     /
;/                                                                             /
;/ Result:                                                                     /
;/ - succ.w               : True if popped up, False if failed                                                        /
;/                                                                             /
;///////////////////////////////////////////////////////////////////////////////
Function.w ntui_PopUp{*engine.tuiEngine,@pubScreenName.s,@*scr.Screen}
  !_ASSERT{*engine}

  succ.w = False
  ntui_SetAttr{*engine,#TUIEA_PUBSCREENNAME,&pubScreenName}
  If _ntui_OpenScreen{*engine,*scr}

    *child.tuiObject=*engine\obj\child
    While *child
      If *child\classID = #TUICLASS_WINDOW
        *win.tuiWindow = (.tuiWindow)*child
        If *win\state = #TUISTATE_ICONIFIED
          ntui_ShowWindow{*win}
        End If
      EndIf
      *child = *child\next_
    Wend

    succ=True
  End If

  Function Return succ

End Function



;///////////////////////////////////////////////////////////////////////////////
;/                                                                             /
;/ Syntax: result.w = ntui_Iconify {*engine.tuiEngine}                      /
;/                                                                             /
;/ Description:                                                                /
;/ * re-open all windows, if any... */                                         /
;/                                                                             /
;/ Inputs:                                                                     /
;/ - *engine.tuiEngine    : ???                                             /
;/                                                                             /
;/ Result:                                                                     /
;/ - result.w     : ???                                                        /
;/                                                                             /
;///////////////////////////////////////////////////////////////////////////////
Function.w ntui_Iconify{*engine.tuiEngine}

  If *engine =Null Then Function Return False
  If *engine\obj\classID><#TUICLASS_ENGINE Then Function Return False
  If *engine\screen_=Null Then Function Return True ; we are already iconified...

  *scr.Screen = *engine\screen_
  ;/* hide all windows */
  *child.tuiObject = *engine\obj\child             ; free all children
  While *child
    If *child\classID=#TUICLASS_WINDOW
      *win.tuiWindow = (.tuiWindow)*child
      ntui_HideWindow{*win}
      *win\state = #TUISTATE_ICONIFIED
    EndIf
    *child = *child\next_
  Wend

  _ntui_CloseScreen{*engine.tuiEngine}

  Function Return True

End Function


Statement _ntui_AmigaCollectIDCMPEvents{*engine.tuiEngine}
  !_ASSERT{*engine}
  If (*engine\winPort=Null) Then Statement Return

  Repeat
    *msg.IntuiMessage = GetMsg_(*engine\winPort)
    If *msg
      *oswin.Window  = *msg\IDCMPWindow
      If *oswin Then *obj.tuiObject = (.tuiObject)*oswin\UserData : Else *obj=*engine

      Select (*msg\Class)    ; start to make some interpretations
        Case #IDCMP_MOUSEBUTTONS
          mouseButtonID.l = *msg\Code & $3

          If (*msg\Code&#IECODE_UP_PREFIX)
            tuiMouseButtonMap(mouseButtonID)=False
          Else
            tuiMouseButtonMap(mouseButtonID)=True
            If (DoubleClick_(*engine\lastSeconds,*engine\lastMicros,*msg\Seconds,*msg\Micros)><0)
              *engine\clickCount+1
            Else
              *engine\clickCount=1
            End If

            *engine\lastSeconds = *msg\Seconds
            *engine\lastMicros  = *msg\Micros
            *engine\lastMouse\x = *msg\MouseX,*msg\MouseY
          End If

          *ev.tuiEvent = _ntui_CreateEvent{*obj,#TUIEV_MOUSEDOWN}
          If *ev
            *ev\qualifier_ = *msg\Qualifier & $FFFF
            If *engine\clickCount=2 Then *ev\qualifier_ | #TUIQUAL_DOUBLECLICK
            If *engine\clickCount=3 Then *ev\qualifier_ | #TUIQUAL_TRIPLECLICK
            *ev\value      = mouseButtonID
            *ev\pos\x      = *msg\MouseX,*msg\MouseY
            *ev\seconds    = *msg\Seconds
            *ev\micros     = *msg\Micros
            If (tuiMouseButtonMap(mouseButtonID)=False) Then *ev\evID = #TUIEV_MOUSEUP
          End If

        Case #IDCMP_MOUSEMOVE
          *ev.tuiEvent = _ntui_CreateEvent{*obj,#TUIEV_MOUSEMOVE}
          If *ev
            *ev\qualifier_ = *msg\Qualifier
            *ev\pos\x      = *msg\MouseX,*msg\MouseY
            *ev\seconds    = *msg\Seconds
            *ev\micros     = *msg\Micros
          End If

        Case #IDCMP_INTUITICKS
          *ev.tuiEvent = _ntui_CreateEvent{*obj,#TUIEV_TICK}
          If *ev
            *ev\qualifier_ = *msg\Qualifier
            *ev\pos\x      = *msg\MouseX,*msg\MouseY
            *ev\seconds    = *msg\Seconds
            *ev\micros     = *msg\Micros
          End If

        Case #IDCMP_MENUPICK
          If *msg\Code=-1
            menuTitle_.l   = -1
            menuItem_.l    = -1
            menuSubItem.l  = -1
          Else
            menuTitle_   = *msg\Code & $1F
            menuItem_    = (*msg\Code LSR 5) & $3F
            menuSubItem  = (*msg\Code LSR 11) & $1F : If menuSubItem = $1f Then menuSubItem=-1
            If menuSubItem>=0
              ;\code = GTMenuChecked(\menu_title,\menu_item,\menu_sub)
            Else
              ;\code = GTMenuChecked(\menu_title,\menu_item)
            End If
          End If
          *ev.tuiEvent = Null
          ; unsupported!

        Case #IDCMP_RAWKEY
          rawKeyCode.l     = *msg\Code&$FF
          Select rawKeyCode
            Case #RAWKEY_WHEELUP
              *ev.tuiEvent = _ntui_CreateEvent{*obj,#TUIEV_VSCROLL}
              If *ev
                *ev\value      = -1
                *ev\qualifier_ = *msg\Qualifier
                *ev\seconds    = *msg\Seconds
                *ev\micros     = *msg\Micros
                *ev\pos\x      = *msg\MouseX,*msg\MouseY
              End If

            Case #RAWKEY_WHEELDOWN
              *ev.tuiEvent = _ntui_CreateEvent{*obj,#TUIEV_VSCROLL}
              If *ev
                *ev\value      = 1
                *ev\qualifier_ = *msg\Qualifier
                *ev\seconds    = *msg\Seconds
                *ev\micros     = *msg\Micros
                *ev\pos\x      = *msg\MouseX,*msg\MouseY
              End If

            Default
              If *msg\Code&#IECODE_UP_PREFIX
                tuiRawKeyMap(rawKeyCode) = False
              Else
                tuiRawKeyMap(rawKeyCode) = True
                inputEvent.InputEvent\ie_NextEvent = 0
                inputEvent\ie_Class     = #IECLASS_RAWKEY
                inputEvent\ie_SubClass  = 0
                inputEvent\ie_Code      = *msg\Code
                inputEvent\ie_Qualifier = *msg\Qualifier
                tmp.l = _ntui_ObtainTempBuffer{128}
                Poke.b tmp,0
                MapRawKey_ inputEvent,tmp,128,False
                vanillaString.s  = Peek.s(tmp)
                _ntui_ReleaseTempBuffer{}
              End If

              *ev.tuiEvent = _ntui_CreateEvent{*obj,#TUIEV_KEYDOWN}
              If *ev
                *ev\value      = rawKeyCode
                *ev\qualifier_ = *msg\Qualifier
                *ev\seconds    = *msg\Seconds
                *ev\micros     = *msg\Micros
                str_Write{&*ev\string,vanillaString}
                *ev\pos\x      = *msg\MouseX,*msg\MouseY
                If (tuiRawKeyMap(rawKeyCode)=False) Then *ev\evID = #TUIEV_KEYUP
              End If
          End Select

        Case #IDCMP_MENUVERIFY
          *ev.tuiEvent = Null
          ; unsupported

        Case #IDCMP_CLOSEWINDOW
          *ev.tuiEvent = _ntui_CreateEvent{*obj,#TUIEV_CLOSE}

        Case #IDCMP_ACTIVEWINDOW
          *ev.tuiEvent = _ntui_CreateEvent{*obj,#TUIEV_FOCUS}

        Case #IDCMP_INACTIVEWINDOW
          *ev.tuiEvent = _ntui_CreateEvent{*obj,#TUIEV_LOSEFOCUS}

        Case #IDCMP_NEWSIZE
          *ev.tuiEvent = Null
          If *oswin
            If !tuiRectWidth{*obj\bbox}><*oswin\Width OR !tuiRectHeight{*obj\bbox}><*oswin\Height
              *rp.RastPort = ntui_ObtainRastPort{*obj}
              If *rp
                _ntui_Layout{*obj,*rp,Null}
                ntui_ReleaseRastPort{*obj}
              End If
              _ntui_Redraw{*obj,*obj\bbox}
            End If
          End If

        Case #IDCMP_REFRESHWINDOW
          If *oswin
            clip.tuiRect\left=0,0,0,0
            If *obj\classID=#TUICLASS_WINDOW
              *win.tuiWindow = (.tuiWindow)*obj
              *win\osrefreshing = True
            Else
              *win=Null
            End If

            LockLayerInfo_ *oswin\WScreen\LayerInfo
            BeginRefresh_ *oswin

            If *oswin\WLayer\DamageList
              clip\left   = *oswin\WLayer\DamageList\bounds\MinX
              clip\top    = *oswin\WLayer\DamageList\bounds\MinY
              clip\right  = *oswin\WLayer\DamageList\bounds\MaxX
              clip\bottom = *oswin\WLayer\DamageList\bounds\MaxY
              If !tuiValidRect{clip}
                _ntui_Redraw{*obj,clip}
              End If
            End If

            EndRefresh_ *oswin,1
            UnlockLayerInfo_ *oswin\WScreen\LayerInfo
            ;error{"Refresh Bounds Damage:"+Str$(clip\left)+"/"+Str$(clip\top)+" - "+Str$(clip\right)+"/"+Str$(clip\bottom)}
            ;error{"Refresh Bounds Clip:"+Str$(clip2\left)+"/"+Str$(clip2\top)+" - "+Str$(clip2\right)+"/"+Str$(clip2\bottom)}
            If *win Then *win\osrefreshing = False
          End If

         Case #IDCMP_EXTENDEDMOUSE
           mcode.l = *msg\Code&$FFFF
           If (mcode = #IMSGCODE_INTUIWHEELDATA)

             *wd.IntuiWheelData  = *msg\IAddress
             If *wd\WheelY><0
               *ev.tuiEvent = _ntui_CreateEvent{*obj,#TUIEV_VSCROLL}
               If *ev
                 *ev\value      = *wd\WheelY
                 *ev\qualifier_ = *msg\Qualifier
                 *ev\seconds    = *msg\Seconds
                 *ev\micros     = *msg\Micros
                 *ev\pos\x      = *msg\MouseX,*msg\MouseY
               End If
               _ntui_DispatchEvent{*ev} : *ev.tuiEvent = Null
             End If
             If *wd\WheelX><0
               *ev.tuiEvent = _ntui_CreateEvent{*obj,#TUIEV_HSCROLL}
               If *ev
                 *ev\value      = *wd\WheelX
                 *ev\qualifier_ = *msg\Qualifier
                 *ev\seconds    = *msg\Seconds
                 *ev\micros     = *msg\Micros
                 *ev\pos\x      = *msg\MouseX,*msg\MouseY
               End If
               _ntui_DispatchEvent{*ev} : *ev.tuiEvent = Null
             End If
           End If

        Default
          *ev.tuiEvent = Null
          ; unsupported

      End Select
      ReplyMsg_ *msg
      If *ev Then _ntui_DispatchEvent{*ev} : *ev.tuiEvent = Null

    End If
  Until (*msg=Null)
End Statement


Statement _ntui_AmigaCollectAppEvents{*engine.tuiEngine}
  If (*engine\appPort=Null) Then Statement Return
  Repeat
    *appMsg.AppMessage = GetMsg_(*engine\appPort)
    If *appMsg
      *win.tuiWindow = *appMsg\am_UserData
      If *win
        *oswin.Window  = *win\oswin
        *obj.tuiObject = *win
      Else
        *oswin         = Null
        *obj           = *engine
      End If

      Select *appMsg\am_ID
        Case #APP_WINDOWDROP
          *ev.tuiEvent = _ntui_CreateEvent{*obj,#TUIEV_WINDOWDROP}
          If *ev
            If *appMsg\am_ArgList
              If *appMsg\am_ArgList\wa_Name><0 AND *appMsg\am_ArgList\wa_Lock><0
                tmpP.l = _ntui_ObtainTempBuffer{#APP_BUFFERSIZE}
                If NameFromLock_(*appMsg\am_ArgList\wa_Lock,tmpP,#APP_BUFFERSIZE)
                  If AddPart_(tmpP,*appMsg\am_ArgList\wa_Name,#APP_BUFFERSIZE)
                    str_Write{&*ev\string,Peek.s(tmpP)}
                  End If
                End If
                _ntui_ReleaseTempBuffer{}
              End If
            End If
            *ev\pos\x = *appMsg\am_MouseX,*appMsg\am_MouseY
          End If

        Case #APP_MENUHIT
          *ev.tuiEvent = _ntui_CreateEvent{*obj,#TUIEV_POPUP}

        Case #APP_ICON  ; drop or click ?
          *ev.tuiEvent = _ntui_CreateEvent{*obj,#TUIEV_ICONDROP}
          If (*ev)
            If (*appMsg\am_ArgList)
              If *appMsg\am_ArgList\wa_Name><0 AND *appMsg\am_ArgList\wa_Lock><0
                tmpP.l = _ntui_ObtainTempBuffer{#APP_BUFFERSIZE}
                If (NameFromLock_(*appMsg\am_ArgList\wa_Lock,tmpP,#APP_BUFFERSIZE))
                  If (AddPart_(tmpP,*appMsg\am_ArgList\wa_Name,#APP_BUFFERSIZE))
                    str_Write{&*ev\string,Peek.s(tmpP)}
                  End If
                End If
                _ntui_ReleaseTempBuffer{}
              End If
            End If
            If (*ev\string=Null) Then *ev\evID = #TUIEV_POPUP
          End If

        Default
          *ev.tuiEvent = Null

      End Select

      ReplyMsg_ *appMsg
      If *ev Then _ntui_DispatchEvent{*ev} : *ev.tuiEvent = Null
    End If
  Until (*appMsg=Null)

End Statement


Statement _ntui_AmigaCollectAREXXEvents{*engine.tuiEngine}
  If *engine\rxPort=Null Then Statement Return

  While _ntui_GetRexxMsg{*engine,True}
    *ev.tuiEvent = _ntui_CreateEvent{*engine,#TUIEV_MESSAGE}
    If (*ev)
      string.s = str_Read{&*engine\rxString}
      str_Write{&*ev\string,string}
      _ntui_DispatchEvent{*ev}
    End If
  Wend

End Statement

Statement _ntui_AmigaCollectScreenNotifyEvents{*engine.tuiEngine}
  If *engine\snPort=Null Then Statement Return

  *msg.ScreenNotifyMessage = GetMsg_(*engine\snPort)
  While *msg
    Select *msg\snm_Type
      Case #SCREENNOTIFY_TYPE_WORKBENCH
        If *msg\snm_Value
          *ev.tuiEvent = _ntui_CreateEvent{*engine,#TUIEV_POPUP}
        Else
          *ev.tuiEvent = _ntui_CreateEvent{*engine,#TUIEV_ICONIFY}
          ntui_Iconify{*engine}
        End If
      Case #SCREENNOTIFY_TYPE_CLOSESCREEN
        If *msg\snm_Value = *engine\screen_
          *ev.tuiEvent = _ntui_CreateEvent{*engine,#TUIEV_ICONIFY}
          ntui_Iconify{*engine}
        End If

    End Select
    If *ev Then _ntui_DispatchEvent{*ev} : *ev.tuiEvent = Null
    ReplyMsg_ *msg
    *msg.ScreenNotifyMessage = GetMsg_(*engine\snPort)
  Wend

End Statement



;///////////////////////////////////////////////////////////////////////////////
;/                                                                             /
;/ Syntax: *ev.tuiEvent = ntui_MoreEvents {*engine.tuiEngine}                   /
;/                                                                             /
;/ Description:                                                                /
;/ Get the next tuiEvent waiting in the input queue.
;/ Note: you should always call this in a loop until there is no more event waiting, because you dont get a signal for already waiting events that wake your task up while Wait_().                   /
;/ Note: To query information from the tuiEvent, check if it is not NULL and use the following macros:
;/                                                                             /
;///////////////////////////////////////////////////////////////////////////////
Function.tuiEvent ntui_GetEvent{*engine.tuiEngine}
  !_ASSERT{*engine}

  _ntui_AmigaCollectAREXXEvents        {*engine}
  _ntui_AmigaCollectAppEvents          {*engine}
  _ntui_AmigaCollectScreenNotifyEvents {*engine}

  If *engine\inputTask = Null       ; no inpout task, so get events it here...
    _ntui_AmigaCollectIDCMPEvents{*engine}
    Repeat
      *ev.tuiEvent =_ntui_UnqueueEvent{*engine\inQ}
      If *ev Then _ntui_DispatchEvent{*ev}
    Until *ev = Null
  End If

  If *engine\event_ Then _ntui_DestroyEvent{*engine\event_}

  *engine\event_ = _ntui_UnqueueEvent{*engine\outQ}

  Function Return *engine\event_
End Function


Function.tuiEvent ntui_WaitEvent{*engine.tuiEngine}
  Repeat
    sigf.l = Wait_ ($FFFFFFFF)
    If (sigf&#SIGBREAKF_CTRL_C) Then ntui_PostEvent{*engine,#TUIEV_QUIT}
    *ev.tuiEvent = ntui_GetEvent{*engine.tuiEngine}
  Until *ev
Function Return *ev
End Function

XINCLUDE "xml.ab3"
XINCLUDE "utilities.ab3"

CNIF #__include=0 ;/* ************* DEMO ************
WBStartup

CNIF @#TUI_HAS_CUSTOMVIEW
_ntui_MyDispatchEvent:
Function.w _ntui_MyDispatchEvent{*customView.tuiCustomView,*ev.tuiEvent}

  done.w = False

  Select  *ev\evID

    Case #TUIEV_TICK  : done=True  ; this event is done!

  End Select

  ; call the super-class function if not done
  If done=False Then done = _ntui_DispatchCustomViewEvent{*customView,*ev}

  Function Return done

End Function
CEND

myProgressCallback:
Function.w myProgressCallback{*notify.str,*ev.tuiEvent}
  SHARED dummyprogress.l,tickcount.l
  tickcount+1
  If tickcount>5
    tickcount=0
    dummyprogress +1
    If dummyprogress>100 Then dummyprogress=0
    ntui_SetValueByID{*ev\obj,"SBAR",dummyprogress}
  End If

  Function Return True

End Function
!_MakeGlobalFuncPointer{myProgressCallback,{Null,Null}}

!autoexit{_ntui_Free}


*engine.tuiEngine = ntuib_Engine{"NTUI-Demo","NTUI","NTUI-Demo-Menu","Sys:disk.info",""}
If *engine=Null Then error{"Unable to initialized Engine!"} : End ; Ooops!
a.s = "Sys:Prefs/Presets/NTUI/def2"
ntui_SetAttr{*engine,#TUIEA_SKINPATH,&a.s}
ntui_SetAttr{*engine,#TUIEA_USESKIN,0}
ntui_SetAttr{*engine,#TUIEA_USEAISS,0}
ntui_SetAttr{*engine,#TUIEA_BORDERSTYLE,0}
ntui_SetAttr{*engine,#TUIEA_FILLSTYLE,1}
ntui_SetAttr{*engine,#TUIEA_DEFPADDING,2}
ntui_SetAttr{*engine,#TUIEA_DEFMARGIN,2}
If ntui_PopUp{*engine}=False Then error{"Unable to pop up!"}:End ; whats wrong with the screen!?


;ntui_BuildFromXMLFile{*engine,"Sourcecodes:Audio/HD-Rec_SVN/HD-Rec_Source/Plugins/MacroWave/gui/main.xml"}
;ntui_BuildFromXMLMem{*engine,?xmldata}
;ntui_BuildFromXMLFile{*engine,"/Data/ntuitest.xml"}

ntui_BeginBuild{*engine}
*demoWin.tuiWindow= ntuib_BeginWindow{"Demo Window","DEMO",12345}
If *demoWin
  ntui_SetAttr{*demoWin,#TUIWA_ONTICKCB,*myProgressCallback}
  a.s = "SYS:Prefs/Presets/Backdrops/1280x1024/Dragon2.iff"
  ;ntui_SetAttr{*demoWin,#TUIA_BGIMAGE,&a}
  a.s ="QUIT"
  ntui_SetAttr{*demoWin,#TUIWA_ONCLOSE,&a}
  CNIF @#TUI_HAS_TABVIEW
  ntuib_BeginTabView{0,#TUITABSTYLE_TOP,"",-1,"TabView"}
  CEND
    *obj.tuiObject = ntuib_BeginVGroup{1,"Label"}
   ; ntui_SetAttr{*obj,#TUIA_LEFTPADDING,7}
   ; ntui_SetAttr{*obj,#TUIA_RIGHTPADDING,7}
   ; ntui_SetAttr{*obj,#TUIA_TOPPADDING,7}
   ; ntui_SetAttr{*obj,#TUIA_BOTTOMPADDING,7}
      ntuib_BeginHGroup{1}
        *obj = ntuib_Label    {"Label"}   : a.s = "Test!" : ntui_SetAttr{*obj,#TUIA_HELP,&a.s}
        ; ntui_SetAttr{*obj,#TUIA_BORDERTYPE,#TUIBORDER_BUTTON}
      ;  imagefile.s = "NTUI:button.9.png"
      ;  ntui_SetAttr{*obj,#TUIA_BGIMAGE,&imagefile}
        ntuib_Label    {"Bold Label",#TUIF_BOLD}
        ntuib_Label    {"Italic Label",#TUIF_ITALIC}
        ntuib_Label    {"Italic+Bold Label",#TUIF_ITALIC|#TUIF_BOLD}
        ntuib_Label    {"Underlined Label",#TUIF_UNDERLINED}
        ntuib_Label    {"Small Font Label",#TUIF_SMALL}
        ntuib_Label    {"Serif Font Label",#TUIF_SERIF}
        ntuib_Label    {"Fix Font Label",#TUIF_FIX}
        ntuib_Label    {"Disabled Label",#TUIF_DISABLED}
        ntuib_Label    {"Left Aligned Label",#TUIF_LEFT}
        ntuib_Label    {"Right Aligned Label",#TUIF_RIGHT}
        CNIF @#TUI_HAS_TEXTBOX
        *con.tuiTextBox = ntuib_Console {"NTUI Console V1.0\\nReady.\\n",100,"",-1,"CON"}
        CELSE
        CNIF @#TUI_HAS_SPACE
        ntuib_Space{0,0}
        CEND
        CEND
      ntui_EndGroup{}
    ntui_EndGroup{}

    CNIF @#TUI_HAS_BUTTON
    *obj = ntuib_BeginHGroup{0,"Button"}
       ;     imeagefile.s = "NTUI:button.9.png"
       ;     ntui_SetAttr{*obj,#TUIA_BGIMAGE,&imagefile}

      ntuib_BeginHGroup{2}
        ntuib_Label    {"Push Button",#TUIF_RIGHT}
        ntuib_Button   {"Button","","TEST","this is a very basic push-button with text"}

        ntuib_Label    {"Toggle Button",#TUIF_RIGHT}
        ntuib_Button   {"Toggle","","","this is a toggle button (on/off)",#TUIF_TOGGLE}

        ntuib_Label    {"Bold Button",#TUIF_RIGHT}
        ntuib_Button   {"Bold","","","this is a button using bold font",#TUIF_BOLD}

        ntuib_Label    {"Italic Button",#TUIF_RIGHT}
        ntuib_Button   {"Italic","","","this is a button using italic font",#TUIF_ITALIC}

        ntuib_Label    {"Italic+Bold Button",#TUIF_RIGHT}
        ntuib_Button   {"Italic+Bold","","","this is an italic and bold button",#TUIF_BOLD|#TUIF_ITALIC}

        ntuib_Label    {"Underlined Button",#TUIF_RIGHT}
        ntuib_Button   {"Underlined","","","this is an underlined button",#TUIF_UNDERLINED}

        ntuib_Label    {"Small Button",#TUIF_RIGHT}
        ntuib_Button   {"Small","","","this is a button using small font",#TUIF_SMALL}

        ntuib_Label    {"Serif Button",#TUIF_RIGHT}
        ntuib_Button   {"Serif","","","this is a button using big font",#TUIF_SERIF}

        ntuib_Label    {"Fix Button",#TUIF_RIGHT}
        ntuib_Button   {"Fix","","","this is a button using fix font",#TUIF_FIX}

        ntuib_Label    {"Disabled Button",#TUIF_RIGHT}
        ntuib_Button   {"Disabled","","","this is a disabled button",#TUIF_DISABLED}

        ntuib_Label    {"Left Button",#TUIF_RIGHT}
        ntuib_Button   {"Left","","","this is a button with left-aligned text",#TUIF_LEFT}

        ntuib_Label    {"Right Button",#TUIF_RIGHT}
        ntuib_Button   {"Right","","","this is a button with right-aligned text",#TUIF_RIGHT}

        ntuib_Space{0,0} : ntuib_Space{0,0}

      ntui_EndGroup{}
      CNIF @#TUI_HAS_SEPERATOR
      ntuib_VSeperator{""}
      CEND

      ntuib_BeginHGroup{2}
        ntuib_Label    {"Arrow Buttons",#TUIF_RIGHT}
        ntuib_BeginHGroup{0}
          ntuib_FunctionButton{#TUIFUNC_UP     ,"this is an arrow button"}
          ntuib_FunctionButton{#TUIFUNC_DOWN   ,"this is an arrow button"}
          ntuib_FunctionButton{#TUIFUNC_LEFT   ,"this is an arrow button"}
          ntuib_FunctionButton{#TUIFUNC_RIGHT  ,"this is an arrow button"}
          ntuib_FunctionButton{#TUIFUNC_UP     ,"this is an arrow button",#TUIF_DISABLED}
          ntuib_FunctionButton{#TUIFUNC_DOWN   ,"this is an arrow button",#TUIF_DISABLED }
          ntuib_FunctionButton{#TUIFUNC_LEFT   ,"this is an arrow button",#TUIF_DISABLED }
          ntuib_FunctionButton{#TUIFUNC_RIGHT  ,"this is an arrow button",#TUIF_DISABLED }
          ntuib_Space{0,0,#TUIF_FIXHEIGHT}
        ntui_EndGroup{} : ntuib_SetFlags{#TUIF_FIXHEIGHT,False}
        ntuib_Label    {"Outlined Arrow Buttons",#TUIF_RIGHT}
        ntuib_BeginHGroup{0}
          ntuib_FunctionButton{#TUIFUNC_UP   ,"this is an outlined arrow button"}
          ntuib_FunctionButton{#TUIFUNC_DOWN ,"this is an outlined arrow button"}
          ntuib_FunctionButton{#TUIFUNC_LEFT ,"this is an outlined arrow button"}
          ntuib_FunctionButton{#TUIFUNC_RIGHT,"this is an outlined arrow button"}
          ntuib_FunctionButton{#TUIFUNC_UP   ,"this is an outlined arrow button",#TUIF_DISABLED }
          ntuib_FunctionButton{#TUIFUNC_DOWN ,"this is an outlined arrow button",#TUIF_DISABLED }
          ntuib_FunctionButton{#TUIFUNC_LEFT ,"this is an outlined arrow button",#TUIF_DISABLED }
          ntuib_FunctionButton{#TUIFUNC_RIGHT,"this is an outlined arrow button",#TUIF_DISABLED }
          ntuib_Space{0,0,#TUIF_FIXHEIGHT}
        ntui_EndGroup{} : ntuib_SetFlags{#TUIF_FIXHEIGHT,False}
;        ntuib_Label    {"Menu Arrow Buttons",#TUIF_RIGHT}
;        ntuib_BeginHGroup{0}
;          ntuib_FunctionButton{#TUIFUNC_POPUP       ,"this is a pop up button"}
;          ntuib_FunctionButton{#TUIFUNC_DROPDOWN    ,"this is a drop down button"}
;          ntuib_FunctionButton{#TUIFUNC_LEFT        ,"this is a left menu button"}
;          ntuib_FunctionButton{#TUIFUNC_RIGHT       ,"this is a right menu button"}
;          ntuib_FunctionButton{#TUIFUNC_POPUP       ,"this is a pop up button",#TUIF_DISABLED }
;          ntuib_FunctionButton{#TUIFUNC_DROPDOWN    ,"this is a drop down button",#TUIF_DISABLED }
;          ntuib_FunctionButton{#TUIFUNC_LEFT        ,"this is a left menu button",#TUIF_DISABLED }
;          ntuib_FunctionButton{#TUIFUNC_RIGHT       ,"this is a right menu button",#TUIF_DISABLED }
;          ntuib_Space{0,0,#TUIF_FIXHEIGHT}
;        ntui_EndGroup{} : ntuib_SetFlags{#TUIF_FIXHEIGHT,False}
        ntuib_Label    {"ASL Buttons",#TUIF_RIGHT}
        ntuib_BeginHGroup{0}
          ntuib_FunctionButton{#TUIFUNC_PATH        ,"this is a button to pop up an ASL path requester"}
          ntuib_FunctionButton{#TUIFUNC_SAVE        ,"this is a button to pop up an ASL file requester in save mode"}
          ntuib_FunctionButton{#TUIFUNC_FILE        ,"this is a button to pop up an ASL file requester in open mode"}
          ntuib_FunctionButton{#TUIFUNC_FONT        ,"this is a button to pop up an ASL font requester"}
          ntuib_FunctionButton{#TUIFUNC_SCREEN      ,"this is a button to pop up an ASL screenmode requester"}
          ntuib_FunctionButton{#TUIFUNC_PATH        ,"this is a button to pop up an ASL path requester",#TUIF_DISABLED }
          ntuib_FunctionButton{#TUIFUNC_SAVE        ,"this is a button to pop up an ASL file requester in save mode",#TUIF_DISABLED }
          ntuib_FunctionButton{#TUIFUNC_FILE        ,"this is a button to pop up an ASL file requester in open mode",#TUIF_DISABLED }
          ntuib_FunctionButton{#TUIFUNC_FONT        ,"this is a button to pop up an ASL font requester",#TUIF_DISABLED }
          ntuib_FunctionButton{#TUIFUNC_SCREEN      ,"this is a button to pop up an ASL screenmode requester",#TUIF_DISABLED }
          ntuib_Space{0,0,#TUIF_FIXHEIGHT}
        ntui_EndGroup{} : ntuib_SetFlags{#TUIF_FIXHEIGHT,False}

        ntuib_Label    {"Other Buttons",#TUIF_RIGHT}
        ntuib_BeginHGroup{0}
          ntuib_FunctionButton{#TUIFUNC_INC         ,"this is a button to increase a value"}
          ntuib_FunctionButton{#TUIFUNC_DEC         ,"this is a button to decrease a value"}
          ntuib_FunctionButton{#TUIFUNC_SPIN        ,"this is a button to in/decrease a value"}
;          ntuib_FunctionButton{#TUIFUNC_PLUSMINUS   ,"this is a button to in/decrease a value"}
          ntuib_FunctionButton{#TUIFUNC_INC         ,"this is a button to increase a value",#TUIF_DISABLED }
          ntuib_FunctionButton{#TUIFUNC_DEC         ,"this is a button to decrease a value",#TUIF_DISABLED }
          ntuib_FunctionButton{#TUIFUNC_SPIN       ,"this is a button to in/decrease a value",#TUIF_DISABLED }
;          ntuib_FunctionButton{#TUIFUNC_PLUSMINUS   ,"this is a button to in/decrease a value",#TUIF_DISABLED }
          ntuib_Space{0,0,#TUIF_FIXHEIGHT}
        ntui_EndGroup{} : ntuib_SetFlags{#TUIF_FIXHEIGHT,False}

        ntuib_Label    {"CheckBox",#TUIF_RIGHT}
        ntuib_BeginHGroup{0}
        ntuib_CheckBox{1,"","this is an on/off checkbox"}
        ntuib_CheckBox{0,"","this is an on/off checkbox"}
        ntuib_CheckBox{1,"","this is an on/off checkbox",#TUIF_DISABLED}
        ntuib_CheckBox{0,"","this is an on/off checkbox",#TUIF_DISABLED}
        ntuib_Space{0,0,#TUIF_FIXHEIGHT}
        ntui_EndGroup{} : ntuib_SetFlags{#TUIF_FIXHEIGHT,False}

        ntuib_Label    {"Radio Button",#TUIF_RIGHT}
        ntuib_BeginHGroup{0}
        ntuib_RadioButton  {"Option 1",0,"","this is a radio button with text",-1,"op1"}
        ntuib_RadioButton  {"Option 2",1,"","this is a radio button with text",-1,"op2"}
        ntuib_RadioButton  {"Option 3",0,"","this is a radio button with text",#TUIF_DISABLED,"op3"}
        ntui_BindByID{*engine,"op1","op2"}
        ntui_BindByID{*engine,"op2","op3"}
        ntuib_Space{0,0,#TUIF_FIXHEIGHT}
        ntui_EndGroup{} : ntuib_SetFlags{#TUIF_FIXHEIGHT,False}

        ntuib_Label    {"Check Button",#TUIF_RIGHT}
        ntuib_BeginHGroup{0}
        ntuib_CheckButton  {"Check 1",1,"","this is a check button with text"}
        ntuib_CheckButton  {"Check 2",0,"","this is a check button with text"}
        ntuib_CheckButton  {"Check 3",1,"","this is a check button with text",#TUIF_DISABLED}
        ntuib_Space{0,0,#TUIF_FIXHEIGHT}
        ntui_EndGroup{} : ntuib_SetFlags{#TUIF_FIXHEIGHT,False}


        CNIF @#TUI_HAS_CYCLE
        ntuib_Label    {"Cycle",#TUIF_RIGHT}
        ntuib_BeginHGroup{0}
        ntuib_Cycle{"Wanderer|Honitos|Blackbird|CJ Stroker|BruZard|Modfr|Wawa|HelmutH|Bernd_|Theoderich",-2,"this is a cycle button",-1,"CYCL"}

        ntuib_Cycle{"A|B|C",0,"this is a cycle button",#TUIF_DISABLED}
        ntui_EndGroup{} : ntuib_SetFlags{#TUIF_FIXHEIGHT,False}
        CEND

        ntuib_Label    {"Button +Image",#TUIF_RIGHT}
        ntuib_BeginHGroup{0}
        ntuib_Button   {"Image","SAVE","","this is a button with in in-line image (image at text size)"}
        ntuib_Button   {"Image","SAVE","","this is a button with in in-line image (image at text size)",#TUIF_DISABLED}
        ntuib_Button   {"Image","TBImages:save","","this is a button with in in-line image (image at text size)"}
        ntuib_Button   {"Image","TBImages:save","","this is a button with in in-line image (image at text size)",#TUIF_DISABLED}
        ntui_EndGroup{} : ntuib_SetFlags{#TUIF_FIXHEIGHT,False}

        ntuib_Label    {"Image Button",#TUIF_RIGHT}
        ntuib_BeginHGroup{0}
        ntuib_ImageButton   {"","SAVE","","this is an image button (image at native size)"}
        ntuib_ImageButton   {"","SAVE","","this is an image button (image at native size)",#TUIF_DISABLED}
        ntuib_ImageButton   {"","TBImages:save","","this is an image button (image at native size)"}
        ntuib_ImageButton   {"","TBImages:save","","this is an image button (image at native size)",#TUIF_DISABLED}
        ntuib_Space{0,0,#TUIF_FIXHEIGHT}
        ntui_EndGroup{} : ntuib_SetFlags{#TUIF_FIXHEIGHT,False}

        ntuib_Label    {"Image Button +Text",#TUIF_RIGHT}
        ntuib_BeginHGroup{0}
        ntuib_ImageButton   {"Image","SAVE","","this is an image button with text"}
        ntuib_ImageButton   {"Image","SAVE","","this is an image button with text",#TUIF_DISABLED}
        ntuib_ImageButton   {"Image","TBImages:save","","this is an image button with text"}
        ntuib_ImageButton   {"Image","TBImages:save","","this is an image button with text",#TUIF_DISABLED}
        ntuib_Space{0,0,#TUIF_FIXHEIGHT}
        ntui_EndGroup{} : ntuib_SetFlags{#TUIF_FIXHEIGHT,False}

        ntuib_Label    {"Tool Button",#TUIF_RIGHT}
        ntuib_BeginHGroup{0}
        ntuib_ToolButton  {"","SAVE",-1,"","this is a toolbar style button"}
        ntuib_ToolButton  {"","SAVE",-1 ,"","this is a toolbar style button",#TUIF_DISABLED}
        ntuib_ToolButton  {"","TBImages:save",-1 ,"","this is a toolbar style button"}
        ntuib_ToolButton  {"","TBImages:save",-1 ,"","this is a toolbar style button",#TUIF_DISABLED}
        ntuib_Space{0,0,#TUIF_FIXHEIGHT}
        ntui_EndGroup{} : ntuib_SetFlags{#TUIF_FIXHEIGHT,False}

        ntuib_Label    {"Tool Button + Text",#TUIF_RIGHT}
        ntuib_BeginHGroup{0}
        ntuib_ToolButton  {"Image","SAVE",-1 ,"","this is a toolbar button with text"}
        ntuib_ToolButton  {"Image","SAVE",-1 ,"","this is a toolbar button with text",#TUIF_DISABLED}
        ntuib_ToolButton  {"Image","TBImages:save",-1 ,"","this is a toolbar button with text"}
        ntuib_ToolButton  {"Image","TBImages:save",-1 ,"","this is a toolbar button with text",#TUIF_DISABLED}
        ntuib_Space{0,0,#TUIF_FIXHEIGHT}
        ntui_EndGroup{} : ntuib_SetFlags{#TUIF_FIXHEIGHT,False}

        ntuib_Space{0,0} : ntuib_Space{0,0}
      ntui_EndGroup{}

    ntui_EndGroup{} ; Button
    CEND

    CNIF @#TUI_HAS_SCROLLER
    ntuib_BeginVGroup{0,"Scoller"}
      ;ntuib_BeginHGroup{10}
      ;  ntuib_BeginVGroup{0}
          ntuib_BeginHGroup{2,"","",#TUIF_FIXSIZE}
            ntuib_Label    {"Horizontal Scroller",#TUIF_RIGHT}
            ntuib_HScroller{0,100,20,"","",-1,"sc1"}
            ntuib_Label    {"HScroller + Arrows",#TUIF_RIGHT}
            ntuib_HScroller{0,10,9,"","",-1,"sc2"} : ntuib_AddArrows{}
  ;          ntui_BindByID{*engine,"sc1","sc2"}
            ntuib_Label    {"Disabled HScroller",#TUIF_RIGHT}
            ntuib_HScroller{0,100,20,"","",#TUIF_DISABLED } : ntuib_AddArrows{}
          ;ntui_EndGroup{} : ntuib_SetFlags{#TUIF_FIXSIZE}

        CNIF @#TUI_HAS_SLIDER
          ;ntuib_BeginHGroup{2}
          ntuib_Label    {"Horizontal Slider",#TUIF_RIGHT}
          ntuib_HSlider{0,0,100,""}
          ntuib_Label    {"HSlider + Label",#TUIF_RIGHT}
          ntuib_HSlider{0,0,100,"%d units"}
          ntuib_Label    {"Disabled HSlider",#TUIF_RIGHT}
          ntuib_HSlider{0,0,100,"%d units","","",#TUIF_DISABLED }

          ntuib_Label    {"HSlider + Label",#TUIF_RIGHT}
          ntuib_HSlider{0,0,100,"%d units","","",#TUIF_UNDERLINED}
          ntuib_Label    {"HSlider + Label",#TUIF_RIGHT}
          ntuib_HSlider{0,0,100,"%d units","","",#TUIF_UNDERLINED} : ntuib_SetBorder{#TUIBORDER_RECESSED}
          ntuib_Label    {"Disabled HSlider + Label",#TUIF_RIGHT}
          ntuib_HSlider{0,0,100,"%d units","","",#TUIF_DISABLED|#TUIF_UNDERLINED }

          ntuib_Label    {"HSlider + Border",#TUIF_RIGHT}
          ntuib_HSlider{0,0,100,"%d units"}  : ntuib_SetBorder{#TUIBORDER_RECESSED}
          ntuib_Label    {"Disabled HSlider + Border",#TUIF_RIGHT}
          ntuib_HSlider{0,0,100,"%d units","","",#TUIF_DISABLED}  : ntuib_SetBorder{#TUIBORDER_RECESSED}
          ntuib_Space{0,0,#TUIF_FIXWIDTH } : ntuib_Space{0,0}
          ;ntui_EndGroup{}  : ntuib_SetFlags{#TUIF_FIXSIZE}
        CEND
        ;ntui_EndGroup{}
      ntui_EndGroup{} : ntuib_SetFlags{#TUIF_FIXHEIGHT}

      ntuib_BeginHGroup{0}
        ntuib_Label    {"Vertical Versions",#TUIF_RIGHT}
        ntuib_VScroller{0,100,20}
        ntuib_VScroller{0,100,90,"","this is a vertical scroller with arrows"} : ntuib_AddArrows{}
        ntuib_VScroller{0,100,20,"","",#TUIF_DISABLED} : ntuib_AddArrows{}
        CNIF @#TUI_HAS_SLIDER
        ntuib_VSlider{0,0,100,""}
        ntuib_VSlider{0,0,100,"%d"}
        ntuib_VSlider{0,0,100,"%d","","",#TUIF_DISABLED }

        ntuib_VSlider{0,0,100,"%d","","",#TUIF_UNDERLINED}
        ntuib_VSlider{0,0,100,"%d","","",#TUIF_DISABLED|#TUIF_UNDERLINED }
        ntuib_VSlider{0,0,100,"%d","","",#TUIF_UNDERLINED}                : ntuib_SetBorder{#TUIBORDER_RECESSED}
        ntuib_VSlider{0,0,100,"%d"}  : ntuib_SetBorder{#TUIBORDER_RECESSED}
        ntuib_VSlider{0,0,100,"%d","","",#TUIF_DISABLED}  : ntuib_SetBorder{#TUIBORDER_RECESSED}
        CEND
        ntuib_Space{0,0}
      ntui_EndGroup{}  : ntuib_ClearFlags{#TUIF_FIXSIZE}
    ntui_EndGroup{} ; scroller
    CEND

    CNIF @#TUI_HAS_TABVIEW
    ntuib_BeginHGroup{2*2,"TabView"}
      ntuib_Label    {"Top Tabs",#TUIF_RIGHT}
      ntuib_BeginTabView{-1,#TUITABSTYLE_TOP}
        *obj.tuiObject = ntuib_BeginVGroup{0,"First Page","TBImages:save"}
          ntuib_Label    {"Content of page #1",#TUIF_CENTER}
        ntui_EndGroup{}
        ntuib_BeginVGroup{0,"Second Page","TBImages:favourites"}
          ntuib_Label    {"Content of page #2",#TUIF_CENTER}
        ntui_EndGroup{}
        ntuib_BeginVGroup{0,"Third Page","ARROWLEFT",#TUIF_DISABLED}
          ntuib_Label    {"Content of page #3",#TUIF_CENTER}
        ntui_EndGroup{}
      ntui_EndTabView{}

      ntuib_Label    {"Bottom Tabs",#TUIF_RIGHT}
      ntuib_BeginTabView{0,#TUITABSTYLE_BOTTOM,"",#TUIF_SMALL}
        *obj.tuiObject =  ntuib_BeginVGroup{0,"First Page","TBImages:save"}
          ntuib_Label    {"Content of page #1",#TUIF_CENTER}
        ntui_EndGroup{}
        ntuib_BeginVGroup{0,"Second Page","TBImages:favourites"}
          ntuib_Label    {"Content of page #2",#TUIF_CENTER}
        ntui_EndGroup{}
        ntuib_BeginVGroup{0,"Third Page","FONT",#TUIF_DISABLED}
          ntuib_Label    {"Content of page #3",#TUIF_CENTER}
        ntui_EndGroup{}
      ntui_EndTabView{}

      ntuib_Label    {"Left Tabs",#TUIF_RIGHT}
      ntuib_BeginTabView{0,#TUITABSTYLE_LEFT}
         *obj.tuiObject =  ntuib_BeginVGroup{0,"First Page","TBImages:save"}
          ntuib_Label    {"Content of page #1",#TUIF_CENTER}
        ntui_EndGroup{}
        ntuib_BeginVGroup{0,"Second Page","TBImages:favourites"}
          ntuib_Label    {"Content of page #2",#TUIF_CENTER}
        ntui_EndGroup{}
        ntuib_BeginVGroup{0,"Third Page","FILE"}
          ntuib_Label    {"Content of page #3",#TUIF_CENTER}
        ntui_EndGroup{}
      ntui_EndTabView{}

      ntuib_Label    {"Right Tabs",#TUIF_RIGHT}
      ntuib_BeginTabView{0,#TUITABSTYLE_RIGHT}
        *obj.tuiObject =  ntuib_BeginVGroup{0,"First Page","TBImages:save"}
          ntuib_Label    {"Content of page #1",#TUIF_CENTER}
        ntui_EndGroup{}
        ntuib_BeginVGroup{0,"Second Page","TBImages:favourites"}
          ntuib_Label    {"Content of page #2",#TUIF_CENTER}
        ntui_EndGroup{}
        ntuib_BeginVGroup{0,"Third Page","SAVE"}
          ntuib_Label    {"Content of page #3",#TUIF_CENTER}
        ntui_EndGroup{}
      ntui_EndTabView{}


      ntuib_Label    {"Empty TabView",#TUIF_RIGHT}
      ntuib_BeginTabView{0,#TUITABSTYLE_TOP}
      ntui_EndTabView{}

      ntuib_BeginVGroup{0}
      ntuib_Label    {"External Tab",#TUIF_RIGHT}
      CNIF @#TUI_HAS_CYCLE
      ntuib_Cycle    {"first|second|third|none",0,"",#TUIF_FIXSIZE,"excr"}
      CEND
      ntuib_Space{0,0,#TUIF_FIXWIDTH}
      ntui_EndGroup{}  : ntuib_SetFlags{#TUIF_FIXSIZE}
      ntuib_BeginTabView{0,#TUITABSTYLE_NONE,"",-1,"expa"}
        *obj.tuiObject =  ntuib_BeginVGroup{0,"First Page"}
          ntuib_Label    {"Content of page #1",#TUIF_CENTER}
        ntui_EndGroup{}
        ntuib_BeginVGroup{0,"Second Page"}
          ntuib_Label    {"Content of page #2",#TUIF_CENTER}
        ntui_EndGroup{}
        ntuib_BeginVGroup{0,"Third Page"}
          ntuib_Label    {"Content of page #3",#TUIF_CENTER}
        ntui_EndGroup{}
      ntui_EndTabView{}
      ntui_BindByID{*engine,"excr","expa"}
      ntuib_Space{0,0,#TUIF_FIXSIZE}
      ntuib_Space{0,0,#TUIF_FIXSIZE}
    ntui_EndGroup{} ; Page
    CEND


    ntuib_BeginHGroup{2,"Misc."}
      CNIF @#TUI_HAS_SEPERATOR
      ntuib_Label    {"Horizontal Seperator",#TUIF_RIGHT}
      ntuib_HSeperator{""}

      ntuib_Label {"Label Seperator",#TUIF_RIGHT}
      ntuib_HSeperator{"Seperator"}

      ntuib_Label    {"Vertical Seperator",#TUIF_RIGHT}
      ntuib_VSeperator{""}  : ntuib_SetFlags{#TUIF_FIXSIZE}
      CEND

      CNIF @#TUI_HAS_PROGRESSBAR
      ntuib_Label    {"ProgressBar",#TUIF_RIGHT}
      ntuib_BeginHGroup{0}
      ntuib_ProgressBar{"%d of %t done (%p %)...",20,100}  : ntuib_SetID{"SBAR"}
      ntuib_ProgressBar{"%d of %t done (%p %)...",20,100,"",#TUIF_DISABLED}
      ntui_EndGroup{} : ntuib_SetFlags{#TUIF_FIXSIZE}
      CEND

      CNIF @#TUI_HAS_SPACE
      ntuib_Label    {"Space (30x30px)",#TUIF_RIGHT}
      ntuib_Space    {30,30,#TUIF_FIXSIZE}
      CEND

      CNIF @#TUI_HAS_BALANCER
      ntuib_Label    {"H/V Balancer",#TUIF_RIGHT}
      ntuib_BeginVGroup{-1}
        ntuib_BeginHGroup{-1}
         ntuib_ImageView{"UNKNOWN"} : ntuib_ClearFlags{#TUIF_FIXSIZE}
         ntuib_VBalancer{}
         ntuib_ImageView{"UNKNOWN"} : ntuib_ClearFlags{#TUIF_FIXSIZE}
        ntui_EndGroup{}  : ntuib_ClearFlags{#TUIF_FIXSIZE}
        ntuib_HBalancer{}
        ntuib_BeginHGroup{-1}
          ntuib_ImageView{"UNKNOWN"} : ntuib_ClearFlags{#TUIF_FIXSIZE}
          ntuib_VBalancer{}
          ntuib_ImageView{"UNKNOWN"} : ntuib_ClearFlags{#TUIF_FIXSIZE}
        ntui_EndGroup{}  : ntuib_ClearFlags{#TUIF_FIXSIZE}
      ntui_EndGroup{}  : ntuib_ClearFlags{#TUIF_FIXSIZE}
      CEND



      CNIF @#TUI_HAS_CUSTOMVIEW
      ntuib_Label    {"CustomView",#TUIF_RIGHT}
      ntuib_BeginHGroup{0}

        ntuib_BeginVGroup{0}
          ntuib_BeginHGroup{0}
            *cs.tuiCustomView = ntuib_CustomView {50,50,"this is a custom view",-1,"CView1"}
            ;a.s = "CVMOVE"    : ntui_SetAttr{*cs,#TUICVA_ONMOUSEMOVE,&a}
            ;a.s = "CVTOUCH"   : ntui_SetAttr{*cs,#TUIA_ONTOUCH,&a}
            ;a.s = "CVRELEASE" : ntui_SetAttr{*cs,#TUIA_ONRELEASE,&a}
            ;a.s = "CVSCROLL"  : ntui_SetAttr{*cs,#TUICVA_ONSCROLL,&a}
            ntui_SetAttr{*cs,#TUICVA_HTOTAL,512}
            ntui_SetAttr{*cs,#TUICVA_VTOTAL,512}

            !_GetFuncPointer{*myDispatchEvent.tuiFunc,_ntui_MyDispatchEvent,{0,0}}
            ntui_SetAttr{*cs,#TUIA_MD_DISPATCHEVENT,*myDispatchEvent}

            *scroller.tuiScroller = ntuib_VScroller{0,100,20,"","",-1,"CVScroller"} : ntuib_AddArrows{}
          ntui_EndGroup{}

          ntuib_BeginHGroup{0}
            *scroller = ntuib_HScroller{0,100,20,"","",-1,"CHScroller"} : ntuib_AddArrows{}
             ntuib_ImageView{"TBImages:list_zoom_100"}
          ntui_EndGroup{}

          ntuib_SetFlags{#TUIF_FIXSIZE}
          ntuib_Label {"CustomView with bound Scrollers",#TUIF_CENTER}
        ntui_EndGroup{}

        ntuib_VBalancer{}

        ntuib_BeginVGroup{0}
          ntuib_BeginScrollView{50,50,"",-1,"SCRVIEW"}
            *cs.tuiCustomView = ntuib_CustomView {512,512,"this is a custom view",-1,"CView2"}
            ;a.s = "CVMOVE"    : ntui_SetAttr{*cs,#TUICVA_ONMOUSEMOVE,&a}
            ;a.s = "CVTOUCH"   : ntui_SetAttr{*cs,#TUIA_ONTOUCH,&a}
            ;a.s = "CVRELEASE" : ntui_SetAttr{*cs,#TUIA_ONRELEASE,&a}
            ntui_SetAttr{*cs,#TUIA_BORDERTYPE,#TUIBORDER_NONE}
            ntui_SetAttr{*cs,#TUIA_PADDING,0}
            ntui_SetAttr{*cs,#TUIA_MARGIN,0}
          ntui_EndScrollView{}
          ntuib_Label {"CustomView in ScrollView",#TUIF_CENTER}
        ntui_EndGroup{}

      ntui_EndGroup{}
      CEND
    ntui_EndGroup{} ; misc

    CNIF @#TUI_HAS_IMAGEVIEW
    ntuib_BeginHGroup{2,"Image"}
      ntuib_BeginHGroup{0}
      ntuib_BeginHGroup{2}

      ntuib_Label    {"Name",#TUIF_LEFT|#TUIF_BOLD}
      ntuib_Label    {"Nrm/Sel/Dis",#TUIF_LEFT|#TUIF_BOLD }

      name.s="" : ntuib_Label{"Unknown",#TUIF_LEFT}
      ntuib_BeginHGroup{0} : ntuib_ImageView{name} : ntuib_ImageView{name,-1,"",#TUIF_ACTIVE} : ntuib_ImageView{name,-1,"",#TUIF_DISABLED} : ntui_EndGroup{}

      name.s="SAVE" : ntuib_Label{name,#TUIF_LEFT}
      ntuib_BeginHGroup{0} : ntuib_ImageView{name} : ntuib_ImageView{name,-1,"",#TUIF_ACTIVE} : ntuib_ImageView{name,-1,"",#TUIF_DISABLED} : ntui_EndGroup{}

      name.s="FILE" : ntuib_Label{name,#TUIF_LEFT}
      ntuib_BeginHGroup{0} : ntuib_ImageView{name} : ntuib_ImageView{name,-1,"",#TUIF_ACTIVE} : ntuib_ImageView{name,-1,"",#TUIF_DISABLED} : ntui_EndGroup{}

      name.s="PATH" : ntuib_Label{name,#TUIF_LEFT}
      ntuib_BeginHGroup{0} : ntuib_ImageView{name} : ntuib_ImageView{name,-1,"",#TUIF_ACTIVE} : ntuib_ImageView{name,-1,"",#TUIF_DISABLED} : ntui_EndGroup{}

      name.s="SCREEN" : ntuib_Label{name,#TUIF_LEFT}
      ntuib_BeginHGroup{0} : ntuib_ImageView{name} : ntuib_ImageView{name,-1,"",#TUIF_ACTIVE} : ntuib_ImageView{name,-1,"",#TUIF_DISABLED} : ntui_EndGroup{}

      name.s="FONT" : ntuib_Label{name,#TUIF_LEFT}
      ntuib_BeginHGroup{0} : ntuib_ImageView{name} : ntuib_ImageView{name,-1,"",#TUIF_ACTIVE} : ntuib_ImageView{name,-1,"",#TUIF_DISABLED} : ntui_EndGroup{}

      name.s="CYCLE" : ntuib_Label{name,#TUIF_LEFT}
      ntuib_BeginHGroup{0} : ntuib_ImageView{name} : ntuib_ImageView{name,-1,"",#TUIF_ACTIVE} : ntuib_ImageView{name,-1,"",#TUIF_DISABLED} : ntui_EndGroup{}

      name.s="INC" : ntuib_Label{name,#TUIF_LEFT}
      ntuib_BeginHGroup{0} : ntuib_ImageView{name} : ntuib_ImageView{name,-1,"",#TUIF_ACTIVE} : ntuib_ImageView{name,-1,"",#TUIF_DISABLED} : ntui_EndGroup{}

      name.s="DEC" : ntuib_Label{name,#TUIF_LEFT}
      ntuib_BeginHGroup{0} : ntuib_ImageView{name} : ntuib_ImageView{name,-1,"",#TUIF_ACTIVE} : ntuib_ImageView{name,-1,"",#TUIF_DISABLED} : ntui_EndGroup{}

      name.s="AMIGA" : ntuib_Label{name,#TUIF_LEFT}
      ntuib_BeginHGroup{0} : ntuib_ImageView{name} : ntuib_ImageView{name,-1,"",#TUIF_ACTIVE} : ntuib_ImageView{name,-1,"",#TUIF_DISABLED} : ntui_EndGroup{}

      name.s="SHIFT" : ntuib_Label{name,#TUIF_LEFT}
      ntuib_BeginHGroup{0} : ntuib_ImageView{name} : ntuib_ImageView{name,-1,"",#TUIF_ACTIVE} : ntuib_ImageView{name,-1,"",#TUIF_DISABLED} : ntui_EndGroup{}

      name.s="CTRL" : ntuib_Label{name,#TUIF_LEFT}
      ntuib_BeginHGroup{0} : ntuib_ImageView{name} : ntuib_ImageView{name,-1,"",#TUIF_ACTIVE} : ntuib_ImageView{name,-1,"",#TUIF_DISABLED} : ntui_EndGroup{}

      name.s="ALT" : ntuib_Label{name,#TUIF_LEFT}
      ntuib_BeginHGroup{0} : ntuib_ImageView{name} : ntuib_ImageView{name,-1,"",#TUIF_ACTIVE} : ntuib_ImageView{name,-1,"",#TUIF_DISABLED} : ntui_EndGroup{}

      ntuib_Space{0,0,#TUIF_FIXWIDTH } : ntuib_Space{0,0,#TUIF_FIXWIDTH }
      ntui_EndGroup{}

      ntuib_VBalancer{}

      ntuib_BeginHGroup{2}
      ntuib_Label    {"Name",#TUIF_LEFT|#TUIF_BOLD}
      ntuib_Label    {"Nrm/Sel/Dis",#TUIF_LEFT|#TUIF_BOLD }

      name.s="ARROWLEFT" : ntuib_Label{name,#TUIF_LEFT}
      ntuib_BeginHGroup{0} : ntuib_ImageView{name} : ntuib_ImageView{name,-1,"",#TUIF_ACTIVE} : ntuib_ImageView{name,-1,"",#TUIF_DISABLED} : ntui_EndGroup{}

      name.s="ARROWRIGHT" : ntuib_Label{name,#TUIF_LEFT}
      ntuib_BeginHGroup{0} : ntuib_ImageView{name} : ntuib_ImageView{name,-1,"",#TUIF_ACTIVE} : ntuib_ImageView{name,-1,"",#TUIF_DISABLED} : ntui_EndGroup{}

      name.s="ARROWUP" : ntuib_Label{name,#TUIF_LEFT}
      ntuib_BeginHGroup{0} : ntuib_ImageView{name} : ntuib_ImageView{name,-1,"",#TUIF_ACTIVE} : ntuib_ImageView{name,-1,"",#TUIF_DISABLED} : ntui_EndGroup{}

      name.s="ARROWDOWN" : ntuib_Label{name,#TUIF_LEFT}
      ntuib_BeginHGroup{0} : ntuib_ImageView{name} : ntuib_ImageView{name,-1,"",#TUIF_ACTIVE} : ntuib_ImageView{name,-1,"",#TUIF_DISABLED} : ntui_EndGroup{}

      name.s="OLARROWLEFT" : ntuib_Label{name,#TUIF_LEFT}
      ntuib_BeginHGroup{0} : ntuib_ImageView{name} : ntuib_ImageView{name,-1,"",#TUIF_ACTIVE} : ntuib_ImageView{name,-1,"",#TUIF_DISABLED} : ntui_EndGroup{}

      name.s="OLARROWRIGHT" : ntuib_Label{name,#TUIF_LEFT}
      ntuib_BeginHGroup{0} : ntuib_ImageView{name} : ntuib_ImageView{name,-1,"",#TUIF_ACTIVE} : ntuib_ImageView{name,-1,"",#TUIF_DISABLED} : ntui_EndGroup{}

      name.s="OLARROWUP" : ntuib_Label{name,#TUIF_LEFT}
      ntuib_BeginHGroup{0} : ntuib_ImageView{name} : ntuib_ImageView{name,-1,"",#TUIF_ACTIVE} : ntuib_ImageView{name,-1,"",#TUIF_DISABLED} : ntui_EndGroup{}

      name.s="OLARROWDOWN" : ntuib_Label{name,#TUIF_LEFT}
      ntuib_BeginHGroup{0} : ntuib_ImageView{name} : ntuib_ImageView{name,-1,"",#TUIF_ACTIVE} : ntuib_ImageView{name,-1,"",#TUIF_DISABLED} : ntui_EndGroup{}

      name.s="LEFT" : ntuib_Label{name,#TUIF_LEFT}
      ntuib_BeginHGroup{0} : ntuib_ImageView{name} : ntuib_ImageView{name,-1,"",#TUIF_ACTIVE} : ntuib_ImageView{name,-1,"",#TUIF_DISABLED} : ntui_EndGroup{}

      name.s="RIGHT" : ntuib_Label{name,#TUIF_LEFT}
      ntuib_BeginHGroup{0} : ntuib_ImageView{name} : ntuib_ImageView{name,-1,"",#TUIF_ACTIVE} : ntuib_ImageView{name,-1,"",#TUIF_DISABLED} : ntui_EndGroup{}

      name.s="POPUP" : ntuib_Label{name,#TUIF_LEFT}
      ntuib_BeginHGroup{0} : ntuib_ImageView{name} : ntuib_ImageView{name,-1,"",#TUIF_ACTIVE} : ntuib_ImageView{name,-1,"",#TUIF_DISABLED} : ntui_EndGroup{}

      name.s="DROPDOWN" : ntuib_Label{name,#TUIF_LEFT}
      ntuib_BeginHGroup{0} : ntuib_ImageView{name} : ntuib_ImageView{name,-1,"",#TUIF_ACTIVE} : ntuib_ImageView{name,-1,"",#TUIF_DISABLED} : ntui_EndGroup{}

      ntuib_Space{0,0,#TUIF_FIXWIDTH} : ntuib_Space{0,0,#TUIF_FIXWIDTH }
      ntui_EndGroup{}

      ntuib_VBalancer{}

      ntuib_BeginHGroup{2}
      ntuib_Label    {"Name",#TUIF_LEFT|#TUIF_BOLD}
      ntuib_Label    {"Nrm/Sel/Dis",#TUIF_LEFT|#TUIF_BOLD }

      name.s="TBImages:save" : ntuib_Label{name,#TUIF_LEFT}
      ntuib_BeginHGroup{0} : ntuib_ImageView{name} : ntuib_ImageView{name,-1,"",#TUIF_ACTIVE} : ntuib_ImageView{name,-1,"",#TUIF_DISABLED} : ntui_EndGroup{}

      name.s="Sys:disk.info" : ntuib_Label{name,#TUIF_LEFT}
      ntuib_BeginHGroup{0} : ntuib_ImageView{name,#TUISIZE_BUTTON  }
      ntuib_ImageView{name,#TUISIZE_BUTTON  ,"",#TUIF_ACTIVE}
      ntuib_ImageView{name,#TUISIZE_BUTTON  ,"",#TUIF_DISABLED} : ntui_EndGroup{}

      name.s="CHECKMARK" : ntuib_Label{name,#TUIF_LEFT}
      ntuib_BeginHGroup{0} : ntuib_ImageView{name} : ntuib_ImageView{name,-1,"",#TUIF_ACTIVE} : ntuib_ImageView{name,-1,"",#TUIF_DISABLED} : ntui_EndGroup{}

      name.s="UNCHECKMARK" : ntuib_Label{name,#TUIF_LEFT}
      ntuib_BeginHGroup{0} : ntuib_ImageView{name} : ntuib_ImageView{name,-1,"",#TUIF_ACTIVE} : ntuib_ImageView{name,-1,"",#TUIF_DISABLED} : ntui_EndGroup{}

      name.s="CHECKON" : ntuib_Label{name,#TUIF_LEFT}
      ntuib_BeginHGroup{0} : ntuib_ImageView{name} : ntuib_ImageView{name,-1,"",#TUIF_ACTIVE} : ntuib_ImageView{name,-1,"",#TUIF_DISABLED} : ntui_EndGroup{}

      name.s="CHECKOFF" : ntuib_Label{name,#TUIF_LEFT}
      ntuib_BeginHGroup{0} : ntuib_ImageView{name} : ntuib_ImageView{name,-1,"",#TUIF_ACTIVE} : ntuib_ImageView{name,-1,"",#TUIF_DISABLED} : ntui_EndGroup{}

      name.s="RADIOON" : ntuib_Label{name,#TUIF_LEFT}
      ntuib_BeginHGroup{0} : ntuib_ImageView{name} : ntuib_ImageView{name,-1,"",#TUIF_ACTIVE} : ntuib_ImageView{name,-1,"",#TUIF_DISABLED} : ntui_EndGroup{}

      name.s="RADIOOFF" : ntuib_Label{name,#TUIF_LEFT}
      ntuib_BeginHGroup{0} : ntuib_ImageView{name} : ntuib_ImageView{name,-1,"",#TUIF_ACTIVE} : ntuib_ImageView{name,-1,"",#TUIF_DISABLED} : ntui_EndGroup{}

      ntuib_Space{0,0,#TUIF_FIXWIDTH} : ntuib_Space{0,0,#TUIF_FIXWIDTH }

    ntui_EndGroup{}

    ntui_EndGroup{}
    ntuib_Space{0,0}
    ntui_EndGroup{} ; image
    CEND

    ntuib_BeginHGroup{2,"Group"}
      ntuib_BeginHGroup{2}
        ntuib_BeginHGroup{-1,"Title Text","TBImages:favourites"} : ntuib_Label{"Titled Group"} : ntui_EndGroup{}                       : ntuib_ClearFlags{#TUIF_FIXSIZE}
        ntuib_BeginVGroup{-1,"Title Text","TBImages:favourites",#TUIF_DISABLED} : ntuib_Label{"Disabled Group"} : ntui_EndGroup{}      : ntuib_ClearFlags{#TUIF_FIXSIZE}
        ntuib_BeginHGroup{-1,"x"} : ntuib_SetBorder{#TUIBORDER_GROUP}    : ntuib_Label{"Group with Group Border"} : ntui_EndGroup{}     : ntuib_ClearFlags{#TUIF_FIXSIZE}
        ntuib_BeginHGroup{-1,"x"} : ntuib_SetBorder{#TUIBORDER_RAISED}   : ntuib_Label{"Group with Raised Border"} : ntui_EndGroup{}    : ntuib_ClearFlags{#TUIF_FIXSIZE}
        ntuib_BeginHGroup{-1,"x"} : ntuib_SetBorder{#TUIBORDER_RECESSED} : ntuib_Label{"Group with Recessed Border"} : ntui_EndGroup{}  : ntuib_ClearFlags{#TUIF_FIXSIZE}
       ; ntuib_BeginHGroup{-1,"x"} : ntuib_SetBorder{#TUIBORDER_FLAT}     : ntuib_Label{"Group with Flat Border"} : ntui_EndGroup{}

          CNIF @#TUI_HAS_SCROLLVIEW
          ntuib_BeginScrollView{50,50}
            ntuib_BeginVGroup{0}
              ntuib_BeginHGroup{0} : ntuib_SetFlags{#TUIF_SAMESIZE}
                ntuib_ToolButton  {"Open","TBImages:open",-1 ,"","this is a toolbar button with text"}
                ntuib_ToolButton  {"Save","TBImages:save",-1 ,"","this is a toolbar button with text"}
                ntuib_ToolButton  {"SaveAll","TBImages:saveall",-1 ,"","this is a toolbar button with text"}
                ntuib_ToolButton  {"Mail","TBImages:mail",-1 ,"","this is a toolbar button with text"}
                ntuib_ToolButton  {"Help","TBImages:help",-1 ,"","this is a toolbar button with text"}
                ntuib_VSeperator{""}
                ntuib_ToolButton  {"Cut","TBImages:cut",-1 ,"","this is a toolbar button with text"}
                ntuib_ToolButton  {"Copy","TBImages:copy",-1 ,"","this is a toolbar button with text"}
                ntuib_ToolButton  {"Paste","TBImages:paste",-1 ,"","this is a toolbar button with text"}
                ntuib_ToolButton  {"Edit","TBImages:edit",-1 ,"","this is a toolbar button with text"}
                ntuib_ToolButton  {"Delete","TBImages:delete",-1 ,"","this is a toolbar button with text"}
                ntuib_VSeperator{""}
                ntuib_ToolButton  {"Undo","TBImages:undo",-1 ,"","this is a toolbar button with text"}
              ntui_EndGroup{}

      ntuib_BeginTabView{-1,#TUITABSTYLE_TOP}
        *obj.tuiObject = ntuib_BeginVGroup{0,"First Page","TBImages:save"}
          ntuib_Label    {"Content of page #1",#TUIF_CENTER}
        ntui_EndGroup{}
        ntuib_BeginVGroup{0,"Second Page","TBImages:favourites"}
          ntuib_Label    {"Content of page #2",#TUIF_CENTER}
        ntui_EndGroup{}
        ntuib_BeginVGroup{0,"Third Page","ARROWLEFT",#TUIF_DISABLED}
          ntuib_Label    {"Content of page #3",#TUIF_CENTER}
        ntui_EndGroup{}
      ntui_EndTabView{}


              ntuib_RadioButton  {"Option 1",0,"","this is a radio button with text",-1,"op21"}
              ntuib_RadioButton  {"Option 2",1,"","this is a radio button with text",-1,"op22"}
              ntuib_RadioButton  {"Option 3",0,"","this is a radio button with text",-1,"op23"}
              ntui_BindByID{*engine,"op21","op22"}
              ntui_BindByID{*engine,"op22","op23"}
              ntuib_HSlider{0,0,100}
              ntuib_HScroller{0,100,10}
              ntuib_CheckButton{"Check 1",False}
              ntuib_CheckButton{"Check 2",False}
              ntuib_CheckButton{"Check 3",False}
              ntuib_CheckButton{"Check 4",False}
              ntuib_CheckButton{"Check 5",False}
              ntuib_CheckButton{"Check 6",False}
            ntui_EndGroup{}
          ntui_EndScrollView{} :ntuib_ClearFlags{#TUIF_FIXSIZE}
          CEND


      ntui_EndGroup{}
    ntui_EndGroup{} ; Group

   CNIF @#TUI_HAS_TEXTBOX
   ntuib_BeginVGroup{0,"Text"}
     ntuib_BeginHGroup{2,"","",#TUIF_FIXHEIGHT}
     ntuib_Label    {"String",#TUIF_RIGHT}
     ntuib_BeginHGroup{-1}
       *s2.tuiObject = ntuib_String   {"String",20,"",-1,"mystring"}
       ntuib_String   {"Disabled",20,"",#TUIF_DISABLED}
       *fb.tuiObject = ntuib_FunctionButton{#TUIFUNC_FILE}
       a.s = "TEST"
       ntui_SetAttr{*fb,#TUIBA_ONRELEASE,&a.s}
       *lb.tuiObject = ntuib_Label{"test"}
     ntui_EndGroup{}
     ntui_Bind{*s2,*fb}
     ntui_Bind{*s2,*lb}
     key.s="KEY"
     ;ntui_SetAttrByID{*engine,"mystring",#TUIA_ONLOSEFOCUS,&key}

     ntuib_Label    {"EditLabel",#TUIF_RIGHT}
     ntuib_BeginHGroup{-1} :ntuib_EditLabel  {"Edit me!",20,#TUIPEN_TRANSPARENT}:ntuib_EditLabel  {"Edit me!",20,#TUIPEN_TRANSPARENT,"",#TUIF_DISABLED}: ntui_EndGroup{}

     ntuib_Label    {"NumString",#TUIF_RIGHT}
     ntuib_BeginHGroup{-1} : ntuib_NumString   {1234,0,10000}:  ntuib_NumString   {1234,0,10000,"",#TUIF_DISABLED} : ntui_EndGroup{}

     ntuib_Label    {"NumEditLabel",#TUIF_RIGHT}
     ntuib_BeginHGroup{-1} : ntuib_NumEditLabel   {1234,0,10000,#TUIPEN_SHINE} : ntuib_NumEditLabel   {1234,0,10000,#TUIPEN_SHINE,"",#TUIF_DISABLED} : ntui_EndGroup{}

;     ntuib_Label    {"TextBox",#TUIF_RIGHT}
;     *obj.tuiObject = ntuib_TextBox   {"The quick bown fox jumps\\nover the lazy dog.\\nThis is a test of the\\ntuiTextBox widget.\\nLet's see how many lines we can have.\\n########\\nbla\\nbla\\nThis is the last line.\\n",100,"",#TUIF_BOLD}
;     ntuib_ClearFlags{#TUIF_FIXSIZE}
     ntui_EndGroup{}
     ntuib_BeginHGroup{2}
     ntuib_Label    {"TextBox with Tab",#TUIF_RIGHT}

     *obj = ntuib_BeginHGroup{0}
     *obj = ntuib_BeginTabView{0}
     ntui_SetAttr{*obj,#TUIA_PADDING,0}

     *obj = ntuib_BeginHGroup{2,"document.txt","FILE"}
     ntui_SetAttr{*obj,#TUIA_MARGIN,0}
     ntui_SetAttr{*obj,#TUIA_PADDING,0}
     *obj = ntuib_TextBox   {"The quick bown fox jumps\\nover the lazy dog.\\nThis is a test of the\\ntuiTextBox widget.\\nLet's see how many lines we can have.\\n########\\nbla\\nbla\\nThis is the last line.\\n",100,"",-1,"TB1"}
     ntui_SetAttr{*obj,#TUIA_BORDERTYPE,#TUIBORDER_NONE}
     ntui_SetAttr{*obj,#TUIA_MARGIN,0}
     ntui_EndGroup{}

     *obj = ntuib_BeginHGroup{2,"alice.txt","FILE"}
     ntui_SetAttr{*obj,#TUIA_MARGIN,0}
     ntui_SetAttr{*obj,#TUIA_PADDING,0}
     *obj = ntuib_TextBox   {Peek.s(?alice),100,"",-1,"TB2"}
     ntui_SetAttr{*obj,#TUIA_BORDERTYPE,#TUIBORDER_NONE}
     ntui_SetAttr{*obj,#TUIA_MARGIN,0}
     ntui_EndGroup{}

     ntui_EndTabView{}
     CNIF @#TUI_HAS_BALANCER
     ntuib_VBalancer{}
     CEND
     CNIF @#TUI_HAS_SCROLLVIEW
     ntuib_BeginScrollView{50,50}
     CEND
       *obj = ntuib_BeginVGroup{0}
       CNIF @#TUI_HAS_SEPERATOR
       ntuib_HSeperator{"Font"}
       CEND
       ntuib_RadioButton{"Normal",True,"TBPREFS","set the textbox font to normal"    ,#TUI_NOFLAGS,"font_normal"}
       ntuib_RadioButton{"Small",    0,"TBPREFS","set the textbox font to small"     ,#TUI_NOFLAGS,"font_small"}
       ntuib_RadioButton{"Fix",      0,"TBPREFS","set the textbox font to fixed-font",#TUI_NOFLAGS,"font_fix"}
       ntuib_RadioButton{"Serif",    0,"TBPREFS","set the textbox font to serif font",#TUI_NOFLAGS,"font_serif"}
       ntui_BindByID{*engine,"font_normal","font_small"}
       ntui_BindByID{*engine,"font_normal","font_fix"}
       ntui_BindByID{*engine,"font_normal","font_serif"}
       CNIF @#TUI_HAS_SEPERATOR
       ntuib_HSeperator{"Style"}
       CEND
       ntuib_CheckButton{"Bold",0,"TBPREFS","set textbox font to bold",#TUI_NOFLAGS,"font_bold"}
       ntuib_CheckButton{"Italic",0,"TBPREFS","set textbox font to itallic",#TUI_NOFLAGS,"font_italic"}
       CNIF @#TUI_HAS_SEPERATOR
       ntuib_HSeperator{"Editor"}
       CEND
       ntuib_CheckButton{"Line Cursor",0,"TBPREFS","use line cursor (block cursor else)",#TUI_NOFLAGS,"tb_linecursor"}
       ntuib_CheckButton{"Blink",0,"TBPREFS","let the textbox cursor blink",#TUI_NOFLAGS,"tb_blink"}
       ntuib_CheckButton{"Show New Line",0,"TBPREFS","show a new line symbol",#TUI_NOFLAGS,"tb_showcr"}
       ntuib_CheckButton{"Read Only",0,"TBPREFS","make the textbox read only",#TUI_NOFLAGS,"tb_readonly"}
       CNIF @#TUI_HAS_SEPERATOR
       ntuib_HSeperator{"Scrollers"}
       CEND
       CNIF @#TUI_HAS_CYCLE
       ntuib_Cycle{"Vertical Automatic|Vertical Always|Vertical Never",0}
       ntuib_Cycle{"Horizontal Automatic|Horizontal Always|Horizontal Never",0}
       CEND

       ntui_EndGroup{}
     CNIF @#TUI_HAS_SCROLLVIEW
     ntui_EndScrollView{}
     CEND
     ntui_EndGroup{}
     ntui_EndGroup{}
   ntui_EndGroup{} ; Text
   CEND
   CNIF @#TUI_HAS_TREEVIEW
    ntuib_BeginHGroup{2,"TreeView"}
      ntuib_Label    {"Path TreeView",#TUIF_RIGHT}
      ntuib_BeginHGroup{0}
      ntuib_DirView{100,100,"#?"}
      ntuib_VBalancer{}
      ntuib_DirView{100,100,"#?"}
      ntui_EndGroup{}
    ntui_EndGroup{}
    CEND

   CNIF @#TUI_HAS_LISTVIEW
    ntuib_BeginVGroup{0,"ListView"}
      ntuib_BeginHGroup{2}
      ntuib_Label    {"Simple/Titled ListView",#TUIF_RIGHT}
      ntuib_BeginHGroup{0}

        *listView.tuiListView=ntuib_ListView {"",False,"","This is a listview",#TUIF_LEFT}
        ntui_AddListItem{*listView,-1,"Wanderer",0,1234,0}
        ntui_AddListItem{*listView,-1,"Honitos",0,1234,0}
        ntui_AddListItem{*listView,-1,"TomSmart1",0,1234,0}
        ntui_AddListItem{*listView,-1,"Wawa",0,1234,0}
        ntui_AddListItem{*listView,-1,"BruZard",0,1234,0}
        ntui_AddListItem{*listView,-1,"Blackbird",0,1234,0}
        ntui_AddListItem{*listView,-1,"Murmel",0,1234,0}
        ntui_AddListItem{*listView,-1,"HelmutH",0,1234,0}
        ntui_AddListItem{*listView,-1,"Bernd_",0,1234,0}
        ntui_AddListItem{*listView,-1,"Modfr",0,1234,0}
        ntui_AddListItem{*listView,-1,"DieterG",0,1234,0}
        ntui_AddListItem{*listView,-1,"DaxB",0,1234,0}
        ntui_AddListItem{*listView,-1,"Deadalus",0,1234,0}
        ntui_AddListItem{*listView,-1,"CJ-Stroker",0,1234,0}
        CNIF @#TUI_HAS_BALANCER
        ntuib_VBalancer{}
        CEND
        *listView.tuiListView=ntuib_ListView {"\pTBImages:favourites\tAmiforce",False,"","This is a listview",#TUIF_LEFT}
        ntui_AddListItem{*listView,-1,"Wanderer",0,1234,0}
        ntui_AddListItem{*listView,-1,"Honitos",0,1234,0}
        ntui_AddListItem{*listView,-1,"TomSmart1",0,1234,0}
        ntui_AddListItem{*listView,-1,"Wawa",0,1234,0}
        ntui_AddListItem{*listView,-1,"BruZard",0,1234,0}
        ntui_AddListItem{*listView,-1,"Blackbird",0,1234,0}
        ntui_AddListItem{*listView,-1,"Murmel",0,1234,0}
        ntui_AddListItem{*listView,-1,"HelmutH",0,1234,0}
        ntui_AddListItem{*listView,-1,"Bernd_",0,1234,0}
        ntui_AddListItem{*listView,-1,"Modfr",0,1234,0}
        ntui_AddListItem{*listView,-1,"DieterG",0,1234,0}
        ntui_AddListItem{*listView,-1,"DaxB",0,1234,0}
        ntui_AddListItem{*listView,-1,"Deadalus",0,1234,0}
        ntui_AddListItem{*listView,-1,"CJ-Stroker",0,1234,0}
      ntui_EndGroup{}
      ntui_EndGroup{}
      CNIF @#TUI_HAS_BALANCEr
      ntuib_HBalancer{}
      CEND

      ntuib_BeginHGroup{3}
      ntuib_Label    {"Multi-Column ListView",#TUIF_RIGHT}
      CNIF @#TUI_HAS_BALANCER
      ntuib_VBalancer{}
      CEND
      *listView.tuiListView=ntuib_ListView {"\pTBImages:question\~|\lAddress\pTBImages:link|\lSubject\pTBImages:help|\f\lDate\pTBImages:date",True}
      ntui_AddListItem{*listView,-1,"\pTBImages:list_mailunread|koehlerthilo@gmx.de|Test Email|11/12/2008",0,1234,#TUIF_BOLD}
      ntui_AddListItem{*listView,-1,"\pTBImages:list_mailunread|\pTBImages:list_list\tkoehlerthilo@gmx.de|Another Test|13/12/2008",0,1234,#TUIF_BOLD }
      ntui_AddListItem{*listView,-1,"\pTBImages:list_mailunread|support@hd-rec.de|RE: Another Test|14/12/2008",0,1234,#TUIF_BOLD }
      ntui_AddListItem{*listView,-1,"\pTBImages:list_mailold|rumpel-stielzchen@web.de|Test Email|11/12/2008",0,1234,0}
      ntui_AddListItem{*listView,-1,"\pTBImages:list_mailspam|spamname@gmx.de|Another Test|13/12/2008",0,1234,0}
      ntui_AddListItem{*listView,-1,"\pTBImages:list_mailspam|koehlerthilo@gmx.de|RE: Another Test|14/12/2008",0,1234,0}
      ntui_AddListItem{*listView,-1,"\pTBImages:list_mailold|koehlerthilo@gmx.de|\dTest Email|11/12/2008",0,1234,0}
      ntui_AddListItem{*listView,-1,"\pTBImages:list_mailold|koehlerthilo@gmx.de|Another Test|13/12/2008",0,1234,#TUIF_DISABLED}
      ntui_AddListItem{*listView,-1,"\pTBImages:list_mailold|koehlerthilo@gmx.de|RE: Another Test|14/12/2008",0,1234,0}
      ntui_EndGroup{}
    ntui_EndGroup{} ; listview
  CEND

  CNIF @#TUI_HAS_MENUITEM
    ntuib_BeginVGroup{0,"Menu"}
      ntuib_BeginHGroup{0,"","",#TUIF_FIXHEIGHT}
      ntuib_BeginHGroup{2,"","",#TUIF_FIXSIZE}
        ntuib_Label    {"MenuBar",#TUIF_RIGHT}
        ntui_BeginMenuBar{Null}
          ntuib_BeginMenuTitle{"MenuTitle 1","TBImages:info"}
            ntuib_MenuItem{"MenuItem 1","","SHIFT+a"}
            ntuib_MenuItem{"MenuItem 2","","SHIFT+b"}
            ntuib_MenuItem{"MenuItem 3","","CTRL+c","MenuHit3","This is a menu entry"}
            ntuib_Label{"test"}
            ntuib_MenuItem{"MenuItem 4","","ALT+d"}
            ntuib_MenuItem{"MenuItem 5","","F1",""}
            ntuib_BeginSubMenu{"SubMenu","TBImages:junk"}
              ntuib_Button{"test","SAVE"}
              ntuib_MenuItem{"SubMenuItem 2","","SHIFT+b"}
              ntuib_MenuItem{"SubMenuItem 3","","CTRL+c"}
              ntuib_MenuItem{"SubMenuItem 4","","ALT+d"}

              ntuib_BeginSubMenu{"SubMenu #2","TBImages:mail","",-1,"stop"}
                ;ntuib_Button{"file","FILE"}
                ntuib_MenuItem{"SubMenuItem 2","","SHIFT+b"}
                ntuib_MenuItem{"SubMenuItem 3","","CTRL+c"}
                ntuib_MenuItem{"SubMenuItem 4","","ALT+d"}
                ;ntuib_HSeperator{}
                ntuib_MenuItem{"SubMenuItem 5","","F1",""}
              ntui_EndMenu{}

              ntuib_BeginSubMenu{"SubMenu #3","TBImages:date","",-1,"stop"}
                ntuib_Button{"file","FILE"}
                ntuib_MenuItem{"SubMenuItem 2","","SHIFT+b"}
                ntuib_MenuItem{"SubMenuItem 3","","CTRL+c"}
                ntuib_MenuItem{"SubMenuItem 4","","ALT+d"}
                ntuib_HSeperator{""}
                ntuib_MenuItem{"SubMenuItem 5","","F1",""}
              ntui_EndMenu{}

              ntuib_HSeperator{""}
              ntuib_MenuItem{"SubMenuItem 5","","F1","TEST"}

            ntui_EndMenu{}
            ntuib_HSeperator{""}
            ntuib_MenuItem{"MenuItem 3","","SHIFT+x","","",#TUIF_DISABLED}

              ntuib_BeginSubMenu{"SubMenu #6","TBImages:help","",-1,"stop"}
                ;ntuib_Button{"file","FILE"}
                ntuib_MenuItem{"SubMenuItem 2","","SHIFT+b"}
                ntuib_MenuItem{"SubMenuItem 3","","CTRL+c"}
                ntuib_MenuItem{"SubMenuItem 4","","ALT+d"}
                ;ntuib_HSeperator{""}
                ntuib_MenuItem{"SubMenuItem 5","","F1",""}
              ntui_EndMenu{}
          ntui_EndMenu{}

          ntuib_BeginMenuTitle{"MenuTitle 2","TBImages:help"}
            ntuib_MenuItem{"MenuItem 1","","SHIFT+a"}
            ntuib_MenuItem{"MenuItem 2","","SHIFT+b"}
            ntuib_MenuItem{"MenuItem 3","","CTRL+c"}
                ntuib_Cycle{"test 1|test 2",0}
            ntuib_MenuItem{"MenuItem 4","","ALT+d"}
            ntuib_MenuItem{"MenuItem 5","","F1"}
            ntuib_BeginSubMenu{"SubMenu","TBImages:open"}
              ;ntuib_HSeperator{""}

              ;ntuib_MenuItem{"SubMenuItem 1","","AMIGA+a","",-1,"stop"}
                      ;    ntuib_Label{"test"}
                                   ntuib_Button{"test","SAVE","TEST"}
              ntuib_MenuItem{"SubMenuItem 2","","SHIFT+b"}
              ;ntuib_MenuItem{"SubMenuItem 3","","CTRL+c"}
              ntuib_MenuItem{"SubMenuItem 4","","ALT+d"}
              ntuib_HSeperator{""}
              ntuib_MenuItem{"SubMenuItem 5","","F1","TEST"}
            ntui_EndMenu{}
            ntuib_HSeperator{""}
            ntuib_MenuItem{"MenuItem 3","","SHIFT+x","","",#TUIF_DISABLED}

              ntuib_BeginSubMenu{"SubMenu #6","TBImages:saveall","",-1,"stop"}
                ;ntuib_Button{"file","FILE"}
                ntuib_MenuItem{"SubMenuItem 2","","SHIFT+b"}
                ntuib_MenuItem{"SubMenuItem 3","","CTRL+c"}
                ntuib_MenuItem{"SubMenuItem 4","","ALT+d"}
                ;ntuib_HSeperator{""}
                ntuib_MenuItem{"SubMenuItem 5","","F1",""}
              ntui_EndMenu{}


          ntui_EndMenu{}



          ntuib_BeginMenuTitle{"MenuTitle 2","","",#TUIF_DISABLED }
            ntuib_MenuItem{"MenuItem 1"}
            ntuib_BeginSubMenu{"SubMenu"}
              ntuib_MenuItem{"MenuItem"}
            ntui_EndMenu{}
            ntuib_HSeperator{""}
            ntuib_MenuItem{"MenuItem 2"}
          ntui_EndMenu{}
        ntui_EndGroup{}

        ntuib_Label    {"PopUp Menu",#TUIF_RIGHT}
        ntuib_BeginHGroup{0}
          ntuib_BeginPopUp{"PopUp"}
            ntuib_MenuItem{"MenuItem 1"}
              ntuib_BeginSubMenu{"SubMenu"}
                ntuib_MenuItem{"MenuItem"}
              ntui_EndMenu{}
              ntuib_HSeperator{""}
              ntuib_MenuItem{"MenuItem 2"}
          ntui_EndMenu{}
          ntuib_BeginPopUp{"PopUp","","",#TUIF_DISABLED}
          ntui_EndMenu{}
        ntui_EndGroup{}

        ntuib_Label    {"Single PopUp Button",#TUIF_RIGHT}
        ntuib_BeginHGroup{0}
          ntuib_BeginPopUp{"",""}
            ntuib_MenuItem{"MenuItem 1"}
              ntuib_BeginSubMenu{"SubMenu",""}
                ntuib_MenuItem{"MenuItem"}
              ntui_EndMenu{}
              ntuib_HSeperator{""}
              ntuib_MenuItem{"MenuItem 2"}
          ntui_EndMenu{}
          ntuib_BeginPopUp{"","","",#TUIF_DISABLED}
          ntui_EndMenu{}
          ntuib_Space{0,0,#TUIF_FIXHEIGHT}
        ntui_EndGroup{}

        ntuib_Label    {"DropDown Menu",#TUIF_RIGHT}
        ntuib_BeginHGroup{0}
          ntuib_BeginDropDown{"DropDown"}
            ntuib_MenuItem{"MenuItem 1"}
              ntuib_BeginSubMenu{"SubMenu",""}
                ntuib_MenuItem{"MenuItem"}
              ntui_EndMenu{}
              ntuib_HSeperator{""}
              ntuib_MenuItem{"MenuItem 2"}
          ntui_EndMenu{}
          ntuib_BeginDropDown{"DropDown","","",#TUIF_DISABLED}
          ntui_EndMenu{}
        ntui_EndGroup{}

        ntuib_Label    {"DropDown Menu +Images",#TUIF_RIGHT}
        ntuib_BeginHGroup{0}
          ntuib_BeginDropDown{"DropDown","TBImages:save"}
            ntuib_MenuItem{"MenuItem 1","TBImages:save","AMIGA+s"}
              ntuib_BeginSubMenu{"SubMenu",""}
                ntuib_MenuItem{"MenuItem","","AMIGA+x"}
              ntui_EndMenu{}
              ntuib_HSeperator{""}
              ntuib_MenuItem{"MenuItem 2"}
          ntui_EndMenu{}
          ntuib_BeginDropDown{"DropDown","TBImages:save","",#TUIF_DISABLED}
          ntui_EndMenu{}
        ntui_EndGroup{}
       ; ntuib_Space{0,0,#TUIF_FIXHEIGHT} : ntuib_Space{0,0,#TUIF_FIXHEIGHT}
      ntui_EndGroup{} ;: ntuib_ClearFlags{#TUIF_FIXSIZE}

        ntuib_Space{0,0,#TUIF_FIXHEIGHT}
      ntui_EndGroup{} ;: ntuib_ClearFlags{#TUIF_FIXSIZE}
      ntuib_Space{0,0}
    ntui_EndGroup{} ; menu


  CEND


CNIF @#TUI_HAS_TABVIEW
  ntui_EndTabView{}
CEND

  *obj.tuiObject = ntui_EndWindow{}
  ;ntui_SetAttr{*obj,#TUIA_CLEARFLAGS,#TUIF_FIXSIZE}
  ;ntui_SetAttr{*obj,#TUIWA_APPNOTIFY,#NOTIFY_DROP}

End If
ntui_EndBuild{}
; ntui_SetAttrByID{*engine,"PAGE",#TUIA_BGPEN,#TUIPEN_HALFSHINE}
;ntui_SetAttr{*objWin,#TUIA_BGPEN,#TUIPEN_HALFSHADOW}


ntui_BindByID{*engine,"CView1","CHScroller"}
ntui_BindByID{*engine,"CView1","CVScroller"}

ntui_ShowWindowByID{*engine,"DEMO"}
ntui_ShowWindowByID{*engine,"xmlwin"}
ntui_ShowWindowByID{*engine,"xuiwin"}
ntui_ShowWindowByID{*engine,"mainwin"}
ntui_ShowAppIcon{*engine,"NTUI Template"}
CNIF @#TUI_HAS_LISTVIEW
*listView = (.tuiListView)ntui_GetObjectByID{*engine,"xuilist"}
If *listView
ntui_AddListItem{*listView,-1,"|Text Color|#447799",0,1234,0}
ntui_AddListItem{*listView,-1,"|Cursor Color|#EEEEEE",0,1234,0}
ntui_AddListItem{*listView,-1,"|Tab Color|#444444",0,1234,0}
ntui_AddListItem{*listView,-1,"|Pattern Color|#447755",0,1234,0}
ntui_AddListItem{*listView,-1,"|Backfill Color|#88CC99",0,1234,0}
ntui_AddListItem{*listView,-1,"|Blabla Color|#000000",0,1234,0}
ntui_AddListItem{*listView,-1,"|Bubble Color|#AAAAAA",0,1234,0}
ntui_AddListItem{*listView,-1,"|Input Text Color|#AAAAAA",0,1234,0}
ntui_AddListItem{*listView,-1,"|Output Color|#BBAA9A",0,1234,0}
ntui_AddListItem{*listView,-1,"\pRIGHT|Images|",0,1234,0}
ntui_AddListItem{*listView,-1,"|Use Images|ON",0,1234,0}
ntui_AddListItem{*listView,-1,"|Load Images|Yes",0,1234,0}
ntui_AddListItem{*listView,-1,"|Image Path|Sys:Prefs/Images",0,1234,0}
ntui_AddListItem{*listView,-1,"|Window Images|window.jpg",0,1234,0}
ntui_AddListItem{*listView,-1,"|My Images|Blabla",0,1234,0}
ntui_AddListItem{*listView,-1,"|Blublub|Haha",0,1234,0}
End If
CEND
CNIF @#TUI_HAS_TEXTBOX
*Address.tuiObject = ntui_GetObjectByID {*engine,"mystring"}
;error{"Address Object: "+Hex$(*Address)}
string.s = ntui_GetStringByID{*Address,"mystring"}
;error{"http = "+string}
CEND

; Checkerboard drawing function for demo
Statement DrawCheckerBoard{*engine.tuiEngine,*rp.RastPort,dx.l,dy.l,xoff.l,yoff.l,width.l,height.l,qs.l}

  r.l = xoff /qs + yoff/qs

  xoff = xoff MOD qs
  yoff = yoff MOD qs

  For y1.l=0 To height+qs-1 Step qs
    For x1.l=0 To width+qs-1 Step qs

      n.l = (y1+x1)/qs+r
      If (n&1)=1
        SetAPen_ *rp,*engine\pen[#TUIPEN_HALFSHADOW]
      Else
        SetAPen_ *rp,*engine\pen[#TUIPEN_HALFSHINE]
      End If

      qx1.l = x1-xoff
      qy1.l = y1-yoff

      qx2.l = qx1+qs-1 : If qx2>=width  Then qx2=width-1
      qy2.l = qy1+qs-1 : If qy2>=height Then qy2=height-1

      If qx1<0 Then qx1=0
      If qy1<0 Then qy1=0

      RectFill_ *rp,qx1+dx,qy1+dy,qx2+dx,qy2+dy

    Next
  Next

End Statement

quitme.l = False
While quitme=False
  Wait_ $FFFFFFFF
  Repeat
    *ev.tuiEvent = ntui_GetEvent{*engine}
    Select ntui_GetEventNotify{*ev}
      CNIF @#TUI_HAS_CUSTOMVIEW
      Case "CVTOUCH"
        *obj.tuiObject = ntui_GetEventObject{*ev}
        ntui_SetFocus{*obj}
        ntui_SetAttr{*obj,#TUIA_ACTIVE,True}
        ntui_SetAttr{*obj,#TUIA_MOUSEPOINTER,#TUIPOINTER_TARGET}

      Case "CVRELEASE"
        *obj.tuiObject = ntui_GetEventObject{*ev}
        ntui_SetAttr{*obj,#TUIA_ACTIVE,False}
        ntui_SetAttr{*obj,#TUIA_MOUSEPOINTER,#TUIPOINTER_DEFAULT}

      Case "CVMOVE"
        *obj.tuiObject = ntui_GetEventObject{*ev}
        *rp.RastPort = ntui_ObtainRastPort{*obj}
        If *rp
          ntui_GetEventAttr{*ev,#TUIEVA_CXPOS,&mx.l}
          ntui_GetEventAttr{*ev,#TUIEVA_CYPOS,&my.l}

          ntui_GetAttr{*obj,#TUIA_XPOS,&objx.l}
          ntui_GetAttr{*obj,#TUIA_YPOS,&objy.l}

          ntui_GetAttr{*obj,#TUICVA_HOFFSET,&xoff.l}
          ntui_GetAttr{*obj,#TUICVA_VOFFSET,&yoff.l}

          a.s = Str$(mx+xoff)+"/"+Str$(my+yoff)+"      " ; draw the mouse koos
          SetAPen_ *rp,1
          SetBPen_ *rp,0
          Move_ *rp,objx+32,objy+32
          Text_ *rp,&a,FLen(a)

          ntui_ReleaseRastPort{*obj}
        End If

      Case "CVSCROLL"
        *obj = ntui_GetEventObject{*ev}
        ntui_PostNotify{*obj,"REFRESH"}
      CEND
      Case "MenuHit3"   :
        *obj = ntui_GetEventObject{*ev}
        *win.tuiWindow = ntui_GetRootWindow{*obj}
        ntui_GetAttr{*win,#TUIWA_INSTANCEID,&instanceID.l}
        error{"Menuhit with: "+Str$(*obj)+" win:"+Str$(*win)+" ID:"+Str$(instanceID)}

      Case "TEST"       : message{"User hit the button!"}
                                                                                                                                                                                                                                                  
      Case "QUIT"       : quitme=True
      Case "CLOSE"      : quitme=True
      Case "POPUP"      : ntui_PopUp{*engine} ; message{"App Pop up!"}
      Case "ICONIFY"    : ntui_Iconify{*engine} ;message{"App iconify!"}
      Case "WINDOWDROP" : message{"Icon drop on Window: " }
        *obj.tuiObject = ntui_GetEventObject{*ev}
        s.s = ntui_GetEventString{*ev}
        ntui_SetAttr{*obj,#TUIWA_TITLE,&s}
      Case "ICONDROP"   : message{"Icon drop on app icon: " }
      Case "MESSAGE"    : message{"got AREXX message: "}
      Case "Cycle"
        ;a.s = Str$(ntui_GetValueByID{*engine,"CYCL"})
        ;b.s = Str$(0)
        ;message{"Cycle Event "+b.s+" and extra by ID "+a.s}
      Case "VIEW"
        ;message{"Click view!"}
      Case "KEY"
        message{"Key Press!"}

      CNIF @#TUI_HAS_TEXTBOX
      Case "TBPREFS" ; preferences for textbox demo have changed
        nflags.l = #TUIF_NORMAL
        ntui_GetAttrByID{*engine,"font_small" ,#TUIA_VALUE,&small.l} : If small  Then nflags = #TUIF_SMALL
        ntui_GetAttrByID{*engine,"font_fix"   ,#TUIA_VALUE,&fix.l}   : If fix    Then nflags = #TUIF_FIX
        ntui_GetAttrByID{*engine,"font_serif" ,#TUIA_VALUE,&serif.l} : If serif  Then nflags = #TUIF_SERIF
        ntui_GetAttrByID{*engine,"font_italic",#TUIA_VALUE,&italic.l}: If italic Then nflags | #TUIF_ITALIC
        ntui_GetAttrByID{*engine,"font_bold"  ,#TUIA_VALUE,&bold.l}  : If bold   Then nflags | #TUIF_BOLD

        tbflags.l = 0
        ntui_GetAttrByID{*engine,"tb_linecursor",#TUIA_VALUE,&lc.l}     : If lc       Then tbflags | #TUITBF_LINECURSOR
        ntui_GetAttrByID{*engine,"tb_blink"     ,#TUIA_VALUE,&blink.l}  : If blink    Then blinkspeed.l= 3 : Else blinkspeed=-1
        ntui_GetAttrByID{*engine,"tb_showcr"    ,#TUIA_VALUE,&showcr.l} : If showcr   Then tbflags | #TUITBF_SHOWCR
        ntui_GetAttrByID{*engine,"tb_readonly"  ,#TUIA_VALUE,&readonly.l} : If readonly Then tbflags | #TUITBF_READONLY

        *tb.tuiTextBox = (.tuiTextBox)ntui_GetObjectByID{*engine,"TB1"}
        ntui_GetAttr{*tb,#TUITBA_TBFLAGS,&flags.l}
        flags | (#TUITBF_LINECURSOR|#TUITBF_SHOWCR|#TUITBF_READONLY)
        flags - (#TUITBF_LINECURSOR|#TUITBF_SHOWCR|#TUITBF_READONLY)
        flags | tbflags

        ntui_SetAttr{*tb,#TUITBA_TBFLAGS,flags}

        ntui_GetAttr{*tb,#TUIA_FLAGS,&flags}
        flags | (#TUIF_SMALL|#TUIF_FIX|#TUIF_SERIF|#TUIF_NORMAL|#TUIF_BOLD|#TUIF_ITALIC)
        flags - (#TUIF_SMALL|#TUIF_FIX|#TUIF_SERIF|#TUIF_NORMAL|#TUIF_BOLD|#TUIF_ITALIC)
        flags | nflags
        ntui_SetAttr{*tb,#TUIA_FLAGS,flags}
        ntui_SendEvent{*tb,#TUIEV_LAYOUT}
        ntui_SetAttr{*tb,#TUITBA_BLINKSPEED,blinkspeed}
      CEND
      Case "REFRESH"
        *obj.tuiObject = ntui_GetEventObject{*ev}
        If *obj
          Select str_Read{&*obj\nameID}
            CNIF @#TUI_HAS_CUSTOMVIEW
            Case "CView1"
              *rp.RastPort = ntui_ObtainRastPort{*obj}
              If *rp

                ntui_GetAttr{*obj,#TUIA_XPOS,&objx}
                ntui_GetAttr{*obj,#TUIA_YPOS,&objy}

                ntui_GetAttr{*obj,#TUIA_WIDTH,&width.l}
                ntui_GetAttr{*obj,#TUIA_HEIGHT,&height.l}

                ntui_GetAttr{*obj,#TUICVA_HOFFSET,&xoff.l}
                ntui_GetAttr{*obj,#TUICVA_VOFFSET,&yoff.l}

                DrawCheckerBoard{*engine,*rp,objx,objy,xoff,yoff,width,height,64}

                ntui_ReleaseRastPort{*obj}
              End If

            Case "CView2"
              *rp.RastPort = ntui_ObtainRastPort{*obj}
              *oldRegion.Region = Null
              If *rp
                *scrview.tuiScrollView = (.tuiScrollView)ntui_GetObjectByID{*engine,"SCRVIEW"}
                If *scrview
                   ntui_GetAttr{*scrview,#TUIA_CBOX,&*cbox.tuiRect}
                  *oldRegion=ntui_AddClip{*rp,*cbox}

                  ntui_GetAttr{*obj,#TUIA_XPOS,&objx}
                  ntui_GetAttr{*obj,#TUIA_YPOS,&objy}

                  ntui_GetAttr{*obj,#TUIA_WIDTH,&width.l}
                  ntui_GetAttr{*obj,#TUIA_HEIGHT,&height.l}

                  ntui_GetAttr{*obj,#TUICVA_HOFFSET,&xoff.l}
                  ntui_GetAttr{*obj,#TUICVA_VOFFSET,&yoff.l}

                  DrawCheckerBoard{*engine,*rp,objx,objy,xoff,yoff,width,height,64}

                  ntui_RemClip{*rp,*oldRegion}
                End If
                ntui_ReleaseRastPort{*obj}
              End If
            CEND
          End Select
        End If
      Default
        If *ev
          If *ev\obj
            a.s = " class="+Str$(*ev\obj\classID)
          Else
            a.s = " obj=NULL!"
          End If
          error{"Unhandles event!"+Hex$(*ev\notifyID)+a}
          CNIF @#TUI_HAS_TEXTBOX
          If *con
            strP.l = str_GetPtr{*ev\notifyID}
            ntui_WriteConsole{*con,strP}
          End If
          CEND
        End If
    End Select
  Until *ev=Null
Wend

ntui_FreeEngine{*engine}

End

xmldata:
Dc.b "<Window id='xmlwin' title='XML Test' onclose='QUIT'>"
Dc.b "  <Group>"
Dc.b "    <TabView activetab=1>"
Dc.b "      <VGroup title='tab #1'>"
Dc.b "        <Label text='Just a button:'/>"
Dc.b "        <Button text='test' onClick='TEST' help='I was created using XML!'/>"
Dc.b "        <ToolButton text='test' help='I was created using XML!' function='filerequest'/>"
Dc.b "        <HGroup>"
Dc.b "          <VGroup>"
Dc.b "            <RadioButton text='Frodo' id=radio1/>"
Dc.b "            <RadioButton text='Gandalf' id='radio2' bind='radio1'/>"
Dc.b "            <RadioButton text='Bilbo' id='radio3' bind='radio2'/>"
Dc.b "          </VGroup>"
Dc.b "          <ImageView image='sys:prefs/ahi.info'/>"
Dc.b "          <VScroller total=100 visible=20  top=10 hasarrows=yes> <space> <label text='xxx'/>"
Dc.b "        </HGroup>"
Dc.b "      </VGroup>"
Dc.b "      <VGroup title='tab #2'>"
Dc.b "      </VGroup>"
Dc.b "    </TabView>"
Dc.b "  </Group>"
Dc.b "</Window>"
Dc.b 0

alice:
Dc.b "Schner, goldner Nachmittag,",10,"Wo Flut und Himmel lacht!",10
Dc.b "Von schwacher Kindeshand bewegt,",10,"Die Ruder pltschern sacht -;",10
Dc.b "Das Steuer hlt ein Kindesarm",10,"Und lenket unsre Fahrt.",10,10
Dc.b "So fuhren wir gemchlich hin",10,"Auf trumerischen Wellen -;",10
Dc.b "Doch ach! die drei vereinten sich,",10,"Den mden Freund zu qulen -;",10
Dc.b "Sie treiben ihn, sie drngten ihn,",10,"Ein Mhrchen zu erzhlen.",10,10
Dc.b "Die Erste gab's Commandowort;",10,"O schnell, o fange an!",10
Dc.b "Und mach' es so, die Zweite bat,",10,"Da man recht lachen kann!",10
Dc.b "Die Dritte lie ihm keine Ruh",10,"Mit wie? und wo? und wann?",10,10
Dc.b "Jetzt lauschen sie vom Zauberland",10,"Der wunderbaren Mhr';",10
Dc.b "Mit Thier und Vogel sind sie bald'",10,"In freundlichem Verkehr,",10
Dc.b "Und fhlen sich so heimisch dort,",10,"Als ob es Wahrheit wr'. -;",10,10
Dc.b "Und jedes Mal, wenn Fantasie",10,"Dem Freunde ganz versiegt: -;",10
Dc.b "",34,"das Uebrige ein ander Mal!",34,"",10,"O nein, sie leiden's nicht.",10
Dc.b "",34," ",34,"Es ist ja schon ein ander Mal!",34," ",34," -;",10,"So rufen sie vergngt.",10,10
Dc.b "So ward vom schnen Wunderland",10,"Das Mrchen ausgedacht,",10
Dc.b "So langsam Stck fr Stck erzhlt,",10,"Beplaudert und belacht,",10
Dc.b "Und froh, als es zu Ende war,",10,"Der Weg nach Haus gemacht.",10,10
Dc.b "Alice! O nimm es freundlich an!",10,"Leg' es mit gt'ger Hand",10
Dc.b "Zum Straue, den Erinnerung",10,"Aus Kindheitstrumen band,",10
Dc.b "Gleich welken Blthen, mitgebracht",10,"Aus liebem, fernen Land.",10,10
Dc.b "- Lewis Carroll";,10
Dc.b 0

CEND


