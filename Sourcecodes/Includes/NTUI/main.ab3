; XTRA
; Embedded .xtra Header
; 
; General Info
; -------------------------------------------------------
; ExePath         = "ram:"
; ExeFile         = "NTUIDemo"
; CreateIcon      = 0
; Residents       = "all.res"
; 
; Compiler
; -------------------------------------------------------
; StringBuffer    = 10240
; MakeSmallest    = 1
; Version         = 0.1.0
; NumberOfBuilds  = 2373
; 
; Debugger
; -------------------------------------------------------
; CliArgs         = ""
; StackSize       = 80000
; RuntimeDebug    = 1
; DebugInfo       = 0
; CreateDbgFile   = 0
; OverflowCheck   = 0
; AssemblerCheck  = 0
; InterruptCheck  = 1
; AutoRun         = 1
; 
; Editor
; -------------------------------------------------------
; CursorLine      = 3010
; CursorColumn    = 44
; LabelSearch     = "get"
; LabelRemark     = 0
; LabelAll        = 0
; LabelPosition   = 0
; 
; Blitz Objects
; -------------------------------------------------------
; Max IconInfo    = 1
; Max NChunky     = 50
; Max MUIObject   = 50
; Max PTModule    = 5
; Max AsyncReq    = 4
; Max Req-Lib     = 5
; Max GTMenuList  = 5
; Max Console     = 5
; Max TCPSock     = 5
; Max XBSound     = 10
; Max Chunky      = 20
; Max File        = 5
; Max GadgetList  = 5
; Max Queue       = 10
; Max Screen      = 5
; Max Shape       = 100
; Max CopList     = 10
; Max Sprite      = 20
; Max Stencil     = 5
; Max Module      = 5
; Max Window      = 20
; Max Anim        = 10
; Max Sound       = 10
; Max Bank        = 5
; Max Buffer      = 10
; Max BitMap      = 10
; Max Slice       = 10
; Max Page        = 4
; Max Tape        = 5
; Max IntuiFont   = 5
; Max MedModule   = 8
; Max Palette     = 4
; Max MenuList    = 5
; Max BlitzFont   = 4
; Max GTList      = 20
; /XTRA
;///////////////////////////////////////////////////////////////////////////////
;/                                                                             /
;/ Name: ntui.include                                                          /
;/                                                                             /
;/ Platforms: Classic, WinUAE, Amithlon, MorphOS, AmigaOS4                     /
;/                                                                             /
;/ Date: 06/01/2009                                                            /
;/                                                                             /
;/ Author: Thilo Koehler                                                       /
;/                                                                             /
;/ Requirements:  Amiblitz3                                                    /
;/                                                                             /
;/ Purpose:                                                                    /
;/ Implementation of NTUI Toolkit (New Thilo's User Interface)                 /
;/                                                                             /
;/ Abstract:                                                                   /
;/ * no abstract available *                                                   /
;/                                                                             /
;/ User Constants:                                                             /
;/ #TUIINQUEUE_MAX = 16 ; define the maximum events in input queue                                                          /
;/ #TUI_DEBUG      = 0  ; define debug mode                                         /
;/                                                                             /
;///////////////////////////////////////////////////////////////////////////////
optimize 7
Syntax 2
#NTUI_DEBUG = 0

XINCLUDE "NTUI/definitions.ab3"

;/* get the window of a tuiObject, if it has any */
Function.l FAST ntui_GetObjectWindow{*obj.tuiObject}
  !_ASSERT{*obj}

  *win.tuiWindow = #NULL
  breakme.l = False

  Repeat
    If *obj
      If *obj\classID = #TUICLASS_WINDOW Then *win = *obj : breakme=True
      *obj = *obj\parent
    Else
      breakme=True
    End If

  Until breakme

  Function Return *win

End Function


Function.l FAST ntui_GetObjectRootWindow{*obj.tuiObject}
  !_ASSERT{*obj}

  *win.tuiWindow = #NULL

  While *obj
    If *obj\classID = #TUICLASS_WINDOW Then *win = *obj
    *obj = *obj\parent
  Wend

  Function Return *win

End Function


;/* get the rastport of a tuiObject */
Function.l FAST ntui_ObtainRastPort{*obj.tuiObject}
  !_ASSERT{*obj}

  *rp.RastPort = #NULL

  *win.tuiWindow = ntui_GetObjectWindow{*obj}
  If *win
    If *win\oswin
      ObtainSemaphore_ *win\lock
      CNIF #__debug=0
      LockLayer_ 0,*win\oswin\WLayer
      CEND
      *rp = *win\rp;win\RPort
    End If
  Else
    ObtainSemaphore_ *obj\tuiEngine\lock
    *rp = *obj\tuiEngine\buildRP
  End If

  Function Return *rp
End Function


;/* release the rastport of a tuiObject */
Statement FAST ntui_ReleaseRastPort{*obj.tuiObject}
  !_ASSERT{*obj}

  *win.tuiWindow = ntui_GetObjectWindow{*obj}
  If *win
    If *win\oswin
      ReleaseSemaphore_ *win\lock
      CNIF #__debug=0
      UnlockLayer_ *win\oswin\WLayer
      CEND
    End If
  Else
    ReleaseSemaphore_ *obj\tuiEngine\lock
  End If

End Statement


;/ include utility functions */
XINCLUDE "mousepointer.ab3"
XINCLUDE "tempbuffer.ab3"
XINCLUDE "file.ab3"
XINCLUDE "png.ab3"
XINCLUDE "bitmap.ab3"
XINCLUDE "border.ab3"
XINCLUDE "asl.ab3"
XINCLUDE "arexx.ab3"
XINCLUDE "global.ab3"
XINCLUDE "drawing.ab3"
XINCLUDE "tree.ab3"
XINCLUDE "event.ab3"

;///////////////////////////////////////////////////////////////////////////////
;/                                                                             /
;/ Syntax: _ntui_FreeObject {*obj.tuiObject}                                   /
;/                                                                             /
;/ Description:                                                                /
;/ private: free a tui object and all children                                 /
;/                                                                             /
;/ Inputs:                                                                     /
;/ - *obj.tuiObject    : tuiObject to free                                     /
;/                                                                             /
;///////////////////////////////////////////////////////////////////////////////
Statement FAST ntui_FreeObject{*obj.tuiObject}
  !_ASSERT{*obj}

  ntui_FlushObjEvents{*obj}

  While (*obj\child)      ; free all children
    ntui_FreeObject{*obj\child}
  Wend

  If *obj\Deinit Then !_CallFuncPointer{*obj\Deinit,ret.l,*obj}

  If *obj\child Then error{"\\__THIS_FUNCTION: Tui object still has children after free! classID="+Str$(*obj\classID)+"!"} : Statement Return

  If *obj\prev  Then *obj\prev\next_ = *obj\next_   ; relink previous
  If *obj\next_ Then *obj\next_\prev = *obj\prev    ; relink next

  If *obj\parent                                    ; relink parent
    If *obj\parent\child = *obj
      *obj\parent\child = *obj\prev
      If *obj\parent\child = #NULL Then *obj\parent\child = *obj\next_  ;: *obj\next_\parent = *obj\parent
    End If
  End If

  str_Free{&*obj\string}
  str_Free{&*obj\help}
  str_Free{&*obj\nameID}
  str_Free{&*obj\onClick}
  str_Free{&*obj\onMouseOver}
  str_Free{&*obj\onDoubleClick}
  str_Free{&*obj\onTouch}
  str_Free{&*obj\onRelease}
  str_Free{&*obj\onKeyDown}
  str_Free{&*obj\onKeyRepeat}
  str_Free{&*obj\onKeyUp}
  str_Free{&*obj\onBeginRefresh}
  str_Free{&*obj\onRefresh}
  str_Free{&*obj\onFocus}
  str_Free{&*obj\onLoseFocus}
  str_Free{&*obj\onHelp}

  If *obj\fgImage Then ntui_FreeTBImage{*obj\fgImage}  : *obj\fgImage = #NULL
  If *obj\bgImage Then ntui_FreeTBImage{*obj\bgImage}  : *obj\bgImage = #NULL

  FreeVec_ *obj

End Statement


;///////////////////////////////////////////////////////////////////////////////
;/                                                                             /
;/ Syntax: result.l = _ntui_GetMinSize {*obj.tuiObject,*rp.RastPort}           /
;/                                                                             /
;/ Description:                                                                /
;/ private: calculate the minimum size needed by the tui object                /
;/                                                                             /
;/ Inputs:                                                                     /
;/ - *obj.tuiObject  : ???                                                     /
;/ - *rp.RastPort    : ???                                                     /
;/                                                                             /
;/ Result:                                                                     /
;/ - result.l     : ???                                                        /
;/                                                                             /
;///////////////////////////////////////////////////////////////////////////////
Function.l _ntui_CalculateMinSize{*obj.tuiObject,*rp.RastPort}
  !_ASSERT{*obj}
  !_ASSERT{*rp}

  initsize.tuiPixel\x = *obj\minsize\x,*obj\minsize\y
  If (*obj\CalculateMinSize)
    !_CallFuncPointer{*obj\CalculateMinSize,ret.l,*obj,*rp}
  Else
    _ntui_GetBorderSize{*obj\tuiEngine,*obj\borderType,*obj\flags,*obj\border}
    *obj\minsize\x = *obj\border\left + *obj\border\right + *obj\padding\left + *obj\padding\right
    *obj\minsize\y = *obj\border\top  + *obj\border\bottom+ *obj\padding\top  + *obj\padding\bottom
  End If

  If initsize\x>*obj\minsize\x Then *obj\minsize\x = initsize\x
  If initsize\y>*obj\minsize\y Then *obj\minsize\y = initsize\y

  Function Return *obj\minsize

End Function



; re-draw a tuiObject and recursively all its child objects dependent on the #TUIF_DIRTY flag.
; The tuiObject must be visible, otherwise it will be ignored.
Statement _ntui_Redraw{*obj.tuiObject,*clip.tuiRect}
  !_ASSERT{*obj}

  If (*obj\flags&#TUIF_ONSCREEN=0) Then Statement Return

  *obj\flags = (*obj\flags | #TUIF_DIRTY) - #TUIF_DIRTY
  If *clip
    !tuiAndRect{rpclip.tuiRect,*obj\bbox,*clip}
  Else
    !tuiCopyRect{rpclip.tuiRect,*obj\bbox}
  End If

  ; TODO: while #TUIF_WANTBG
  While *obj\classID><#TUICLASS_WINDOW AND *obj\parent
    *obj = *obj\parent
    !tuiAndRect{rpclip,rpclip,*obj\bbox}
  Wend

  If (*obj\classID=#TUICLASS_WINDOW)
    *rp.RastPort = ntui_ObtainRastPort{*obj}
    If *rp
      _ntui_Draw{*obj,*rp,rpclip}
      ntui_ReleaseRastPort{*obj}
    End If
  End If

End Statement



; /* calculate the layout of a tui Object and all its children */
Statement FAST _ntui_Layout{*obj.tuiObject,*rp.RastPort,*bbox.tuiRect}
  !_ASSERT{*obj}
  !_ASSERT{*rp}
  ; !_ASSERT{*bbox} <= might be NULL if not defined yet!

  *obj\flags = *obj\flags | (#TUIF_ONSCREEN|#TUIF_DIRTY)

;  If Left$(str_Read{&*obj\nameID},1)="x"
;    error{"Layout object: "+str_Read{&*obj\nameID}+"("+Str$(!tuiRectWidth{*bbox})+"/"+Str$(!tuiRectHeight{*bbox})+")"}
;  End If

  If (*obj\Layout)
    !_CallFuncPointer{*obj\Layout,ret.l,*obj,*rp,*bbox}
  Else ; default layout function
    If *bbox
      !tuiCopyRect{*obj\bbox,*bbox}
    End If
    !tuiRemBorder{*obj\ibox,*obj\bbox,*obj\border}
    !tuiRemBorder{*obj\cbox,*obj\ibox,*obj\padding}
  End If

  If (*obj\flags & #TUIF_INVISIBLE) Then *obj\flags = (*obj\flags | #TUIF_ONSCREEN) - #TUIF_ONSCREEN
  ; TODO: Check if bbox is actually on window

End Statement


;///////////////////////////////////////////////////////////////////////////////
;/                                                                             /
;/ Syntax: result.l = ntui_GetObjectByID {*obj.tuiObject,nameID.s}
;/                                                                             /
;/ Description:                                                                /
;/ Find a tui object by nameID and return its pointer
;/                                                                             /
;/ Inputs:                                                                     /
;/ - *obj.tuiObject : tuiObject from were to start the search (typically a tuiEngine or tuiWindow)                                                   /
;/ - nameID.s       : nameID of the tuiObject                                                         /
;/                                                                             /
;/ Result:                                                                     /
;/ - result.l       : pointer to the tuiObject or NULL if not found                                                       /
;/                                                                             /
;///////////////////////////////////////////////////////////////////////////////
Function.l ntui_GetObjectByID{*obj.tuiObject,ID.s}
  !_ASSERT{*obj}

  If (str_Comp{*obj\nameID,&ID-4}=0) Then Function Return *obj

  *obj = *obj\child
  While (*obj><#NULL)
    *found.tuiObject = ntui_GetObjectByID{*obj,ID}
    If *found Then Function Return *found
    *obj=*obj\next_
  Wend

  Function Return #NULL

End Function

; get the child object
Function.l ntui_GetChildObject{*obj.tuiObject}
  If *obj=#NULL Then Function Return #NULL
  Function Return *obj\child            ; descend
End Function


; get the next object in the tab-cycle chain
Function.l ntui_GetParentObject{*obj.tuiObject}
  If *obj=#NULL Then Function Return #NULL
  Function Return *obj\parent
End Function


; get the previous object
Function.l ntui_GetPrevObject{*obj.tuiObject}
  If *obj=#NULL Then Function Return #NULL
  Function Return *obj\prev
End Function


; get the next object
Function.l ntui_GetNextObject{*obj.tuiObject}
  If *obj=#NULL Then Function Return #NULL
  Function Return *obj\next_
End Function


; get the previos object in the tab-cycle chain
Function.l ntui_GetPrevTabObject{*obj.tuiObject}

  While *obj><#NULL
    If *obj\child            ; descend
      *obj = *obj\child
      While *obj\next_ : *obj=*obj\next_ : Wend ; go to the last child
      Function Return *obj
    Else
      If *obj\prev
        Function Return *obj\prev  ; go side
      Else
        While *obj\parent><#NULL ; go up
          *obj = *obj\parent
          If *obj\prev
            Function Return *obj\prev  ; go side
          End If
        Wend
        *obj=#NULL
      End If
    End If
  Wend

  Function Return *obj

End Function


; get the next object in the tab-cycle chain
Function.l ntui_GetNextTabObject{*obj.tuiObject}

  While *obj><#NULL
    If *obj\child            ; descend
      Function Return *obj\child
    Else
      If *obj\next_
        Function Return *obj\next_  ; go side
      Else
        While *obj\parent><#NULL
          *obj = *obj\parent
          If *obj\next_
            Function Return *obj\next_  ; go side
          End If
        Wend
        *obj=#NULL
      End If
    End If
  Wend

  Function Return *obj

End Function


; begin the building process.
; This need to be called always before building any new tuiObject
Statement ntui_BeginBuild{*parent.tuiObject}

  ObtainSemaphore_ tuiGlobal\buildHelper\lock

  If *parent
    tuiGlobal\buildHelper\tuiEngine = *parent\tuiEngine
    tuiGlobal\buildHelper\tuiPrev   = #NULL
    *child.tuiObject = *parent\child
    While *child
      tuiGlobal\buildHelper\tuiPrev = *child
      *child = *child\next_
    Wend
  Else
    tuiGlobal\buildHelper\tuiEngine = #NULL
    tuiGlobal\buildHelper\tuiPrev   = #NULL
  End If
  tuiGlobal\buildHelper\tuiParent   = *parent
  tuiGlobal\buildHelper\tuiCurrent  = #NULL
  tuiGlobal\buildHelper\windowDepth = 0
  tuiGlobal\buildHelper\tuiRootWin  = #NULL

End Statement


Statement ntui_EndBuild{}

  tuiGlobal\buildHelper\tuiEngine   = #NULL
  tuiGlobal\buildHelper\tuiParent   = #NULL
  tuiGlobal\buildHelper\tuiCurrent  = #NULL
  tuiGlobal\buildHelper\tuiPrev     = #NULL
  tuiGlobal\buildHelper\windowDepth = 0
  tuiGlobal\buildHelper\tuiRootWin  = #NULL

  ReleaseSemaphore_ tuiGlobal\buildHelper\lock

End Statement



;///////////////////////////////////////////////////////////////////////////////
;/                                                                             /
;/ Syntax: result.l = ntui_GetObjectByID {*obj.tuiObject,@userID.l,@typeID.l:: /
;/ ,@notify.l}                                                                 /
;/                                                                             /
;/ Description:                                                                /
;/ Find a tui object by userID, optionally add typeID and notify value for more restricted search */    /
;/                                                                             /
;/ Inputs:                                                                     /
;/ - *obj.tuiObject : tuiObject from were to start the search (typically a tuiEngine or tuiWindow)                                                   /
;/ - userID.l       : userID of the tuiObject                                                         /
;/ - typeID.l       : typeID of the tuiObject                                                         /
;/ - notify.l       : notify value of the tuiObject                                                         /
;/                                                                             /
;/ Result:                                                                     /
;/ - result.l       : pointer to the tuiObject or NULL if not found                                                       /
;/                                                                             /
;///////////////////////////////////////////////////////////////////////////////
;Function.l _ntui_GetObjectByID{*obj.tuiObject,*objID.str}
;!_ASSERT{*obj}
;If str_Comp{*obj\objID,*objID}=0 Then Function Return *obj
;*obj = *obj\child
;While *obj><#NULL
;  *objfind.tuiObject = _ntui_GetObjectByID{*obj,*objID}
;  If *objfind Then Function Return *objfind
;  *obj=*obj\next_
;Wend
;Function Return #NULL
;End Function
;
;; Amiblitz Stub
;Function.l ntui_GetObjectByID{*obj.tuiObject,userID.s}
;str_Write{&*userID.str,userID}
;*obj = _ntui_GetObjectByID{*obj,*userID}
;str_Free{&*userID}
;Function Return *obj
;End Function



;///////////////////////////////////////////////////////////////////////////////
;/                                                                             /
;/ Syntax: ntui_SetFlags {flags.l,@nochildren.l,@*obj.tuiObject}               /
;/                                                                             /
;/ Description:                                                                /
;/ Set flags of a tuiObject                              /
;/                                                                             /
;/ Inputs:                                                                     /
;/ - flags.l        : tuiFlags to set (#TUIF_...)                                                         /
;/ - nochildren.l   : if false, the flags are recursively set for all its children tuiObjects                                                     /
;/ - *obj.tuiObject : tuiObject, if not during build-time                                                   /
;/                                                                             /
;///////////////////////////////////////////////////////////////////////////////
Statement FAST ntui_SetFlags{flags.l,@nochildren.l,@*obj.tuiObject}

  If *obj=#NULL OR *obj=-1 Then *obj=tuiGlobal\buildHelper\tuiCurrent
  If *obj=#NULL Then Statement Return
  *obj\flags|flags
  If nochildren=False
    *obj = *obj\child
    While *obj><#NULL
      ntui_SetFlags{flags,nochildren,*obj}
      *obj = *obj\next_
    Wend
  End If

End Statement


;///////////////////////////////////////////////////////////////////////////////
;/                                                                             /
;/ Syntax: ntui_ClearFlags {flags.l,@nochildren.l,@*obj.tuiObject}             /
;/                                                                             /
;/ Description:                                                                /
;/ Reset flags of a tui object                          /
;/                                                                             /
;/ Inputs:                                                                     /
;/ - flags.l        : tuiFlags to reset (#TUIF_...)                                                         /
;/ - nochildren.l   : if false, the flags are recursively set for all its children tuiObjects                                                     /
;/ - *obj.tuiObject : tuiObject, if not during build-time                                                   /
;/                                                                             /
;///////////////////////////////////////////////////////////////////////////////
Statement FAST ntui_ClearFlags{flags.l,@nochildren.l,@*obj.tuiObject}

  If *obj=#NULL OR *obj=-1 Then *obj=tuiGlobal\buildHelper\tuiCurrent
  If *obj=#NULL Then Statement Return
  *obj\flags = (*obj\flags|flags) - flags
  If nochildren=False
    *obj = *obj\child
    While *obj><#NULL
      ntui_ClearFlags{flags,nochildren,*obj}
      *obj = *obj\next_
    Wend
  End If

End Statement


;///////////////////////////////////////////////////////////////////////////////
;/                                                                             /
;/ Syntax: ntui_Refresh {*obj.tuiObject}                                       /
;/                                                                             /
;/ Description:                                                                /
;/ Make a tuiObject dirty, so that it gets refreshed the next time the ntui Engine gets the focus.
;/                                                                             /
;/ Inputs:                                                                     /
;/ - *obj.tuiObject    : tuiObject to refresh                                                  /
;/                                                                             /
;///////////////////////////////////////////////////////////////////////////////
Statement ntui_Refresh{*obj.tuiObject,*clip.tuiRect}
  ;If *obj=#NULL Then Statement Return
  !_ASSERT{*obj}

  If (*obj\flags&#TUIF_ONSCREEN)
  ;If ((*obj\flags&(#TUIF_ONSCREEN|#TUIF_DIRTY))=#TUIF_ONSCREEN )
    If (*obj\flags&(#TUIF_DIRTY)=0) Then ntui_SetFlags{#TUIF_DIRTY,False,*obj}
  ;  *obj\flags|#TUIF_DIRTY
    If *clip
      ntui_PostEvent{*obj,#TUIEV_REFRESH,0,"",*clip\left,*clip\top,!tuiRectWidth{*clip},!tuiRectHeight{*clip}}
    Else
      ntui_PostEvent{*obj,#TUIEV_REFRESH}
    End If
  End If

End Statement


;///////////////////////////////////////////////////////////////////////////////
;/                                                                             /
;/ Syntax: ntui_SetFocus {*obj.tuiObject}                                      /
;/                                                                             /
;/ Description:                                                                /
;/ Set the foucs to a given tuiObject.
;/ The tuiObject will receive the user input then. Most tuiObjects change their visual appearance slightly.                                                /
;/ Note: This is the same as if the tuiObject was hit with the mouse. If the user selects another tuiObject, the foucs is gone.
;/                                                                             /
;/ Inputs:                                                                     /
;/ - *obj.tuiObject    : tuiObject                                                   /
;/                                                                             /
;///////////////////////////////////////////////////////////////////////////////
Statement ntui_SetFocus{*obj.tuiObject}
  ;If *obj=#NULL Then Statement Return
  !_ASSERT{*obj}
  *engine.tuiEngine=*obj\tuiEngine
  !_ASSERT{*engine}
  If *obj=*engine\focus Then Statement Return ; has already the focus

  *win.tuiWindow = ntui_GetObjectWindow{*obj}

  If *win=#NULL Then Statement Return

  If *obj=*win\focus Then Statement Return ; has already the focus

  If *win\focus ; remove the focus
    If (*win\focus\flags&#TUIF_FOCUS)
      *win\focus\flags|#TUIF_FOCUS - #TUIF_FOCUS
      ntui_PostEvent{*win\focus,#TUIEV_LOSEFOCUS}
      ;ntui_Refresh{*win\focus,#NULL} ; this is up to the widget that should react on focus if it needs a refresh
    End If
    *win\focus = #NULL
  End If

  If *obj><*engine ; set the focus
    If ((*obj\flags&#TUIF_DISABLED)=0)
      *win\focus = *obj
      If (*obj\flags&#TUIF_FOCUS)=0
        *obj\flags|#TUIF_FOCUS
        ntui_PostEvent{*obj,#TUIEV_FOCUS}
        ;ntui_Refresh{*obj,#NULL} ; this is up to the widget that should react on focus if it needs a refresh
      End If
    End If
  End If

  *engine\focus = *win\focus

End Statement



;///////////////////////////////////////////////////////////////////////////////
;/                                                                             /
;/ Syntax: _ntui_SetMover {*win.tuiWindow,*obj.tuiObject}                /
;/                                                                             /
;/ Description:                                                                /
;/ private
;/                                                                             /
;/ Inputs:                                                                     /
;/ - *win.tuiWindow    : ???                                             /
;/ - *obj.tuiObject    : ???                                                   /
;/                                                                             /
;///////////////////////////////////////////////////////////////////////////////
Statement _ntui_SetMover{*obj.tuiObject}
  If *obj=#NULL Then Statement Return
  !_ASSERT{*obj}
  *engine.tuiEngine=*obj\tuiEngine
  !_ASSERT{*engine}

  If *obj=*engine\mover Then Statement Return ; has already the mover

  *engine\restingTime=0
  If *engine\mover
    *engine\mover\flags|#TUIF_MOVER - #TUIF_MOVER
    ntui_Refresh{*engine\mover,#NULL}
    *engine\mover = #NULL
  End If

  If *obj><*engine
    If ((*obj\flags&#TUIF_WANTMOVER)><0)
     *engine\mover = *obj
     *engine\mover\flags|#TUIF_MOVER
      ntui_Refresh{*engine\mover,#NULL}
    End If
    *win.tuiWindow = ntui_GetObjectWindow{*obj}
    If *win
      _ntui_UsePointer{*win,*obj\pointerID}
    End If
  End If

End Statement


;///////////////////////////////////////////////////////////////////////////////
;/                                                                             /
;/ Syntax: _ntui_PropagateBind {*objMaster.tuiObject}                          /
;/                                                                             /
;/ Description:                                                                /
;/ private                                                                     /
;/                                                                             /
;/ Inputs:                                                                     /
;/ - *objMaster.tuiObject    : ???                                             /
;/                                                                             /
;///////////////////////////////////////////////////////////////////////////////
Statement _ntui_PropagateBind{*master.tuiObject,bindEventID.l,value.l,*string.str}

  *obj.tuiObject =*master\bind
  While *obj><#NULL AND *obj><*master
    *objEvent.tuiEvent = _ntui_CreateEvent{*master,bindEventID}
    *objEvent\value = value
    If *string Then str_WritePtr{&*objEvent\string,&*string\str,*string\size}
    If *obj\DispatchEvent Then !_CallFuncPointer{*obj\DispatchEvent,done.w,*obj,*objEvent}
    If done=False Then _ntui_DestroyEvent{*objEvent}
    *obj=*obj\bind
  Wend

End Statement


;///////////////////////////////////////////////////////////////////////////////
;/                                                                             /
;/ Syntax: result.l = ntui_SetAttr {*obj.tuiObject,ti_Tag.l,ti_Data.l}         /
;/                                                                             /
;/ Description:                                                                /
;/ Set an attribute of a tuiObject.
;/ Note: This is a very powerful function that can change almost any aspect of the tuiObject.
;/
;/ Supported attributes:
;/ #TUIA_STRING      Get/Set
;/                   Pointer to 0-terminated string value of tuiObject (if set, a cooy of the string will be made)
;/ #TUIA_VALUE       Get/Set
;/                   32bit integer value of tuiObject
;/ #TUIA_HELP        Get/Set
;/                   Pointer to 0-terminated string that holds the short help text (if set, a copy is made)
;/ #TUIA_NOTIFY      Get/Set
;/                   Notify value of the tuiObject
;/ #TUIA_FLAGS       Get/Set
;/                   All tuiFlags at once - use TUIA_SETFLAGS to manipulate single flags
;/                   Note: not all flags have an effect if changed during life-time of the tuiObject
;/ #TUIA_WIDTH       Get
;/                   Current width in layout (might be 0 if the tuiObject is not visibke)
;/ #TUIA_HEIGHT      Get
;/                   Current height in layout (might be 0 if the tuiObject is not visible)
;/ #TUIA_MINWIDTH    Get/Set
;/                   Minimum width of the tuiObject (might be 0 if the tuiObject was never visible)
;/ #TUIA_MINHEIGHT   Get/Set
;/                   Minimum height of the tuiObject (might be 0 if the tuiObject was never visible)
;/ #TUIA_MAXWIDTH    Get/Set
;/                   Maximum width of the tuiObject (might be 0 if the tuiObject was never visible)
;/ #TUIA_MAXHEIGHT   Get/Set
;/                   Minimum height of the tuiObject (might be 0 if the tuiObject was never visible)
;/ #TUIA_XPOS        Get
;/                   X-position in layout of the tuiObject
;/ #TUIA_YPOS        Get
;/                   Y-position in layout of the tuiObject
;/ #TUIA_BORDERTYPE  Get/Set
;/                   Type of border, see #TUIBORDER_... for possible border types
;/ #TUIA_GAPNORTH    Get
;/                   Gap at north side of the tuiObject
;/ #TUIA_GAPSOUTH    Get
;/                   Gap at south side of the tuiObject
;/ #TUIA_GAPEAST     Get
;/                   Gap at east side of the tuiObject
;/ #TUIA_GAPWEST     Get
;/                   Gap at west side of the tuiObject
;/ #TUIA_FOCUS       Get/Set
;/                   True, if the tuiObject currently holds the focus, False otherwise
;/ #TUIA_TYPEID      Get
;/                   TypeID of the tuiObject (#TUICLASS_...)
;/ #TUIA_USERID      Get/Set
;/                   UserID of the tuiObject
;/ #TUIA_BGPEN       Get/Set
;/                   Background tuiPen (NOT AmigaOS pen!)
;/ #TUIA_SETFLAGS    Set
;/                   Set the flags stated in ti_Data
;/ #TUIA_CLEARFLAGS  Set
;/                   Clear the flags stated in ti_Data
;/ #TUIA_HIDDEN      Get/Set
;/                   True if the Object is hidden, False otherwise
;/ #TUIA_ENABLED     Get/Set
;/                   True, if the tuiObject is enabled, False otherwise
;/ #TUIA_DISABLED    Get/Set
;/                   True, if the tuiObject is disabled, False otherwise
;/ #TUIA_VISIBLE     Get/Set
;/                   True, if the tuiObject is visible, False otherwise
;/ #TUIA_ACTIVE      Get/Set
;/                   True, if the tuiObject is active, False otherwise
;/ #TUIA_MOVER       Get/Set
;/                   True, if the tuiObject is the current Mouse-Over object, False otherwise
;/
; tuiWindow attributes
;/ #TUIWA_WFLAGS     Get/Set
;/                   AmigaOS Window flags of the tuiWindow (#WFLG_...)
;/ #TUIWA_APPNOTIFY  Get/Set
;/                   Notify value for an app event of this tuiWindow, default is #NOTIFY_WINDROP
;
;/* tuiListView attributes */
;/ #TUILVA_MULTISELECT Get/Set
;/                   Defines if the tuiListView can select multiple ListItems at a time
;/                                                                             /
;/ Inputs:                                                                     /
;/ - *obj.tuiObject  : tuiObject                                                   /
;/ - ti_Tag.l        : tuiAttribute (#TUIA_...)                                                         /
;/ - ti_Data.l       : value                                                        /
;/                                                                             /
;/ Result:                                                                     /
;/ - result.l        : true if the attribute was successfuly changed, false otherwise                                                        /
;/                                                                             /
;///////////////////////////////////////////////////////////////////////////////
Function.w ntui_SetAttr{*obj.tuiObject,ti_Tag.l,ti_Data.l}
  !_ASSERT{*obj}

  If (*obj\SetAttr)  ; try the objects SetAttr first
    !_CallFuncPointer{*obj\SetAttr,done.w,*obj,ti_Tag,ti_Data}
    If done Then Function Return done
  End If

  done=True
  Select ti_Tag
    Case #TUIA_VALUE
      *obj\value = ti_Data
      ntui_Refresh{*obj,#NULL}
      _ntui_PropagateBind{*obj,#TUIEV_BIND_VALUE,*obj\value,#NULL}

    Case #TUIA_STRING
      str_WritePtr{&*obj\string,ti_Data}
      ntui_Refresh{*obj,#NULL}
      _ntui_PropagateBind{*obj,#TUIEV_BIND_STRING,0,*obj\string}

    Case #TUIA_ONCLICK        : str_WritePtr{&*obj\onClick       ,ti_Data} ; notifyID when the object is clicked
    Case #TUIA_ONMOUSEOVER    : str_WritePtr{&*obj\onMouseOver   ,ti_Data} ; notifyID when mouse gets over the object
    Case #TUIA_ONDOUBLECLICK  : str_WritePtr{&*obj\onDoubleClick ,ti_Data} ; notifyID when the object is double clicked
    Case #TUIA_ONTOUCH        : str_WritePtr{&*obj\onTouch       ,ti_Data} ; notifyID when the object is touched (mouse down)
    Case #TUIA_ONRELEASE      : str_WritePtr{&*obj\onRelease     ,ti_Data} ; notifyID when the object is released (mouse up, doesn't need to be a valid click)
    Case #TUIA_ONKEY          : str_WritePtr{&*obj\onKeyDown     ,ti_Data} ; notifyID when a key is pressed
    Case #TUIA_ONBEGINREFRESH : str_WritePtr{&*obj\onBeginRefresh,ti_Data} ; notifyID before internal refresh
    Case #TUIA_ONREFRESH      : str_WritePtr{&*obj\onRefresh     ,ti_Data} ; notifyID after internal refresh
    Case #TUIA_ONFOCUS        : str_WritePtr{&*obj\onFocus       ,ti_Data} ; notifyID when object gains focus
    Case #TUIA_ONLOSEFOCUS    : str_WritePtr{&*obj\onLoseFocus   ,ti_Data} ; notifyID when object loses focus
    Case #TUIA_ONHELP         : str_WritePtr{&*obj\onHelp        ,ti_Data} ; notifyID when mouse rests over object or HELP key is pressed

    Case #TUIA_HELP       : str_WritePtr{&*obj\help,ti_Data} ; TODO: Update Tooltip!
    Case #TUIA_FLAGS      : *obj\flags       = ti_Data                        : ntui_Refresh{*obj,#NULL} ;: _ntui_PropagateBind{*obj}
    Case #TUIA_SETFLAGS   : *obj\flags       = *obj\flags|ti_Data             : ntui_Refresh{*obj,#NULL} ;: _ntui_PropagateBind{*obj}
    Case #TUIA_CLEARFLAGS : *obj\flags       = (*obj\flags|ti_Data) - ti_Data : ntui_Refresh{*obj,#NULL} ;: _ntui_PropagateBind{*obj}
    Case #TUIA_HEIGHT     : *obj\bbox\bottom = *obj\bbox\top +ti_Data-1 : ntui_Refresh{*obj,#NULL}
    Case #TUIA_WIDTH      : *obj\bbox\right  = *obj\bbox\left+ti_Data-1 : ntui_Refresh{*obj,#NULL}
    Case #TUIA_XPOS       : *obj\bbox\left   = ti_Data : ntui_Refresh{*obj,#NULL}
    Case #TUIA_YPOS       : *obj\bbox\top    = ti_Data : ntui_Refresh{*obj,#NULL}
    Case #TUIA_MINHEIGHT  : *obj\minsize\y   = ti_Data
    Case #TUIA_MINWIDTH   : *obj\minsize\x   = ti_Data
    Case #TUIA_MAXHEIGHT  : *obj\maxsize\y   = ti_Data
    Case #TUIA_MAXWIDTH   : *obj\maxsize\x   = ti_Data
    Case #TUIA_BORDERTYPE : *obj\borderType  = ti_Data : ntui_Refresh{*obj,#NULL}
    Case #TUIA_FOCUS      : If ti_Data
                              ntui_SetFocus{*obj}
                            Else
                              If *obj\tuiEngine\focus = *obj Then ntui_SetFocus{*obj\tuiEngine}
                            End If
    Case #TUIA_MOVER      : If ti_Data
                              _ntui_SetMover{*obj}
                            Else
                              If *obj\tuiEngine\focus = *obj Then _ntui_SetMover{*obj\tuiEngine}
                            End If

    Case #TUIA_CLASSID    : ;/* we cant set the typeID - read only! */
    Case #TUIA_INVISIBLE  : *obj\flags|#TUIF_INVISIBLE : If ti_Data=False Then *obj\flags-#TUIF_INVISIBLE
                            ntui_Refresh{*obj,#NULL} ;: _ntui_PropagateBind{*obj}
    Case #TUIA_ENABLED    : *obj\flags|#TUIF_DISABLED  : If ti_Data       Then *obj\flags-#TUIF_DISABLED
                            ntui_Refresh{*obj,#NULL} ;: _ntui_PropagateBind{*obj}
    Case #TUIA_ACTIVE     : *obj\flags|#TUIF_ACTIVE    : If ti_Data=False Then *obj\flags-#TUIF_ACTIVE
    Case #TUIA_DISABLED   : *obj\flags|#TUIF_DISABLED  : If ti_Data=False Then *obj\flags-#TUIF_DISABLED
                            ntui_Refresh{*obj,#NULL} ;: _ntui_PropagateBind{*obj}
    Case #TUIA_ID         : str_WritePtr{&*obj\nameID,ti_Data}
    Case #TUIA_BGPEN      : *obj\bgTuiPen  = ti_Data : ntui_Refresh{*obj,#NULL}
    Case #TUIA_FGPEN      : *obj\fgTuiPen  = ti_Data : ntui_Refresh{*obj,#NULL}
    Case #TUIA_BGRGB      : *obj\bgRGB  = ti_Data : ntui_Refresh{*obj,#NULL}
    Case #TUIA_FGRGB      : *obj\fgRGB  = ti_Data : ntui_Refresh{*obj,#NULL}
    Case #TUIA_BGIMAGE    : imagefile.s = Peek.s(ti_Data)
                            *obj\bgImage = ntui_CreateTBImage{*obj\tuiEngine,imagefile,#TUISIZE_NATIVE,#TUISIZE_NATIVE}
    Case #TUIA_FGIMAGE    : imagefile.s = Peek.s(ti_Data)
                            *obj\fgImage = ntui_CreateTBImage{*obj\tuiEngine,imagefile,#TUISIZE_NATIVE,#TUISIZE_NATIVE}
    Case #TUIA_PADDING       : *obj\padding\left   = ti_Data,ti_Data,ti_Data,ti_Data : ntui_Refresh{*obj,#NULL}
    Case #TUIA_LEFTPADDING   : *obj\padding\left   = ti_Data : ntui_Refresh{*obj,#NULL}
    Case #TUIA_TOPPADDING    : *obj\padding\top    = ti_Data : ntui_Refresh{*obj,#NULL}
    Case #TUIA_RIGHTPADDING  : *obj\padding\right  = ti_Data : ntui_Refresh{*obj,#NULL}
    Case #TUIA_BOTTOMPADDING : *obj\padding\bottom = ti_Data : ntui_Refresh{*obj,#NULL}
    Case #TUIA_MARGIN        : *obj\margin\left    = ti_Data,ti_Data,ti_Data,ti_Data : ntui_Refresh{*obj,#NULL}
    Case #TUIA_LEFTMARGIN    : *obj\margin\left    = ti_Data : ntui_Refresh{*obj,#NULL}
    Case #TUIA_TOPMARGIN     : *obj\margin\top     = ti_Data : ntui_Refresh{*obj,#NULL}
    Case #TUIA_RIGHTMARGIN   : *obj\margin\right   = ti_Data : ntui_Refresh{*obj,#NULL}
    Case #TUIA_BOTTOMMARGIN  : *obj\margin\bottom  = ti_Data : ntui_Refresh{*obj,#NULL}
    Case #TUIA_USERDATA      : *obj\userData       = ti_Data

    Case #TUIA_XMLATTR
      *xmlA.tuiXmlAttr = ti_Data
      Select Peek.s(*xmlA\nameP)
        Case "string"          : done=ntui_SetAttr{*obj,#TUIA_STRING         ,*xmlA\stringP}
        Case "value"           : done=ntui_SetAttr{*obj,#TUIA_VALUE          ,*xmlA\value}
        Case "onclick"         : done=ntui_SetAttr{*obj,#TUIA_ONCLICK        ,*xmlA\stringP}
        Case "onmouseover"     : done=ntui_SetAttr{*obj,#TUIA_ONMOUSEOVER    ,*xmlA\stringP}
        Case "ondoubleclick"   : done=ntui_SetAttr{*obj,#TUIA_ONDOUBLECLICK  ,*xmlA\stringP}
        Case "ontouch"         : done=ntui_SetAttr{*obj,#TUIA_ONTOUCH        ,*xmlA\stringP}
        Case "onrelease"       : done=ntui_SetAttr{*obj,#TUIA_ONRELEASE      ,*xmlA\stringP}
        Case "onkey"           : done=ntui_SetAttr{*obj,#TUIA_ONKEY          ,*xmlA\stringP}
        Case "onbeginrefresh"  : done=ntui_SetAttr{*obj,#TUIA_ONBEGINREFRESH ,*xmlA\stringP}
        Case "onrefresh"       : done=ntui_SetAttr{*obj,#TUIA_ONREFRESH      ,*xmlA\stringP}
        Case "onfocus"         : done=ntui_SetAttr{*obj,#TUIA_ONFOCUS        ,*xmlA\stringP}
        Case "onlosefocus"     : done=ntui_SetAttr{*obj,#TUIA_ONLOSEFOCUS    ,*xmlA\stringP}
        Case "onhelp"          : done=ntui_SetAttr{*obj,#TUIA_ONHELP         ,*xmlA\stringP}
        Case "help"            : done=ntui_SetAttr{*obj,#TUIA_HELP           ,*xmlA\stringP}
        Case "flags"           : done=ntui_SetAttr{*obj,#TUIA_FLAGS          ,*xmlA\value} ; TODO: as text
        Case "minheight"       : done=ntui_SetAttr{*obj,#TUIA_MINHEIGHT      ,*xmlA\value}
        Case "minwidth"        : done=ntui_SetAttr{*obj,#TUIA_MINWIDTH       ,*xmlA\value}
        Case "maxheight"       : done=ntui_SetAttr{*obj,#TUIA_MAXHEIGHT      ,*xmlA\value}
        Case "maxwidth"        : done=ntui_SetAttr{*obj,#TUIA_MAXWIDTH       ,*xmlA\value}
        Case "bordertype"      : done=ntui_SetAttr{*obj,#TUIA_BORDERTYPE     ,*xmlA\value} ; TODO: as text
        Case "focus"           : done=ntui_SetAttr{*obj,#TUIA_FOCUS          ,*xmlA\value}
        Case "invisible"       : done=ntui_SetAttr{*obj,#TUIA_INVISIBLE      ,*xmlA\value}
        Case "disabled"        : done=ntui_SetAttr{*obj,#TUIA_DISABLED       ,*xmlA\value}
        Case "id"              : done=ntui_SetAttr{*obj,#TUIA_ID             ,*xmlA\stringP}
        Case "bgpen"           : done=ntui_SetAttr{*obj,#TUIA_BGPEN          ,*xmlA\value} ; TODO: as text
        Case "fgpen"           : done=ntui_SetAttr{*obj,#TUIA_FGPEN          ,*xmlA\value} ; TODO: as text
        Case "bgrgb"           : done=ntui_SetAttr{*obj,#TUIA_BGRGB          ,*xmlA\value} ; TODO: as text
        Case "fgrgb"           : done=ntui_SetAttr{*obj,#TUIA_FGRGB          ,*xmlA\value} ; TODO: as text
        Case "bgimage"         : done=ntui_SetAttr{*obj,#TUIA_BGIMAGE        ,*xmlA\stringP}
        Case "fgimage"         : done=ntui_SetAttr{*obj,#TUIA_FGIMAGE        ,*xmlA\stringP}
        Case "leftpadding"     : done=ntui_SetAttr{*obj,#TUIA_LEFTPADDING    ,*xmlA\value}
        Case "toppadding"      : done=ntui_SetAttr{*obj,#TUIA_TOPPADDING     ,*xmlA\value}
        Case "rightpadding"    : done=ntui_SetAttr{*obj,#TUIA_RIGHTPADDING   ,*xmlA\value}
        Case "bottompadding"   : done=ntui_SetAttr{*obj,#TUIA_BOTTOMPADDING  ,*xmlA\value}
        Case "leftmargin"      : done=ntui_SetAttr{*obj,#TUIA_LEFTMARGIN     ,*xmlA\value}
        Case "topmargin"       : done=ntui_SetAttr{*obj,#TUIA_TOPMARGIN      ,*xmlA\value}
        Case "rightmargin"     : done=ntui_SetAttr{*obj,#TUIA_RIGHTMARGIN    ,*xmlA\value}
        Case "bottommargin"    : done=ntui_SetAttr{*obj,#TUIA_BOTTOMMARGIN   ,*xmlA\value}
        Case "padding"         : done=ntui_SetAttr{*obj,#TUIA_PADDING        ,*xmlA\value}
        Case "margin"          : done=ntui_SetAttr{*obj,#TUIA_MARGIN         ,*xmlA\value}
        Case "samewidth"       : If (*xmlA\value) Then done=ntui_SetAttr{*obj,#TUIA_SETFLAGS,#TUIF_SAMEWIDTH}  : done=ntui_SetAttr{*obj,#TUIA_CLEARFLAGS,#TUIF_SAMEWIDTH}
        Case "sameheight"      : If (*xmlA\value) Then done=ntui_SetAttr{*obj,#TUIA_SETFLAGS,#TUIF_SAMEHEIGHT} : done=ntui_SetAttr{*obj,#TUIA_CLEARFLAGS,#TUIF_SAMEHEIGHT}
        Case "samesize"        : If (*xmlA\value) Then done=ntui_SetAttr{*obj,#TUIA_SETFLAGS,#TUIF_SAMESIZE}   : done=ntui_SetAttr{*obj,#TUIA_CLEARFLAGS,#TUIF_SAMESIZE}
        Case "fixwidth"        : If (*xmlA\value) Then done=ntui_SetAttr{*obj,#TUIA_SETFLAGS,#TUIF_FIXWIDTH}   : done=ntui_SetAttr{*obj,#TUIA_CLEARFLAGS,#TUIF_FIXWIDTH}
        Case "fixheight"       : If (*xmlA\value) Then done=ntui_SetAttr{*obj,#TUIA_SETFLAGS,#TUIF_FIXHEIGHT}  : done=ntui_SetAttr{*obj,#TUIA_CLEARFLAGS,#TUIF_FIXHEIGHT}
        Case "fixsize"         : If (*xmlA\value) Then done=ntui_SetAttr{*obj,#TUIA_SETFLAGS,#TUIF_FIXSIZE}    : done=ntui_SetAttr{*obj,#TUIA_CLEARFLAGS,#TUIF_FIXSIZE}
        Case "userdata"        : done=ntui_SetAttr{*obj,#TUIA_USERDATA       ,*xmlA\value}
        ;/* extras */

        Case "align"
          done = False
          If *xmlA\stringP
            string.s = Peek.s(*xmlA\stringP)
            If Instr(string,"left")   Then *obj\flags = *obj\flags|(#TUIF_RIGHT|#TUIF_LEFT|#TUIF_CENTER) - (#TUIF_RIGHT |#TUIF_CENTER) : ntui_Refresh{*obj,#NULL}
            If Instr(string,"right")  Then *obj\flags = *obj\flags|(#TUIF_RIGHT|#TUIF_LEFT|#TUIF_CENTER) - (#TUIF_LEFT  |#TUIF_CENTER) : ntui_Refresh{*obj,#NULL}
            If Instr(string,"center") Then *obj\flags = *obj\flags|(#TUIF_RIGHT|#TUIF_LEFT|#TUIF_CENTER) - (#TUIF_RIGHT |#TUIF_CENTER) : ntui_Refresh{*obj,#NULL}
            If Instr(string,"top")    Then *obj\flags = *obj\flags|(#TUIF_TOP|#TUIF_BOTTOM|#TUIF_MIDDLE) - (#TUIF_BOTTOM|#TUIF_MIDDLE) : ntui_Refresh{*obj,#NULL}
            If Instr(string,"bottom") Then *obj\flags = *obj\flags|(#TUIF_TOP|#TUIF_BOTTOM|#TUIF_MIDDLE) - (#TUIF_TOP   |#TUIF_MIDDLE) : ntui_Refresh{*obj,#NULL}
            If Instr(string,"middle") Then *obj\flags = *obj\flags|(#TUIF_TOP|#TUIF_BOTTOM|#TUIF_MIDDLE) - (#TUIF_BOTTOM|#TUIF_TOP   ) : ntui_Refresh{*obj,#NULL}
          End If
        Default
          done=False
      End Select

    Default
      done = False ; unknown attr
  End Select

  Function  Return done

End Function



;///////////////////////////////////////////////////////////////////////////////
;/                                                                             /
;/ Syntax: result.l = ntui_GetAttr {*obj.tuiObject,ti_Tag.l,*tagData.l}        /
;/                                                                             /
;/ Description:                                                                /
;/ Get an attribute of a tuiObject.                                                /
;/ See ntui_SetAttr for possible attributes.
;/                                                                             /
;/ Inputs:                                                                     /
;/ - *obj.tuiObject : tuiObject                                                  /
;/ - ti_Tag.l       : tuiAttribute (#TUIA_...)                                                         /
;/ - tagData.l      : pointer to data to be filled out                                         /
;/                                                                             /
;/ Result:                                                                     /
;/ - result.w       : success if attribute has been get                                                        /
;/                                                                             /
;///////////////////////////////////////////////////////////////////////////////
Function.l ntui_GetAttr{*obj.tuiObject,ti_Tag.l,ti_Data.l}
  !_ASSERT{*obj}

  If (*obj\GetAttr)  ; try the objects GetAttr first
    !_CallFuncPointer{*obj\GetAttr,done.w,*obj,ti_Tag,&ti_Data}
    If done Then Function Return ti_Data
  End If

  Select ti_Tag
    Case #TUIA_CLASSID       : ti_Data = *obj\classID
    Case #TUIA_ID            : ti_Data = str_GetPtr{*obj\nameID        }
    Case #TUIA_STRING        : ti_Data = str_GetPtr{*obj\string        }
    Case #TUIA_ONCLICK       : ti_Data = str_GetPtr{*obj\onClick       } ; notifyID when the object is clicked
    Case #TUIA_ONMOUSEOVER   : ti_Data = str_GetPtr{*obj\onMouseOver   } ; notifyID when mouse gets over the object
    Case #TUIA_ONDOUBLECLICK : ti_Data = str_GetPtr{*obj\onDoubleClick } ; notifyID when the object is double clicked
    Case #TUIA_ONTOUCH       : ti_Data = str_GetPtr{*obj\onTouch       } ; notifyID when the object is touched (mouse down)
    Case #TUIA_ONRELEASE     : ti_Data = str_GetPtr{*obj\onRelease     } ; notifyID when the object is released (mouse up, doesn't need to be a valid click)
    Case #TUIA_ONKEY         : ti_Data = str_GetPtr{*obj\onKeyDown     } ; notifyID when a key is pressed
    Case #TUIA_ONBEGINREFRESH: ti_Data = str_GetPtr{*obj\onBeginRefresh} ; notifyID before internal refresh
    Case #TUIA_ONREFRESH     : ti_Data = str_GetPtr{*obj\onRefresh     } ; notifyID after internal refresh
    Case #TUIA_ONFOCUS       : ti_Data = str_GetPtr{*obj\onFocus       } ; notifyID when object gains focus
    Case #TUIA_ONLOSEFOCUS   : ti_Data = str_GetPtr{*obj\onLoseFocus   } ; notifyID when object loses focus
    Case #TUIA_ONHELP        : ti_Data = str_GetPtr{*obj\onHelp        } ; notifyID when mouse rests over object or HELP key is pressed
    Case #TUIA_HELP          : ti_Data = str_GetPtr{*obj\help          }
    Case #TUIA_VALUE         : ti_Data = *obj\value
    Case #TUIA_FLAGS         : ti_Data = *obj\flags
    Case #TUIA_WIDTH         : ti_Data = !tuiRectWidth{*obj\cbox}
    Case #TUIA_HEIGHT        : ti_Data = !tuiRectHeight{*obj\cbox}
    Case #TUIA_MINWIDTH      : ti_Data = *obj\minsize\x
    Case #TUIA_MINHEIGHT     : ti_Data = *obj\minsize\y
    Case #TUIA_MAXWIDTH      : ti_Data = *obj\maxsize\x
    Case #TUIA_MAXHEIGHT     : ti_Data = *obj\maxsize\y
    Case #TUIA_XPOS          : ti_Data = *obj\cbox\left
    Case #TUIA_YPOS          : ti_Data = *obj\cbox\top
    Case #TUIA_BORDERTYPE    : ti_Data = *obj\borderType
    Case #TUIA_FOCUS         : If (*obj\flags&#TUIF_FOCUS    ) Then ti_Data=True:Else ti_Data=False
    Case #TUIA_MOVER         : If (*obj\flags&#TUIF_MOVER    ) Then ti_Data=True:Else ti_Data=False
    Case #TUIA_ACTIVE        : If (*obj\flags&#TUIF_ACTIVE   ) Then ti_Data=True:Else ti_Data=False
    Case #TUIA_INVISIBLE     : If (*obj\flags&#TUIF_INVISIBLE) Then ti_Data=True:Else ti_Data=False
    Case #TUIA_ONSCREEN      : If (*obj\flags&#TUIF_ONSCREEN ) Then ti_Data=True:Else ti_Data=False
    Case #TUIA_ENABLED       : If (*obj\flags&#TUIF_DISABLED ) Then ti_Data=False:Else ti_Data=True
    Case #TUIA_DISABLED      : If (*obj\flags&#TUIF_DISABLED ) Then ti_Data=True:Else ti_Data=False
    Case #TUIA_LEFTMARGIN    : ti_Data = *obj\margin\left
    Case #TUIA_TOPMARGIN     : ti_Data = *obj\margin\top
    Case #TUIA_RIGHTMARGIN   : ti_Data = *obj\margin\right
    Case #TUIA_BOTTOMMARGIN  : ti_Data = *obj\margin\bottom
    Case #TUIA_LEFTPADDING   : ti_Data = *obj\padding\left
    Case #TUIA_TOPPADDING    : ti_Data = *obj\padding\top
    Case #TUIA_RIGHTPADDING  : ti_Data = *obj\padding\right
    Case #TUIA_BOTTOMPADDING : ti_Data = *obj\padding\bottom
    Case #TUIA_BGPEN         : ti_Data = *obj\bgTuiPen
    Case #TUIA_FGPEN         : ti_Data = *obj\fgTuiPen
    Case #TUIA_BGRGB         : ti_Data = *obj\bgRGB
    Case #TUIA_FGRGB         : ti_Data = *obj\fgRGB
    Case #TUIA_BGIMAGE       : ti_Data = *obj\bgImage
    Case #TUIA_FGIMAGE       : ti_Data = *obj\fgImage
    Case #TUIA_USERDATA      : ti_Data = *obj\userData
    Default
      ;
  End Select

  Function Return ti_Data

End Function


Statement _ntui_DispatchEvent{*ev.tuiEvent}
  !_ASSERT{*ev}
  *obj.tuiObject = *ev\tuiObject
  !_ASSERT{*obj}

  If *obj\DispatchEvent Then !_CallFuncPointer{*obj\DispatchEvent,done.w,*obj,*ev}
  If done Then Statement Return

  ;/* generic handling of unhandled events */
  *engine.tuiEngine = *obj\tuiEngine

  Select *ev\evID
    Case #TUIEV_FOCUS
      ntui_Refresh{*obj,#NULL}

    Case #TUIEV_LOSEFOCUS
      ntui_Refresh{*obj,#NULL}

    Case #TUIEV_REFRESH
      If (*obj\flags&(#TUIF_DIRTY|#TUIF_ONSCREEN) = (#TUIF_DIRTY|#TUIF_ONSCREEN))
        clip.tuiRect\left = *ev\pos\x,*ev\pos\y,*ev\size\x+*ev\pos\x-1,*ev\size\y+*ev\pos\y-1
        If !tuiValidRect{clip}
          _ntui_Redraw{*obj,clip}
        Else
          _ntui_Redraw{*obj,#NULL}
        End If
      End If

    Case #TUIEV_PROPAGATE
      ;_ntui_PropagateBind{*obj}

    Case #TUIEV_LAYOUT
      *win.tuiWindow = ntui_GetObjectWindow{*obj}
      If *win
        *rp.RastPort = ntui_ObtainRastPort{*win}
        If *rp
          _ntui_Layout{*obj,*rp,#NULL}
          ntui_ReleaseRastPort{*win}
        End If
      End If

    Case #TUIEV_ASLFONT
      fontname.s = str_Read{&*ev\tuiObject\string}
      fontsize.l = *ev\tuiObject\value
      If ntui_ASLFontRequest{*engine,fontname,fontsize}
        ntui_SetAttr{*ev\tuiObject,#TUIA_STRING,*engine\aslfo\fo_TAttr\tta_Name}
        ntui_SetAttr{*ev\tuiObject,#TUIA_VALUE,*engine\aslfo\fo_TAttr\tta_YSize}
      End If

    Case #TUIEV_ASLFILE
      filename.s = str_Read{&*ev\tuiObject\string}
      path.s = _ntui_PathPart{filename}
      fil.s  = _ntui_FilePart{filename}
      patt.s = "#?"
      If ntui_ASLFileRequest{*engine,path,fil,patt,False,False,False,True}
        fil  = Peek.s(*engine\aslfr\fr_File)
        path = Peek.s(*engine\aslfr\fr_Drawer)
        patt = Peek.s(*engine\aslfr\fr_Pattern)
        filename = _ntui_AddPart{path,fil}
        ntui_SetAttr{*ev\tuiObject,#TUIA_STRING,&filename}
      End If

    Case #TUIEV_ASLSAVE
      filename.s = str_Read{&*ev\tuiObject\string}
      path.s = _ntui_PathPart{filename}
      fil.s  = _ntui_FilePart{filename}
      patt.s = "#?"
      If ntui_ASLFileRequest{*engine,path,fil,patt,True,False,False,True}
        fil  = Peek.s(*engine\aslfr\fr_File)
        path = Peek.s(*engine\aslfr\fr_Drawer)
        patt = Peek.s(*engine\aslfr\fr_Pattern)
        filename = _ntui_AddPart{path,fil}
        ntui_SetAttr{*ev\tuiObject,#TUIA_STRING,&filename}
      End If

    Case #TUIEV_ASLPATH
      path.s = str_Read{&*ev\tuiObject\string}
      If ntui_ASLFileRequest{*engine,path,"","",False,False,True,True}
        path = Peek.s(*engine\aslfr\fr_Drawer)
        ntui_SetAttr{*ev\tuiObject,#TUIA_STRING,&path}
      End If

    Case #TUIEV_ASLSCREEN
      name.s = str_Read{&*ev\tuiObject\string}
      modeID.l = *ev\tuiObject\value
      width.l  = 0
      height.l = 0
      depth_.l = 0
      If ntui_ASLScreenModeRequest{*engine,modeID,width,height,depth_}
        modeID = *engine\aslsm\sm_DisplayID
        depth_ = *engine\aslsm\sm_DisplayDepth
        width  = *engine\aslsm\sm_DisplayWidth
        height = *engine\aslsm\sm_DisplayHeight
        *ev\tuiObject\value = modeID
        mode_ptr.l = FindDisplayInfo_ (modeID)
        If mode_ptr
          count.l = GetDisplayInfoData_ (mode_ptr,&naminfo.NameInfo,SizeOf.NameInfo,#DTAG_NAME,modeID)
          If count Then modename.s = Peek.s(&naminfo\Name) : Else modename = "???"
        End If

        modename.s + " ("+Str$(width)+"x"+Str$(height)+"x"+Str$(depth_)+")"
        ntui_SetAttr{*ev\tuiObject,#TUIA_STRING,&modename}
      End If
    Case #TUIEV_TOOLTIP
      !_CallFuncPointer{tuiGlobal\showToolTip,ret.l,*engine,*ev\tuiObject\help,*engine\scr\MouseX,*engine\scr\MouseY}
;      ntui_ShowTooltip{*engine,str_Read{&*engine\mover\help},*engine\scr\MouseX,*engine\scr\MouseY}


  End Select

  If (*ev\notifyID)
    *ev\tuiObject = *obj\tuiEngine
    _ntui_QueueEvent{*obj\tuiEngine\outQ,*ev}
  Else
    _ntui_DestroyEvent{*ev}
  End If

End Statement


;///////////////////////////////////////////////////////////////////////////////
;/                                                                             /
;/ Syntax: result.l = ntui_CauseEvent {notify.l,*win.tuiWindow,@*objOb:: /
;/ ject.tuiObject,@x.l,@y.l,@width.l,@height.l}                                /
;/                                                                             /
;/ Description:                                                                /
;/ Cause an event to happen manually. The event will be passed to the tuiEngines input queue.                                      /
;/                                                                             /
;/ Inputs:                                                                     /
;/ - notify.l               : notify value                                                         /
;/ - *win.tuiWindow   : tuiWindow of the event                                             /
;/ - *objObject.tuiObject   : tuiObject of the event (if any)                                             /
;/ - x/y.l                  : x/y position of event                                                    /
;/ - width.l                : ???                                                          /
;/ - height.l               : ???                                                         /
;/                                                                             /
;/ Result:                                                                     /
;/ - result.l               : pointer to created tuiEvent struture                                                        /
;/                                                                             /
;///////////////////////////////////////////////////////////////////////////////
Statement ntui_SendEvent{*obj.tuiObject,evID.l,@value.l,@string.s,@*pos.tuiPixel,@*size.tuiPixel,@notifyID.s}
  !_ASSERT{*obj}
  *engine.tuiEngine = *obj\tuiEngine
  !_ASSERT{*engine}

  *ev.tuiEvent = _ntui_CreateEvent{*obj,evID}
  If *ev
    If string Then str_Write{&*ev\string,string}
    *ev\value  = value
    If *pos><-1  AND *pos ><#NULL Then *ev\pos\x  = *pos\x,*pos\y
    If *size><-1 AND *size><#NULL Then *ev\size\x = *size\x,*size\y
    If notifyID Then str_Write{&*ev\notifyID,string}
    _ntui_DispatchEvent{*ev}
  End If

End Statement


;///////////////////////////////////////////////////////////////////////////////
;/                                                                             /
;/ Syntax: result.l = _ntui_CreateObject {typeID.l,sizeof_obj.l,notify.l,hel:: /
;/ p.s,flags.l,userID.l}                                                       /
;/                                                                             /
;/ Description:                                                                /
;/ private: generic create function for every tui object. This is called by every tuiObject creation function.                 /
;/ This function allocates the memory for the tuiObject and fills out the mandatory fields.
;/ It also links the object according the buildHelper and updates the buildHelper.
;/                                                                             /
;/ Inputs:                                                                     /
;/ - typeID.l      : typeID of the tuiObject                                                         /
;/ - sizeof_obj.l  : size of the tuiObject specific structure                                                     /
;/ - notify.l      : notify value                                                         /
;/ - help.s        : short help text for this tuiObject                                                          /
;/ - flags.l       : initial tuiFlags (#TUIF_...)                                                         /
;/ - userID.l      : userID to find the object later                                                         /
;/                                                                             /
;/ Result:                                                                     /
;/ - result.l      : pointer to the tuiObject or NULL if failed                                                        /
;/                                                                             /
;///////////////////////////////////////////////////////////////////////////////
Function.l _ntui_CreateObject{typeID.l,sizeof_obj.l,help.s,flags.l,nameID.s}
  *obj.tuiObject = #NULL

  If sizeof_obj<SizeOf.tuiObject
    error{"\\__THIS_FUNCTION: The size of an tuiObject can not be smaller than the basic object structure size!"} : Function Return #NULL
  End If

  If typeID<1 OR typeID>=#TUIMAX_CLASSES
    error{"\\__THIS_FUNCTION: Trying to create an tui object with invalid ID="+Str$(typeID)+", name=\\22"+nameID+"\\22!"} : Function Return #NULL
  End If

  If (flags = #TUI_NOFLAGS) Then flags=0

  *obj.tuiObject = AllocVec_(sizeof_obj,#MEMF_CLEAR)
  If *obj
    If typeID = #TUICLASS_ENGINE
      *engine.tuiEngine = *obj
    Else
      *engine.tuiEngine =  tuiGlobal\buildHelper\tuiEngine
      If *engine = #NULL
        error{"\\__THIS_FUNCTION: Trying to create an tui object without tuiEngine!"} : Function Return #NULL
      End If
    End If

    *obj\classID        = typeID
    *obj\tuiEngine      = *engine
    *obj\flags          = (flags|#TUIF_ONSCREEN) - #TUIF_ONSCREEN
    *obj\value          = 0
    *obj\string         = #NULL
    *obj\help           = #NULL
    *obj\nameID         = #NULL
    *obj\pointerID      = #TUIPOINTER_DEFAULT
    ;*obj\bindFlags      = 0

    *obj\bbox\left      = 0,0,0,0
    *obj\cbox\left      = 0,0,0,0
    *obj\minsize\x      = 0,0
    *obj\maxsize\x      = 0,0
    *obj\border\left    = 0,0,0,0
    *obj\padding\left   = *engine\defPadding\left
    *obj\padding\top    = *engine\defPadding\top
    *obj\padding\right  = *engine\defPadding\right
    *obj\padding\bottom = *engine\defPadding\bottom

    *obj\borderType     = #TUIBORDER_NONE
    *obj\bgTuiPen       = #TUIPEN_TRANSPARENT
    *obj\fgTuiPen       = #TUIPEN_TEXT
    *obj\bgImage        = #NULL
    *obj\fgImage        = #NULL
    *obj\bgRGB          = $00000000
    *obj\fgRGB          = $00000000

    *obj\Deinit           = #NULL
    *obj\DispatchEvent    = #NULL
    *obj\Draw             = #NULL
    *obj\CalculateMinSize = #NULL
    *obj\Layout           = #NULL
    *obj\SetAttr          = #NULL
    *obj\GetAttr          = #NULL

    *obj\margin\left   = *engine\defMargin\left
    *obj\margin\top    = *engine\defMargin\top
    *obj\margin\right  = *engine\defMargin\right
    *obj\margin\bottom = *engine\defMargin\bottom

    *obj\parent      = tuiGlobal\buildHelper\tuiParent               ; parent/child
    If (tuiGlobal\buildHelper\tuiParent)
      If (tuiGlobal\buildHelper\tuiParent\child=#NULL) Then tuiGlobal\buildHelper\tuiParent\child = *obj
    End If

    If (tuiGlobal\buildHelper\tuiPrev) ; previous/next
      *obj\prev       = tuiGlobal\buildHelper\tuiPrev
      *obj\next_      = tuiGlobal\buildHelper\tuiPrev\next_
      tuiGlobal\buildHelper\tuiPrev\next_ = *obj
      If *obj\next_ Then *obj\next_\prev = *obj
    Else
      *obj\prev        = #NULL
      *obj\next_       = #NULL                    ; there is no next yet
    End If
    *obj\child       = #NULL                    ; there is no child yet
    *obj\bind        = #NULL

    If (help)   Then str_Write{&*obj\help   ,help}
    If (nameID) Then str_Write{&*obj\nameID, nameID}

    bpflags.l = 0
    If (*obj\flags&#TUIF_FIXWIDTH  = 0) Then bpflags | #TUIF_FIXWIDTH
    If (*obj\flags&#TUIF_FIXHEIGHT = 0) Then bpflags | #TUIF_FIXHEIGHT
    If (bpflags) ; backpropagate the sizing
      *parent.tuiObject = *obj\parent
      While *parent
        If (*parent\flags&bpflags)
          *parent\flags = (*parent\flags|bpflags) - bpflags
          If *parent\classID><#TUICLASS_WINDOW AND *parent\classID><#TUICLASS_ENGINE Then *parent = *parent\parent : Else *parent=#NULL
        Else
          *parent=#NULL
        End If
      Wend
    End If

    tuiGlobal\buildHelper\tuiCurrent = *obj
    tuiGlobal\buildHelper\tuiPrev    = *obj
  EndIf

  Function Return *obj

End Function

Statement _ntui_BeginChildren{*obj.tuiObject}

  tuiGlobal\buildHelper\tuiParent              = *obj
  tuiGlobal\buildHelper\tuiPrev                = #NULL

End Statement

Function.l _ntui_EndChildren{classID.l}

  tuiGlobal\buildHelper\tuiPrev    = tuiGlobal\buildHelper\tuiParent
  tuiGlobal\buildHelper\tuiCurrent = tuiGlobal\buildHelper\tuiParent
  If tuiGlobal\buildHelper\tuiParent Then tuiGlobal\buildHelper\tuiParent = tuiGlobal\buildHelper\tuiParent\parent : Else tuiGlobal\buildHelper\tuiParent = #NULL
  If tuiGlobal\buildHelper\tuiCurrent
    If tuiGlobal\buildHelper\tuiCurrent\classID><classID Then error{"\\__THIS_FUNCTION: EndChildren does not match a BeginChildren!"}
  Else
    error{"\\__THIS_FUNCTION: EndChildren without BeginChildren!"}
  End If

  Function Return tuiGlobal\buildHelper\tuiCurrent

End Function

;///////////////////////////////////////////////////////////////////////////////
;/                                                                             /
;/ Syntax: result.l = ntui_HitObject {*obj.tuiObject,x.l,y.l}                  /
;/                                                                             /
;/ Description:                                                                /
;/ Test the x/y coordinates which tuiObject they would hit.                                              /
;/ note: This function does also hit disalbed tuiObjects.
;/* note: This function returns NULL, if no object is hit.
;/                                                                             /
;/ Inputs:                                                                     /
;/ - *obj.tuiObject  : parent tuiObject, usually a tuiWindow                                                   /
;/ - x.l             : x coordinate                                                             /
;/ - y.l             : y coordinate                                                             /
;/                                                                             /
;/ Result:                                                                     /
;/ - result.l        : pointer to tuiObject or NULL                                                       /
;/                                                                             /
;///////////////////////////////////////////////////////////////////////////////
Function.l ntui_HitObject{*obj.tuiObject,x.l,y.l}
  !_ASSERT{*obj}

  *hit.tuiObject=#NULL
  If (*obj\flags&#TUIF_ONSCREEN)
    If x>=*obj\bbox\left AND x<=*obj\bbox\right
      If y>=*obj\bbox\top AND y<=*obj\bbox\bottom
        *hit = *obj ; we hit the object!
        *child.tuiObject = *obj\child ; but do we have any children?
        While *child
          *hitchild.tuiObject = ntui_HitObject{*child,x,y}
          If *hitchild Then Function Return *hitchild
          *child = *child\next_
        Wend
      End If
    End If
  End If

  Function Return *hit

End Function


;///////////////////////////////////////////////////////////////////////////////
;/                                                                             /
;/ Syntax: ntui_Bind {*objMaster.tuiObject,*objSlave.tuiObject}                /
;/                                                                             /
;/ Description:
;/ Bind (connect) the two tuiObjects.                                                       /
;/ Master and Slave are only important during the call of this function and determines from which tuiObject to take the initial paramters.
;/* Lateron, the two tuiObject will stay synchron, means a change on one of the tuiObjects will case a change on the other tuiObject.
;/* Note: Which parameters will be synchronized is hardcoded in the tuiObject. E.g. if you bind a horizonal scroller with a vertical scroller, nothing will happen since they change different parameters (vertical vs. horizontal scroll offset).
;/* Note: If you bind A+B, and B+C ,A is automatically binded with C too.
;/* Note: The typical usage is to bind a custom view with a horizontal and vertical scroller. Once they are binded, you only have to take care about the custom view, and the scrollers will follow all changes.
;/                                                                             /
;/ Inputs:                                                                     /
;/ - *objMaster.tuiObject : first tuiObject                                             /
;/ - *objSlave.tuiObject  : second tuiObject                                              /
;/                                                                             /
;///////////////////////////////////////////////////////////////////////////////
Statement ntui_Bind{*master.tuiObject,*slave.tuiObject}
  !_ASSERT{*master}
  !_ASSERT{*slave}

  If *master\bind = #NULL AND *slave\bind=#NULL
   *master\bind = *slave
   *slave\bind = *master
   ntui_SendEvent{*master,#TUIEV_PROPAGATE}
   Statement Return
  End If

  If *master\bind=#NULL AND *slave\bind><#NULL
    *master\bind = *slave\bind
    *slave\bind  = *master
    ntui_SendEvent{*master,#TUIEV_PROPAGATE}
    Statement Return
  End If

  If *master\bind><#NULL AND *slave\bind=#NULL
    *slave\bind  = *master\bind
    *master\bind = *slave
    ntui_SendEvent{*master,#TUIEV_PROPAGATE}
    Statement Return
  End If

  If *master\bind><#NULL AND *slave\bind><#NULL
    *objExchange.tuiObject = *master\bind
    *master\bind = *slave\bind
    *slave\bind  = *objExchange
    ntui_SendEvent{*master,#TUIEV_PROPAGATE}
  End If

End Statement


XINCLUDE "obj.engine.ab3"
XINCLUDE "obj.group.ab3"
XINCLUDE "obj.space.ab3"
XINCLUDE "obj.button.ab3"
XINCLUDE "obj.label.ab3"
XINCLUDE "obj.scroller.ab3"
XINCLUDE "obj.slider.ab3"
XINCLUDE "obj.cycle.ab3"
XINCLUDE "obj.customview.ab3"
XINCLUDE "obj.scrollview.ab3"
XINCLUDE "obj.progressbar.ab3"
XINCLUDE "obj.tabview.ab3"
XINCLUDE "obj.textbox.ab3"
XINCLUDE "obj.seperator.ab3"
XINCLUDE "obj.balancer.ab3"
XINCLUDE "obj.treeview.ab3"
XINCLUDE "obj.window.ab3"
XINCLUDE "obj.menuitem.ab3"
XINCLUDE "obj.listview.ab3"
XINCLUDE "obj.image.ab3"


;///////////////////////////////////////////////////////////////////////////////
;/                                                                             /
;/ Syntax: ntui_ShowBubble {*engine.tuiEngine,*text.str,x.l,y.l}            /
;/                                                                             /
;/ Description:                                                                /
;/ Show a help bubble over the given coordinates.                                                /
;/                                                                             /
;/ Inputs:                                                                     /
;/ - *engine.tuiEngine    : ???                                             /
;/ - *text.str    : ???                                                        /
;/ - x.l    : ???                                                              /
;/ - y.l    : ???                                                              /
;/                                                                             /
;///////////////////////////////////////////////////////////////////////////////
_ntui_ShowToolTip:
Statement _ntui_ShowToolTip{*engine.tuiEngine,*text.str,x.l,y.l}

  If *engine=#NULL Then Statement Return   ; no engine!
  If *engine\scr=#NULL Then Statement Return  ; engine not popped up on screen
  If *engine\toolTip Then ntui_FreeObject{*engine\toolTip} : *engine\toolTip=#NULL

  ntui_BeginBuild{*engine}
  *toolTip.tuiWindow = ntui_BeginWindow{"","TOOLTIPWIN"}
  If *toolTip
    *group.tuiGroup = ntui_BeginHGroup{-1}: ntui_SetAttr{*group,#TUIA_BORDERTYPE,#TUIBORDER_TOOLTIP}
    CNIF @#TUI_HAS_IMAGE
    If *engine\useAISS
      ntui_Image{"TBImages:help",#TUISIZE_INLINE,#TUISIZE_INLINE}
    End If
    CEND
    ntui_Label{str_Read{&*text},#TUIF_CENTER,"TOOLTIP"}
    ntui_EndGroup{}
    ntui_EndWindow{}

    *group\obj\margin\left=0,0,0,0

    *toolTip\oswinFlags = #WFLG_BORDERLESS|#WFLG_NOCAREREFRESH|#WFLG_SIMPLE_REFRESH
    *toolTip\obj\flags|#TUIF_FIXSIZE
    *toolTip\obj\bgTuiPen=#TUIPEN_TRANSPARENT
    *toolTip\obj\padding\left = 0,0,0,0
    *toolTip\obj\margin\left  = 0,0,0,0
    ;Forbid_
    ;*oldFont.TextFont =*engine\scr\RastPort\Font
    _ntui_CalculateMinSize{*toolTip,*engine\buildRP}
    ;SetFont_ *engine\scr\RastPort,*oldFont
    ;Permit_
    *group\obj\bgTuiPen = #TUIPEN_TOOLTIPBG
    x = x -*toolTip\obj\minsize\x/2
   ; y = y + *engine\font[#TUIFONT_NORMAL]\tf_YSize-1

    addy.l = *engine\font[#TUIFONT_NORMAL]\tf_YSize - *group\obj\border\bottom

    y = y -*toolTip\obj\minsize\y - addy
    If y + *engine\font[#TUIFONT_NORMAL]\tf_YSize +10 >*engine\scr\Height
      ;y = y - *engine\font[#TUIFONT_NORMAL]\tf_YSize -15-15
      y = y -*toolTip\obj\minsize\y - *engine\font[#TUIFONT_NORMAL]\tf_YSize*2+2
    End If
    *engine\toolTip = *toolTip
    ntui_ShowWindow{*toolTip,x,y}
  End If
  ntui_EndBuild{}

End Statement

!_GetFuncPointer{tuiGlobal\showToolTip     ,_ntui_ShowToolTip,{0,#NULL,0,0}}


;///////////////////////////////////////////////////////////////////////////////
;/                                                                             /
;/ Syntax: succ.w = ntui_PopUp {*engine.tuiEngine,@pubname.s,@*scr.Scre:: /
;/ en}                                                                         /
;/                                                                             /
;/ Description:                                                                /
;/ Pop the tui engine up on a screen.                                      /
;/ This reverses the effect of ntui_Iconify.
;/ In iconified mode (or if tui tuiEngine was just created), it is not allowed to open any window. The engine must first "popped" to a screen.
;/                                                                             /
;/ Inputs:                                                                     /
;/ - *engine.tuiEngine : tuiEngine to pop up                                             /
;/ - pubname.s            : name of the PubScreen (e.g. "Workbench")                                                       /
;/ - *scr.Screen          : pointer to a custom screen                                                     /
;/                                                                             /
;/ Result:                                                                     /
;/ - succ.w               : True if popped up, False if failed                                                        /
;/                                                                             /
;///////////////////////////////////////////////////////////////////////////////
Function.w ntui_PopUp{*engine.tuiEngine,@pubname.s,@*scr.Screen}
  !_ASSERT{*engine}

  succ.w = False
  If _ntui_OpenScreen{*engine,pubname.s,*scr}

    *child.tuiObject=*engine\obj\child
    While *child
      If *child\classID = #TUICLASS_WINDOW
        *win.tuiWindow = *child
        If *win\state = #TUISTATE_ICONIFIED
          ntui_ShowWindow{*win}
        End If
      EndIf
      *child = *child\next_
    Wend

    succ=True
  End If

  Function Return succ

End Function



;///////////////////////////////////////////////////////////////////////////////
;/                                                                             /
;/ Syntax: result.w = ntui_Iconify {*engine.tuiEngine}                      /
;/                                                                             /
;/ Description:                                                                /
;/ * re-open all windows, if any... */                                         /
;/                                                                             /
;/ Inputs:                                                                     /
;/ - *engine.tuiEngine    : ???                                             /
;/                                                                             /
;/ Result:                                                                     /
;/ - result.w     : ???                                                        /
;/                                                                             /
;///////////////////////////////////////////////////////////////////////////////
Function.w ntui_Iconify{*engine.tuiEngine}

  If *engine =#NULL Then Function Return False
  If *engine\obj\classID><#TUICLASS_ENGINE Then Function Return False
  If *engine\scr=#NULL Then Function Return True ; we are already iconified...

  *scr.Screen = *engine\scr
  ;/* hide all windows */
  *child.tuiObject = *engine\obj\child             ; free all children
  While *child
    If *child\classID=#TUICLASS_WINDOW
      *win.tuiWindow = *child
      ntui_HideWindow{*win}
      *win\state = #TUISTATE_ICONIFIED
    EndIf
    *child = *child\next_
  Wend

  _ntui_CloseScreen{*engine.tuiEngine}

  Function Return True

End Function


Statement _ntui_AmigaCollectIDCMPEvents{*engine.tuiEngine}
  !_ASSERT{*engine}
  If (*engine\winPort=#NULL) Then Statement Return

  Repeat
    *msg.IntuiMessage = GetMsg_(*engine\winPort)
    If *msg
      *oswin.Window  = *msg\IDCMPWindow
      If *oswin Then *obj.tuiObject = *oswin\UserData : Else *obj=*engine

      Select (*msg\Class)    ; start to make some interpretations
        Case #IDCMP_MOUSEBUTTONS
          mouseButtonID.l = *msg\Code & $3

          If (*msg\Code&#IECODE_UP_PREFIX)
            tuiMouseButtonMap(mouseButtonID)=False
          Else
            tuiMouseButtonMap(mouseButtonID)=True
            If (DoubleClick_(*engine\lastSeconds,*engine\lastMicros,*msg\Seconds,*msg\Micros)><0)
              *engine\clickCount+1
            Else
              *engine\clickCount=1
            End If

            *engine\lastSeconds = *msg\Seconds
            *engine\lastMicros  = *msg\Micros
            *engine\lastMouse\x = *msg\MouseX,*msg\MouseY
          End If

          *ev.tuiEvent = _ntui_CreateEvent{*obj,#TUIEV_MOUSEDOWN}
          If *ev
            *ev\qualifier_ = *msg\Qualifier & $FFFF
            If *engine\clickCount=2 Then *ev\qualifier_ | #TUIQUAL_DOUBLECLICK
            If *engine\clickCount=3 Then *ev\qualifier_ | #TUIQUAL_TRIPLECLICK
            *ev\value      = mouseButtonID
            *ev\pos\x      = *msg\MouseX,*msg\MouseY
            *ev\seconds    = *msg\Seconds
            *ev\micros     = *msg\Micros
            If (tuiMouseButtonMap(mouseButtonID)=False) Then *ev\evID = #TUIEV_MOUSEUP
          End If

        Case #IDCMP_MOUSEMOVE
          *ev.tuiEvent = _ntui_CreateEvent{*obj,#TUIEV_MOUSEMOVE}
          If *ev
            *ev\qualifier_ = *msg\Qualifier
            *ev\pos\x      = *msg\MouseX,*msg\MouseY
            *ev\seconds    = *msg\Seconds
            *ev\micros     = *msg\Micros
          End If

        Case #IDCMP_INTUITICKS
          *ev.tuiEvent = _ntui_CreateEvent{*obj,#TUIEV_TICK}
          If *ev
            *ev\qualifier_ = *msg\Qualifier
            *ev\pos\x      = *msg\MouseX,*msg\MouseY
            *ev\seconds    = *msg\Seconds
            *ev\micros     = *msg\Micros
          End If

        Case #IDCMP_MENUPICK
          If *msg\Code=-1
            menuTitle_.l   = -1
            menuItem_.l    = -1
            menuSubItem.l  = -1
          Else
            menuTitle_   = *msg\Code & $1F
            menuItem_    = (*msg\Code LSR 5) & $3F
            menuSubItem  = (*msg\Code LSR 11) & $1F : If menuSubItem = $1f Then menuSubItem=-1
            If menuSubItem>=0
              ;\code = GTMenuChecked(\menu_title,\menu_item,\menu_sub)
            Else
              ;\code = GTMenuChecked(\menu_title,\menu_item)
            End If
          End If
          *ev.tuiEvent = #NULL
          ; unsupported!

        Case #IDCMP_RAWKEY
          rawKeyCode.l     = *msg\Code&$FF
          Select rawKeyCode
            Case #RAWKEY_WHEELUP
              *ev.tuiEvent = _ntui_CreateEvent{*obj,#TUIEV_VSCROLL}
              If *ev
                *ev\value      = -1
                *ev\qualifier_ = *msg\Qualifier
                *ev\seconds    = *msg\Seconds
                *ev\micros     = *msg\Micros
                *ev\pos\x      = *msg\MouseX,*msg\MouseY
              End If

            Case #RAWKEY_WHEELDOWN
              *ev.tuiEvent = _ntui_CreateEvent{*obj,#TUIEV_VSCROLL}
              If *ev
                *ev\value      = 1
                *ev\qualifier_ = *msg\Qualifier
                *ev\seconds    = *msg\Seconds
                *ev\micros     = *msg\Micros
                *ev\pos\x      = *msg\MouseX,*msg\MouseY
              End If

            Default
              If *msg\Code&#IECODE_UP_PREFIX
                tuiRawKeyMap(rawKeyCode) = False
              Else
                tuiRawKeyMap(rawKeyCode) = True
                inputEvent.InputEvent\ie_NextEvent = 0
                inputEvent\ie_Class     = #IECLASS_RAWKEY
                inputEvent\ie_SubClass  = 0
                inputEvent\ie_Code      = *msg\Code
                inputEvent\ie_Qualifier = *msg\Qualifier
                tmp.l = _ntui_ObtainTempBuffer{128}
                Poke.b tmp,0
                MapRawKey_ inputEvent,tmp,128,False
                vanillaString.s  = Peek.s(tmp)
                _ntui_ReleaseTempBuffer{}
              End If

              *ev.tuiEvent = _ntui_CreateEvent{*obj,#TUIEV_KEYDOWN}
              If *ev
                *ev\value      = rawKeyCode
                *ev\qualifier_ = *msg\Qualifier
                *ev\seconds    = *msg\Seconds
                *ev\micros     = *msg\Micros
                str_Write{&*ev\string,vanillaString}
                *ev\pos\x      = *msg\MouseX,*msg\MouseY
                If (tuiRawKeyMap(rawKeyCode)=False) Then *ev\evID = #TUIEV_KEYUP
              End If
          End Select

        Case #IDCMP_MENUVERIFY
          *ev.tuiEvent = #NULL
          ; unsupported

        Case #IDCMP_CLOSEWINDOW
          *ev.tuiEvent = _ntui_CreateEvent{*obj,#TUIEV_CLOSE}

        Case #IDCMP_ACTIVEWINDOW
          *ev.tuiEvent = _ntui_CreateEvent{*obj,#TUIEV_FOCUS}

        Case #IDCMP_INACTIVEWINDOW
          *ev.tuiEvent = _ntui_CreateEvent{*obj,#TUIEV_LOSEFOCUS}

        Case #IDCMP_NEWSIZE
          *ev.tuiEvent = #NULL

        Case #IDCMP_REFRESHWINDOW
          If !tuiRectWidth{*obj\bbox}><*oswin\Width OR !tuiRectHeight{*obj\bbox}><*oswin\Height
            *ev.tuiEvent = _ntui_CreateEvent{*obj,#TUIEV_RESIZE}
            If *ev
              If *oswin
                *ev\pos\x  = *oswin\LeftEdge,*oswin\TopEdge
                *ev\size\x = *oswin\Width,*oswin\Height
              End If
            End If
          Else
            *ev.tuiEvent = #NULL
          End If

        Case #IDCMP_EXTENDEDMOUSE
          mcode.l = *msg\Code&$FFFF
          If (mcode = #IMSGCODE_INTUIWHEELDATA)

           *wd.IntuiWheelData  = *msg\IAddress
           If *wd\WheelY><0
             *ev.tuiEvent = _ntui_CreateEvent{*obj,#TUIEV_VSCROLL}
             If *ev
               *ev\value      = *wd\WheelY
               *ev\qualifier_ = *msg\Qualifier
               *ev\seconds    = *msg\Seconds
               *ev\micros     = *msg\Micros
               *ev\pos\x      = *msg\MouseX,*msg\MouseY
             End If
             _ntui_DispatchEvent{*ev} : *ev.tuiEvent = #NULL
           End If
           If *wd\WheelX><0
             *ev.tuiEvent = _ntui_CreateEvent{*obj,#TUIEV_HSCROLL}
             If *ev
               *ev\value      = *wd\WheelX
               *ev\qualifier_ = *msg\Qualifier
               *ev\seconds    = *msg\Seconds
               *ev\micros     = *msg\Micros
               *ev\pos\x      = *msg\MouseX,*msg\MouseY
             End If
             _ntui_DispatchEvent{*ev} : *ev.tuiEvent = #NULL
           End If
           End If

        Default
          *ev.tuiEvent = #NULL
          ; unsupported

      End Select
     ReplyMsg_ *msg
      If *ev Then _ntui_DispatchEvent{*ev} : *ev.tuiEvent = #NULL


    End If
  Until (*msg=#NULL)
End Statement


Statement _ntui_AmigaCollectAppEvents{*engine.tuiEngine}
  If (*engine\appPort=#NULL) Then Statement Return
  Repeat
    *appMsg.AppMessage = GetMsg_(*engine\appPort)
    If *appMsg
      *win.tuiWindow = *appMsg\am_UserData
      If *win
        *oswin.Window  = *win\oswin
        *obj.tuiObject = *win
      Else
        *oswin         = #NULL
        *obj           = *engine
      End If

      Select *appMsg\am_ID
        Case #APP_WINDOWDROP
          *ev.tuiEvent = _ntui_CreateEvent{*obj,#TUIEV_WINDOWDROP}
          If *ev
            If *appMsg\am_ArgList
              If *appMsg\am_ArgList\wa_Name><0 AND *appMsg\am_ArgList\wa_Lock><0
                tmpP.l = _ntui_ObtainTempBuffer{#APP_BUFFERSIZE}
                If NameFromLock_(*appMsg\am_ArgList\wa_Lock,tmpP,#APP_BUFFERSIZE)
                  If AddPart_(tmpP,*appMsg\am_ArgList\wa_Name,#APP_BUFFERSIZE)
                    str_Write{&*ev\string,Peek.s(tmpP)}
                  End If
                End If
                _ntui_ReleaseTempBuffer{}
              End If
            End If
            *ev\pos\x = *appMsg\am_MouseX,*appMsg\am_MouseY
          End If

        Case #APP_MENUHIT
          *ev.tuiEvent = _ntui_CreateEvent{*obj,#TUIEV_POPUP}

        Case #APP_ICON  ; drop or click ?
          *ev.tuiEvent = _ntui_CreateEvent{*obj,#TUIEV_ICONDROP}
          If (*ev)
            If (*appMsg\am_ArgList)
              If *appMsg\am_ArgList\wa_Name><0 AND *appMsg\am_ArgList\wa_Lock><0
                tmpP.l = _ntui_ObtainTempBuffer{#APP_BUFFERSIZE}
                If (NameFromLock_(*appMsg\am_ArgList\wa_Lock,tmpP,#APP_BUFFERSIZE))
                  If (AddPart_(tmpP,*appMsg\am_ArgList\wa_Name,#APP_BUFFERSIZE))
                    str_Write{&*ev\string,Peek.s(tmpP)}
                  End If
                End If
                _ntui_ReleaseTempBuffer{}
              End If
            End If
            If (*ev\string=#NULL) Then *ev\evID = #TUIEV_POPUP
          End If

        Default
          *ev.tuiEvent = #NULL

      End Select

      ReplyMsg_ *appMsg
      If *ev Then _ntui_DispatchEvent{*ev} : *ev.tuiEvent = #NULL
    End If
  Until (*appMsg=#NULL)

End Statement


Statement _ntui_AmigaCollectAREXXEvents{*engine.tuiEngine}
  If *engine\rxPort=#NULL Then Statement Return

  While _ntui_GetRexxMsg{*engine,True}
    *ev.tuiEvent = _ntui_CreateEvent{*engine,#TUIEV_MESSAGE}
    If (*ev)
      string.s = str_Read{&*engine\rxString}
      str_Write{&*ev\string,string}
      _ntui_DispatchEvent{*ev}
    End If
  Wend

End Statement

Statement _ntui_AmigaCollectScreenNotifyEvents{*engine.tuiEngine}
  If *engine\snPort=#NULL Then Statement Return

  *msg.ScreenNotifyMessage = GetMsg_(*engine\snPort)
  While *msg
    Select *msg\snm_Type
      Case #SCREENNOTIFY_TYPE_WORKBENCH
        If *msg\snm_Value
          *ev.tuiEvent = _ntui_CreateEvent{*engine,#TUIEV_POPUP}
        Else
          *ev.tuiEvent = _ntui_CreateEvent{*engine,#TUIEV_ICONIFY}
          ntui_Iconify{*engine}
        End If
      Case #SCREENNOTIFY_TYPE_CLOSESCREEN
        If *msg\snm_Value = *engine\scr
          *ev.tuiEvent = _ntui_CreateEvent{*engine,#TUIEV_ICONIFY}
          ntui_Iconify{*engine}
        End If

    End Select
    If *ev Then _ntui_DispatchEvent{*ev} : *ev.tuiEvent = #NULL
    ReplyMsg_ *msg
    *msg.ScreenNotifyMessage = GetMsg_(*engine\snPort)
  Wend

End Statement



;///////////////////////////////////////////////////////////////////////////////
;/                                                                             /
;/ Syntax: *ev.tuiEvent = ntui_MoreEvents {*engine.tuiEngine}                   /
;/                                                                             /
;/ Description:                                                                /
;/ Get the next tuiEvent waiting in the input queue.
;/ Note: you should always call this in a loop until there is no more event waiting, because you dont get a signal for already waiting events that wake your task up while Wait_().                   /
;/ Note: To query information from the tuiEvent, check if it is not NULL and use the following macros:
;/                                                                             /
;///////////////////////////////////////////////////////////////////////////////
Function.l ntui_GetEvent{*engine.tuiEngine}
  !_ASSERT{*engine}

  _ntui_AmigaCollectAREXXEvents        {*engine}
  _ntui_AmigaCollectAppEvents          {*engine}
  _ntui_AmigaCollectScreenNotifyEvents {*engine}

  If *engine\inputTask = #NULL       ; no inpout task, so get events it here...
    _ntui_AmigaCollectIDCMPEvents{*engine}
    Repeat
      *ev.tuiEvent =_ntui_UnqueueEvent{*engine\inQ}
      If *ev Then _ntui_DispatchEvent{*ev}
    Until *ev = #NULL
  End If

  If *engine\tuiEvent Then _ntui_DestroyEvent{*engine\tuiEvent}

  *engine\tuiEvent = _ntui_UnqueueEvent{*engine\outQ}

  Function Return *engine\tuiEvent
End Function

Function.l ntui_WaitEvent{*engine.tuiEngine}
  Repeat
    sigf.l = Wait_ ($FFFFFFFF)
    If (sigf&#SIGBREAKF_CTRL_C) Then ntui_PostEvent{*engine,#TUIEV_QUIT}
    *ev.tuiEvent = ntui_GetEvent{*engine.tuiEngine}
  Until *ev
Function Return *ev
End Function


XINCLUDE "xml.ab3"
XINCLUDE "utilities.ab3"

CNIF #__include=0 ;/* ************* DEMO ************
WBStartup

!autoexit{_ntui_Free}

;/* auto init */
If _ntui_Init{} =False Then End ; and call it

*engine.tuiEngine = ntui_CreateEngine{"NTUI-Demo","NTUI","NTUI-Demo-Menu","sys:disk.info",""}
If *engine=#NULL Then error{"Unable to initialized Engine!"} : End ; Ooops!
a.s = "Sys:Prefs/Presets/NTUI/def2"
ntui_SetAttr{*engine,#TUIEA_SKINPATH,&a.s}
ntui_SetAttr{*engine,#TUIEA_USESKIN,0}
ntui_SetAttr{*engine,#TUIEA_USEAISS,0}
ntui_SetAttr{*engine,#TUIEA_BORDERSTYLE,1}
ntui_SetAttr{*engine,#TUIEA_DEFPADDING,2}
ntui_SetAttr{*engine,#TUIEA_DEFMARGIN,2}
If ntui_PopUp{*engine}=False Then error{"Unable to pop up!"}:End ; whats wrong with the screen!?


ntui_BuildFromXMLMem{*engine,?xmldata}

ntui_BeginBuild{*engine}
*demoWin.tuiWindow= ntui_BeginWindow{"Demo Window","DEMO",12345}
If *demoWin
  CNIF @#TUI_HAS_TABVIEW
  ntui_BeginTabView{0,"",-1,"TabView"}
  CEND
    *obj.tuiObject = ntui_BeginVGroup{1,"Label"}
   ; ntui_SetAttr{*obj,#TUIA_LEFTPADDING,7}
   ; ntui_SetAttr{*obj,#TUIA_RIGHTPADDING,7}
   ; ntui_SetAttr{*obj,#TUIA_TOPPADDING,7}
   ; ntui_SetAttr{*obj,#TUIA_BOTTOMPADDING,7}
      ntui_BeginHGroup{1}
        *obj = ntui_Label    {"Label"}   : a.s = "Test!" : ntui_SetAttr{*obj,#TUIA_HELP,&a.s}
        ; ntui_SetAttr{*obj,#TUIA_BORDERTYPE,#TUIBORDER_BUTTON}
      ;  imagefile.s = "NTUI:button.9.png"
      ;  ntui_SetAttr{*obj,#TUIA_BGIMAGE,&imagefile}
        ntui_Label    {"Bold Label",#TUIF_BOLD}
        ntui_Label    {"Italic Label",#TUIF_ITALIC}
        ntui_Label    {"Italic+Bold Label",#TUIF_ITALIC|#TUIF_BOLD}
        ntui_Label    {"Underlined Label",#TUIF_UNDERLINED}
        ntui_Label    {"Small Font Label",#TUIF_SMALL}
        ntui_Label    {"Serif Font Label",#TUIF_SERIF}
        ntui_Label    {"Fix Font Label",#TUIF_FIX}
        ntui_Label    {"Disabled Label",#TUIF_DISABLED}
        ntui_Label    {"Left Aligned Label",#TUIF_LEFT}
        ntui_Label    {"Right Aligned Label",#TUIF_RIGHT}
        *con.tuiTextBox = ntui_Console {"NTUI Console V1.0\\nReady.\\n",100,"",-1,"CON"}
        CNIF @#TUI_HAS_SPACE
        ntui_Space{0,0}
        CEND
      ntui_EndGroup{}
    ntui_EndGroup{}

    CNIF @#TUI_HAS_BUTTON
    *obj = ntui_BeginHGroup{0,"Button"}
       ;     imagefile.s = "NTUI:button.9.png"
       ;     ntui_SetAttr{*obj,#TUIA_BGIMAGE,&imagefile}

      ntui_BeginHGroup{2}
        ntui_Label    {"Push Button",#TUIF_RIGHT}
        ntui_Button   {"Button","","TEST","this is a very basic push-button with text"}

        ntui_Label    {"Toggle Button",#TUIF_RIGHT}
        ntui_Button   {"Toggle","","","this is a toggle button (on/off)",#TUIF_TOGGLE}

        ntui_Label    {"Bold Button",#TUIF_RIGHT}
        ntui_Button   {"Bold","","","this is a button using bold font",#TUIF_BOLD}

        ntui_Label    {"Italic Button",#TUIF_RIGHT}
        ntui_Button   {"Italic","","","this is a button using italic font",#TUIF_ITALIC}

        ntui_Label    {"Italic+Bold Button",#TUIF_RIGHT}
        ntui_Button   {"Italic+Bold","","","this is an italic and bold button",#TUIF_BOLD|#TUIF_ITALIC}

        ntui_Label    {"Underlined Button",#TUIF_RIGHT}
        ntui_Button   {"Underlined","","","this is an underlined button",#TUIF_UNDERLINED}

        ntui_Label    {"Small Button",#TUIF_RIGHT}
        ntui_Button   {"Small","","","this is a button using small font",#TUIF_SMALL}

        ntui_Label    {"Serif Button",#TUIF_RIGHT}
        ntui_Button   {"Serif","","","this is a button using big font",#TUIF_SERIF}

        ntui_Label    {"Fix Button",#TUIF_RIGHT}
        ntui_Button   {"Fix","","","this is a button using fix font",#TUIF_FIX}

        ntui_Label    {"Disabled Button",#TUIF_RIGHT}
        ntui_Button   {"Disabled","","","this is a disabled button",#TUIF_DISABLED}

        ntui_Label    {"Left Button",#TUIF_RIGHT}
        ntui_Button   {"Left","","","this is a button with left-aligned text",#TUIF_LEFT}

        ntui_Label    {"Right Button",#TUIF_RIGHT}
        ntui_Button   {"Right","","","this is a button with right-aligned text",#TUIF_RIGHT}

       ntui_Space{0,0} : ntui_Space{0,0}
      ntui_EndGroup{}
      CNIF @#TUI_HAS_SEPERATOR
      ntui_VSeperator{}
      CEND

      ntui_BeginHGroup{2}
        ntui_Label    {"Arrow Buttons",#TUIF_RIGHT}
        ntui_BeginHGroup{0}
          ntui_FunctionButton{#TUIBUTTON_ARROWUP     ,"this is an arrow button"}
          ntui_FunctionButton{#TUIBUTTON_ARROWDOWN   ,"this is an arrow button"}
          ntui_FunctionButton{#TUIBUTTON_ARROWLEFT   ,"this is an arrow button"}
          ntui_FunctionButton{#TUIBUTTON_ARROWRIGHT  ,"this is an arrow button"}
          ntui_FunctionButton{#TUIBUTTON_ARROWUP     ,"this is an arrow button",#TUIF_DISABLED}
          ntui_FunctionButton{#TUIBUTTON_ARROWDOWN   ,"this is an arrow button",#TUIF_DISABLED }
          ntui_FunctionButton{#TUIBUTTON_ARROWLEFT   ,"this is an arrow button",#TUIF_DISABLED }
          ntui_FunctionButton{#TUIBUTTON_ARROWRIGHT  ,"this is an arrow button",#TUIF_DISABLED }
          ntui_Space{0,0,#TUIF_FIXHEIGHT}
        ntui_EndGroup{}
        ntui_Label    {"Outlined Arrow Buttons",#TUIF_RIGHT}
        ntui_BeginHGroup{0}
          ntui_FunctionButton{#TUIBUTTON_OLARROWUP   ,"this is an outlined arrow button"}
          ntui_FunctionButton{#TUIBUTTON_OLARROWDOWN ,"this is an outlined arrow button"}
          ntui_FunctionButton{#TUIBUTTON_OLARROWLEFT ,"this is an outlined arrow button"}
          ntui_FunctionButton{#TUIBUTTON_OLARROWRIGHT,"this is an outlined arrow button"}
          ntui_FunctionButton{#TUIBUTTON_OLARROWUP   ,"this is an outlined arrow button",#TUIF_DISABLED }
          ntui_FunctionButton{#TUIBUTTON_OLARROWDOWN ,"this is an outlined arrow button",#TUIF_DISABLED }
          ntui_FunctionButton{#TUIBUTTON_OLARROWLEFT ,"this is an outlined arrow button",#TUIF_DISABLED }
          ntui_FunctionButton{#TUIBUTTON_OLARROWRIGHT,"this is an outlined arrow button",#TUIF_DISABLED }
          ntui_Space{0,0,#TUIF_FIXHEIGHT}
        ntui_EndGroup{}
        ntui_Label    {"Menu Arrow Buttons",#TUIF_RIGHT}
        ntui_BeginHGroup{0}
          ntui_FunctionButton{#TUIBUTTON_POPUP       ,"this is a pop up button"}
          ntui_FunctionButton{#TUIBUTTON_DROPDOWN    ,"this is a drop down button"}
          ntui_FunctionButton{#TUIBUTTON_LEFT        ,"this is a left menu button"}
          ntui_FunctionButton{#TUIBUTTON_RIGHT       ,"this is a right menu button"}
          ntui_FunctionButton{#TUIBUTTON_POPUP       ,"this is a pop up button",#TUIF_DISABLED }
          ntui_FunctionButton{#TUIBUTTON_DROPDOWN    ,"this is a drop down button",#TUIF_DISABLED }
          ntui_FunctionButton{#TUIBUTTON_LEFT        ,"this is a left menu button",#TUIF_DISABLED }
          ntui_FunctionButton{#TUIBUTTON_RIGHT       ,"this is a right menu button",#TUIF_DISABLED }
          ntui_Space{0,0,#TUIF_FIXHEIGHT}
        ntui_EndGroup{}
        ntui_Label    {"ASL Buttons",#TUIF_RIGHT}
        ntui_BeginHGroup{0}
          ntui_FunctionButton{#TUIBUTTON_PATH        ,"this is a button to pop up an ASL path requester"}
          ntui_FunctionButton{#TUIBUTTON_SAVE        ,"this is a button to pop up an ASL file requester in save mode"}
          ntui_FunctionButton{#TUIBUTTON_FILE        ,"this is a button to pop up an ASL file requester in open mode"}
          ntui_FunctionButton{#TUIBUTTON_FONT        ,"this is a button to pop up an ASL font requester"}
          ntui_FunctionButton{#TUIBUTTON_SCREEN      ,"this is a button to pop up an ASL screenmode requester"}
          ntui_FunctionButton{#TUIBUTTON_PATH        ,"this is a button to pop up an ASL path requester",#TUIF_DISABLED }
          ntui_FunctionButton{#TUIBUTTON_SAVE        ,"this is a button to pop up an ASL file requester in save mode",#TUIF_DISABLED }
          ntui_FunctionButton{#TUIBUTTON_FILE        ,"this is a button to pop up an ASL file requester in open mode",#TUIF_DISABLED }
          ntui_FunctionButton{#TUIBUTTON_FONT        ,"this is a button to pop up an ASL font requester",#TUIF_DISABLED }
          ntui_FunctionButton{#TUIBUTTON_SCREEN      ,"this is a button to pop up an ASL screenmode requester",#TUIF_DISABLED }
          ntui_Space{0,0,#TUIF_FIXHEIGHT}
        ntui_EndGroup{}

        ntui_Label    {"Other Buttons",#TUIF_RIGHT}
        ntui_BeginHGroup{0}
          ntui_FunctionButton{#TUIBUTTON_INC         ,"this is a button to increase a value"}
          ntui_FunctionButton{#TUIBUTTON_DEC         ,"this is a button to decrease a value"}
          ntui_FunctionButton{#TUIBUTTON_SPINNER     ,"this is a button to in/decrease a value"}
          ntui_FunctionButton{#TUIBUTTON_PLUSMINUS   ,"this is a button to in/decrease a value"}
          ntui_FunctionButton{#TUIBUTTON_INC         ,"this is a button to increase a value",#TUIF_DISABLED }
          ntui_FunctionButton{#TUIBUTTON_DEC         ,"this is a button to decrease a value",#TUIF_DISABLED }
          ntui_FunctionButton{#TUIBUTTON_SPINNER     ,"this is a button to in/decrease a value",#TUIF_DISABLED }
          ntui_FunctionButton{#TUIBUTTON_PLUSMINUS   ,"this is a button to in/decrease a value",#TUIF_DISABLED }
          ntui_Space{0,0,#TUIF_FIXHEIGHT}
        ntui_EndGroup{}

        ntui_Label    {"CheckBox",#TUIF_RIGHT}
        ntui_BeginHGroup{0}
        ntui_CheckBox{1,"","this is an on/off checkbox"}
        ntui_CheckBox{0,"","this is an on/off checkbox"}
        ntui_CheckBox{1,"","this is an on/off checkbox",#TUIF_DISABLED}
        ntui_CheckBox{0,"","this is an on/off checkbox",#TUIF_DISABLED}
        ntui_Space{0,0,#TUIF_FIXHEIGHT}
        ntui_EndGroup{}


        ntui_Label    {"Radio Button",#TUIF_RIGHT}
        ntui_BeginHGroup{0}
        ntui_RadioButton  {"Option 1",0,"","this is a radio button with text",-1,"op1"}
        ntui_RadioButton  {"Option 2",0,"","this is a radio button with text",-1,"op2"}
        ntui_RadioButton  {"Option 3",0,"","this is a radio button with text",#TUIF_DISABLED,"op3"}
        ntui_BindByID{*engine,"op1","op2"}
        ntui_BindByID{*engine,"op2","op3"}
        ntui_Space{0,0,#TUIF_FIXHEIGHT}
        ntui_EndGroup{}

        ntui_Label    {"Check Button",#TUIF_RIGHT}
        ntui_BeginHGroup{0}
        ntui_CheckButton  {"Check 1",1,"","this is a check button with text"}
        ntui_CheckButton  {"Check 2",0,"","this is a check button with text"}
        ntui_CheckButton  {"Check 3",1,"","this is a check button with text",#TUIF_DISABLED}
        ntui_Space{0,0,#TUIF_FIXHEIGHT}
        ntui_EndGroup{}


        CNIF @#TUI_HAS_CYCLE
        ntui_Label    {"Cycle",#TUIF_RIGHT}
        ntui_BeginHGroup{0}
        ntui_Cycle{"Wanderer|Honitos|Blackbird|CJ Stroker|BruZard|Modfr|Wawa|HelmutH|Bernd_|Theoderich",-2,"this is a cycle button",-1,"CYCL"}

        ntui_Cycle{"A|B|C",0,"this is a cycle button",#TUIF_DISABLED}
        ntui_EndGroup{}
        CEND

        ntui_Label    {"Button +Image",#TUIF_RIGHT}
        ntui_BeginHGroup{0}
        ntui_Button   {"Image","SAVE","","this is a button with in in-line image (image at text size)"}
        ntui_Button   {"Image","SAVE","","this is a button with in in-line image (image at text size)",#TUIF_DISABLED}
        ntui_Button   {"Image","TBImages:save","","this is a button with in in-line image (image at text size)"}
        ntui_Button   {"Image","TBImages:save","","this is a button with in in-line image (image at text size)",#TUIF_DISABLED}
        ntui_EndGroup{}

        ntui_Label    {"Image Button",#TUIF_RIGHT}
        ntui_BeginHGroup{0}
        ntui_ImageButton   {"","SAVE","","this is an image button (image at native size)"}
        ntui_ImageButton   {"","SAVE","","this is an image button (image at native size)",#TUIF_DISABLED}
        ntui_ImageButton   {"","TBImages:save","","this is an image button (image at native size)"}
        ntui_ImageButton   {"","TBImages:save","","this is an image button (image at native size)",#TUIF_DISABLED}
        ntui_Space{0,0,#TUIF_FIXHEIGHT}
        ntui_EndGroup{}

        ntui_Label    {"Image Button +Text",#TUIF_RIGHT}
        ntui_BeginHGroup{0}
        ntui_ImageButton   {"Image","SAVE","","this is an image button with text"}
        ntui_ImageButton   {"Image","SAVE","","this is an image button with text",#TUIF_DISABLED}
        ntui_ImageButton   {"Image","TBImages:save","","this is an image button with text"}
        ntui_ImageButton   {"Image","TBImages:save","","this is an image button with text",#TUIF_DISABLED}
        ntui_Space{0,0,#TUIF_FIXHEIGHT}
        ntui_EndGroup{}

        ntui_Label    {"Tool Button",#TUIF_RIGHT}
        ntui_BeginHGroup{0}
        ntui_ToolButton  {"","SAVE",-1,"","this is a toolbar style button"}
        ntui_ToolButton  {"","SAVE",-1 ,"","this is a toolbar style button",#TUIF_DISABLED}
        ntui_ToolButton  {"","TBImages:save",-1 ,"","this is a toolbar style button"}
        ntui_ToolButton  {"","TBImages:save",-1 ,"","this is a toolbar style button",#TUIF_DISABLED}
        ntui_Space{0,0,#TUIF_FIXHEIGHT}
        ntui_EndGroup{}

        ntui_Label    {"Tool Button + Text",#TUIF_RIGHT}
        ntui_BeginHGroup{0}
        ntui_ToolButton  {"Image","SAVE",-1 ,"","this is a toolbar button with text"}
        ntui_ToolButton  {"Image","SAVE",-1 ,"","this is a toolbar button with text",#TUIF_DISABLED}
        ntui_ToolButton  {"Image","TBImages:save",-1 ,"","this is a toolbar button with text"}
        ntui_ToolButton  {"Image","TBImages:save",-1 ,"","this is a toolbar button with text",#TUIF_DISABLED}
        ntui_Space{0,0,#TUIF_FIXHEIGHT}
        ntui_EndGroup{}

        ntui_Space{0,0} : ntui_Space{0,0}
      ntui_EndGroup{}

    ntui_EndGroup{} ; Button
    CEND

    CNIF @#TUI_HAS_SCROLLER
    ntui_BeginVGroup{0,"Scoller"}
      ntui_BeginHGroup{10}
        ntui_BeginVGroup{0}
          ntui_BeginHGroup{2,"","",#TUIF_FIXSIZE}
            ntui_Label    {"Horizontal Scroller",#TUIF_RIGHT}
            ntui_HScroller{0,100,20,"","",-1,"sc1"}
            ntui_Label    {"HScroller + Arrows",#TUIF_RIGHT}
            ntui_HScroller{0,10,9,"","",-1,"sc2"} : ntui_AddArrows{#NULL}
  ;          ntui_BindByID{*engine,"sc1","sc2"}
            ntui_Label    {"Disabled HScroller",#TUIF_RIGHT}
            ntui_HScroller{0,100,20,"","",#TUIF_DISABLED } : ntui_AddArrows{#NULL}
          ntui_EndGroup{} : ntui_SetFlags{#TUIF_FIXSIZE}
          ntui_Space{0,0}
        ntui_EndGroup{}

        CNIF @#TUI_HAS_SLIDER
        ntui_BeginHGroup{2}
          ntui_Label    {"Horizontal Slider",#TUIF_RIGHT}
          ntui_HSlider{0,0,100,""}
          ntui_Label    {"HSlider + Label",#TUIF_RIGHT}
          ntui_HSlider{0,0,100,"%d units"}
          ntui_Label    {"Disabled HSlider",#TUIF_RIGHT}
          ntui_HSlider{0,0,100,"%d units","","",#TUIF_DISABLED }

          ntui_Label    {"HSlider + Label",#TUIF_RIGHT}
          ntui_HSlider{0,0,100,"%d units","","",#TUIF_UNDERLINED}
          ntui_Label    {"HSlider + Label",#TUIF_RIGHT}
          ntui_HSlider{0,0,100,"%d units","","",#TUIF_UNDERLINED} : ntui_SetBorder{#TUIBORDER_RECESSED}
          ntui_Label    {"Disabled HSlider + Label",#TUIF_RIGHT}
          ntui_HSlider{0,0,100,"%d units","","",#TUIF_DISABLED|#TUIF_UNDERLINED }

          ntui_Label    {"HSlider + Border",#TUIF_RIGHT}
          ntui_HSlider{0,0,100,"%d units"}  : ntui_SetBorder{#TUIBORDER_RECESSED}
          ntui_Label    {"Disabled HSlider + Border",#TUIF_RIGHT}
          ntui_HSlider{0,0,100,"%d units","","",#TUIF_DISABLED}  : ntui_SetBorder{#TUIBORDER_RECESSED}
          ntui_Space{0,0,#TUIF_FIXWIDTH } : ntui_Space{0,0}
        ntui_EndGroup{}  : ntui_SetFlags{#TUIF_FIXSIZE}
        CEND
      ntui_EndGroup{}

      ntui_BeginHGroup{0}
        ntui_Label    {"Vertical Versions",#TUIF_RIGHT}
        ntui_VScroller{0,100,20}
        ntui_VScroller{0,100,90,"","this is a vertical scroller with arrows"}                                   : ntui_AddArrows{#NULL}
        ntui_VScroller{0,100,20,"","",#TUIF_DISABLED} : ntui_AddArrows{#NULL}
        CNIF @#TUI_HAS_SLIDER
        ntui_VSlider{0,0,100,""}
        ntui_VSlider{0,0,100,"%d"}
        ntui_VSlider{0,0,100,"%d","","",#TUIF_DISABLED }

        ntui_VSlider{0,0,100,"%d","","",#TUIF_UNDERLINED}
        ntui_VSlider{0,0,100,"%d","","",#TUIF_DISABLED|#TUIF_UNDERLINED }
        ntui_VSlider{0,0,100,"%d","","",#TUIF_UNDERLINED}                : ntui_SetBorder{#TUIBORDER_RECESSED}
        ntui_VSlider{0,0,100,"%d"}  : ntui_SetBorder{#TUIBORDER_RECESSED}
        ntui_VSlider{0,0,100,"%d","","",#TUIF_DISABLED}  : ntui_SetBorder{#TUIBORDER_RECESSED}
        CEND
        ntui_Space{0,0}
      ntui_EndGroup{}  : ntui_ClearFlags{#TUIF_FIXSIZE}
    ntui_EndGroup{} ; scroller
    CEND

    CNIF @#TUI_HAS_TABVIEW
    ntui_BeginHGroup{2*2,"TabView"}
      ntui_Label    {"Top Tabs",#TUIF_RIGHT}
      ntui_BeginTabView{-1,"",#TUIF_TOP}
        *obj.tuiObject = ntui_BeginVGroup{0,"First Page","TBImages:save"}
          ntui_Label    {"Content of page #1",#TUIF_CENTER}
        ntui_EndGroup{}
        ntui_BeginVGroup{0,"Second Page","TBImages:favourites"}
          ntui_Label    {"Content of page #2",#TUIF_CENTER}
        ntui_EndGroup{}
        ntui_BeginVGroup{0,"Third Page","ARROWLEFT",#TUIF_DISABLED}
          ntui_Label    {"Content of page #3",#TUIF_CENTER}
        ntui_EndGroup{}
      ntui_EndTabView{}

      ntui_Label    {"Bottom Tabs",#TUIF_RIGHT}
      ntui_BeginTabView{0,"",#TUIF_BOTTOM|#TUIF_SMALL}
        *obj.tuiObject =  ntui_BeginVGroup{0,"First Page","TBImages:save"}
          ntui_Label    {"Content of page #1",#TUIF_CENTER}
        ntui_EndGroup{}
        ntui_BeginVGroup{0,"Second Page","TBImages:favourites"}
          ntui_Label    {"Content of page #2",#TUIF_CENTER}
        ntui_EndGroup{}
        ntui_BeginVGroup{0,"Third Page","FONT",#TUIF_DISABLED}
          ntui_Label    {"Content of page #3",#TUIF_CENTER}
        ntui_EndGroup{}
      ntui_EndTabView{}

      ntui_Label    {"Left Tabs",#TUIF_RIGHT}
      ntui_BeginTabView{0,"",#TUIF_LEFT}
         *obj.tuiObject =  ntui_BeginVGroup{0,"First Page","TBImages:save"}
          ntui_Label    {"Content of page #1",#TUIF_CENTER}
        ntui_EndGroup{}
        ntui_BeginVGroup{0,"Second Page","TBImages:favourites"}
          ntui_Label    {"Content of page #2",#TUIF_CENTER}
        ntui_EndGroup{}
        ntui_BeginVGroup{0,"Third Page","FILE"}
          ntui_Label    {"Content of page #3",#TUIF_CENTER}
        ntui_EndGroup{}
      ntui_EndTabView{}

      ntui_Label    {"Right Tabs",#TUIF_RIGHT}
      ntui_BeginTabView{0,"",#TUIF_RIGHT}
        *obj.tuiObject =  ntui_BeginVGroup{0,"First Page","TBImages:save"}
          ntui_Label    {"Content of page #1",#TUIF_CENTER}
        ntui_EndGroup{}
        ntui_BeginVGroup{0,"Second Page","TBImages:favourites"}
          ntui_Label    {"Content of page #2",#TUIF_CENTER}
        ntui_EndGroup{}
        ntui_BeginVGroup{0,"Third Page","SAVE"}
          ntui_Label    {"Content of page #3",#TUIF_CENTER}
        ntui_EndGroup{}
      ntui_EndTabView{}


      ntui_Label    {"Empty TabView",#TUIF_RIGHT}
      ntui_BeginTabView{0,"",#TUIF_TOP}
      ntui_EndTabView{}

      ntui_BeginVGroup{0}
      ntui_Label    {"External Tab",#TUIF_RIGHT}
      CNIF @#TUI_HAS_CYCLE
      ntui_Cycle    {"first|second|third|none",0,"",#TUIF_FIXSIZE,"excr"}
      CEND
      ntui_Space{0,0,#TUIF_FIXWIDTH}
      ntui_EndGroup{}  : ntui_SetFlags{#TUIF_FIXSIZE}
      ntui_BeginTabView{0,"",#TUIF_CENTER,"expa"}
        *obj.tuiObject =  ntui_BeginVGroup{0,"First Page"}
          ntui_Label    {"Content of page #1",#TUIF_CENTER}
        ntui_EndGroup{}
        ntui_BeginVGroup{0,"Second Page"}
          ntui_Label    {"Content of page #2",#TUIF_CENTER}
        ntui_EndGroup{}
        ntui_BeginVGroup{0,"Third Page"}
          ntui_Label    {"Content of page #3",#TUIF_CENTER}
        ntui_EndGroup{}
      ntui_EndTabView{}
      ntui_BindByID{*engine,"excr","expa"}
      ntui_Space{0,0,#TUIF_FIXSIZE}
      ntui_Space{0,0,#TUIF_FIXSIZE}
    ntui_EndGroup{} ; Page
    CEND


    ntui_BeginHGroup{2,"Misc."}
      CNIF @#TUI_HAS_SEPERATOR
      ntui_Label    {"Horizontal Seperator",#TUIF_RIGHT}
      ntui_HSeperator{}

      ntui_Label {"Label Seperator",#TUIF_RIGHT}
      ntui_Seperator{"Seperator"}

      ntui_Label    {"Vertical Seperator",#TUIF_RIGHT}
      ntui_VSeperator{}
      CEND

      CNIF @#TUI_HAS_PROGRESSBAR
      ntui_Label    {"ProgressBar",#TUIF_RIGHT}
      ntui_BeginHGroup{0}
      ntui_ProgressBar{"%d of %t done (%p %)...",20,100}  : ntui_SetID{"SBAR"}
      ntui_ProgressBar{"%d of %t done (%p %)...",20,100,"",#TUIF_DISABLED}
      ntui_EndGroup{}
      CEND

      CNIF @#TUI_HAS_SPACE
      ntui_Label    {"Space(30x30px)",#TUIF_RIGHT}
      ntui_Space    {30,30,#TUIF_FIXSIZE}
      CEND

      CNIF @#TUI_HAS_BALANCER
      ntui_Label    {"H/V Balancer",#TUIF_RIGHT}
      ntui_BeginVGroup{-1}
        ntui_BeginHGroup{-1}
         ntui_Image{"UNKNOWN"} : ntui_ClearFlags{#TUIF_FIXSIZE}
         ntui_VBalancer{}
         ntui_Image{"UNKNOWN"} : ntui_ClearFlags{#TUIF_FIXSIZE}
        ntui_EndGroup{}  : ntui_ClearFlags{#TUIF_FIXSIZE}
        ntui_HBalancer{}
        ntui_BeginHGroup{-1}
          ntui_Image{"UNKNOWN"} : ntui_ClearFlags{#TUIF_FIXSIZE}
          ntui_VBalancer{}
          ntui_Image{"UNKNOWN"} : ntui_ClearFlags{#TUIF_FIXSIZE}
        ntui_EndGroup{}  : ntui_ClearFlags{#TUIF_FIXSIZE}
      ntui_EndGroup{}  : ntui_ClearFlags{#TUIF_FIXSIZE}
      CEND


      CNIF @#TUI_HAS_CUSTOMVIEW
      ntui_Label    {"Custom View",#TUIF_RIGHT}
      ntui_BeginVGroup{0}
        ntui_BeginHGroup{0}
          *cs.tuiCustomView = ntui_CustomView {50,50,"this is a custom view",-1,"CView"}
          a.s = "CVMOVE"
          ntui_SetAttr{*cs,#TUICVA_ONMOUSEMOVE,&a}
          a.s = "CVTOUCH"
          ntui_SetAttr{*cs,#TUIA_ONTOUCH,&a}
          a.s = "CVRELEASE"
          ntui_SetAttr{*cs,#TUIA_ONRELEASE,&a}
          ntui_SetAttr{*cs,#TUICVA_HTOTAL,1024}
          ntui_SetAttr{*cs,#TUICVA_VTOTAL,1024}
          *obj.tuiObject = ntui_VScroller{0,100,20,"","",-1,"CVScroller"} : ntui_AddArrows{*obj}
        ntui_EndGroup{}
        *obj = ntui_HScroller{0,100,20,"","",-1,"CHScroller"} : ntui_AddArrows{*obj}
      ntui_EndGroup{}
      CEND
    ntui_EndGroup{} ; misc

    CNIF @#TUI_HAS_IMAGE
    ntui_BeginHGroup{2,"Image"}
      ntui_BeginHGroup{0}
      ntui_BeginHGroup{2}

      ntui_Label    {"Name",#TUIF_LEFT|#TUIF_BOLD}
      ntui_Label    {"Nrm/Sel/Dis",#TUIF_LEFT|#TUIF_BOLD }

      name.s="" : ntui_Label{"Unknown",#TUIF_LEFT}
      ntui_BeginHGroup{0} : ntui_Image{name} : ntui_Image{name,-1,-1,"",#TUIF_ACTIVE} : ntui_Image{name,-1,-1,"",#TUIF_DISABLED} : ntui_EndGroup{}

      name.s="SAVE" : ntui_Label{name,#TUIF_LEFT}
      ntui_BeginHGroup{0} : ntui_Image{name} : ntui_Image{name,-1,-1,"",#TUIF_ACTIVE} : ntui_Image{name,-1,-1,"",#TUIF_DISABLED} : ntui_EndGroup{}

      name.s="FILE" : ntui_Label{name,#TUIF_LEFT}
      ntui_BeginHGroup{0} : ntui_Image{name} : ntui_Image{name,-1,-1,"",#TUIF_ACTIVE} : ntui_Image{name,-1,-1,"",#TUIF_DISABLED} : ntui_EndGroup{}

      name.s="PATH" : ntui_Label{name,#TUIF_LEFT}
      ntui_BeginHGroup{0} : ntui_Image{name} : ntui_Image{name,-1,-1,"",#TUIF_ACTIVE} : ntui_Image{name,-1,-1,"",#TUIF_DISABLED} : ntui_EndGroup{}

      name.s="SCREEN" : ntui_Label{name,#TUIF_LEFT}
      ntui_BeginHGroup{0} : ntui_Image{name} : ntui_Image{name,-1,-1,"",#TUIF_ACTIVE} : ntui_Image{name,-1,-1,"",#TUIF_DISABLED} : ntui_EndGroup{}

      name.s="FONT" : ntui_Label{name,#TUIF_LEFT}
      ntui_BeginHGroup{0} : ntui_Image{name} : ntui_Image{name,-1,-1,"",#TUIF_ACTIVE} : ntui_Image{name,-1,-1,"",#TUIF_DISABLED} : ntui_EndGroup{}

      name.s="CYCLE" : ntui_Label{name,#TUIF_LEFT}
      ntui_BeginHGroup{0} : ntui_Image{name} : ntui_Image{name,-1,-1,"",#TUIF_ACTIVE} : ntui_Image{name,-1,-1,"",#TUIF_DISABLED} : ntui_EndGroup{}

      name.s="INC" : ntui_Label{name,#TUIF_LEFT}
      ntui_BeginHGroup{0} : ntui_Image{name} : ntui_Image{name,-1,-1,"",#TUIF_ACTIVE} : ntui_Image{name,-1,-1,"",#TUIF_DISABLED} : ntui_EndGroup{}

      name.s="DEC" : ntui_Label{name,#TUIF_LEFT}
      ntui_BeginHGroup{0} : ntui_Image{name} : ntui_Image{name,-1,-1,"",#TUIF_ACTIVE} : ntui_Image{name,-1,-1,"",#TUIF_DISABLED} : ntui_EndGroup{}

      name.s="AMIGA" : ntui_Label{name,#TUIF_LEFT}
      ntui_BeginHGroup{0} : ntui_Image{name} : ntui_Image{name,-1,-1,"",#TUIF_ACTIVE} : ntui_Image{name,-1,-1,"",#TUIF_DISABLED} : ntui_EndGroup{}

      name.s="SHIFT" : ntui_Label{name,#TUIF_LEFT}
      ntui_BeginHGroup{0} : ntui_Image{name} : ntui_Image{name,-1,-1,"",#TUIF_ACTIVE} : ntui_Image{name,-1,-1,"",#TUIF_DISABLED} : ntui_EndGroup{}

      name.s="CTRL" : ntui_Label{name,#TUIF_LEFT}
      ntui_BeginHGroup{0} : ntui_Image{name} : ntui_Image{name,-1,-1,"",#TUIF_ACTIVE} : ntui_Image{name,-1,-1,"",#TUIF_DISABLED} : ntui_EndGroup{}

      name.s="ALT" : ntui_Label{name,#TUIF_LEFT}
      ntui_BeginHGroup{0} : ntui_Image{name} : ntui_Image{name,-1,-1,"",#TUIF_ACTIVE} : ntui_Image{name,-1,-1,"",#TUIF_DISABLED} : ntui_EndGroup{}

      ntui_Space{0,0,#TUIF_FIXWIDTH } : ntui_Space{0,0,#TUIF_FIXWIDTH }
      ntui_EndGroup{}

      ntui_VSeperator{};Balancer{}

      ntui_BeginHGroup{2}
      ntui_Label    {"Name",#TUIF_LEFT|#TUIF_BOLD}
      ntui_Label    {"Nrm/Sel/Dis",#TUIF_LEFT|#TUIF_BOLD }

      name.s="ARROWLEFT" : ntui_Label{name,#TUIF_LEFT}
      ntui_BeginHGroup{0} : ntui_Image{name} : ntui_Image{name,-1,-1,"",#TUIF_ACTIVE} : ntui_Image{name,-1,-1,"",#TUIF_DISABLED} : ntui_EndGroup{}

      name.s="ARROWRIGHT" : ntui_Label{name,#TUIF_LEFT}
      ntui_BeginHGroup{0} : ntui_Image{name} : ntui_Image{name,-1,-1,"",#TUIF_ACTIVE} : ntui_Image{name,-1,-1,"",#TUIF_DISABLED} : ntui_EndGroup{}

      name.s="ARROWUP" : ntui_Label{name,#TUIF_LEFT}
      ntui_BeginHGroup{0} : ntui_Image{name} : ntui_Image{name,-1,-1,"",#TUIF_ACTIVE} : ntui_Image{name,-1,-1,"",#TUIF_DISABLED} : ntui_EndGroup{}

      name.s="ARROWDOWN" : ntui_Label{name,#TUIF_LEFT}
      ntui_BeginHGroup{0} : ntui_Image{name} : ntui_Image{name,-1,-1,"",#TUIF_ACTIVE} : ntui_Image{name,-1,-1,"",#TUIF_DISABLED} : ntui_EndGroup{}

      name.s="OLARROWLEFT" : ntui_Label{name,#TUIF_LEFT}
      ntui_BeginHGroup{0} : ntui_Image{name} : ntui_Image{name,-1,-1,"",#TUIF_ACTIVE} : ntui_Image{name,-1,-1,"",#TUIF_DISABLED} : ntui_EndGroup{}

      name.s="OLARROWRIGHT" : ntui_Label{name,#TUIF_LEFT}
      ntui_BeginHGroup{0} : ntui_Image{name} : ntui_Image{name,-1,-1,"",#TUIF_ACTIVE} : ntui_Image{name,-1,-1,"",#TUIF_DISABLED} : ntui_EndGroup{}

      name.s="OLARROWUP" : ntui_Label{name,#TUIF_LEFT}
      ntui_BeginHGroup{0} : ntui_Image{name} : ntui_Image{name,-1,-1,"",#TUIF_ACTIVE} : ntui_Image{name,-1,-1,"",#TUIF_DISABLED} : ntui_EndGroup{}

      name.s="OLARROWDOWN" : ntui_Label{name,#TUIF_LEFT}
      ntui_BeginHGroup{0} : ntui_Image{name} : ntui_Image{name,-1,-1,"",#TUIF_ACTIVE} : ntui_Image{name,-1,-1,"",#TUIF_DISABLED} : ntui_EndGroup{}

      name.s="LEFT" : ntui_Label{name,#TUIF_LEFT}
      ntui_BeginHGroup{0} : ntui_Image{name} : ntui_Image{name,-1,-1,"",#TUIF_ACTIVE} : ntui_Image{name,-1,-1,"",#TUIF_DISABLED} : ntui_EndGroup{}

      name.s="RIGHT" : ntui_Label{name,#TUIF_LEFT}
      ntui_BeginHGroup{0} : ntui_Image{name} : ntui_Image{name,-1,-1,"",#TUIF_ACTIVE} : ntui_Image{name,-1,-1,"",#TUIF_DISABLED} : ntui_EndGroup{}

      name.s="POPUP" : ntui_Label{name,#TUIF_LEFT}
      ntui_BeginHGroup{0} : ntui_Image{name} : ntui_Image{name,-1,-1,"",#TUIF_ACTIVE} : ntui_Image{name,-1,-1,"",#TUIF_DISABLED} : ntui_EndGroup{}

      name.s="DROPDOWN" : ntui_Label{name,#TUIF_LEFT}
      ntui_BeginHGroup{0} : ntui_Image{name} : ntui_Image{name,-1,-1,"",#TUIF_ACTIVE} : ntui_Image{name,-1,-1,"",#TUIF_DISABLED} : ntui_EndGroup{}

      ntui_Space{0,0,#TUIF_FIXWIDTH} : ntui_Space{0,0,#TUIF_FIXWIDTH }
      ntui_EndGroup{}

      ntui_VSeperator{};Balancer{}

      ntui_BeginHGroup{2}
      ntui_Label    {"Name",#TUIF_LEFT|#TUIF_BOLD}
      ntui_Label    {"Nrm/Sel/Dis",#TUIF_LEFT|#TUIF_BOLD }

      name.s="TBImages:save" : ntui_Label{name,#TUIF_LEFT}
      ntui_BeginHGroup{0} : ntui_Image{name} : ntui_Image{name,-1,-1,"",#TUIF_ACTIVE} : ntui_Image{name,-1,-1,"",#TUIF_DISABLED} : ntui_EndGroup{}

      name.s="Sys:disk.info" : ntui_Label{name,#TUIF_LEFT}
      ntui_BeginHGroup{0} : ntui_Image{name,#TUISIZE_BUTTON ,#TUISIZE_BUTTON }
      ntui_Image{name,#TUISIZE_BUTTON,#TUISIZE_BUTTON  ,"",#TUIF_ACTIVE}
      ntui_Image{name,#TUISIZE_BUTTON,#TUISIZE_BUTTON  ,"",#TUIF_DISABLED} : ntui_EndGroup{}

      name.s="CHECKMARK" : ntui_Label{name,#TUIF_LEFT}
      ntui_BeginHGroup{0} : ntui_Image{name} : ntui_Image{name,-1,-1,"",#TUIF_ACTIVE} : ntui_Image{name,-1,-1,"",#TUIF_DISABLED} : ntui_EndGroup{}

      name.s="UNCHECKMARK" : ntui_Label{name,#TUIF_LEFT}
      ntui_BeginHGroup{0} : ntui_Image{name} : ntui_Image{name,-1,-1,"",#TUIF_ACTIVE} : ntui_Image{name,-1,-1,"",#TUIF_DISABLED} : ntui_EndGroup{}

      name.s="CHECKON" : ntui_Label{name,#TUIF_LEFT}
      ntui_BeginHGroup{0} : ntui_Image{name} : ntui_Image{name,-1,-1,"",#TUIF_ACTIVE} : ntui_Image{name,-1,-1,"",#TUIF_DISABLED} : ntui_EndGroup{}

      name.s="CHECKOFF" : ntui_Label{name,#TUIF_LEFT}
      ntui_BeginHGroup{0} : ntui_Image{name} : ntui_Image{name,-1,-1,"",#TUIF_ACTIVE} : ntui_Image{name,-1,-1,"",#TUIF_DISABLED} : ntui_EndGroup{}

      name.s="RADIOON" : ntui_Label{name,#TUIF_LEFT}
      ntui_BeginHGroup{0} : ntui_Image{name} : ntui_Image{name,-1,-1,"",#TUIF_ACTIVE} : ntui_Image{name,-1,-1,"",#TUIF_DISABLED} : ntui_EndGroup{}

      name.s="RADIOOFF" : ntui_Label{name,#TUIF_LEFT}
      ntui_BeginHGroup{0} : ntui_Image{name} : ntui_Image{name,-1,-1,"",#TUIF_ACTIVE} : ntui_Image{name,-1,-1,"",#TUIF_DISABLED} : ntui_EndGroup{}

      ntui_Space{0,0,#TUIF_FIXWIDTH} : ntui_Space{0,0,#TUIF_FIXWIDTH }

    ntui_EndGroup{}

    ntui_EndGroup{}
    ntui_Space{0,0}
    ntui_EndGroup{} ; image
    CEND

    ntui_BeginHGroup{2,"Group"}
      ntui_BeginHGroup{2}
        ntui_BeginHGroup{-1,"Title Text","TBImages:favourites"} : ntui_Label{"Titled Group"} : ntui_EndGroup{}                       : ntui_ClearFlags{#TUIF_FIXSIZE}
        ntui_BeginVGroup{-1,"Title Text","TBImages:favourites",#TUIF_DISABLED} : ntui_Label{"Disabled Group"} : ntui_EndGroup{}      : ntui_ClearFlags{#TUIF_FIXSIZE}
        ntui_BeginHGroup{-1,"x"} : ntui_SetBorder{#TUIBORDER_GROUP}    : ntui_Label{"Group with Group Border"} : ntui_EndGroup{}     : ntui_ClearFlags{#TUIF_FIXSIZE}
        ntui_BeginHGroup{-1,"x"} : ntui_SetBorder{#TUIBORDER_RAISED}   : ntui_Label{"Group with Raised Border"} : ntui_EndGroup{}    : ntui_ClearFlags{#TUIF_FIXSIZE}
        ntui_BeginHGroup{-1,"x"} : ntui_SetBorder{#TUIBORDER_RECESSED} : ntui_Label{"Group with Recessed Border"} : ntui_EndGroup{}  : ntui_ClearFlags{#TUIF_FIXSIZE}
       ; ntui_BeginHGroup{-1,"x"} : ntui_SetBorder{#TUIBORDER_FLAT}     : ntui_Label{"Group with Flat Border"} : ntui_EndGroup{}

          CNIF @#TUI_HAS_SCROLLVIEW
          ntui_BeginScrollView{50,50}
            ntui_BeginVGroup{0}
              ntui_BeginHGroup{0} : ntui_SetFlags{#TUIF_SAMESIZE}
                ntui_ToolButton  {"Open","TBImages:open",-1 ,"","this is a toolbar button with text"}
                ntui_ToolButton  {"Save","TBImages:save",-1 ,"","this is a toolbar button with text"}
                ntui_ToolButton  {"SaveAll","TBImages:saveall",-1 ,"","this is a toolbar button with text"}
                ntui_ToolButton  {"Mail","TBImages:mail",-1 ,"","this is a toolbar button with text"}
                ntui_ToolButton  {"Help","TBImages:help",-1 ,"","this is a toolbar button with text"}
                ntui_VSeperator{}
                ntui_ToolButton  {"Cut","TBImages:cut",-1 ,"","this is a toolbar button with text"}
                ntui_ToolButton  {"Copy","TBImages:copy",-1 ,"","this is a toolbar button with text"}
                ntui_ToolButton  {"Paste","TBImages:paste",-1 ,"","this is a toolbar button with text"}
                ntui_ToolButton  {"Edit","TBImages:edit",-1 ,"","this is a toolbar button with text"}
                ntui_ToolButton  {"Delete","TBImages:delete",-1 ,"","this is a toolbar button with text"}
                ntui_VSeperator{}
                ntui_ToolButton  {"Undo","TBImages:undo",-1 ,"","this is a toolbar button with text"}
              ntui_EndGroup{}

      ntui_BeginTabView{-1,"",#TUIF_TOP}
        *obj.tuiObject = ntui_BeginVGroup{0,"First Page","TBImages:save"}
          ntui_Label    {"Content of page #1",#TUIF_CENTER}
        ntui_EndGroup{}
        ntui_BeginVGroup{0,"Second Page","TBImages:favourites"}
          ntui_Label    {"Content of page #2",#TUIF_CENTER}
        ntui_EndGroup{}
        ntui_BeginVGroup{0,"Third Page","ARROWLEFT",#TUIF_DISABLED}
          ntui_Label    {"Content of page #3",#TUIF_CENTER}
        ntui_EndGroup{}
      ntui_EndTabView{}


              ntui_RadioButton  {"Option 1",0,"","this is a radio button with text",-1,"op21"}
              ntui_RadioButton  {"Option 2",0,"","this is a radio button with text",-1,"op22"}
              ntui_RadioButton  {"Option 3",0,"","this is a radio button with text",-1,"op23"}
              ntui_BindByID{*engine,"op21","op22"}
              ntui_BindByID{*engine,"op22","op23"}
              ntui_HSlider{0,0,100}
              ntui_HScroller{0,100,10}
              ntui_CheckButton{"Check 1",False}
              ntui_CheckButton{"Check 2",False}
              ntui_CheckButton{"Check 3",False}
              ntui_CheckButton{"Check 4",False}
              ntui_CheckButton{"Check 5",False}
              ntui_CheckButton{"Check 6",False}
            ntui_EndGroup{}
          ntui_EndScrollView{} :ntui_ClearFlags{#TUIF_FIXSIZE}
          CEND


      ntui_EndGroup{}
    ntui_EndGroup{} ; Group

   CNIF @#TUI_HAS_TEXTBOX
   ntui_BeginHGroup{2,"Text"}
     ntui_Label    {"String",#TUIF_RIGHT}
     ntui_BeginHGroup{-1} : ntui_String   {"String",20,"",-1,"mystring"} :  ntui_String   {"Disabled",20,"",#TUIF_DISABLED} : ntui_EndGroup{}
     key.s="KEY"
     ntui_SetAttrByID{*engine,"mystring",#TUIA_ONLOSEFOCUS,&key}

     ntui_Label    {"EditLabel",#TUIF_RIGHT}
     ntui_BeginHGroup{-1} :ntui_EditLabel  {"Edit me!",20,#TUIPEN_BACKGROUND}:ntui_EditLabel  {"Edit me!",20,#TUIPEN_BACKGROUND,"",#TUIF_DISABLED}: ntui_EndGroup{}

     ntui_Label    {"NumString",#TUIF_RIGHT}
     ntui_BeginHGroup{-1} : ntui_NumString   {1234,0,10000}:  ntui_NumString   {1234,0,10000,"",#TUIF_DISABLED} : ntui_EndGroup{}

     ntui_Label    {"NumEditLabel",#TUIF_RIGHT}
     ntui_BeginHGroup{-1} : ntui_NumEditLabel   {1234,0,10000,#TUIPEN_SHINE} : ntui_NumEditLabel   {1234,0,10000,#TUIPEN_SHINE,"",#TUIF_DISABLED} : ntui_EndGroup{}

;     ntui_Label    {"TextBox",#TUIF_RIGHT}
;     *obj.tuiObject = ntui_TextBox   {"The quick bown fox jumps\\nover the lazy dog.\\nThis is a test of the\\ntuiTextBox widget.\\nLet's see how many lines we can have.\\n########\\nbla\\nbla\\nThis is the last line.\\n",100,"",#TUIF_BOLD}
;     ntui_ClearFlags{#TUIF_FIXSIZE}

     ntui_Label    {"TextBox with Tab",#TUIF_RIGHT}

     *obj = ntui_BeginHGroup{0}
     *obj = ntui_BeginTabView{0}
     ntui_SetAttr{*obj,#TUIA_PADDING,0}

     *obj = ntui_BeginHGroup{2,"document.txt","FILE"}
     ntui_SetAttr{*obj,#TUIA_MARGIN,0}
     ntui_SetAttr{*obj,#TUIA_PADDING,0}
     *obj = ntui_TextBox   {"The quick bown fox jumps\\nover the lazy dog.\\nThis is a test of the\\ntuiTextBox widget.\\nLet's see how many lines we can have.\\n########\\nbla\\nbla\\nThis is the last line.\\n",100,""}
     ntui_SetAttr{*obj,#TUIA_BORDERTYPE,#TUIBORDER_NONE}
     ntui_SetAttr{*obj,#TUIA_MARGIN,0}
     ntui_EndGroup{}

     *obj = ntui_BeginHGroup{2,"alice.txt","FILE"}
     ntui_SetAttr{*obj,#TUIA_MARGIN,0}
     ntui_SetAttr{*obj,#TUIA_PADDING,0}
     *obj = ntui_TextBox   {Peek.s(?alice),100}
     ntui_SetAttr{*obj,#TUIA_BORDERTYPE,#TUIBORDER_NONE}
     ntui_SetAttr{*obj,#TUIA_MARGIN,0}
     ntui_EndGroup{}

     ntui_EndTabView{}
     ntui_VBalancer{}
     ntui_BeginScrollView{50,50}
       *obj = ntui_BeginVGroup{0}

       ntui_Seperator{"Font"}
       ntui_RadioButton{"Normal",True,"","",#TUI_NOFLAGS,"font_normal"}
       ntui_RadioButton{"Small",0,"","",#TUI_NOFLAGS,"font_small"}
       ntui_RadioButton{"Fix",0,"","",#TUI_NOFLAGS,"font_fix"}
       ntui_RadioButton{"Serif",0,"","",#TUI_NOFLAGS,"font_serif"}
       ntui_BindByID{*engine,"font_normal","font_small"}
       ntui_BindByID{*engine,"font_normal","font_fix"}
       ntui_BindByID{*engine,"font_normal","font_serif"}

       ntui_Seperator{"Style"}
       ntui_CheckButton{"Bold",0}
       ntui_CheckButton{"Italic",0}

       ntui_Seperator{"Editor"}
       ntui_CheckButton{"Line Cursor",0}
       ntui_CheckButton{"Blink",0}
       ntui_CheckButton{"Show Return Code",0}
       ntui_CheckButton{"Read Only",0}

       ntui_Seperator{"Scrollers"}
       ntui_Cycle{"Vertical Automatic|Vertical Always|Vertical Never",0}
       ntui_Cycle{"Horizontal Automatic|Horizontal Always|Horizontal Never",0}

       ntui_EndGroup{}
     ntui_EndScrollView{}
     ntui_EndGroup{}
   ntui_EndGroup{} ; Text
   CEND
   CNIF @#TUI_HAS_TREEVIEW
    ntui_BeginHGroup{2,"TreeView"}
      ntui_Label    {"Path TreeView",#TUIF_RIGHT}
      ntui_BeginHGroup{0}
      ntui_DirView{100,100,"#?"}
      ntui_VBalancer{}
      ntui_DirView{100,100,"#?"}
      ntui_EndGroup{}
    ntui_EndGroup{}
    CEND

   CNIF @#TUI_HAS_LISTVIEW
    ntui_BeginVGroup{0,"ListView"}
      ntui_BeginHGroup{2}
      ntui_Label    {"Simple/Titled ListView",#TUIF_RIGHT}
      ntui_BeginHGroup{0}

        *listView.tuiListView=ntui_ListView {"",False,"","This is a listview",#TUIF_LEFT}
        ntui_AddListItem{*listView,-1,"Wanderer",0,1234,0}
        ntui_AddListItem{*listView,-1,"Honitos",0,1234,0}
        ntui_AddListItem{*listView,-1,"TomSmart1",0,1234,0}
        ntui_AddListItem{*listView,-1,"Wawa",0,1234,0}
        ntui_AddListItem{*listView,-1,"BruZard",0,1234,0}
        ntui_AddListItem{*listView,-1,"Blackbird",0,1234,0}
        ntui_AddListItem{*listView,-1,"Murmel",0,1234,0}
        ntui_AddListItem{*listView,-1,"HelmutH",0,1234,0}
        ntui_AddListItem{*listView,-1,"Bernd_",0,1234,0}
        ntui_AddListItem{*listView,-1,"Modfr",0,1234,0}
        ntui_AddListItem{*listView,-1,"DieterG",0,1234,0}
        ntui_AddListItem{*listView,-1,"DaxB",0,1234,0}
        ntui_AddListItem{*listView,-1,"Deadalus",0,1234,0}
        ntui_AddListItem{*listView,-1,"CJ-Stroker",0,1234,0}
        ntui_VBalancer{}
        *listView.tuiListView=ntui_ListView {"\pTBImages:favourites\tAmiforce",False,"","This is a listview",#TUIF_LEFT}
        ntui_AddListItem{*listView,-1,"Wanderer",0,1234,0}
        ntui_AddListItem{*listView,-1,"Honitos",0,1234,0}
        ntui_AddListItem{*listView,-1,"TomSmart1",0,1234,0}
        ntui_AddListItem{*listView,-1,"Wawa",0,1234,0}
        ntui_AddListItem{*listView,-1,"BruZard",0,1234,0}
        ntui_AddListItem{*listView,-1,"Blackbird",0,1234,0}
        ntui_AddListItem{*listView,-1,"Murmel",0,1234,0}
        ntui_AddListItem{*listView,-1,"HelmutH",0,1234,0}
        ntui_AddListItem{*listView,-1,"Bernd_",0,1234,0}
        ntui_AddListItem{*listView,-1,"Modfr",0,1234,0}
        ntui_AddListItem{*listView,-1,"DieterG",0,1234,0}
        ntui_AddListItem{*listView,-1,"DaxB",0,1234,0}
        ntui_AddListItem{*listView,-1,"Deadalus",0,1234,0}
        ntui_AddListItem{*listView,-1,"CJ-Stroker",0,1234,0}
      ntui_EndGroup{}
      ntui_EndGroup{}

      ntui_HBalancer{}

      ntui_BeginHGroup{2}
      ntui_Label    {"Multi-Column ListView",#TUIF_RIGHT}
      *listView.tuiListView=ntui_ListView {"\pTBImages:question\~|\lAddress\pTBImages:link|\lSubject\pTBImages:help|\lDate\pTBImages:date",True}
      ntui_AddListItem{*listView,-1,"\pTBImages:mail|koehlerthilo@gmx.de|Test Email|11/12/2008",0,1234,#TUIF_BOLD}
      ntui_AddListItem{*listView,-1,"\pTBImages:mail|koehlerthilo@gmx.de|Another Test|13/12/2008",0,1234,#TUIF_BOLD }
      ntui_AddListItem{*listView,-1,"\pTBImages:mail|support@hd-rec.de|RE: Another Test|14/12/2008",0,1234,#TUIF_BOLD }
      ntui_AddListItem{*listView,-1,"\pTBImages:mail|rumpel-stielzchen@web.de|Test Email|11/12/2008",0,1234,0}
      ntui_AddListItem{*listView,-1,"\pTBImages:mailspam|spamname@gmx.de|Another Test|13/12/2008",0,1234,0}
      ntui_AddListItem{*listView,-1,"\pTBImages:mailspam|koehlerthilo@gmx.de|RE: Another Test|14/12/2008",0,1234,0}
      ntui_AddListItem{*listView,-1,"\pTBImages:mail|koehlerthilo@gmx.de|\dTest Email|11/12/2008",0,1234,0}
      ntui_AddListItem{*listView,-1,"\pTBImages:mail|koehlerthilo@gmx.de|Another Test|13/12/2008",0,1234,#TUIF_DISABLED}
      ntui_AddListItem{*listView,-1,"\pTBImages:mail|koehlerthilo@gmx.de|RE: Another Test|14/12/2008",0,1234,0}
      ntui_EndGroup{}
    ntui_EndGroup{} ; listview
  CEND

  CNIF @#TUI_HAS_MENUITEM
    ntui_BeginHGroup{0,"Menu"}
      ntui_BeginHGroup{0}
      ntui_BeginHGroup{2}
        ntui_Label    {"MenuBar",#TUIF_RIGHT}
        ntui_BeginMenuBar{}
          ntui_BeginMenuTitle{"MenuTitle 1","TBImages:info"}
            ntui_MenuItem{"MenuItem 1","","SHIFT+a"}
            ntui_MenuItem{"MenuItem 2","","SHIFT+b"}
            ntui_MenuItem{"MenuItem 3","","CTRL+c","MenuHit3","This is a menu entry"}
            ntui_Label{"test"}
            ntui_MenuItem{"MenuItem 4","","ALT+d"}
            ntui_MenuItem{"MenuItem 5","","F1",""}
            ntui_BeginSubMenu{"SubMenu","TBImages:junk"}
              ntui_Button{"test","SAVE"}
              ntui_MenuItem{"SubMenuItem 2","","SHIFT+b"}
              ntui_MenuItem{"SubMenuItem 3","","CTRL+c"}
              ntui_MenuItem{"SubMenuItem 4","","ALT+d"}

              ntui_BeginSubMenu{"SubMenu #2","TBImages:mail","",-1,"stop"}
                ;ntui_Button{"file","FILE"}
                ntui_MenuItem{"SubMenuItem 2","","SHIFT+b"}
                ntui_MenuItem{"SubMenuItem 3","","CTRL+c"}
                ntui_MenuItem{"SubMenuItem 4","","ALT+d"}
                ;ntui_HSeperator{}
                ntui_MenuItem{"SubMenuItem 5","","F1",""}
              ntui_EndMenu{}

              ntui_BeginSubMenu{"SubMenu #3","TBImages:date","",-1,"stop"}
                ntui_Button{"file","FILE"}
                ntui_MenuItem{"SubMenuItem 2","","SHIFT+b"}
                ntui_MenuItem{"SubMenuItem 3","","CTRL+c"}
                ntui_MenuItem{"SubMenuItem 4","","ALT+d"}
                ntui_HSeperator{}
                ntui_MenuItem{"SubMenuItem 5","","F1",""}
              ntui_EndMenu{}

              ntui_HSeperator{}
              ntui_MenuItem{"SubMenuItem 5","","F1","TEST"}

            ntui_EndMenu{}
            ntui_HSeperator{}
            ntui_MenuItem{"MenuItem 3","","SHIFT+x","","",#TUIF_DISABLED}

              ntui_BeginSubMenu{"SubMenu #6","TBImages:help","",-1,"stop"}
                ;ntui_Button{"file","FILE"}
                ntui_MenuItem{"SubMenuItem 2","","SHIFT+b"}
                ntui_MenuItem{"SubMenuItem 3","","CTRL+c"}
                ntui_MenuItem{"SubMenuItem 4","","ALT+d"}
                ;ntui_HSeperator{}
                ntui_MenuItem{"SubMenuItem 5","","F1",""}
              ntui_EndMenu{}
          ntui_EndMenu{}

          ntui_BeginMenuTitle{"MenuTitle 2","TBImages:help"}
            ntui_MenuItem{"MenuItem 1","","SHIFT+a"}
            ntui_MenuItem{"MenuItem 2","","SHIFT+b"}
            ntui_MenuItem{"MenuItem 3","","CTRL+c"}
                ntui_Cycle{"test 1|test 2",0}
            ntui_MenuItem{"MenuItem 4","","ALT+d"}
            ntui_MenuItem{"MenuItem 5","","F1"}
            ntui_BeginSubMenu{"SubMenu","TBImages:open"}
              ;ntui_HSeperator{}

              ;ntui_MenuItem{"SubMenuItem 1","","AMIGA+a","",-1,"stop"}
                      ;    ntui_Label{"test"}
                                   ntui_Button{"test","SAVE","TEST"}
              ntui_MenuItem{"SubMenuItem 2","","SHIFT+b"}
              ;ntui_MenuItem{"SubMenuItem 3","","CTRL+c"}
              ntui_MenuItem{"SubMenuItem 4","","ALT+d"}
              ntui_HSeperator{}
              ntui_MenuItem{"SubMenuItem 5","","F1","TEST"}
            ntui_EndMenu{}
            ntui_HSeperator{}
            ntui_MenuItem{"MenuItem 3","","SHIFT+x","","",#TUIF_DISABLED}

              ntui_BeginSubMenu{"SubMenu #6","TBImages:saveall","",-1,"stop"}
                ;ntui_Button{"file","FILE"}
                ntui_MenuItem{"SubMenuItem 2","","SHIFT+b"}
                ntui_MenuItem{"SubMenuItem 3","","CTRL+c"}
                ntui_MenuItem{"SubMenuItem 4","","ALT+d"}
                ;ntui_HSeperator{}
                ntui_MenuItem{"SubMenuItem 5","","F1",""}
              ntui_EndMenu{}


          ntui_EndMenu{}



          ntui_BeginMenuTitle{"MenuTitle 2","","",#TUIF_DISABLED }
            ntui_MenuItem{"MenuItem 1"}
            ntui_BeginSubMenu{"SubMenu"}
              ntui_MenuItem{"MenuItem"}
            ntui_EndMenu{}
            ntui_HSeperator{}
            ntui_MenuItem{"MenuItem 2"}
          ntui_EndMenu{}
        ntui_EndGroup{}

        ntui_Label    {"PopUp Menu",#TUIF_RIGHT}
        ntui_BeginHGroup{0}
          ntui_BeginPopUp{"PopUp"}
            ntui_MenuItem{"MenuItem 1"}
              ntui_BeginSubMenu{"SubMenu"}
                ntui_MenuItem{"MenuItem"}
              ntui_EndMenu{}
              ntui_HSeperator{}
              ntui_MenuItem{"MenuItem 2"}
          ntui_EndMenu{}
          ntui_BeginPopUp{"PopUp","","",#TUIF_DISABLED}
          ntui_EndMenu{}
        ntui_EndGroup{}

        ntui_Label    {"Single PopUp Button",#TUIF_RIGHT}
        ntui_BeginHGroup{0}
          ntui_BeginPopUp{"",""}
            ntui_MenuItem{"MenuItem 1"}
              ntui_BeginSubMenu{"SubMenu",""}
                ntui_MenuItem{"MenuItem"}
              ntui_EndMenu{}
              ntui_HSeperator{}
              ntui_MenuItem{"MenuItem 2"}
          ntui_EndMenu{}
          ntui_BeginPopUp{"","","",#TUIF_DISABLED}
          ntui_EndMenu{}
          ntui_Space{0,0,#TUIF_FIXHEIGHT}
        ntui_EndGroup{}

        ntui_Label    {"DropDown Menu",#TUIF_RIGHT}
        ntui_BeginHGroup{0}
          ntui_BeginDropDown{"DropDown"}
            ntui_MenuItem{"MenuItem 1"}
              ntui_BeginSubMenu{"SubMenu",""}
                ntui_MenuItem{"MenuItem"}
              ntui_EndMenu{}
              ntui_HSeperator{}
              ntui_MenuItem{"MenuItem 2"}
          ntui_EndMenu{}
          ntui_BeginDropDown{"DropDown","","",#TUIF_DISABLED}
          ntui_EndMenu{}
        ntui_EndGroup{}

        ntui_Label    {"DropDown Menu +Images",#TUIF_RIGHT}
        ntui_BeginHGroup{0}
          ntui_BeginDropDown{"DropDown","TBImages:save"}
            ntui_MenuItem{"MenuItem 1","TBImages:save","AMIGA+s"}
              ntui_BeginSubMenu{"SubMenu",""}
                ntui_MenuItem{"MenuItem","","AMIGA+x"}
              ntui_EndMenu{}
              ntui_HSeperator{}
              ntui_MenuItem{"MenuItem 2"}
          ntui_EndMenu{}
          ntui_BeginDropDown{"DropDown","TBImages:save","",#TUIF_DISABLED}
          ntui_EndMenu{}
        ntui_EndGroup{}
        ntui_Space{0,0,#TUIF_FIXWIDTH} : ntui_Space{0,0,#TUIF_FIXWIDTH}
      ntui_EndGroup{}

        ntui_Space{0,0}
      ntui_EndGroup{}

    ntui_EndGroup{} ; menu


  CEND


CNIF @#TUI_HAS_TABVIEW
  ntui_EndTabView{}
CEND

  *obj.tuiObject = ntui_EndWindow{}
  ;ntui_SetAttr{*obj,#TUIA_CLEARFLAGS,#TUIF_FIXSIZE}
  ;ntui_SetAttr{*obj,#TUIWA_APPNOTIFY,#NOTIFY_DROP}

End If
ntui_EndBuild{}
; ntui_SetAttrByID{*engine,"PAGE",#TUIA_BGPEN,#TUIPEN_HALFSHINE}
;ntui_SetAttr{*objWin,#TUIA_BGPEN,#TUIPEN_HALFSHADOW}


ntui_BindByID{*engine,"CView","CHScroller"}
ntui_BindByID{*engine,"CView","CVScroller"}


ntui_ShowWindowByID{*engine,"DEMO"}
ntui_ShowWindowByID{*engine,"xmlwin"}
ntui_ShowWindowByID{*engine,"xuiwin"}
ntui_ShowAppIcon{*engine,"NTUI Template"}

*listView = ntui_GetObjectByID{*engine,"xuilist"}
If *listView
ntui_AddListItem{*listView,-1,"|Text Color|#447799",0,1234,0}
ntui_AddListItem{*listView,-1,"|Cursor Color|#EEEEEE",0,1234,0}
ntui_AddListItem{*listView,-1,"|Tab Color|#444444",0,1234,0}
ntui_AddListItem{*listView,-1,"|Pattern Color|#447755",0,1234,0}
ntui_AddListItem{*listView,-1,"|Backfill Color|#88CC99",0,1234,0}
ntui_AddListItem{*listView,-1,"|Blabla Color|#000000",0,1234,0}
ntui_AddListItem{*listView,-1,"|Bubble Color|#AAAAAA",0,1234,0}
ntui_AddListItem{*listView,-1,"|Input Text Color|#AAAAAA",0,1234,0}
ntui_AddListItem{*listView,-1,"|Output Color|#BBAA9A",0,1234,0}
ntui_AddListItem{*listView,-1,"\pRIGHT|Images|",0,1234,0}
ntui_AddListItem{*listView,-1,"|Use Images|ON",0,1234,0}
ntui_AddListItem{*listView,-1,"|Load Images|Yes",0,1234,0}
ntui_AddListItem{*listView,-1,"|Image Path|Sys:Prefs/Images",0,1234,0}
ntui_AddListItem{*listView,-1,"|Window Images|window.jpg",0,1234,0}
ntui_AddListItem{*listView,-1,"|My Images|Blabla",0,1234,0}
ntui_AddListItem{*listView,-1,"|Blublub|Haha",0,1234,0}
End If

*Address.tuiObject = ntui_GetObjectByID {*engine,"mystring"}
;error{"Address Object: "+Hex$(*Address)}
string.s = ntui_GetStringByID{*Address,"mystring"}
;error{"http = "+string}


quitme.l = False
While quitme=False
  Wait_ $FFFFFFFF
  Repeat
    *ev.tuiEvent = ntui_GetEvent{*engine}
    Select ntui_GetEventNotify{*ev}
      Case "CVTOUCH"
        *obj.tuiObject = ntui_GetEventObject{*ev}
        ntui_SetFocus{*obj}
        ntui_SetAttr{*obj,#TUIA_ACTIVE,True}
      Case "CVRELEASE"
        *obj.tuiObject = ntui_GetEventObject{*ev}
        ntui_SetAttr{*obj,#TUIA_ACTIVE,False}
      Case "CVMOVE"
        *obj.tuiObject = ntui_GetEventObject{*ev}
        *rp.RastPort = ntui_ObtainRastPort{*obj}
        If *rp
          ox.l = ntui_GetAttr{*obj,#TUIA_XPOS,0}
          oy.l = ntui_GetAttr{*obj,#TUIA_YPOS,0}
          x.l = ntui_GetEventAttr{*ev,#TUIEVA_CXPOS,0}
          y.l = ntui_GetEventAttr{*ev,#TUIEVA_CYPOS,0}
          xof.l = ntui_GetAttr{*obj,#TUICVA_HOFFSET,0}
          yof.l = ntui_GetAttr{*obj,#TUICVA_VOFFSET,0}

          a.s = Str$(x+xof)+"/"+Str$(y+yof)+"    "
          SetAPen_ *rp,1
          SetBPen_ *rp,0
          Move_ *rp,ox,oy+ 20
          Text_ *rp,&a,FLen(a)
          ntui_ReleaseRastPort{*obj}
        End If

      Case "MenuHit3"   :
        *obj = ntui_GetEventObject{*ev}
        *win.tuiWindow = ntui_GetObjectRootWindow{*obj}
        instanceID.l = ntui_GetAttr{*win,#TUIWA_INSTANCEID,0}
        error{"Menugit with: "+Str$(*obj)+" win:"+Str$(*win)+" ID:"+Str$(instanceID)}
      Case "TEST"       : message{"User hit the button!"} : s.s = "TEST TITLE!" : ntui_SetAttr{*obj,#TUIWA_TITLE,&s}
        *obj = ntui_GetEventObject{*ev}
        *win.tuiWindow = ntui_GetObjectRootWindow{*obj}
        instanceID.l = ntui_GetAttr{*win,#TUIWA_INSTANCEID,0}
        error{"Menugit with: "+Str$(*obj)+" win:"+Str$(*win)+" ID:"+Str$(instanceID)}
                                                                                                                                                                                                                                                  
      Case "QUIT"       : quitme=True
      Case "CLOSE"      : quitme=True
      Case "POPUP"      : ntui_PopUp{*engine} ; message{"App Pop up!"}
      Case "ICONIFY"    : ntui_Iconify{*engine} ;message{"App iconify!"}
      Case "WINDOWDROP" : message{"Icon drop on Window: " }
        *obj.tuiObject = ntui_GetEventObject{*ev}
        s.s = ntui_GetEventString{*ev}
        ntui_SetAttr{*obj,#TUIWA_TITLE,&s}
      Case "ICONDROP"   : message{"Icon drop on app icon: " }
      Case "MESSAGE"    : message{"got AREXX message: "}
      Case "Cycle"
        ;a.s = Str$(ntui_GetValueByID{*engine,"CYCL"})
        ;b.s = Str$(0)
        ;message{"Cycle Event "+b.s+" and extra by ID "+a.s}
      Case "VIEW"
        ;message{"Click view!"}
      Case "KEY"
        message{"Key Press!"}
      Case "REFRESH"
        *obj.tuiObject = ntui_GetEventObject{*ev}
        If *obj
          Select str_Read{&*obj\nameID}
            Case "CView"
              CNIF #TUI_DEBUG
              r.l = r+1
              CELSE
              r.l=0
              CEND
              *rp.RastPort = ntui_ObtainRastPort{*obj}
              If *rp
                x.l = ntui_GetAttr{*obj,#TUIA_XPOS,0}
                y.l = ntui_GetAttr{*obj,#TUIA_YPOS,0}

                w.l = ntui_GetAttr{*obj,#TUIA_WIDTH,0}
                h.l = ntui_GetAttr{*obj,#TUIA_HEIGHT,0}

                xof.l = ntui_GetAttr{*obj,#TUICVA_HOFFSET,0}
                yof.l = ntui_GetAttr{*obj,#TUICVA_VOFFSET,0}

                xto.l = ntui_GetAttr{*obj,#TUICVA_HTOTAL,0}
                yto.l = ntui_GetAttr{*obj,#TUICVA_VTOTAL,0}

                qs.l = 64

                For ay1.l=0 To yto Step qs
                  For ax1.l=0 To xto Step qs
                    x1.l = ax1-xof
                    y1.l = ay1-yof

                    If x1>-qs AND y1>-qs  AND x1<w AND y1<h

                      n.l = (ay1+ax1)/qs +r
                      If (n&1)=1
                        SetAPen_ *rp,*engine\pen[#TUIPEN_HALFSHADOW]
                      Else
                        SetAPen_ *rp,*engine\pen[#TUIPEN_HALFSHINE]
                      End If
                      x2.l = x1+qs-1 : If x2>=w Then x2=w-1
                      y2.l = y1+qs-1 : If y2>=h Then y2=h-1
                      If x1<0 Then x1=0
                      If y1<0 Then y1=0
                      RectFill_ *rp,x1+x,y1+y,x2+x,y2+y
                    End If
                  Next
                Next
                         a.s = Str$(xof)+"/"+Str$(yof)+"    "

         SetAPen_ *rp,1
         SetBPen_ *rp,0
         Move_ *rp,x,y+ 20
         Text_ *rp,&a,FLen(a)

                ntui_ReleaseRastPort{*obj}
              End If
          End Select
        End If
      Default
        If *ev
        If *con
          strP.l = str_GetPtr{*ev\notifyID}
          ntui_WriteConsole{*con,strP}
        End If
        End If
    End Select
  Until *ev=#NULL
Wend

ntui_FreeEngine{*engine}

End

xmldata:
Dc.b "<Window id='xmlwin' title='XML Test'>"
Dc.b "  <Group>"
Dc.b "    <TabView activetab=1>"
Dc.b "      <VGroup title='tab #1'>"
Dc.b "        <Label string='Just a button:'/>"
Dc.b "        <Button text='test' onClick='TEST' help='I was created using XML!'/>"
Dc.b "        <HGroup>"
Dc.b "          <VGroup>"
Dc.b "            <RadioButton text='Frodo' id=radio1/>"
Dc.b "            <RadioButton text='Gandalf' id='radio2' bind='radio1'/>"
Dc.b "            <RadioButton text='Bilbo' id='radio3' bind='radio2'/>"
Dc.b "          </Group>"
Dc.b "          <Image src='sys:prefs/ahi.info'/>"
Dc.b "          <VScroller total=100 visible=20  top=10 hasarrows=yes> <space> <label text=''/>"
Dc.b "        </Group>"
Dc.b "      </Group>"
Dc.b "      <Group title='tab #2'>"
Dc.b "      </Group>"
Dc.b "    </TabView>"
Dc.b "  </Group>"
Dc.b "</Window>"
Dc.b 0

alice:
Dc.b "Schner, goldner Nachmittag,",10,"Wo Flut und Himmel lacht!",10
Dc.b "Von schwacher Kindeshand bewegt,",10,"Die Ruder pltschern sacht -;",10
Dc.b "Das Steuer hlt ein Kindesarm",10,"Und lenket unsre Fahrt.",10,10
Dc.b "So fuhren wir gemchlich hin",10,"Auf trumerischen Wellen -;",10
Dc.b "Doch ach! die drei vereinten sich,",10,"Den mden Freund zu qulen -;",10
Dc.b "Sie treiben ihn, sie drngten ihn,",10,"Ein Mhrchen zu erzhlen.",10,10
Dc.b "Die Erste gab's Commandowort;",10,"O schnell, o fange an!",10
Dc.b "Und mach' es so, die Zweite bat,",10,"Da man recht lachen kann!",10
Dc.b "Die Dritte lie ihm keine Ruh",10,"Mit wie? und wo? und wann?",10,10
Dc.b "Jetzt lauschen sie vom Zauberland",10,"Der wunderbaren Mhr';",10
Dc.b "Mit Thier und Vogel sind sie bald'",10,"In freundlichem Verkehr,",10
Dc.b "Und fhlen sich so heimisch dort,",10,"Als ob es Wahrheit wr'. -;",10,10
Dc.b "Und jedes Mal, wenn Fantasie",10,"Dem Freunde ganz versiegt: -;",10
Dc.b "",34,"das Uebrige ein ander Mal!",34,"",10,"O nein, sie leiden's nicht.",10
Dc.b "",34," ",34,"Es ist ja schon ein ander Mal!",34," ",34," -;",10,"So rufen sie vergngt.",10,10
Dc.b "So ward vom schnen Wunderland",10,"Das Mrchen ausgedacht,",10
Dc.b "So langsam Stck fr Stck erzhlt,",10,"Beplaudert und belacht,",10
Dc.b "Und froh, als es zu Ende war,",10,"Der Weg nach Haus gemacht.",10,10
Dc.b "Alice! O nimm es freundlich an!",10,"Leg' es mit gt'ger Hand",10
Dc.b "Zum Straue, den Erinnerung",10,"Aus Kindheitstrumen band,",10
Dc.b "Gleich welken Blthen, mitgebracht",10,"Aus liebem, fernen Land.",10,10
Dc.b "- Lewis Carroll",10
Dc.b 0

CEND


