; XTRA
; Embedded .xtra Header
; 
; General Info
; -------------------------------------------------------
; ExePath         = "RAM:"
; ExeFile         = "Prog.exe"
; CreateIcon      = 0
; Residents       = "all.res"
; 
; Compiler
; -------------------------------------------------------
; StringBuffer    = 8192
; MakeSmallest    = 1
; FuncOptimize    = 0
; Version         = 0.0.0
; NumberOfBuilds  = 0
; 
; Debugger
; -------------------------------------------------------
; CliArgs         = ""
; StackSize       = 32768
; RuntimeDebug    = 0
; DebugInfo       = 0
; CreateDbgFile   = 0
; OverflowCheck   = 0
; AssemblerCheck  = 1
; InterruptCheck  = 1
; AutoRun         = 1
; 
; Editor
; -------------------------------------------------------
; CursorLine      = 52
; CursorColumn    = 7
; LabelSearch     = ""
; LabelRemark     = 0
; LabelAll        = 0
; LabelPosition   = 0
; 
; Blitz Objects
; -------------------------------------------------------
; Max IconInfo    = 1
; Max NChunky     = 50
; Max MUIObject   = 50
; Max PTModule    = 5
; Max AsyncReq    = 4
; Max Req-Lib     = 5
; Max GTMenuList  = 5
; Max Console     = 5
; Max TCPSock     = 5
; Max XBSound     = 10
; Max Chunky      = 20
; Max File        = 5
; Max GadgetList  = 5
; Max Queue       = 10
; Max Screen      = 5
; Max Shape       = 100
; Max CopList     = 10
; Max Sprite      = 20
; Max Stencil     = 5
; Max Module      = 5
; Max Window      = 20
; Max Anim        = 10
; Max Sound       = 10
; Max Bank        = 5
; Max Buffer      = 10
; Max BitMap      = 10
; Max Slice       = 10
; Max Page        = 4
; Max Tape        = 5
; Max IntuiFont   = 5
; Max MedModule   = 8
; Max Palette     = 4
; Max MenuList    = 5
; Max BlitzFont   = 4
; Max GTList      = 20
; /XTRA
#TUI_HAS_AREXX = 1

Function.l ntui_ReplyRexxMsg{*engine.tuiEngine,result.l}
  !_ASSERT{*engine}

  succ.l = False
  If *engine\rxMsg
    *engine\rxMsg\rm_Result1 = result,0
    ReplyMsg_ *engine\rxMsg : *engine\rxMsg = Null : succ=True
  End If

  Function Return succ

End Function


Function.l ntui_ReplyRexxMsgString{*engine.tuiEngine, string.s}
  !_ASSERT{*engine}

  succ.l = False
  If *engine\rxMsg
    *engine\rxMsg\rm_Result1 = 0,0
    If (*engine\rxMsg\rm_Action & #RXFF_RESULT)
      argstring.l = CreateArgstring_(&string,FLen(string))
      *engine\rxMsg\rm_Result1 = 0,argstring
    End If
    ReplyMsg_ *engine\rxMsg : *engine\rxMsg = Null : succ=True
  End If

  Function Return succ

End Function


Function.l ntui_FailRexxMsg{*engine.tuiEngine, errcode.l}
  !_ASSERT{*engine}

  succ.l = False
  If *engine\rxMsg
    *engine\rxMsg\rm_Result1 = 10,errcode
    ReplyMsg_ *engine\rxMsg : *engine\rxMsg = Null : succ=True
  End If

  Function Return succ

End Function
 

Function.l _ntui_GetRexxMsg{*engine.tuiEngine, lateReply.l}
  !_ASSERT{*engine}

  succ.l = False
  If *engine\rxPort=Null Then Function Return False

  If *engine\rxMsg Then ntui_ReplyRexxMsg{*engine.tuiEngine,0}
  *engine\rxMsg = GetMsg_(*engine\rxPort)
  If *engine\rxMsg
    If IsRexxMsg_(*engine\rxMsg)
      If LengthArgstring_ (*engine\rxMsg\rm_Args[0])>0
        str_Write{&*engine\rxString,Peek.s(*engine\rxMsg\rm_Args[0])}
      Else
        str_Free{&*engine\rxString}
      End If
      If lateReply=False Then ntui_ReplyRexxMsg{*engine.tuiEngine, 0}
      succ=True
    Else
      ReplyMsg_ *engine\rxMsg : *engine\rxMsg = Null
    End If
  End If

  Function Return succ

End Function


Statement _ntui_FreeRexx{*engine.tuiEngine}
  !_ASSERT{*engine}

  If *engine\rxPort
    If *engine\rxMsg Then ntui_ReplyRexxMsg{*engine,0}
    RemPort_ *engine\rxPort  : DeleteMsgPort_ *engine\rxPort
    *engine\rxPort  = Null
    str_Free{&*engine\rxPortName}
    *engine\rxMsg = Null
    str_Free{&*engine\rxString}
  End If

End Statement


Function.MsgPort _ntui_InitRexx{*engine.tuiEngine,portName.s}
  !_ASSERT{*engine}

  If *engine\rxPort Then _ntui_FreeRexx{*engine}

  If portName
    *engine\rxPort  = CreateMsgPort_()
    If *engine\rxPort
      str_Write{&*engine\rxPortName,portName}
      *engine\rxPort\mp_Node\ln_Name = &*engine\rxPortName\str
      *engine\rxPort\mp_Node\ln_Pri  = 0
      AddPort_ *engine\rxPort
      *engine\rxMsg     = Null
      *engine\rxString  = Null
    End If
  End If

  Function Return *engine\rxPort

End Function
