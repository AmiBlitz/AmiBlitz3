; XTRA
; Embedded .xtra Header
; 
; General Info
; -------------------------------------------------------
; ExePath         = "RAM:"
; ExeFile         = "Prog.exe"
; CreateIcon      = 0
; Residents       = ""
; 
; Compiler
; -------------------------------------------------------
; StringBuffer    = 8192
; MakeSmallest    = 1
; Version         = 0.0.0
; NumberOfBuilds  = 0
; 
; Debugger
; -------------------------------------------------------
; CliArgs         = ""
; StackSize       = 32768
; RuntimeDebug    = 0
; DebugInfo       = 0
; CreateDbgFile   = 0
; OverflowCheck   = 0
; AssemblerCheck  = 1
; InterruptCheck  = 1
; AutoRun         = 1
; 
; Editor
; -------------------------------------------------------
; CursorLine      = 78
; CursorColumn    = 57
; LabelSearch     = ""
; LabelRemark     = 0
; LabelAll        = 0
; LabelPosition   = 0
; 
; Blitz Objects
; -------------------------------------------------------
; Max IconInfo    = 1
; Max NChunky     = 50
; Max MUIObject   = 50
; Max PTModule    = 5
; Max AsyncReq    = 4
; Max Req-Lib     = 5
; Max GTMenuList  = 5
; Max Console     = 5
; Max TCPSock     = 5
; Max XBSound     = 10
; Max Chunky      = 20
; Max File        = 5
; Max GadgetList  = 5
; Max Queue       = 10
; Max Screen      = 5
; Max Shape       = 100
; Max CopList     = 10
; Max Sprite      = 20
; Max Stencil     = 5
; Max Module      = 5
; Max Window      = 20
; Max Anim        = 10
; Max Sound       = 10
; Max Bank        = 5
; Max Buffer      = 10
; Max BitMap      = 10
; Max Slice       = 10
; Max Page        = 4
; Max Tape        = 5
; Max IntuiFont   = 5
; Max MedModule   = 8
; Max Palette     = 4
; Max MenuList    = 5
; Max BlitzFont   = 4
; Max GTList      = 20
; /XTRA
Function.l ntui_ReplyRexxMsg{*tuiEngine.tuiEngine,result.l}
succ.l = False
If *tuiEngine\rxMsg
  *tuiEngine\rxMsg\rm_Result1 = result,0
  ReplyMsg_ *tuiEngine\rxMsg : *tuiEngine\rxMsg = #NULL : succ=True
End If
Function Return succ
End Function


Function.l ntui_ReplyRexxMsgString{*tuiEngine.tuiEngine, string.s}
succ.l = False
If *tuiEngine\rxMsg
  *tuiEngine\rxMsg\rm_Result1 = 0,0
  If (*tuiEngine\rxMsg\rm_Action & #RXFF_RESULT)
    argstring.l = CreateArgstring_(&string,FLen(string))
    *tuiEngine\rxMsg\rm_Result1 = 0,argstring
  End If
  ReplyMsg_ *tuiEngine\rxMsg : *tuiEngine\rxMsg = #NULL : succ=True
End If
Function Return succ
End Function


Function.l ntui_FailRexxMsg{*tuiEngine.tuiEngine, errcode.l}
succ.l = False
If *tuiEngine\rxMsg
  *tuiEngine\rxMsg\rm_Result1 = 10,errcode
  ReplyMsg_ *tuiEngine\rxMsg : *tuiEngine\rxMsg = #NULL : succ=True
End If
Function Return succ
End Function
 

Function.l _ntui_GetRexxMsg{*tuiEngine.tuiEngine, lateReply.l}
succ.l = False
If *tuiEngine\rxPort=#NULL Then Function Return False
If *tuiEngine\rxMsg Then ntui_ReplyRexxMsg{*tuiEngine.tuiEngine,0}
*tuiEngine\rxMsg = GetMsg_(*tuiEngine\rxPort)
If *tuiEngine\rxMsg
  If IsRexxMsg_(*tuiEngine\rxMsg)
    If LengthArgstring_ (*tuiEngine\rxMsg\rm_Args[0])>0
      str_Write{&*tuiEngine\rxString,Peek.s(*tuiEngine\rxMsg\rm_Args[0])}
    Else
      str_Free{&*tuiEngine\rxString}
    End If
    If lateReply=False Then ntui_ReplyRexxMsg{*tuiEngine.tuiEngine, 0}
    succ=True
  Else
    ReplyMsg_ *tuiEngine\rxMsg : *tuiEngine\rxMsg = #NULL
  End If
End If
Function Return succ
End Function



Statement _ntui_FreeRexx{*tuiEngine.tuiEngine}
If *tuiEngine\rxPort
  If *tuiEngine\rxMsg Then ntui_ReplyRexxMsg{*tuiEngine,0}
  RemPort_ *tuiEngine\rxPort  : DeleteMsgPort_ *tuiEngine\rxPort
  *tuiEngine\rxPort  = #NULL
  str_Free{&*tuiEngine\rxPortName}
  *tuiEngine\rxMsg = #NULL
  str_Free{&*tuiEngine\rxString}
End If
End Statement


Function.l _ntui_InitRexx{*tuiEngine.tuiEngine,portName.s}
If *tuiEngine\rxPort Then _ntui_FreeRexx{*tuiEngine}
*tuiEngine\rxPort  = CreateMsgPort_()
If *tuiEngine\rxPort
  str_Write{&*tuiEngine\rxPortName,portName}
  *tuiEngine\rxPort\mp_Node\ln_Name = &*tuiEngine\rxPortName\str
  *tuiEngine\rxPort\mp_Node\ln_Pri  = 0
  AddPort_ *tuiEngine\rxPort
  *tuiEngine\rxMsg     = #NULL
  *tuiEngine\rxString  = #NULL
End If
Function Return *tuiEngine\rxPort
End Function





