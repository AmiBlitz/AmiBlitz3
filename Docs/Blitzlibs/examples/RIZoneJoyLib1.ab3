; XTRA
; Embedded .xtra Header
; 
; General Info
; -------------------------------------------------------
; ExePath         = ""
; ExeFile         = ""
; CreateIcon      = 1
; Residents       = ""
; 
; Compiler
; -------------------------------------------------------
; StringBuffer    = 10240
; MakeSmallest    = 1
; FuncOptimize    = 1
; Version         = 0.0.0
; NumberOfBuilds  = 0
; 
; Debugger
; -------------------------------------------------------
; CliArgs         = ""
; StackSize       = 8191
; RuntimeDebug    = 1
; DebugInfo       = 0
; CreateDbgFile   = 0
; OverflowCheck   = 0
; AssemblerCheck  = 0
; InterruptCheck  = 1
; AutoRun         = 1
; 
; Editor
; -------------------------------------------------------
; CursorLine      = 1
; CursorColumn    = 1
; LabelSearch     = ""
; LabelRemark     = 0
; LabelAll        = 0
; LabelPosition   = 0
; 
; Blitz Objects
; -------------------------------------------------------
; Max IconInfo    = 1
; Max NChunky     = 50
; Max MUIObject   = 5
; Max PTModule    = 5
; Max AsyncReq    = 4
; Max Req-Lib     = 5
; Max GTMenuList  = 5
; Max Console     = 5
; Max TCPSock     = 5
; Max XBSound     = 10
; Max Chunky      = 20
; Max File        = 5
; Max GadgetList  = 5
; Max Queue       = 10
; Max Screen      = 5
; Max Shape       = 100
; Max CopList     = 10
; Max Sprite      = 20
; Max Stencil     = 5
; Max Module      = 5
; Max Window      = 5
; Max Anim        = 100
; Max Sound       = 10
; Max Bank        = 5
; Max Buffer      = 10
; Max BitMap      = 10
; Max Slice       = 10
; Max Page        = 4
; Max Tape        = 5
; Max IntuiFont   = 5
; Max MedModule   = 8
; Max Palette     = 4
; Max MenuList    = 5
; Max BlitzFont   = 4
; Max GTList      = 5
; /XTRA
;Monkey Island Type Interface Using Zones & FNS
;
;=================================================
; Variables, Constant and Newtype definitions
;=================================================

DEFTYPE.w
#font=1               ; Font number for Times15.FNS
#sfont=0              ; Smaller font
#buttonbitmap=1       ; Bitmap for all buttons etc.
#numbuttons=19        ; Total number of buttons to zonetest for

;Button constants:
#_LookAt=0
#_PickUp=1
#_Give=2
#_Use=3
#_Open=4
#_Close=5
#_TalkTo=6
#_Push=7
#_Pull=8
#_InvUp=9
#_InvDown=10

NEWTYPE.mybuttontype
  x.w : y.w : width.w : height.w
  text$ : textx.w : texty.w
  buttype.b : pad.b
End NEWTYPE

NEWTYPE.scrobject
  x.w : y.w : image.w
  name$ : flags.l
  defbut.b : pad.b
  zx1.w : zy1.w : zx2.w : zy2.w
End NEWTYPE

Dim buttons.mybuttontype(19)
Dim sceneobject.scrobject(19)

buthigh=-1            ; Currently highlighted button
objhigh=-1            ; Currently highlighted object in scene
dobutton=-1           ; Command button that was pressed
totalobjs=0           ; Number of objects in scene

;=================================================
; Statements and Functions
;=================================================

BLITZ
;Statement to reprint the text attached to a button
Statement HighLightButton{num.w,hih.b}
  Use BitMap #buttonbitmap

  SHARED buttons()

  USEPATH buttons(num)
  If hih
    hih=17
  Else
    hih=2
  EndIf

  If \buttype=0
    FNSPrint #font,\textx,\texty,\text,%1,hih
  Else
    If hih=2 Then hih=1
    Box \x,\y,\x+\width-1,\y+\height-1,hih
    If hih=1 Then hih=0
    HighLightButton{#_LookAt,hih}
  EndIf
End Statement

Statement RedoButton {num.w}
  SHARED buttons()
  USEPATH buttons(num)

  x.w=\x
  y.w=\y
  SetZone num,x,y,x+\width-1,y+\height-1
  Boxf x,y,x+\width-1,y+\height-1,1
  If \buttype=0 Then FNSPrint #font,\textx,\texty,\text
End Statement

Statement WriteCommand {obj.w}
  SHARED buttons(),sceneobject(),dobutton

  Use BitMap 1
  Boxf 0,0,319,7,0

  but.w=dobutton

  If but<>-1
    t$=buttons(but)\text
  Else
    t$="Walk to"
  EndIf
  If obj<>-1 Then t$+" "+sceneobject(obj)\name

  FNSPrint #sfont,160,1,t$
End Statement

;=================================================
; Program startup
;=================================================

Gosub LoadFiles

AGAPalRGB 0,0,0,0,0
AGAPalRGB 0,17,255,255,255
AGAPalRGB 0,1,255,50,50

BitMap 0,320,256,5
BitMap #buttonbitmap,320,56,5

gap=3
InitCopList 0,44,200,$10005,8,32,0
InitCopList 1,44+200+gap,56,$10005,8,32,0

DisplayBitMap 0,0 : DisplayPalette 0,0
DisplayBitMap 1,#buttonbitmap : DisplayPalette 1,1

BLITZ
CreateDisplay 0,1

Gosub CreateButtons

;Get an object in the scene
USEPATH sceneobject(0)
\x=160
\y=50
\zx1=140
\zx2=180
\zy1=40
\zy2=60
\name="Test object"
\defbut=#_LookAt
Use BitMap 0
Boxf \zx1,\zy1,\zx2,\zy2,1
SetZone 50,\zx1,\zy1,\zx2,\zy2

USEPATH sceneobject(1)
\x=260
\y=120
\zx1=240
\zx2=280
\zy1=110
\zy2=130
\name="Captains Log"
\defbut=#_LookAt
Use BitMap 0
Boxf \zx1,\zy1,\zx2,\zy2,1
SetZone 51,\zx1,\zy1,\zx2,\zy2

FNSOutput 0,1
FNSPrefs %1,1
FNSPrint #sfont,160,0,"Zone Library Demo Program - Click QUIT to exit"

FNSOutput #buttonbitmap,1
FNSPrefs %1,2

totalobjs=2

MouseArea 0,0,320,256+gap
Mouse On
Repeat
  VWait
  x=MouseX : y=MouseY
  DisplaySprite 0,0,x,y,0
  DisplaySprite 1,0,x,y-200-gap,0

  zn=ZoneTest(0,#numbuttons-1,x,y-200-gap)

  If zn<>-1 AND zn<>buthigh
    If buthigh<>-1 Then HighLightButton{buthigh,0}
    buthigh=zn
    HighLight=zn
    HighLightButton{buthigh,1}
  EndIf
  If zn=-1 AND buthigh<>-1
    HighLightButton{buthigh,0}
    buthigh=-1
  EndIf

  zn=ZoneTest(50,50+totalobjs-1,x,y)
  If zn<>-1 AND zn-50<>objhigh
    If objhigh<>-1
      a=sceneobject(objhigh)\defbut
      If a Then HighLightButton{a,0}
    EndIf
    objhigh=zn-50
    WriteCommand{objhigh}

    a=sceneobject(objhigh)\defbut
    If a<>-1 Then HighLightButton{a,1}
  EndIf
  If zn=-1 AND objhigh<>-1
    a=sceneobject(objhigh)\defbut
    If a<>-1 Then HighLightButton{a,0}
    WriteCommand{-1}
    objhigh=-1
  EndIf

  j=Joyb(0)
  If j=0 AND firerelease Then firerelease=0
  If j<>0 AND j<>3 AND firerelease=0
;Handle mouse button pressing
    If buthigh<>-1 AND buthigh<9
      dobutton=buthigh
      WriteCommand{-1}
      firerelease=1
      If buthigh=8 Then End
    Else
      If dobutton<>-1
        Use BitMap 1
        Boxf 0,0,319,7,0
        dobutton=-1
        WriteCommand{objhigh}
      EndIf
    EndIf
  EndIf

Until True=False
End

.LoadFiles:
  QAMIGA
  LoadPalette 1,"Buttons.iff"
  LoadSprites 0,"Pointer.sprite"
  suc=BLoad("Times15.FNS",0)
  a=InstallFNS(#font,Start(0))
  suc=BLoad("7_High.FNS",1)
  a=InstallFNS(#sfont,Start(1))
  VWait 20
  BLITZ
Return


  BLITZ
.CreateButtons:
  Use BitMap #buttonbitmap

  FNSOutput #buttonbitmap,1
  FNSPrefs %1,2
  ZoneInit


;Get our command buttons
  Restore buttontext
  x=0 : y=8
  num=0
  USEPATH buttons(h)
  For h=0 To 8
    Read \text
    \x=x
    \y=y
    \textx=x+25
    \texty=y+1
    \width=52
    \height=15
    \buttype=0

    RedoButton{h}
    y+16
    num+1
    If num=3
      x+54
      y=8
      num=0
    EndIf
  Next h

; Now do our inventory buttons
; 2 arrows:

  For h=9 To 10
    Read \text,\x,\y,\width,\height,\textx,\texty
    \buttype=0
    RedoButton{h}
  Next h

; Now do actual inventory items
  x=179
  y=8
  num=0
  For h=11 To 18
    \x=x
    \y=y
    \width=34
    \height=23
    \buttype=1
    RedoButton{h}

    x+35
    num+1
    If num=4
      num=0
      x=179
      y+24
    EndIf
  Next h
Return

buttontext:
  Data$ "Look at","Pick up","Give","Use","Open","Close"
  Data$ "Talk to","Push","QUIT"

;Arrow data: x,y,width,height,textx,texty
  Data$ "[" : Data.w 164,8,13,23,170,15
  Data$ "]" : Data.w 164,32,13,23,170,40


