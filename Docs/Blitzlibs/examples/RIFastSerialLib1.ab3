; XTRA
; Embedded .xtra Header
; 
; General Info
; -------------------------------------------------------
; ExePath         = "SYS:Utilities"
; ExeFile         = "Net"
; CreateIcon      = 1
; Residents       = "blitzlibs:gadtools.res"
; 
; Compiler
; -------------------------------------------------------
; StringBuffer    = 10240
; MakeSmallest    = 1
; FuncOptimize    = 1
; Version         = 0.0.0
; NumberOfBuilds  = 0
; 
; Debugger
; -------------------------------------------------------
; CliArgs         = ""
; StackSize       = 8191
; RuntimeDebug    = 0
; DebugInfo       = 0
; CreateDbgFile   = 0
; OverflowCheck   = 0
; AssemblerCheck  = 0
; InterruptCheck  = 1
; AutoRun         = 1
; 
; Editor
; -------------------------------------------------------
; CursorLine      = 1
; CursorColumn    = 1
; LabelSearch     = ""
; LabelRemark     = 0
; LabelAll        = 0
; LabelPosition   = 0
; 
; Blitz Objects
; -------------------------------------------------------
; Max IconInfo    = 1
; Max NChunky     = 50
; Max MUIObject   = 5
; Max PTModule    = 5
; Max AsyncReq    = 4
; Max Req-Lib     = 5
; Max GTMenuList  = 5
; Max Console     = 5
; Max TCPSock     = 5
; Max XBSound     = 10
; Max Chunky      = 20
; Max File        = 5
; Max GadgetList  = 5
; Max Queue       = 10
; Max Screen      = 5
; Max Shape       = 100
; Max CopList     = 10
; Max Sprite      = 20
; Max Stencil     = 5
; Max Module      = 5
; Max Window      = 5
; Max Anim        = 100
; Max Sound       = 10
; Max Bank        = 5
; Max Buffer      = 10
; Max BitMap      = 10
; Max Slice       = 10
; Max Page        = 4
; Max Tape        = 5
; Max IntuiFont   = 5
; Max MedModule   = 8
; Max Palette     = 4
; Max MenuList    = 5
; Max BlitzFont   = 4
; Max GTList      = 5
; /XTRA
; Serial based file transfer program

WBStartup
NoCli
;
; .----------.
; |NET v0.666|
; `----------'
;
; (C) 1996 Red When Excited Ltd
;

; Maximum number of files to be transferred at once
#maxfilecount=1000

;CONSTANTS
#master=1
#slave=2
#abort=3

DEFTYPE.l pathtype,numfiles,cli,newfiles
DEFTYPE.b multitask
DEFTYPE .w

MaxLen spa$=192
MaxLen sfi$=192

NEWTYPE .node
  pad.w
  filename$
  path$
End NEWTYPE

Dim List newfile.node(#maxfilecount)


#buffersize=8192
#size=64

Statements
Function.w Sync{timeout.l,syncbyte.b}
  TimerReset
  If CheckSerial=syncbyte Then PutSerial syncbyte:FlushSerialIO:Function Return #slave
  PutSerial syncbyte
  Repeat
    If CheckSerial=syncbyte
      Pop Repeat
      Function Return #master
    EndIf
  Until Timer>=timeout OR RawStatus(69)
  Function Return #abort
End Function


Statement SendString{a$}
  For n=1 To Len(a$)
    PutSerial Asc(Mid$(a$,n,1))
    Repeat:Until CheckSerial<>-1
  Next n
  PutSerial 0
  Repeat:Until CheckSerial<>-1
End Statement

Function$ ReadString{}
  c$=""
  Repeat
    a.w=GetSerial
    If a<>0
      c$+Chr$(a)
    EndIf
    PutSerial 0
  Until a=0
  Function Return c$
End Function


;Split a filename in the form of: Work:Blitz2/Progs/MyFile.rimp
;Into:                    path$ = Work:Blitz2/Progs
;                         file$ = MyFile.rimp
;The filename is in fil$, and the path and file are returned
;respectively in pa$ and fi$
Statement SplitFilename {}
  SHARED pa$,fi$,fil$
  For h.w=Len(fil$) To 1 Step -1
    If Mid$(fil$,h,1)="/" OR Mid$(fil$,h,1)=":"
      fi$=Right$(fil$,Len(fil$)-h)
      pa$=Left$(fil$,h)
      If Right$(pa$,1)="/" Then pa$=Left$(pa$,Len(pa$)-1)
      Statement Return
    EndIf
  Next h
End Statement
;BLITZ
Statement ReadMemSerial{ad.l,l.l}
  SHARED multitask,ht,sze.l
  l2.l=l
  l2/sze
  If l2>0
  b.l=0
  s.f=314/l2
  y.f=s*1024.0
  t.l=y
  wh=WindowHeight
  InnerCls
  VWait
  If multitask=0 Then Forbid_
  For n=1 To l2
    WBox (b/1024)+1,ht+1,((b+t)/1024),wh-3,3
    b+t
    SyncGet ad,sze
    ad+sze
  Next n
  If l2*sze<>l Then SyncGet ad,l-(l2*sze)
  Else
    InnerCls
    If multitask=0 Then Forbid_
    SyncGet ad,l
  EndIf
  If multitask=0 Then Permit_
End Statement

Statement WriteMemSerial{ad.l,l.l}
  SHARED multitask,ht,sze.l
  l2.l=l
  l2/sze
  If l2>0
    b.l=0
    s.f=314/l2
    y.f=s*1024.0
    t.l=y
    wh=WindowHeight
    InnerCls
    VWait
    If multitask=0 Then Forbid_
    For n=1 To l2
      WBox (b/1024)+1,ht+1,((b+t)/1024),wh-3,3
      b+t
      SyncPut ad,sze
      ad+sze
    Next n
    If l2*sze<>l Then SyncPut ad,l-(l2*sze)
  Else
    If multitask=0 Then Forbid_
      InnerCls
      SyncPut ad,l
  EndIf
  If multitask=0 Then Permit_
End Statement
;AMIGA


sendprog:
Statement Send{filename$}
  SHARED numfiles,fnum.l
  suc=BLoad(filename$,0)
  If suc=0 Then Statement Return
  WTitle "SEND "+filename$+"("+Str$(Length(0))+")  ["+Str$(fnum)+"/"+Str$(numfiles)+"]"
  fnum+1
  If Sync{500,1}=#abort Then Statement Return
  Use Window 1
  SendString{filename$}     ; Send complete original path
  SendString{Str$(Length(0))}
  WriteMemSerial{Start(0),Length(0)}
End Statement

Statement ScanDir{cd$}
  alock.l=Lock_ (cd$,-2)   ; Try to get a shared lock on the dir
  If alock
    fib.l=AllocDosObject_ (2,0) ; Get a FileInfoBlock for examining.
    Examine_  alock,fib         ; Examine it to determine if it
    While ExNext_ (alock,fib)   ; is a dir or not.
      ftype.l=Peek.l(fib+4)     ; ftype>0=Directory <0=File
      filename$=Peek$(fib+8)
      If ftype>0
        a$=""
        If Right$(cd$,1)<>"/" Then a$="/"
        ScanDir{cd$+a$+filename$}
      Else
        a$=""
        If Right$(cd$,1)<>"/" Then a$="/"
        Send{cd$+a$+filename$}
      EndIf
    Wend
    FreeDosObject_  2,fib     ; Free our DosObject
    UnLock_  alock            ; Unlock the directory
  EndIf
End Statement

Statement CountDir{cd$}
  SHARED numfiles.l
  alock.l=Lock_ (cd$,-2)   ; Try to get a shared lock on the dir
  If alock
    fib.l=AllocDosObject_ (2,0) ; Get a FileInfoBlock for examining.
    Examine_  alock,fib         ; Examine it to determine if it
    While ExNext_ (alock,fib)   ; is a dir or not.
      ftype.l=Peek.l(fib+4)     ; ftype>0=Directory <0=File
      filename$=Peek$(fib+8)
      If ftype>0
        a$=""
        If Right$(cd$,1)<>"/" Then a$="/"
        CountDir{cd$+a$+filename$}
      Else
        a$=""
        If Right$(cd$,1)<>"/" Then a$="/"
        numfiles+1
      EndIf
    Wend
    FreeDosObject_  2,fib     ; Free our DosObject
    UnLock_  alock            ; Unlock the directory
  EndIf
End Statement



.main:
multitask=-1
cli=0
path$="TEMP:"


SetTaskPri_ FindTask_(0),-1     ; we're not an important task

Gosub docommodity
Gosub getfontheight



If FromCLI AND NumPars=2 Then cli.l=True
If cli
  path$=Par$(2)
  filename$=Par$(1)
  Send{filename$}
  End
EndIf

CludgeShapes 0,4,?myshapes
If FromIDE=0
  If GetIconObject(ProgPath$)=0 Then EZRequest "Cant open icon file!"
Else
  If GetIconObject("SYS:Utilities/Net")=0 Then EZRequest "Cant open icon file!"
EndIf
If FindToolType("PATH") Then path$=FindToolValue("PATH")
baud.l=57600
If FindToolType("BAUD") Then baud.l=Val(FindToolValue("BAUD"))
multitask=FindToolType("MULTITASK")
ShapeToIcon 0,1
PutIconObject "T:YOUWONTHAVETHIS"
AddAppIcon 0,"Out->","T:YOUWONTHAVETHIS",600,100
KillFile "T:YOUWONTHAVETHIS.info"

InitSerialIO baud,0,0,0
ShapeToIcon 2,3
;Gosub showicon
skip:
Repeat
  VWait
  ev.l=AppEvent
  ai.l=AppEventID
  sr.w=CheckSerial
  ce.l=CommodityEvent
  em.l=ExchangeMessage
  If ce<>0 AND (em=CxKill) Then End
Until ev<>0 OR (sr<>-1)
If sr=1 Then PutSerial 1:Gosub receive:CloseWindow 1:EraseAll
If ev
  If ai=0 AND AppNumFiles=0 Then Gosub drawgui:ev=-1
  If ai=0 AND AppNumFiles<>0 Then Gosub send
  If ai=1 AND AppNumFiles=0 Then Gosub showfiles
EndIf
Goto skip


showfiles:
newfiles=0
GTString 1,47,20,100,280,12,"",0,255,""
GTTags GTTagValue(53),GTGadPtr(1,47)
GTListView 1,50,20,20,280,80,"New Files",0,newfile()
GTButton 1,48,314,20,64,12,"Move",0
GTButton 1,49,314,34,64,12,"Delete",0

Window 2,120,64,400,128,$1100a,"New Files",1,2
AttachGTList 1,2
ci.l=0
Repeat
  VWait
  ev.l=Event
  ec.l=EventCode
  gh.l=GadgetHit
  If ev=64
    Select gh
      Case 48
        ResetList newfile()
        For n.l=0 To ci
          NextItem newfile()
        Next n
        fil$=newfile()\path
        SplitFilename{}
        RTEZSetDefaultDirectory 1,pa$
        dest$=RTEZLoadFile("Choose Dest",fi$)
        CopyFile fil$,dest$
        KillFile fil$
        KillItem newfile()
        GTChangeList 1,50
        GTChangeList 1,50,newfile()
      Case 49
        ResetList newfile()
        For n.l=0 To ci
          NextItem newfile()
        Next n
        fil$=newfile()\path
        KillFile fil$
        KillItem newfile()
        GTChangeList 1,50
        GTChangeList 1,50,newfile()
      Case 50
        ci=ec
    End Select
  EndIf
Until ev=$200
DetachGTList 1
CloseWindow 2
DelAppIcon 1
ResetList newfile()
While NextItem(newfile())
  KillItem newfile()
Wend
ResetList newfile()
Return

.send:
Window 1,0,0,320,ht*2,$a,"SEND",1,2
For n.l=1 To AppNumFiles
  filename$=NextAppFile
  a$=Right$(filename$,1)
  Select a$
    Case "/"
      numfiles=0
      CountDir{filename$}
      WTitle Str$(numfiles)+" files."
      fnum=1
      ScanDir{filename$}
    Case ":"
      numfiles=0
      CountDir{filename$}
      WTitle Str$(numfiles)+" files."
      fnum=1
      ScanDir{filename$}
    Default
      fnum=1
      numfiles=1
      Send{filename$}
      If Exists(filename$+".info") Then Send{filename$+".info"}
  End Select
  EraseAll
Next n
CloseWindow 1
Return



.receive:
Window 1,0,0,320,ht*2,$a,"GET",1,2
a$=ReadString{}

fil$=a$
a$=ReadString{}
b$=""
For n.l=1 To Len(a$)
  If Asc(Mid$(a$,n,1))>=Asc("0") AND Asc(Mid$(a$,n,1))<=Asc("9")
    b$+Mid$(a$,n,1)
  EndIf
Next n

flen.l=Val(b$)
WTitle "GET "+fil$+"("+Str$(flen)+")  ["+Str$(fnum)+"]"

suc=Reserve(0,flen)
If suc=0 Then EZRequest "MemAlloc Err!("+Str$(flen)+")|"+fil$+"|("+Str$(AvailMem_(0))+" free)":End
ReadMemSerial{Start(0),Length(0)}

SplitFilename{}

dir$=Mid$(pa$,Instr(pa$,":")+1)
newpath$=path$
If Right$(newpath$,1)<>"/" AND Right$(newpath$,1)<>":" Then newpath$+"/"
pa$=newpath$
d$=pa$
For n=1 To Len(dir$)
  a$=Mid$(dir$,n,1)
  If a$<>"/" Then d$+a$
  If a$="/" OR n=Len(dir$)
    flg=0
    l.l=Lock_(d$,-1)
    If l
      ad.l=AllocDosObject_(2,0)
      Examine_ l,ad
      If Peek.l(ad+4)>0 Then flg=1
      FreeDosObject_ 2,ad
      UnLock_ l
    Else
      flg=1
    EndIf
    If flg Then MakeDir d$
    d$+"/"
  EndIf
Next n

dest$=newpath$+dir$
If Right$(dest$,1)<>":" AND Right$(dest$,1)<>"/" Then dest$+"/"
dest$+fi$

Select pathtype
  Case 2
     If Right$(fi$,5)<>".info"
       RTEZSetDefaultDirectory 0,pa$
       dest$=RTEZLoadFile("Choose drawer",fi$)
      Else
       dest$=dest$+".info"
     EndIf
End Select

AddItem newfile()
newfile()\path=dest$


BSave dest$,0
Gosub showicon

Return

.sendafile:
num=ASLMultiSelect("Send..",spa$,sfi$)
If num
  Window 1,0,0,320,ht*2,$a,"SEND",1,2
  For n.l=1 To ASLNumFiles
    filename$=ASLNextFile
    a$=Right$(filename$,1)
    Select a$
      Case "/"
        numfiles=0
        CountDir{filename$}
        WTitle Str$(numfiles)+" files."
        fnum=1
        ScanDir{filename$}
      Case ":"
        numfiles=0
        CountDir{filename$}
        WTitle Str$(numfiles)+" files."
        fnum=1
        ScanDir{filename$}
      Default
        fnum=1
        numfiles=1
        Send{filename$}
        If Exists(filename$+".info") Then Send{filename$+".info"}
    End Select
    EraseAll
  Next n
  CloseWindow 1
EndIf
Return

showicon:
If newfiles=0
  newfiles+1
  GetIconObject "PROGDIR:NET"
  PutIconObject "T:YOUWONTHAVETHIS"
  AddAppIcon 1,"INCOMING","T:YOUWONTHAVETHIS",540,140
  KillFile "T:YOUWONTHAVETHIS.info"
EndIf
Return
End

  Even

  INCDIR ""
myshapes:
  IncBin "Blitz2:REDHelp/REDBlitzLibs/Net.shapes"
; ** Source Created by Gadget Tools Box
; ** 23/10/1994

drawgui:
  DefaultIDCMP  $00000274
  Window        0,142,71,390,104,$0020120E,"Project Window",1,2
  GTString      0,0,124,3,252,16,"Incoming path:",$0001,255,path$
  GTTags        $8008003D,2,$80080039,-1
  GTMX          0,1,220,28,17,29,"Incoming file path",$0002,"Incoming|File path|Select",pathtype
  GTText        0,6,220,42,0,0,"Incoming destination path",0,""
  Wline         4,82,384,82,1
  Wline         4,81,384,81,2
  GTButton      0,3,4,74,92,14,"Okay",$0010
  GTButton      0,4,98,74,92,14,"Quit",$0010
  GTButton      0,7,192,74,92,14,"Send File",$0010
  GTButton      0,5,286,74,92,14,"Cancel",$0010
  AttachGTList  0,0

  oldpath$=path$
  oldpathtype.l=pathtype.l
  Repeat
    VWait
    ev.l=Event
    ec.l=EventCode
    gh.l=GadgetHit
    If ev=64
      Select gh
        Case 0
          path$=GTGetString(0,0)
        Case 7
          Gosub sendafile
      End Select
    EndIf
    If ev=32
      Select gh
        Case 1
          pathtype.l=ec
      End Select
    EndIf
    aev.l=AppEvent
    ai.l=AppEventID
    sr=CheckSerial
    ce.l=CommodityEvent
    em.l=ExchangeMessage
    If ce<>0 AND (em=CxKill) Then End
    If gh=4 Then End
    If gh=5 Then path$=oldpath$:pathtype.l=oldpathtype.l

    If sr=1 Then Gosub receive:CloseWindow 1:EraseAll

    If aev
      If ai=0 AND AppNumFiles=0 Then Gosub drawgui:ev=-1
      If ai=0 AND AppNumFiles<>0 Then Gosub send
      If ai=1 AND AppNumFiles=0 Then Gosub showfiles
    EndIf

  Until aev.l<>0 OR ev=$200 OR (ev.l=64 AND (gh=3 OR gh=4 OR gh=5)) OR (sr<>-1)

DetachGTList 0 : CloseWindow 0
Return

docommodity:
MakeCommodity "Net","Net v0.666","Serial File Transfer by Red When Excited"
Return


getfontheight:
WbToScreen 0                    ; obtain the font height
a.l=Addr Screen(0)
a=Peek.l(a)
a+30
ht.w=Peek.b(a)
Return

