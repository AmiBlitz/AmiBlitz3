; XTRA
; Embedded .xtra Header
; 
; General Info
; -------------------------------------------------------
; ExePath         = "RAM:"
; ExeFile         = "shit"
; CreateIcon      = 0
; Residents       = "amigalibs.res"
; 
; Compiler
; -------------------------------------------------------
; StringBuffer    = 10240
; MakeSmallest    = 1
; FuncOptimize    = 1
; Version         = 0.0.0
; NumberOfBuilds  = 0
; 
; Debugger
; -------------------------------------------------------
; CliArgs         = ""
; StackSize       = 8191
; RuntimeDebug    = 1
; DebugInfo       = 0
; CreateDbgFile   = 0
; OverflowCheck   = 0
; AssemblerCheck  = 0
; InterruptCheck  = 1
; AutoRun         = 1
; 
; Editor
; -------------------------------------------------------
; CursorLine      = 1
; CursorColumn    = 1
; LabelSearch     = ""
; LabelRemark     = 0
; LabelAll        = 0
; LabelPosition   = 0
; 
; Blitz Objects
; -------------------------------------------------------
; Max IconInfo    = 1
; Max NChunky     = 50
; Max MUIObject   = 5
; Max PTModule    = 5
; Max AsyncReq    = 4
; Max Req-Lib     = 5
; Max GTMenuList  = 5
; Max Console     = 5
; Max TCPSock     = 5
; Max XBSound     = 10
; Max Chunky      = 20
; Max File        = 5
; Max GadgetList  = 5
; Max Queue       = 10
; Max Screen      = 5
; Max Shape       = 100
; Max CopList     = 10
; Max Sprite      = 20
; Max Stencil     = 5
; Max Module      = 5
; Max Window      = 5
; Max Anim        = 100
; Max Sound       = 10
; Max Bank        = 5
; Max Buffer      = 10
; Max BitMap      = 10
; Max Slice       = 10
; Max Page        = 4
; Max Tape        = 5
; Max IntuiFont   = 5
; Max MedModule   = 8
; Max Palette     = 4
; Max MenuList    = 5
; Max BlitzFont   = 4
; Max GTList      = 5
; /XTRA
; Datatypes Example - Images inside a Window

WbToScreen 0
WBenchToFront_
; Simple program to load in a picture and display it inside a window
;
; *************************
; BOTH MOUSEBUTTON TO QUIT!
; *************************

; Requires Blitzlibs:amigalibs.res !

;#IDMP_IDCMPUPDATE is the event sent to our window's msgport when
;the gadget has been 'laid' out. (ready to be drawn)

DefaultIDCMP #IDCMP_IDCMPUPDATE|#IDCMP_MOUSEBUTTONS

MaxLen pat$=192
MaxLen fit$=192

pa$=ASLFileRequest$("Choose a picture..",pat$,fit$)
If pa$="" Then End

If DataTypeOpen(pa$)                    ; This scans the datatype file
  If DataTypeGroup="pict"               ; we are only interested in pics
    If DataTypeCreate                   ; create the BOOPSI object
      DataTypeBitmap                    ; get the bitmap structure
      w.w=DataTypeWidth                 ; width
      h.w=DataTypeHeight                ; height
      d.w=DataTypeDepth                 ; depth

      PalRGB 0,(1 ASL d)-1,0,0,0        ; InitPalette crashes on some machines

      Poke.l ?swidth,w
      Poke.l ?sheight,h
      Poke.l ?sdepth,d
      Poke.l ?mode,DataTypeModeID

      ScreenTags 0,"",?scrntags

      DataTypesPalette 0                ; obtain the palette
      ShowPalette 0                     ; show the palette

      wflgs.l=#WFLG_BORDERLESS

      Window 0,0,0,w,h,wflgs,"",-1,-1

      redrawn.b=0                       ; Have we already redrawn it?

      If AttachDataType(0,50)           ; attach to window
        Repeat                          ; should really be font sensitive!
          ev.l=WaitEvent
          ec.l=EventCode
          Select ev
            Case #IDCMP_IDCMPUPDATE
              If redrawn=0
                If DataTypeReady
                  redrawn=1
                  Redraw 0,50        ; We can use the windows library
                EndIf                 ; to redraw it as its just a gadget
              EndIf
            Case #IDCMP_MOUSEBUTTONS
                quitflag=-1
          End Select
        Until quitflag=-1
        DetachDataType                  ; remove it from window
        CloseWindow 0
      EndIf
    EndIf
  EndIf
  DataTypeClose
EndIf

End

scrntags:
        Dc.l  #SA_Width
swidth: Dc.l  0

          Dc.l  #SA_Height
sheight:  Dc.l  0

        Dc.l  #SA_Depth
sdepth: Dc.l  0

      Dc.l  #SA_DisplayID
mode: Dc.l  0

  Dc.l  #SA_AutoScroll,-1
  Dc.l  #SA_Quiet,-1
  Dc.l  #TAG_DONE

